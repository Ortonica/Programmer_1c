
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов;
	ПараметрыДиалога.МножественныйВыбор = Ложь;
	ПараметрыДиалога.Заголовок = НСтр("ru = 'Выберите файл'; en = 'Select file'");
   	ПараметрыДиалога.Фильтр = НСтр("ru = 'Файл XML'; en = 'XML file'") + " (*.xml)|*.xml|";
	
	ДопПарам = Новый Структура;
	ДопПарам.Вставить("ВариантМакета", ПараметрКоманды); 
	ДопПарам.Вставить("Владелец", ПараметрыВыполненияКоманды.Источник); 
	
	ОписаниеЗавершенияПомещенияНаСервер = Новый ОписаниеОповещения("ПослеПомещенияНаСервер", ЭтотОбъект, ДопПарам);
	
	НачатьПомещениеФайлаНаСервер(ОписаниеЗавершенияПомещенияНаСервер,,,,ПараметрыДиалога);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПомещенияНаСервер(ОписаниеПомещенногоФайла, ДополнительныеПараметры) Экспорт

	Если ОписаниеПомещенногоФайла = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ЗаписатьМакетВКарточку(ОписаниеПомещенногоФайла.Адрес, ДополнительныеПараметры.ВариантМакета);
	Если ДополнительныеПараметры.Владелец.ИмяФормы = "Справочник.РСК_ВариантыМакетовПечатныхФорм.Форма.ФормаЭлемента" Тогда
		ДополнительныеПараметры.Владелец.Прочитать();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаписатьМакетВКарточку(АдресХранилища, ВариантМакета)
	
	ПутьКФайлу = ПолучитьИмяВременногоФайла("xml");
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресХранилища);
	ДвоичныеДанные.Записать(ПутьКФайлу);
	
	ЧтениеТекста = Новый ЧтениеТекста;
	ЧтениеТекста.Открыть(ПутьКФайлу);
	Текст = ЧтениеТекста.Прочитать();

	ТабДок = ОбщегоНазначения.ЗначениеИзСтрокиXML(Текст);
	
	оВариантМакета = ВариантМакета.ПолучитьОбъект();
	оВариантМакета.Макет = Новый ХранилищеЗначения(ТабДок);
	оВариантМакета.Записать();

КонецПроцедуры // ЗаписатьМакетВКарточку()
