
&ИзменениеИКонтроль("ОбработкаЗаполнения")
Процедура РСК_ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда

		Если ДанныеЗаполнения.Свойство("Партнер") И ЗначениеЗаполнено(ДанныеЗаполнения.Партнер) Тогда
			Запрос = Новый Запрос(
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	Партнеры.Наименование,
			|	Партнеры.НаименованиеПолное,
			|	ВЫБОР
			|		КОГДА Партнеры.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.КомпанияЧастноеЛицо.ЧастноеЛицо)
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ЭтоФизЛицо
			|ИЗ
			|	Справочник.Партнеры КАК Партнеры
			|ГДЕ
			|	Партнеры.Ссылка = &Партнер");
			Запрос.УстановитьПараметр("Партнер", ДанныеЗаполнения.Партнер);
			Результат = Запрос.Выполнить();
			Если Результат.Пустой() Тогда
				Возврат;
			КонецЕсли;
			ПартнерВладелец = Результат.Выбрать();
			ПартнерВладелец.Следующий();
			Партнер            = ДанныеЗаполнения.Партнер;
			Наименование       = ПартнерВладелец.Наименование;
			#Вставка
			//++ РС Консалт: Трофимов Евгений 07.11.2022 Задача 21693
			//e1cib/data/Документ.Задание?ref=99adf5824350e1c448d8a78619d80997
			//Претензия. Наталья Евстафьева: Не заполнять автоматически! Выдавать предупреждение. (Skype: ERP Ортоника 04.11.22 14:13)
			#КонецВставки
			#Удаление
			НаименованиеПолное = ПартнерВладелец.НаименованиеПолное;
			#КонецУдаления
			#Вставка
			Если Партнер <> Справочники.Партнеры.РозничныйПокупатель Тогда
				НаименованиеПолное = ПартнерВладелец.НаименованиеПолное;
			КонецЕсли;
			//-- КонецЗадачи 21693			
			#КонецВставки
			Если ПартнерВладелец.ЭтоФизЛицо ИЛИ Партнер = Справочники.Партнеры.РозничныйПокупатель Тогда
				ЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо;
			КонецЕсли;

		КонецЕсли;

		Если ДанныеЗаполнения.Свойство("СокращенноеНаименование")
			И Не ПустаяСтрока(ДанныеЗаполнения.СокращенноеНаименование) Тогда

			НаименованиеПолное = ДанныеЗаполнения.СокращенноеНаименование;

		КонецЕсли;

		Если ДанныеЗаполнения.Свойство("ЮридическийАдрес") Тогда

			УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(ЭтотОбъект, 
			ДанныеЗаполнения.ЮридическийАдрес, 
			Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента,
			ТекущаяДатаСеанса());

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

&После("ОбработкаПроверкиЗаполнения")
Процедура РСК_ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	//++ РС Консалт: Трофимов Евгений 07.11.2022 Задача 21693
	//e1cib/data/Документ.Задание?ref=99adf5824350e1c448d8a78619d80997
	//Претензия. Наталья Евстафьева: Не заполнять автоматически! Выдавать предупреждение. (Skype: ERP Ортоника 04.11.22 14:13)
	Если Партнер = Справочники.Партнеры.РозничныйПокупатель Тогда
		ПроверяемыеРеквизиты.Добавить("НаименованиеПолное");
	КонецЕсли;
	//-- КонецЗадачи 21693			
КонецПроцедуры
