
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	//++ РС Консалт: Трофимов Евгений 26.10.2022 Задача 21385
	//e1cib/data/Документ.Задание?ref=b392e035a480d21e48a9c3654611fb81
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	СвойстваСписка.ТекстЗапроса = ТекстЗапросаСписка();
	СвойстваСписка.ОсновнаяТаблица = "";
	СвойстваСписка.ДинамическоеСчитываниеДанных = Истина;
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.Список, СвойстваСписка);
	//-- КонецЗадачи 21385
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ТекстЗапросаСписка()

	Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	НоменклатураКонтрагентов.Ссылка КАК ТСР,
		|	ЕСТЬNULL(ГосударственныеКонтрактыНоменклатураОбъектовЗакупки.Ссылка, ЗНАЧЕНИЕ(Справочник.ГосударственныеКонтракты.ПустаяСсылка)) КАК ГК,
		|	ЕСТЬNULL(ГосударственныеКонтрактыНоменклатураОбъектовЗакупки.Идентификатор, """") КАК Идентификатор
		|ПОМЕСТИТЬ ТСРиГК
		|ИЗ
		|	Справочник.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГосударственныеКонтракты.НоменклатураОбъектовЗакупки КАК ГосударственныеКонтрактыНоменклатураОбъектовЗакупки
		|		ПО НоменклатураКонтрагентов.Ссылка = ГосударственныеКонтрактыНоменклатураОбъектовЗакупки.НоменклатураПартнера
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ТСРиГК.ГК КАК ГК,
		|	ДоговорыКонтрагентов.Ссылка КАК Договор,
		|	МАКСИМУМ(ЗаказКлиента.Ссылка) КАК Заказ
		|ПОМЕСТИТЬ ДоговорыГК
		|ИЗ
		|	ТСРиГК КАК ТСРиГК
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ЗаказКлиента
		|			ПО ДоговорыКонтрагентов.Ссылка = ЗаказКлиента.Договор
		|				И (НЕ ЗаказКлиента.ПометкаУдаления)
		|		ПО ТСРиГК.ГК = ДоговорыКонтрагентов.ГосударственныйКонтракт
		|ГДЕ
		|	ДоговорыКонтрагентов.ГосударственныйКонтракт <> ЗНАЧЕНИЕ(Справочник.ГосударственныеКонтракты.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	ТСРиГК.ГК,
		|	ДоговорыКонтрагентов.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ТСРиГК.ТСР КАК ТСР,
		|	ТСРиГК.ГК КАК ГК,
		|	НоменклатураКонтрагентов.Наименование КАК Наименование,
		|	НоменклатураКонтрагентов.НаименованиеНоменклатуры КАК Модель,
		|	НоменклатураКонтрагентов.НаименованиеПолное КАК ПолноеНаименование,
		|	ГосударственныеКонтракты.Контрагент КАК Контрагент,
		|	ГосударственныеКонтракты.Организация КАК Организация,
		|	ЕСТЬNULL(ГосударственныеКонтрактыОбъектыЗакупки.Наименование, """") КАК ПредставлениеГК,
		|	ЕСТЬNULL(ДоговорыГК.Договор, ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)) КАК Договор,
		|	ЕСТЬNULL(ДоговорыГК.Заказ, ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)) КАК Заказ
		|ИЗ
		|	ТСРиГК КАК ТСРиГК
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
		|		ПО ТСРиГК.ТСР = НоменклатураКонтрагентов.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГосударственныеКонтракты КАК ГосударственныеКонтракты
		|		ПО ТСРиГК.ГК = ГосударственныеКонтракты.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ГосударственныеКонтракты.ОбъектыЗакупки КАК ГосударственныеКонтрактыОбъектыЗакупки
		|		ПО ТСРиГК.ГК = ГосударственныеКонтрактыОбъектыЗакупки.Ссылка
		|			И ТСРиГК.Идентификатор = ГосударственныеКонтрактыОбъектыЗакупки.Идентификатор
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДоговорыГК КАК ДоговорыГК
		|		ПО ТСРиГК.ГК = ДоговорыГК.ГК
		|";

	
	Возврат Текст;

КонецФункции // ТекстЗапросаСписка()

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
    ДанныеСтроки = Элемент.ДанныеСтроки(ВыбраннаяСтрока);
	//ИмяПоля = Сред(Поле.Имя, СтрДлина(Элемент.Имя)+1);
	//ПоказатьЗначение(, ДанныеСтроки[ИмяПоля]);
	ПоказатьЗначение(, ДанныеСтроки[Поле.Имя]);
КонецПроцедуры
