

&После("ПередЗаписью")
Процедура РСК_ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	//+РС Консалт Назаров М.Ю. 25.11.2022
	ПроверкаУникальностиНоменклатурыОбъектовЗакупки();
	//-РС Консалт Назаров М.Ю. 25.11.2022              
		
КонецПроцедуры

// Перед запись необходимо проверить уникальность по строкам товаров "Номенклатура + Характеристика + Номенклатура контрагента"
// Чтобы не было несколько идентификаторов товаров из ЕИС с одинаковыми сопоставлениями
// Для этого к таким случаям будет создаваться новая "номенклатура контарегента" (чтобы была другая ссылка)
Процедура ПроверкаУникальностиНоменклатурыОбъектовЗакупки()
	
	Если Не ЗначениеЗаполнено(ЭтотОбъект.Ссылка) Тогда 
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	Таблица.Идентификатор КАК Идентификатор,
	               |	Таблица.НоменклатураПартнера КАК НоменклатураПартнера,
	               |	Таблица.Номенклатура КАК Номенклатура,
	               |	Таблица.Характеристика КАК Характеристика
	               |ПОМЕСТИТЬ Таблица
	               |ИЗ
	               |	&Таблица КАК Таблица
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Таблица.Идентификатор) КАК Идентификатор,
	               |	Таблица.НоменклатураПартнера КАК НоменклатураПартнера,
	               |	Таблица.Номенклатура КАК Номенклатура,
	               |	Таблица.Характеристика КАК Характеристика
	               |ИЗ
	               |	Таблица КАК Таблица
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Таблица.НоменклатураПартнера,
	               |	Таблица.Номенклатура,
	               |	Таблица.Характеристика
	               |
	               |ИМЕЮЩИЕ
	               |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Таблица.Идентификатор) > 1";       
	
	Запрос.УстановитьПараметр("Таблица", НоменклатураОбъектовЗакупки.Выгрузить());
	
	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда 
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		КоличествоПовторений = Выборка.Идентификатор;
		
		НоменклатураПартнера = Выборка.НоменклатураПартнера;
		Номенклатура = Выборка.Номенклатура;
		Характеристика = Выборка.Характеристика;
		
		Отбор = Новый Структура;
		Отбор.Вставить("Номенклатура", Номенклатура);
		Отбор.Вставить("НоменклатураПартнера", НоменклатураПартнера);
		Отбор.Вставить("Характеристика", Характеристика);
		
		НайденныеСтроки = НоменклатураОбъектовЗакупки.НайтиСтроки(Отбор);
		
		ПерваяСтрока = Истина;
		
		Для Каждого Строка Из НайденныеСтроки Цикл 
			Если ПерваяСтрока Тогда 
				ПерваяСтрока = Ложь;
				Продолжить;
			КонецЕсли;                                                                                                       
			
			НоваяНоменклатура = Справочники.НоменклатураКонтрагентов.СоздатьЭлемент();
			
			ЗаполнитьЗначенияСвойств(НоваяНоменклатура, НоменклатураПартнера, , "Код");
			НоваяНоменклатура.ДругиеШтрихкодыНоменклатуры.Загрузить(НоменклатураПартнера.ДругиеШтрихкодыНоменклатуры.Выгрузить());
			
			Попытка
				НоваяНоменклатура.Записать();
			Исключение 
				НоваяНоменклатура.ОбменДанными.Загрузка = Истина;
				НоваяНоменклатура.Записать();
				
			КонецПопытки;
			
			Если ЗначениеЗаполнено(НоваяНоменклатура.Ссылка) Тогда 
				Строка.НоменклатураПартнера = НоваяНоменклатура.Ссылка;
			КонецЕсли;
		
		КонецЦикла;
				
	КонецЦикла; 
	    	
КонецПроцедуры

  
