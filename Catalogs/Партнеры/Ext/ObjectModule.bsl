
&После("ПередЗаписью")
Процедура РСК_ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	СинхронизацияСпискаТелефоновСМСсоСпискомТелефоновПартнера();
КонецПроцедуры

Процедура СинхронизацияСпискаТелефоновСМСсоСпискомТелефоновПартнера()

	//++ РС Консалт: Трофимов Евгений 18.11.2022 Задача 21964
	//e1cib/data/Документ.Задание?ref=99d02c8af77c5fef43e43a23b0134c1a
	Если НЕ ЗначениеЗаполнено(Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПартнерыКонтактнаяИнформация.НомерТелефона КАК НомерТелефона
		|ИЗ
		|	Справочник.Партнеры.КонтактнаяИнформация КАК ПартнерыКонтактнаяИнформация
		|ГДЕ
		|	ПартнерыКонтактнаяИнформация.Ссылка = &Ссылка
		|	И ПартнерыКонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Телефон)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РСК_ТелефоныДляРассылкиСМС.НомерТелефона КАК НомерТелефона
		|ИЗ
		|	РегистрСведений.РСК_ТелефоныДляРассылкиСМС КАК РСК_ТелефоныДляРассылкиСМС
		|ГДЕ
		|	РСК_ТелефоныДляРассылкиСМС.Партнер = &Ссылка
		|";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Результаты = Запрос.ВыполнитьПакет();
	
	Было = Результаты[0].Выгрузить().ВыгрузитьКолонку("НомерТелефона");
	Стало = КонтактнаяИнформация.Выгрузить(
		Новый Структура("Тип", Перечисления.ТипыКонтактнойИнформации.Телефон)
	).ВыгрузитьКолонку("НомерТелефона");
	Отмечено = Результаты[1].Выгрузить().ВыгрузитьКолонку("НомерТелефона");
	
	//Удаляем лишние
	Для Каждого НомерТелефона Из Отмечено Цикл
		Если Стало.Найти(НомерТелефона) = Неопределено Тогда
			НЗ = РегистрыСведений.РСК_ТелефоныДляРассылкиСМС.СоздатьНаборЗаписей();
			НЗ.Отбор.Партнер.Установить(Ссылка);
			НЗ.Отбор.НомерТелефона.Установить(НомерТелефона);
			НЗ.Записать();
		КонецЕсли;
	КонецЦикла;
	
	//Добавляем недостающее
	Для Каждого НомерТелефона Из Стало Цикл
		Если Было.Найти(НомерТелефона) = Неопределено И СтрДлина(НомерТелефона) = 11 Тогда
			Запись = РегистрыСведений.РСК_ТелефоныДляРассылкиСМС.СоздатьМенеджерЗаписи();
			Запись.Партнер = Ссылка;
			Запись.НомерТелефона = НомерТелефона;
			Запись.Записать(Истина);
		КонецЕсли;
	КонецЦикла;
	
	//-- КонецЗадачи 21964	

КонецПроцедуры
