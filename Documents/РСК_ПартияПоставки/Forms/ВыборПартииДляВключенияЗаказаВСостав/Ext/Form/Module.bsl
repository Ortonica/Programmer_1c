&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	//++ РС Консалт: Трофимов Евгений 24.10.2022 Задача 21304
	//e1cib/data/Документ.Задание?ref=95ba1fec1efd0de4442f386a0a00044a
	Параметры.Свойство("ЗаказПоставщику", ЗаказПоставщику);
	Параметры.Свойство("КодСтроки", КодСтроки);
	Параметры.Свойство("Поставщик", Поставщик);
	Список.ТекстЗапроса =
	"ВЫБРАТЬ
	|	РСК_ПартияПоставки.Ссылка КАК Ссылка,
	|	РСК_ПартияПоставки.Дата КАК Дата,
	|	РСК_ПартияПоставки.Номер КАК Номер
	|ИЗ
	|	Документ.РСК_ПартияПоставки КАК РСК_ПартияПоставки
	|ГДЕ
	|	НЕ РСК_ПартияПоставки.ПометкаУдаления
	|	И РСК_ПартияПоставки.Поставщик = (ВЫРАЗИТЬ(&Поставщик КАК Справочник.Партнеры))
	|	И НЕ РСК_ПартияПоставки.СтатусПартии = ЗНАЧЕНИЕ(Перечисление.РСК_СтатусыПартийПоставки.Получен)";
	Список.Параметры.УстановитьЗначениеПараметра("Поставщик", Поставщик);
	Если ЗначениеЗаполнено(ЗаказПоставщику) Тогда
		СтруктЗаказа = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЗаказПоставщику, "Дата,Проведен");
		Если СтруктЗаказа.Дата < '2022.01.01' ИЛИ СтруктЗаказа.Проведен Тогда
			ЗаказПроведён = Истина;
		КонецЕсли;
	КонецЕсли;
	//-- КонецЗадачи 21304
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	//++ РС Консалт: Трофимов Евгений 24.10.2022 Задача 21304
	//e1cib/data/Документ.Задание?ref=95ba1fec1efd0de4442f386a0a00044a
	СтандартнаяОбработка = Ложь;
	ДанныеСтроки = Элемент.ДанныеСтроки(ВыбраннаяСтрока);
	Ответ = Новый Структура();
	Ответ.Вставить("ПартияПоставки", ДанныеСтроки.Ссылка);
	Ответ.Вставить("ДатаПартии", ДанныеСтроки.Дата);
	Ответ.Вставить("НомерПартии", ДанныеСтроки.Номер);
	
	Если ДанныеСтроки.Проведен И НЕ ЗаказПроведён Тогда
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ПослеВопросаОПроведенииЗаказаПоставщику", ЭтаФорма, Ответ), 
			"Вы пытаетесь включить в проведённую партию поставки непроведённый заказ поставщику. Сначала нужно провести заказ. Провести заказ сейчас?", 
			РежимДиалогаВопрос.ДаНет
		);
		Возврат;
	КонецЕсли;
	
	ВнестиИзменениеВПартию(Ответ);
	//-- КонецЗадачи 21304
КонецПроцедуры

&НаКлиенте
Процедура ПослеВопросаОПроведенииЗаказаПоставщику(РезультатВопроса, ДополнительныеПараметры) Экспорт
	//++ РС Консалт: Трофимов Евгений 24.10.2022 Задача 21304
	//e1cib/data/Документ.Задание?ref=95ba1fec1efd0de4442f386a0a00044a
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаписи = Новый Структура(
		"РежимЗаписи,РежимПроведения",
		РежимЗаписиДокумента.Проведение,
		РежимПроведенияДокумента.Неоперативный
	);
	
	ЭтаФорма.ВладелецФормы.Записать(ПараметрыЗаписи);
	ВнестиИзменениеВПартию(ДополнительныеПараметры);
	//-- КонецЗадачи 21304
	
КонецПроцедуры

&НаКлиенте
Процедура ВнестиИзменениеВПартию(Ответ)
	//++ РС Консалт: Трофимов Евгений 24.10.2022 Задача 21304
	//e1cib/data/Документ.Задание?ref=95ba1fec1efd0de4442f386a0a00044a
	ВнестиИзменениеВПартиюНаСервере(Ответ.ПартияПоставки, ЗаказПоставщику, КодСтроки);	
	
	Закрыть(Ответ);
	//-- КонецЗадачи 21304
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВнестиИзменениеВПартиюНаСервере(ПартияПоставки, ЗаказПоставщику, КодСтроки)
	//++ РС Консалт: Трофимов Евгений 24.10.2022 Задача 21304
	//e1cib/data/Документ.Задание?ref=95ba1fec1efd0de4442f386a0a00044a
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаказПоставщикуТовары.Номенклатура КАК Номенклатура,
		|	ЗаказПоставщикуТовары.Характеристика КАК Характеристика,
		|	ЗаказПоставщикуТовары.Серия КАК Серия,
		|	ЗаказПоставщикуТовары.Сумма КАК Сумма,
		|	ЗаказПоставщикуТовары.СуммаСНДС КАК СуммаСНДС,
		|	ЗаказПоставщикуТовары.Упаковка КАК Упаковка,
		|	ЗаказПоставщикуТовары.Количество КАК Количество,
		|	ЗаказПоставщикуТовары.КоличествоУпаковок КАК КоличествоУпаковок
		|ИЗ
		|	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
		|ГДЕ
		|	ЗаказПоставщикуТовары.Ссылка = &ЗаказПоставщику
		|	И ЗаказПоставщикуТовары.КодСтроки = &КодСтроки";
	
	Запрос.УстановитьПараметр("ЗаказПоставщику", ЗаказПоставщику);
	Запрос.УстановитьПараметр("КодСтроки", КодСтроки);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		оДок = ПартияПоставки.ПолучитьОбъект();
		НС = оДок.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НС, Выборка);
		НС.ЗаказПоставщику = ЗаказПоставщику;
		НС.КодСтроки = КодСтроки;
		Если оДок.Проведен Тогда
			оДок.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
		Иначе
			оДок.Записать();
		КонецЕсли;
	КонецЕсли;
	//-- КонецЗадачи 21304
КонецПроцедуры // ВнестиИзменениеВПартиюНаСервере()

&НаКлиенте
Процедура СоздатьНовуюПартию(Команда)
	//++ РС Консалт: Трофимов Евгений 24.10.2022 Задача 21304
	//e1cib/data/Документ.Задание?ref=95ba1fec1efd0de4442f386a0a00044a
    ФормаДокумента = ПолучитьФорму("Документ.РСК_ПартияПоставки.ФормаОбъекта",,ЭтаФорма.ВладелецФормы);
    ДанныеФормы = ФормаДокумента.Объект;
    СоздатьНовуюПартиюНаСервере(ДанныеФормы,ЗаказПоставщику, КодСтроки);
    КопироватьДанныеФормы(ДанныеФормы, ФормаДокумента.Объект);
    ФормаДокумента.Открыть();
	Закрыть();
	//-- КонецЗадачи 21304
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СоздатьНовуюПартиюНаСервере(ДанныеФормы, ЗаказПоставщику, КодСтроки)
	//++ РС Консалт: Трофимов Евгений 24.10.2022 Задача 21304
	//e1cib/data/Документ.Задание?ref=95ba1fec1efd0de4442f386a0a00044a
    оДок = Документы.РСК_ПартияПоставки.СоздатьДокумент();
    оДок.Заполнить(ЗаказПоставщику);
	тзТовары = оДок.Товары.Выгрузить(Новый Структура("КодСтроки", КодСтроки));
	оДок.Товары.Загрузить(тзТовары);
    ЗначениеВДанныеФормы(оДок, ДанныеФормы);
	//-- КонецЗадачи 21304
КонецПроцедуры
