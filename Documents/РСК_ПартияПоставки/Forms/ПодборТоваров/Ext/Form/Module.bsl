&НаКлиенте
Перем КэшированныеЗначения;

//++ РС Консалт: Трофимов Евгений 12.07.2022 Задача 18725
//e1cib/data/Документ.Задание?ref=82a4670c1ac44f3949c1b038bf806fda
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
//++ РС Консалт: Трофимов Евгений 07.07.2022 Задача 18725
//e1cib/data/Документ.Задание?ref=82a4670c1ac44f3949c1b038bf806fda
    
	Параметры.Свойство("ПартияПоставки", ПартияПоставки);
	Параметры.Свойство("Поставщик", Поставщик);
	
	Список.ТекстЗапроса = ТекстЗапросаСписка();
	//++ РС Консалт: Трофимов Евгений 22.03.2023 Тикет 24593
	//e1cib/data/Документ.Задание?ref=90ddce3be8a078b241eb10b75147434e
	//Нужно смотрет остатки на конец света, поэтому комментим.
	//Если ЗначениеЗаполнено(ПартияПоставки) Тогда
	//	Список.Параметры.УстановитьЗначениеПараметра("МоментОстатков", Новый Граница(ПартияПоставки.МоментВремени(), ВидГраницы.Исключая));
	//Иначе
	//	Список.Параметры.УстановитьЗначениеПараметра("МоментОстатков", КонецДня(ТекущаяДата()));
	//КонецЕсли;
	//-- КонецТикета 24593	
	Список.Параметры.УстановитьЗначениеПараметра("Поставщик", Поставщик);
	Список.Параметры.УстановитьЗначениеПараметра("ТекущаяПартия", ПартияПоставки);
	Список.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("АдресХраненияПозиций", Параметры.АдресХраненияПозиций);
	
	БлокируемыеПоля = Новый Массив;
	БлокируемыеПоля.Добавить("ВЭтомДокументе");
	
	Список.УстановитьОграниченияИспользованияВГруппировке(БлокируемыеПоля);
	Список.УстановитьОграниченияИспользованияВОтборе(БлокируемыеПоля);
	Список.УстановитьОграниченияИспользованияВПорядке(БлокируемыеПоля);
	
//-- КонецЗадачи 18725	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 12.07.2022 Задача 18725
//e1cib/data/Документ.Задание?ref=82a4670c1ac44f3949c1b038bf806fda
&НаКлиентеНаСервереБезКонтекста
Функция ТекстЗапросаСписка()
//++ РС Консалт: Трофимов Евгений 07.07.2022 Задача 18725
//e1cib/data/Документ.Задание?ref=82a4670c1ac44f3949c1b038bf806fda

	Текст = 
		"ВЫБРАТЬ
		|	РСК_ТоварыКПартиямПоставкиОстатки.ЗаказПоставщику КАК ЗаказПоставщику,
		|	РСК_ТоварыКПартиямПоставкиОстатки.НомерСтрокиЗаказа КАК НомерСтрокиЗаказа,
		|	РСК_ТоварыКПартиямПоставкиОстатки.КоличествоОстаток КАК НеВПартии,
		|	ВЫРАЗИТЬ(0 КАК ЧИСЛО(10, 0)) КАК ВЭтомДокументе,
		|	ЗаказПоставщикуТовары.Номенклатура КАК Номенклатура,
		|	ЗаказПоставщикуТовары.Характеристика КАК Характеристика
		|ИЗ
		//|	РегистрНакопления.РСК_ТоварыКПартиямПоставки.Остатки(&МоментОстатков, ЗаказПоставщику.Партнер = &Поставщик) КАК РСК_ТоварыКПартиямПоставкиОстатки
		|	РегистрНакопления.РСК_ТоварыКПартиямПоставки.Остатки(, ЗаказПоставщику.Партнер = &Поставщик) КАК РСК_ТоварыКПартиямПоставкиОстатки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
		|		ПО РСК_ТоварыКПартиямПоставкиОстатки.ЗаказПоставщику = ЗаказПоставщикуТовары.Ссылка
		|			И РСК_ТоварыКПартиямПоставкиОстатки.НомерСтрокиЗаказа = ЗаказПоставщикуТовары.КодСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РСК_ТоварыКПартиямПоставки.Обороты(, , Запись, ЗаказПоставщику.Партнер = &Поставщик) КАК ИспользованныеВДругихПартиях
		|		ПО РСК_ТоварыКПартиямПоставкиОстатки.ЗаказПоставщику = ИспользованныеВДругихПартиях.ЗаказПоставщику
		|			И РСК_ТоварыКПартиямПоставкиОстатки.НомерСтрокиЗаказа = ИспользованныеВДругихПартиях.НомерСтрокиЗаказа
		|			И (ИспользованныеВДругихПартиях.Регистратор <> &ТекущаяПартия)
		|			И (ИспользованныеВДругихПартиях.Регистратор ССЫЛКА Документ.РСК_ПартияПоставки)
		|ГДЕ
		|	РСК_ТоварыКПартиямПоставкиОстатки.КоличествоОстаток > 0
		|	И ИспользованныеВДругихПартиях.Регистратор ЕСТЬ NULL
		|	И НЕ ЗаказПоставщикуТовары.Отменено";
	
	Возврат Текст;

//-- КонецЗадачи 18725
КонецФункции // ТекстЗапросаСписка()

//++ РС Консалт: Трофимов Евгений 12.07.2022 Задача 18725
//e1cib/data/Документ.Задание?ref=82a4670c1ac44f3949c1b038bf806fda
&НаКлиенте
Процедура ДобавляемыеПозицииПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 12.07.2022 Задача 18725
//e1cib/data/Документ.Задание?ref=82a4670c1ac44f3949c1b038bf806fda
&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ДанныеСтроки = Элементы.Список.ДанныеСтроки(ВыбраннаяСтрока);
	НС = ДобавляемыеПозиции.Добавить();
	ЗаполнитьЗначенияСвойств(НС, ДанныеСтроки);
	НС.КодСтроки = ДанныеСтроки.НомерСтрокиЗаказа;
	НС.Количество = ДанныеСтроки.НеВПартии - ДанныеСтроки.ВЭтомДокументе;
	СтруктураДействий = Новый Структура;	
	СтруктураДействий.Вставить("ПересчитатьКоличествоУпаковок");
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(НС, СтруктураДействий, КэшированныеЗначения);	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 12.07.2022 Задача 18725
//e1cib/data/Документ.Задание?ref=82a4670c1ac44f3949c1b038bf806fda
&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	тзТовары = ПолучитьИзВременногоХранилища(Настройки.ДополнительныеСвойства.АдресХраненияПозиций);
	СтруктураОтбора = Новый Структура("ЗаказПоставщику,КодСтроки");
	Для Каждого СтрокаДС Из Строки Цикл
		СДС = СтрокаДС.Значение.Данные;
		СтруктураОтбора.ЗаказПоставщику = СДС.ЗаказПоставщику;
		СтруктураОтбора.КодСтроки = СДС.НомерСтрокиЗаказа;
		СтрокиТоваров = тзТовары.НайтиСтроки(СтруктураОтбора);
		Если СтрокиТоваров.Количество() = 0 Тогда
			СДС.ВЭтомДокументе = 0;
		Иначе
			СДС.ВЭтомДокументе = СтрокиТоваров[0].Количество;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 12.07.2022 Задача 18725
//e1cib/data/Документ.Задание?ref=82a4670c1ac44f3949c1b038bf806fda
&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	РезультатЗакрытия = Новый Структура;
	РезультатЗакрытия.Вставить("АдресХраненияДобавляемыхПозиций", ПолучитьАдресХраненияДобавляемыхПозиций());
	Закрыть(РезультатЗакрытия);
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 12.07.2022 Задача 18725
//e1cib/data/Документ.Задание?ref=82a4670c1ac44f3949c1b038bf806fda
&НаСервере
Функция ПолучитьАдресХраненияДобавляемыхПозиций()

	Возврат ПоместитьВоВременноеХранилище(ДобавляемыеПозиции.Выгрузить(), Новый УникальныйИдентификатор);

КонецФункции // ПолучитьАдресХраненияДобавляемыхПозиций()

//++ РС Консалт: Трофимов Евгений 12.07.2022 Задача 18725
//e1cib/data/Документ.Задание?ref=82a4670c1ac44f3949c1b038bf806fda
&НаКлиенте
Процедура ДобавляемыеПозицииКоличествоУпаковокПриИзменении(Элемент)
	ТекущаяСтрока = Элементы.ДобавляемыеПозиции.ТекущиеДанные;
	СтруктураДействий = Новый Структура;	
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
	
	ОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(ТекущаяСтрока, СтруктураДействий, КэшированныеЗначения);	
КонецПроцедуры

