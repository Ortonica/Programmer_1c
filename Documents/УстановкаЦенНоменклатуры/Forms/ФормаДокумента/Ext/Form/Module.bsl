#Область Загрузка_из_Файла_Конарев

&НаКлиенте
Процедура РСК_ЗагрузитьНоменклатуруПосле(Команда) 
	
	Если ИспользуетсяЦенообразование25 Тогда
		ИмяТЧ = "Товары2_5";
	Иначе
		ИмяТЧ = "Товары";
	КонецЕсли;
	
	ПараметрыЗаполнения = Новый Структура();
	ПараметрыЗаполнения.Вставить("ИмяСобытия", "ЗагрузкаДанныхИзФайла");
	ПараметрыЗаполнения.Вставить("ИмяТЧ", ИмяТЧ);
	ПараметрыЗаполнения.Вставить("Заголовок", НСтр("ru = 'Загрузка номенклатуры из файла';
													|en = 'Import products from the file'"));
	Если Объект[ИмяТЧ].Количество() Тогда
		ПараметрыОповещения = ПараметрыЗаполнения;
		ОчисткаТЧОписаниеОповещения = Новый ОписаниеОповещения("ВопросОчисткиТабличнойЧастиЗавершение", ЭтотОбъект, ПараметрыОповещения);
		ТекстВопроса = НСтр("ru = 'Очистить таблицу номенклатуры перед заполнением по данным из файла?';
							|en = 'Clear the table of the products before filling in according to the data from the file?'");
		ПоказатьВопрос(ОчисткаТЧОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	Иначе
		ЗагрузитьДанныеИзФайлаНачало(ПараметрыЗаполнения);
	КонецЕсли;

КонецПроцедуры 

&НаКлиенте
Функция ОписаниеКолонокМакетаДляЗагрузкиРаботников();
	
	КолонкиМакета = Новый Массив;
	
	Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Номенклатура", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(250)), НСтр("ru = 'Номенклатура';
																																					|en = 'Product'"), 15);
	Колонка.Позиция = 1;
	КолонкиМакета.Добавить(Колонка);
	
	Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("ВидЦены", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(250)), НСтр("ru = 'Вид цены';
																																						|en = 'Type of price'"), 30);
	Колонка.Позиция = 2;
	КолонкиМакета.Добавить(Колонка); 
	
	Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Валюта", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(10)), НСтр("ru = 'Валюта (по классификатору)';
																																						|en = 'Currency'"), 30);
	Колонка.Позиция = 3;
	КолонкиМакета.Добавить(Колонка);
	
	Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Цена", Новый ОписаниеТипов("Число",, Новый КвалификаторыЧисла(15,2)), НСтр("ru = 'Цена';
																																						|en = 'Price'"), 30);
	Колонка.Позиция = 4;
	КолонкиМакета.Добавить(Колонка);

	Возврат КолонкиМакета;
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьДанныеИзФайлаНачало(ПараметрыЗаполнения)
	
	ПараметрыЗагрузки = ЗагрузкаДанныхИзФайлаКлиент.ПараметрыЗагрузкиДанных();
	ПараметрыЗагрузки.ПолноеИмяТабличнойЧасти = "УстановкаЦенНоменклатуры." + ПараметрыЗаполнения.ИмяТЧ;
	ПараметрыЗагрузки.Заголовок = ПараметрыЗаполнения.Заголовок;
	ПараметрыЗагрузки.ДополнительныеПараметры = ПараметрыЗаполнения;
	ПараметрыЗагрузки.КолонкиМакета = ОписаниеКолонокМакетаДляЗагрузкиРаботников(); 
	
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьДанныеИзФайлаЗавершение", ЭтотОбъект, ПараметрыЗаполнения);
	ЗагрузкаДанныхИзФайлаКлиент.ПоказатьФормуЗагрузки(ПараметрыЗагрузки, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДанныеИзФайлаЗавершение(АдресЗагруженныхДанных, ДополнительныеПараметры) Экспорт
	
	Если АдресЗагруженныхДанных = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ЗагрузитьДанныеИзФайлаНаСервере(АдресЗагруженныхДанных, ДополнительныеПараметры);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанныеИзФайлаНаСервере(АдресЗагруженныхДанных, ДополнительныеПараметры)
		
	ЗагруженныеДанные = ПолучитьИзВременногоХранилища(АдресЗагруженныхДанных);
	Если Объект[ДополнительныеПараметры.ИмяТЧ].Количество() > 0 Тогда
		Для Каждого Стр Из ЗагруженныеДанные Цикл
			НоваяСтрока = Объект[ДополнительныеПараметры.ИмяТЧ].Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр);
		КонецЦикла;
	Иначе
		Объект[ДополнительныеПараметры.ИмяТЧ].Загрузить(ЗагруженныеДанные);
	КонецЕсли;
	ЗаполнитьДокумент();  
	
	ЭтаФорма.Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросОчисткиТабличнойЧастиЗавершение(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") И ДополнительныеПараметры.Свойство("ИмяТЧ") Тогда
			
			ИмяТабличнойЧасти = ДополнительныеПараметры.ИмяТЧ; 
			Объект[ИмяТабличнойЧасти].Очистить(); 
			ЗаполнитьДокумент(); 
			
			ЗагрузитьДанныеИзФайлаНачало(ДополнительныеПараметры); 
			
		КонецЕсли;
	КонецЕсли;
	
	Если Результат = КодВозвратаДиалога.Нет Тогда 
		ЗагрузитьДанныеИзФайлаНачало(ДополнительныеПараметры);	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти