
&После("ОбработкаЗаполнения")
Процедура РСК_ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	//++ РС Консалт: Трофимов Евгений 09.11.2022 Задача 21783
	//e1cib/data/Документ.Задание?ref=9faa675ffab234684f22d326f89dcf89
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") И ДанныеЗаполнения.Свойство("ДокументОснование") Тогда
		Если (ТипЗнч(ДанныеЗаполнения.ДокументОснование) = Тип("ДокументСсылка.ЗаказПоставщику")
			Или ТипЗнч(ДанныеЗаполнения.ДокументОснование) = Тип("Массив")) Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
				"ВЫБРАТЬ
				|	ЗаказПоставщикуТовары.Ссылка КАК ЗаказПоставщику,
				|	ЗаказПоставщикуТовары.КодСтроки КАК КодСтроки,
				|	ЗаказПоставщикуТовары.Серия КАК Серия
				|ИЗ
				|	Документ.ЗаказПоставщику.Товары КАК ЗаказПоставщикуТовары
				|ГДЕ
				|	ЗаказПоставщикуТовары.Ссылка В(&Ссылка)";
			
			Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения.ДокументОснование);
			Выборка = Запрос.Выполнить().Выбрать();
			Фильтр = Новый Структура("ЗаказПоставщику,КодСтроки");	
			
			Для Каждого Стр Из Товары Цикл
				ЗаполнитьЗначенияСвойств(Фильтр, Стр);
				Выборка.Сбросить();
				Если Выборка.НайтиСледующий(Фильтр) Тогда
					Стр.Серия = Выборка.Серия;
				Иначе
					Стр.Серия = Неопределено;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	//-- КонецЗадачи 21783	
КонецПроцедуры

&После("ПередЗаписью")
Процедура РСК_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	//++ РС Консалт: Трофимов Евгений 22.12.2022 Задача 22454
	//e1cib/data/Документ.Задание?ref=b14c6ffbdbd828834649b3741d796378
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РСК_ПартияПоставкиКонтейнеры.Ссылка КАК ПартияПоставки,
		|	РСК_ПартияПоставкиКонтейнеры.ДатаПоступленияНаСклад КАК ДатаПоступленияНаСклад
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг.Товары КАК ПриобретениеТоваровУслугТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РСК_ПартияПоставки.Товары КАК РСК_ПартияПоставкиТовары
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РСК_ПартияПоставки.Контейнеры КАК РСК_ПартияПоставкиКонтейнеры
		|			ПО РСК_ПартияПоставкиТовары.Ссылка = РСК_ПартияПоставкиКонтейнеры.Ссылка
		|				И РСК_ПартияПоставкиТовары.НомерКонтейнера = РСК_ПартияПоставкиКонтейнеры.НомерКонтейнера
		|		ПО ПриобретениеТоваровУслугТовары.ЗаказПоставщику = РСК_ПартияПоставкиТовары.ЗаказПоставщику
		|			И ПриобретениеТоваровУслугТовары.КодСтроки = РСК_ПартияПоставкиТовары.КодСтроки
		|ГДЕ
		|	ПриобретениеТоваровУслугТовары.Ссылка = &Ссылка
		|	И РСК_ПартияПоставкиКонтейнеры.ДокументПриобретения = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ДатаПоступления = Выборка.ДатаПоступленияНаСклад;
	КонецЕсли;
	//-- КонецЗадачи 22454	
КонецПроцедуры

&После("ОбработкаПроведения")
Процедура РСК_ОбработкаПроведения(Отказ, РежимПроведения)
	//++ РС Консалт: Трофимов Евгений 15.02.2023 Тикет 23869
	//e1cib/data/Документ.Задание?ref=b1973c40e57cc54d449ee6a43bc679d3
	Если РСК_ПроведениеТолькоПоТоварнымРегистрам Тогда
		
		МассивНужныхРегистров = Новый Массив;
		МассивНужныхРегистров.Добавить("РегистрНакопления.ТоварыКОформлениюДокументовИмпорта");
		МассивНужныхРегистров.Добавить("РегистрНакопления.ТоварыКПоступлению");
		МассивНужныхРегистров.Добавить("РегистрНакопления.ТоварыОрганизаций");
		//++ РС Консалт: Минаков А.С. Как же так получается, по товарным регистрам ничего не отражается
		МассивНужныхРегистров.Добавить("РегистрНакопления.ТоварыНаСкладах");
		МассивНужныхРегистров.Добавить("РегистрНакопления.ЗапасыИПотребности");
		МассивНужныхРегистров.Добавить("РегистрНакопления.РаспределениеЗапасовДвижения");
		МассивНужныхРегистров.Добавить("РегистрНакопления.ПрочиеАктивыПассивы");
		МассивНужныхРегистров.Добавить("РегистрНакопления.СебестоимостьТоваров");
		//++ РС Консалт: Минаков А.С.
		
		Для Каждого НЗ Из Движения Цикл
			
			Если МассивНужныхРегистров.Найти(НЗ.Метаданные().ПолноеИмя()) = Неопределено Тогда
				НЗ.ДополнительныеСвойства.Вставить("РассчитыватьИзменения", Ложь);
				Если НЗ.ДополнительныеСвойства.Свойство("ТаблицыКонтроля") Тогда
					НЗ.ДополнительныеСвойства.Удалить("ТаблицыКонтроля");
				КонецЕсли;
				НЗ.ОбменДанными.Загрузка = Истина;
				
				НЗ.Очистить();
				НЗ.Записать();
			КонецЕсли;
			
			//++ РС Консалт: Минаков А.С. Как же так получается, по товарным регистрам ничего не отражается
			Если НЗ.Метаданные().ПолноеИмя() = "РегистрНакопления.ПрочиеАктивыПассивы" Тогда
				Сч = 0;
				Пока Сч < НЗ.Количество() Цикл
					Строка = НЗ.Получить(Сч);
					Если Строка.Статья = ПланыВидовХарактеристик.СтатьиАктивовПассивов.ЗадолженностьПередПоставщиками Тогда
						НЗ.Удалить(Строка)
					Иначе
						Сч = Сч + 1
					КонецЕсли
				КонецЦикла	
			КонецЕсли;	
			//++ РС Консалт: Минаков А.С.
			
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	РасчетыСПоставщикамиПоСрокам.Регистратор КАК Регистратор,
			|	""РасчетыСПоставщикамиПоСрокам"" КАК ИмяРегистра
			|ИЗ
			|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК РасчетыСПоставщикамиПоСрокам
			|ГДЕ
			|	РасчетыСПоставщикамиПоСрокам.ДокументРегистратор = &ДокументРегистратор
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	РасчетыСПоставщикамиПланОплат.Регистратор,
			|	""РасчетыСПоставщикамиПланОплат""
			|ИЗ
			|	РегистрНакопления.РасчетыСПоставщикамиПланОплат КАК РасчетыСПоставщикамиПланОплат
			|ГДЕ
			|	РасчетыСПоставщикамиПланОплат.ДокументРегистратор = &ДокументРегистратор
			|";
		Запрос.УстановитьПараметр("ДокументРегистратор", Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НЗ = РегистрыНакопления[Выборка.ИмяРегистра].СоздатьНаборЗаписей();
			НЗ.Отбор.Регистратор.Установить(Выборка.Регистратор);
			НЗ.Прочитать();
			тзПроводки = НЗ.Выгрузить();
			УдаляемыеСтроки = тзПроводки.НайтиСтроки(Новый Структура("ДокументРегистратор", Ссылка));
			Для Каждого УдаляемаяСтрока Из УдаляемыеСтроки Цикл
				тзПроводки.Удалить(УдаляемаяСтрока);
			КонецЦикла;
			НЗ.Загрузить(тзПроводки);
			НЗ.Записать();
		КонецЦикла;
		
	КонецЕсли;
	//-- КонецТикета 23869	
КонецПроцедуры
