 
#Область ПодсистемаВесионирования
 
// СтандартныеПодсистемы.ВерсионированиеОбъектов
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	//принять решение о необходимости скрытия из отчетов по версиям реквизитов и табличных частей, 
	//имеющих техническое назначение. Для скрытия реквизитов и табличных частей необходимо в 
	//процедуре ПриОпределенииНастроекВерсионированияОбъектов модуля менеджера объекта добавить следующий код:
	
	//Настройки.ПриПолученииСлужебныхРеквизитов = Истина;
КонецПроцедуры

// Ограничивает видимость реквизитов объекта в отчете по версии.
//
// Параметры:
// Реквизиты - Массив - список имен реквизитов объекта.
Процедура ПриПолученииСлужебныхРеквизитов(Реквизиты) Экспорт
	//Реквизиты.Добавить("ИмяРеквизита"); // реквизит объекта
	//Реквизиты.Добавить("ИмяТабличнойЧасти.*"); // табличная часть объекта
КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
 
#КонецОбласти
 
// Текст запроса данных печати акта выдачи ТСР
// 
//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17536
//e1cib/data/Документ.Задание?ref=8c3621e5d045e076498c0cd0a57b495c
Функция ТекстЗапросаДанныхПечатиАктаВыдачиТСР() Экспорт

	Текст = 
		"ВЫБРАТЬ
		|	ПоручениеЭкспедитору.Ссылка КАК Акт,
		|	МАКСИМУМ(КонтактныеЛицаПартнеров.Ссылка) КАК ПредставительЗаявителя
		|ПОМЕСТИТЬ ПредставителиЗаявителей
		|ИЗ
		|	Документ.ПоручениеЭкспедитору КАК ПоручениеЭкспедитору
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛицаПартнеров
		|		ПО ПоручениеЭкспедитору.Пункт = КонтактныеЛицаПартнеров.Владелец
		|ГДЕ
		|	НЕ КонтактныеЛицаПартнеров.ПометкаУдаления
		|	И ПоручениеЭкспедитору.Ссылка В(&МассивОбъектов)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоручениеЭкспедитору.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаказКлиента.ДатаПоДаннымКлиента КАК ДатаКонтракта,
		|	ЗаказКлиента.НомерПоДаннымКлиента КАК НомерКонтракта,
		|	ПоручениеЭкспедитору.Номер КАК НомерАкта,
		|	ПоручениеЭкспедитору.Дата КАК ДатаАкта,
		|	ПоручениеЭкспедитору.РСК_НомерРеестра КАК НомерРеестра,
		|	ПоручениеЭкспедитору.РСК_ДатаРеестра КАК ДатаРеестра,
		|	ЗаказКлиента.Организация КАК Организация,
		|	ПоручениеЭкспедитору.РСК_ТСР КАК ТСР,
		|	ПоручениеЭкспедитору.РСК_Количество КАК Количество,
		|	ПоручениеЭкспедитору.РСК_Цена КАК Цена,
		|	ЗаказКлиента.РСК_СрокДействияГК КАК СрокДействияГК,
		|	ЗаказКлиента.РСК_ОбъектПоставкиГК КАК ОбъектПоставки,
		|	ПоручениеЭкспедитору.РСК_НомерНаправления КАК НомерНаправления,
		|	ПоручениеЭкспедитору.РСК_ДатаНаправления КАК ДатаНаправления,
		|	ПоручениеЭкспедитору.Ссылка КАК Акт,
		|	ПоручениеЭкспедитору.АдресДоставки КАК АдресДоставки,
		|	ПоручениеЭкспедитору.ДатаВыполнения КАК ДатаВыдачи,
		|	НоменклатураКонтрагентов.НаименованиеПолное КАК ПолноеНаименованиеТСР,
		|	НоменклатураКонтрагентов.НаименованиеНоменклатуры КАК Модель,
		|	ВЫБОР
		|		КОГДА спрНоменклатура.НаименованиеПолное = """"
		|			ТОГДА спрНоменклатура.Наименование
		|		ИНАЧЕ спрНоменклатура.НаименованиеПолное
		|	КОНЕЦ КАК Номенклатура,
		|	ЗаказКлиента.РСК_ФИОПодписанта КАК ФИОПодписанта,
		|	ЗаказКлиента.РСК_ПоставщикГК КАК ПоставщикГК,
		|	ЗаказКлиента.РСК_АдресПоставщикаГК КАК АдресПоставщикаГК,
		|	ЗаказКлиента.РСК_ПредставительПоставщикаГК КАК ПредставительПоставщикаГК,
		|	ЗаказКлиента.РСК_ДоверенностьПредставителяГК КАК ДоверенностьПредставителяГК,
		|	ПоручениеЭкспедитору.Пункт КАК ПолучательСсылка,
		|	Партнеры.Наименование КАК ФИО,
		|	Партнеры.РСК_СНИЛС КАК СНИЛС,
		|	Партнеры.РСК_ДокументУЛ КАК ПаспортныеДанные,
		|	ЕСТЬNULL(КонтактныеЛицаПартнеров.Наименование, Партнеры.Наименование) КАК ПолучательФИО,
		|	ЕСТЬNULL(КонтактныеЛицаПартнеров.Наименование, """") КАК ФИОПредставителя,
		|	ЕСТЬNULL(КонтактныеЛицаПартнеров.ДополнительнаяИнформация, """") КАК ДокументПодтверждающийПолномочия,
		|	ЕСТЬNULL(КонтактныеЛицаПартнеров.РСК_ДокументУЛ, """") КАК ПаспортПредставителя,
		|	ЕСТЬNULL(ПредставителиЗаявителей.ПредставительЗаявителя, ЗНАЧЕНИЕ(Справочник.КонтактныеЛицаПартнеров.ПустаяСсылка)) КАК ПредставительЗаявителя,
		|	ВЫРАЗИТЬ(ЕСТЬNULL(НомераФилиалов.Значение, """") КАК СТРОКА(50)) КАК НомерФилиала,
		|	ПРЕДСТАВЛЕНИЕ(спрНоменклатура.ЕдиницаИзмерения) КАК ЕдИзм,
		|	спрНоменклатура.Артикул КАК Артикул
		|ИЗ
		|	Документ.ПоручениеЭкспедитору КАК ПоручениеЭкспедитору
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоручениеЭкспедитору.Основания КАК ПоручениеЭкспедиторуОснования
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ЗаказКлиента
		|			ПО ПоручениеЭкспедиторуОснования.Основание = ЗаказКлиента.Ссылка
		|		ПО ПоручениеЭкспедитору.Ссылка = ПоручениеЭкспедиторуОснования.Ссылка
		|			И (ПоручениеЭкспедиторуОснования.НомерСтроки = 1)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Партнеры КАК Партнеры
		|		ПО ПоручениеЭкспедитору.Пункт = Партнеры.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
		|		ПО ПоручениеЭкспедитору.РСК_ТСР = НоменклатураКонтрагентов.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
		|		ПО ПоручениеЭкспедитору.РСК_Номенклатура = спрНоменклатура.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПредставителиЗаявителей КАК ПредставителиЗаявителей
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КонтактныеЛицаПартнеров КАК КонтактныеЛицаПартнеров
		|			ПО ПредставителиЗаявителей.ПредставительЗаявителя = КонтактныеЛицаПартнеров.Ссылка
		|		ПО ПоручениеЭкспедитору.Ссылка = ПредставителиЗаявителей.Акт
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК НомераФилиалов
		|		ПО ПоручениеЭкспедитору.Пункт = НомераФилиалов.Объект
		|			И (НомераФилиалов.Свойство.Имя = ""НомерФилиала"")
		|ГДЕ
		|	ПоручениеЭкспедитору.Ссылка В(&МассивОбъектов)
		|";

	
	Возврат Текст;

КонецФункции // ТекстЗапросаСбораДанных()

&После("ОбработкаПолученияПолейПредставления")
Процедура РСК_ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17924
	//e1cib/data/Документ.Задание?ref=b6d469d456107fef47bae0d8601fa57f
	
	//Замена представления документов, в которых переключается интерфейс на подсистему «Выдача ТСР»
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("Номер");
	Поля.Добавить("Дата");
	Поля.Добавить("РСК_АктВыдачиТСР");
	//-- КонецЗадачи 17924
КонецПроцедуры

&После("ОбработкаПолученияПредставления")
Процедура РСК_ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17924
	//e1cib/data/Документ.Задание?ref=b6d469d456107fef47bae0d8601fa57f
	
	//Замена представления документов, в которых переключается интерфейс на подсистему «Выдача ТСР»
	СтандартнаяОбработка = Ложь;
	Если Данные.РСК_АктВыдачиТСР Тогда
		Представление = "Акт выдачи ТСР ";
	Иначе
		Представление = "Поручение экспедитору ";
	КонецЕсли;
	Представление = Представление + Данные.Номер + " от " + Формат(Данные.Дата, "ДЛФ=DT");
	//-- КонецЗадачи 17924
КонецПроцедуры


