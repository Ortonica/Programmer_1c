
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
    ПараметрыРазмещения.КоманднаяПанель = Элементы.ПодменюПечать;
    ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

#Область ОбработчикиКомандФормы
 
// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры
 
&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры
 
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
 
#КонецОбласти

&НаКлиенте
//Добавление возможности группового изменения реквизитов
//РС Консалт: Трофимов Евгений 05.07.2022 Выдача ТСР
Процедура ИзменитьВыделенные(Команда)
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
КонецПроцедуры

&НаКлиенте
//++ РС Консалт: Трофимов Евгений 25.07.2022 Задача 19208
//e1cib/data/Документ.Задание?ref=b240f4f00342a1014b20d05d81910626
Процедура ВыполнитьРассылкуСМС(Команда)
	СтруктураПараметров = ПолучитьПараметрыГрупповойРассылкиСМС();
	ОткрытьФорму("РегистрСведений.РСК_СообщенияСМС.Форма.ФормаГрупповойОтправкиСМССообщений",СтруктураПараметров,);
КонецПроцедуры

&НаСервере
//++ РС Консалт: Трофимов Евгений 25.07.2022 Задача 19208
//e1cib/data/Документ.Задание?ref=b240f4f00342a1014b20d05d81910626
Функция ПолучитьПараметрыГрупповойРассылкиСМС()
	СтруктураПараметров = Новый Структура;
	
	Схема = Элементы.Список.ПолучитьИсполняемуюСхемуКомпоновкиДанных();
	Настройки = Элементы.Список.ПолучитьИсполняемыеНастройкиКомпоновкиДанных();

	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных();
	МакетКомпоновки = КомпоновщикМакета.Выполнить(Схема,Настройки,,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));

	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки);
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;

	Результат = ПроцессорВывода.Вывести(ПроцессорКомпоновки);	
	
	Если Результат.Колонки.Найти("Ссылка") = Неопределено Тогда
		ОбщегоНазначения.СообщитьПользователю("Включите, пожалуйста, видимость колонки ""Ссылка""");
		СтруктураПараметров.Вставить("МассивАктов", Новый Массив);
	Иначе
		МассивАктов = Результат.ВыгрузитьКолонку("Ссылка");
		СтруктураПараметров.Вставить("МассивАктов", МассивАктов);
	КонецЕсли;

	Возврат СтруктураПараметров;

КонецФункции

//++ РС Консалт: Трофимов Евгений 30.11.2022 Задача 22184
//e1cib/data/Документ.Задание?ref=bad28d29de103210439d8f3b1785bb2b
&НаКлиенте
Процедура ВключитьВОтчетПоВыдаче(Команда)
	
	Парам = Новый Структура("МассивАктов", Элементы.Список.ВыделенныеСтроки);
	ОткрытьФорму("Документ.РСК_ОтчетПоВыдачеТСР.Форма.ФормаДокумента", Парам, ЭтаФорма);
	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 23.12.2022 Задача 22672
//e1cib/data/Документ.Задание?ref=8b703a6eb576fe2f430e5bc59ed27c9a
&НаКлиенте
Процедура Заполнить(Команда)
	ДопПарам = Новый Структура;
	ДопПарам.Вставить("МассивАктов", Элементы.Список.ВыделенныеСтроки);
	ОткрытьФорму(
		"Документ.ПоручениеЭкспедитору.Форма.РСК_ИзменитьВыделенные",,
		ЭтаФорма,,,, 
		Новый ОписаниеОповещения("ПослеУказанияПараметровЗаполнение", ЭтаФорма, ДопПарам)
	);
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 23.12.2022 Задача 22672
//e1cib/data/Документ.Задание?ref=8b703a6eb576fe2f430e5bc59ed27c9a
&НаКлиенте
Процедура ПослеУказанияПараметровЗаполнение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ё = 0;
	Для Каждого АктВыдачи Из ДополнительныеПараметры.МассивАктов Цикл
		ё=ё+1;
		Состояние(""+ё+" из "+ДополнительныеПараметры.МассивАктов.Количество(),ё*100/ДополнительныеПараметры.МассивАктов.Количество());
		ОбработкаПрерыванияПользователя();
		ЗаполнитьНаСервере(АктВыдачи, Результат);
	КонецЦикла;
	Элементы.Список.Обновить();
	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 23.12.2022 Задача 22672
//e1cib/data/Документ.Задание?ref=8b703a6eb576fe2f430e5bc59ed27c9a
&НаСервереБезКонтекста
Процедура ЗаполнитьНаСервере(АктВыдачи, ПараметрыЗаполнения)
	о = АктВыдачи.ПолучитьОбъект();
	Если ПараметрыЗаполнения.ИзменятьСтатус Тогда
		о.РСК_СтатусАктаВыдачиТСР = ПараметрыЗаполнения.НовыйСтатус;
	КонецЕсли;
	Если ПараметрыЗаполнения.ДополнитьКомментарий Тогда
		о.Комментарий = о.Комментарий + ?(ПустаяСтрока(о.Комментарий),"",", ") + ПараметрыЗаполнения.ДополнительныйКомментарий;
	КонецЕсли;
	Если о.Проведен Тогда
		Попытка
			о.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
		Исключение
			ОбщегоНазначения.СообщитьПользователю("Не удалось провести "+о.Ссылка, о.Ссылка);
		КонецПопытки;
	Иначе
		Попытка
			о.Записать(РежимЗаписиДокумента.Запись);
		Исключение
			ОбщегоНазначения.СообщитьПользователю("Не удалось записать "+о.Ссылка, о.Ссылка);
		КонецПопытки;
	КонецЕсли;		
КонецПроцедуры // ЗаполнитьНаСервере()
