&НаСервере
//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17534
//e1cib/data/Документ.Задание?ref=a3eb9b03bd1c14b84aeaf2f6294e0848
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Параметры.Свойство("Контракт", Контракт);
	Параметры.Свойство("ДатаНачала", ПериодВыборки.ДатаНачала);
	Параметры.Свойство("ДатаОкончания", ПериодВыборки.ДатаОкончания);
КонецПроцедуры

&НаКлиенте
//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17534
//e1cib/data/Документ.Задание?ref=a3eb9b03bd1c14b84aeaf2f6294e0848
Процедура ПриОткрытии(Отказ)
	Элементы.ПериодВыборки.Доступность = НЕ ВсеВыданныеПоКонтракту;
КонецПроцедуры

&НаСервере
//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17534
//e1cib/data/Документ.Задание?ref=a3eb9b03bd1c14b84aeaf2f6294e0848
Процедура СформироватьАктыНаСервере()
	ЗапросАкты = Новый Запрос;
	Если ВсеВыданныеПоКонтракту Тогда	
		ЗапросАкты.Текст = 
			"ВЫБРАТЬ
			|	ПоручениеЭкспедитору.Ссылка КАК Акт,
			|	ПоручениеЭкспедитору.Пункт КАК Заявитель,
			|	ПоручениеЭкспедитору.РСК_Номенклатура КАК Номенклатура,
			|	ПоручениеЭкспедитору.РСК_Характеристика КАК ХарактеристикаНоменклатуры,
			|	ПоручениеЭкспедитору.РСК_ТСР КАК ТСР,
			|	ПоручениеЭкспедитору.РСК_Количество КАК Количество,
			|	ПоручениеЭкспедитору.РСК_Цена КАК Цена,
			|	ПоручениеЭкспедитору.РСК_Сумма КАК Сумма,
			|	ПоручениеЭкспедитору.РСК_НомерНаправления КАК НомерНаправления,
			|	ПоручениеЭкспедитору.РСК_ДатаНаправления КАК ДатаНаправления,
			|	ПоручениеЭкспедитору.РСК_НомерРеестра КАК НомерРеестра,
			|	ПоручениеЭкспедитору.РСК_ДатаРеестра КАК ДатаРеестра
			|ИЗ
			|	Документ.ПоручениеЭкспедитору.Основания КАК ПоручениеЭкспедиторуОснования
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоручениеЭкспедитору КАК ПоручениеЭкспедитору
			|		ПО ПоручениеЭкспедиторуОснования.Ссылка = ПоручениеЭкспедитору.Ссылка
			|			И (ПоручениеЭкспедиторуОснования.НомерСтроки = 1)
			|ГДЕ
			|	ПоручениеЭкспедиторуОснования.Основание = &Контракт
			|	И ПоручениеЭкспедитору.РСК_СтатусАктаВыдачиТСР = ЗНАЧЕНИЕ(Перечисление.РСК_СтатусыАктовВыдачи.Выдан)
			|	И ПоручениеЭкспедитору.РСК_АктВыдачиТСР
			|	И НЕ ПоручениеЭкспедитору.ПометкаУдаления";
	Иначе
		ЗапросАкты.Текст = 
			"ВЫБРАТЬ
			|	ПоручениеЭкспедитору.Ссылка КАК Акт,
			|	ПоручениеЭкспедитору.Пункт КАК Заявитель,
			|	ПоручениеЭкспедитору.РСК_Номенклатура КАК Номенклатура,
			|	ПоручениеЭкспедитору.РСК_Характеристика КАК ХарактеристикаНоменклатуры,
			|	ПоручениеЭкспедитору.РСК_ТСР КАК ТСР,
			|	ПоручениеЭкспедитору.РСК_Количество КАК Количество,
			|	ПоручениеЭкспедитору.РСК_Цена КАК Цена,
			|	ПоручениеЭкспедитору.РСК_Сумма КАК Сумма,
			|	ПоручениеЭкспедитору.РСК_НомерНаправления КАК НомерНаправления,
			|	ПоручениеЭкспедитору.РСК_ДатаНаправления КАК ДатаНаправления,
			|	ПоручениеЭкспедитору.РСК_НомерРеестра КАК НомерРеестра,
			|	ПоручениеЭкспедитору.РСК_ДатаРеестра КАК ДатаРеестра
			|ИЗ
			|	Документ.ПоручениеЭкспедитору.Основания КАК ПоручениеЭкспедиторуОснования
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоручениеЭкспедитору КАК ПоручениеЭкспедитору
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РСК_СтатусыВыполненияКонтрактов КАК РСК_СтатусыВыполненияКонтрактов
			|			ПО (РСК_СтатусыВыполненияКонтрактов.Регистратор = ПоручениеЭкспедитору.Ссылка)
			|		ПО ПоручениеЭкспедиторуОснования.Ссылка = ПоручениеЭкспедитору.Ссылка
			|			И (ПоручениеЭкспедиторуОснования.НомерСтроки = 1)
			|ГДЕ
			|	ПоручениеЭкспедиторуОснования.Основание = &Контракт
			|	И ПоручениеЭкспедитору.РСК_СтатусАктаВыдачиТСР = ЗНАЧЕНИЕ(Перечисление.РСК_СтатусыАктовВыдачи.Выдан)
			|	И ПоручениеЭкспедитору.РСК_АктВыдачиТСР
			|	И РСК_СтатусыВыполненияКонтрактов.ДатаЗаписи МЕЖДУ &ДатаНачала И &ДатаОкончания
			|	И РСК_СтатусыВыполненияКонтрактов.Статус = ЗНАЧЕНИЕ(Перечисление.РСК_СтатусыАктовВыдачи.Выдан)";
		
		ЗапросАкты.УстановитьПараметр("ДатаНачала",НачалоДня(ПериодВыборки.ДатаНачала));
		ЗапросАкты.УстановитьПараметр("ДатаОкончания",?(
			ЗначениеЗаполнено(ПериодВыборки.ДатаОкончания),
			КонецДня(ПериодВыборки.ДатаОкончания),
			'3999-12-31 23:59:59'
		));
	КонецЕсли;
	
	ЗапросАкты.УстановитьПараметр("Контракт",Контракт);
	Реестр.Загрузить(ЗапросАкты.Выполнить().Выгрузить());

КонецПроцедуры

&НаКлиенте
//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17534
//e1cib/data/Документ.Задание?ref=a3eb9b03bd1c14b84aeaf2f6294e0848
Процедура СформироватьАкты(Команда)
	Если НЕ ПроверитьЗаполнениеНаСервере() Тогда
		Возврат;
	КонецЕсли;
	
	Если Реестр.Количество()=0 Тогда
		СформироватьАктыНаСервере();
	Иначе
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ПослеВопросаОПерезаполненииТаблицы", ЭтаФорма), 
			"Табличная часть заполнена, перезаполнить?",
			РежимДиалогаВопрос.ДаНет
		);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17534
//e1cib/data/Документ.Задание?ref=a3eb9b03bd1c14b84aeaf2f6294e0848
Процедура ПослеВопросаОПерезаполненииТаблицы(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	СформироватьАктыНаСервере();

КонецПроцедуры

&НаСервере
//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17534
//e1cib/data/Документ.Задание?ref=a3eb9b03bd1c14b84aeaf2f6294e0848
Функция ПроверитьЗаполнениеНаСервере()

	Возврат ПроверитьЗаполнение();

КонецФункции // ПроверитьЗаполнениеНаСервере()

&НаКлиенте
//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17534
//e1cib/data/Документ.Задание?ref=a3eb9b03bd1c14b84aeaf2f6294e0848
Процедура ВыбратьВсе(Команда)
	
	Для Каждого СтрокаТЧ ИЗ Реестр Цикл
		СтрокаТЧ.Выбран = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17534
//e1cib/data/Документ.Задание?ref=a3eb9b03bd1c14b84aeaf2f6294e0848
Процедура ОтменитьВсе(Команда)
	
	Для Каждого СтрокаТЧ ИЗ Реестр Цикл
		СтрокаТЧ.Выбран = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17534
//e1cib/data/Документ.Задание?ref=a3eb9b03bd1c14b84aeaf2f6294e0848
Процедура УдалитьНеотмеченные(Команда)
	
	сзСтрокиДляУдаления = Новый СписокЗначений;
	Для Каждого СтрокаТЧ ИЗ Реестр Цикл
		Если СтрокаТЧ.Выбран Тогда Продолжить; КонецЕсли;
		сзСтрокиДляУдаления.Добавить(СтрокаТЧ);
	КонецЦикла;
	Для Каждого СтрокаСписка Из сзСтрокиДляУдаления Цикл
		Реестр.Удалить(СтрокаСписка.Значение);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17534
//e1cib/data/Документ.Задание?ref=a3eb9b03bd1c14b84aeaf2f6294e0848
Процедура Удалитьотмеченные(Команда)

	сзСтрокиДляУдаления = Новый СписокЗначений;
	Для Каждого СтрокаТЧ ИЗ Реестр Цикл
		Если НЕ СтрокаТЧ.Выбран Тогда Продолжить; КонецЕсли;
		сзСтрокиДляУдаления.Добавить(СтрокаТЧ);
	КонецЦикла;
	Для Каждого СтрокаСписка Из сзСтрокиДляУдаления Цикл
		Реестр.Удалить(СтрокаСписка.Значение);
	КонецЦикла;
	
КонецПроцедуры


&НаСервере
//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17534
//e1cib/data/Документ.Задание?ref=a3eb9b03bd1c14b84aeaf2f6294e0848
Функция ВыгрузитьНаСервере()
	Возврат ПоместитьВоВременноеХранилище(Реестр.Выгрузить(Новый Структура("Выбран", Истина)), Новый УникальныйИдентификатор);
КонецФункции

&НаКлиенте
//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17534
//e1cib/data/Документ.Задание?ref=a3eb9b03bd1c14b84aeaf2f6294e0848
Процедура ПеренестиДанные(Команда)
	Закрыть(ВыгрузитьНаСервере());
КонецПроцедуры

&НаКлиенте
//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17534
//e1cib/data/Документ.Задание?ref=a3eb9b03bd1c14b84aeaf2f6294e0848
Процедура ПоискПоАктуАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	Элементы.Реестр.ОтборСтрок = Новый ФиксированнаяСтруктура("Акт",Текст);
КонецПроцедуры

&НаКлиенте
//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17534
//e1cib/data/Документ.Задание?ref=a3eb9b03bd1c14b84aeaf2f6294e0848
Процедура ПоискПоЗаявителюАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	Элементы.Реестр.ОтборСтрок = Новый ФиксированнаяСтруктура("Заявитель",Текст);
КонецПроцедуры

&НаКлиенте
//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17534
//e1cib/data/Документ.Задание?ref=a3eb9b03bd1c14b84aeaf2f6294e0848
Процедура РеестрАктОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	РеквизитыАкта = ПолучитьРеквизитыАкта(ВыбранноеЗначение);
	ЗаполнитьЗначенияСвойств(ЭтаФорма.ТекущийЭлемент.ТекущиеДанные, РеквизитыАкта);
КонецПроцедуры

&НаСервере
//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17534
//e1cib/data/Документ.Задание?ref=a3eb9b03bd1c14b84aeaf2f6294e0848
Функция ПолучитьРеквизитыАкта(СсылкаАкт)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПоручениеЭкспедитору.Ссылка КАК Акт,
		|	ПоручениеЭкспедитору.Пункт КАК Заявитель,
		|	НоменклатураКонтрагентов.Номенклатура КАК Номенклатура,
		|	НоменклатураКонтрагентов.Характеристика КАК ХарактеристикаНоменклатуры,
		|	ПоручениеЭкспедитору.РСК_ТСР КАК ТСР,
		|	ПоручениеЭкспедитору.РСК_Количество КАК Количество,
		|	ПоручениеЭкспедитору.РСК_Цена КАК Цена,
		|	ПоручениеЭкспедитору.РСК_Сумма КАК Сумма,
		|	ПоручениеЭкспедитору.РСК_НомерНаправления КАК НомерНаправления,
		|	ПоручениеЭкспедитору.РСК_ДатаНаправления КАК ДатаНаправления,
		|	ПоручениеЭкспедитору.РСК_НомерРеестра КАК НомерРеестра,
		|	ПоручениеЭкспедитору.РСК_ДатаРеестра КАК ДатаРеестра
		|ИЗ
		|	Документ.ПоручениеЭкспедитору КАК ПоручениеЭкспедитору
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
		|		ПО ПоручениеЭкспедитору.РСК_ТСР = НоменклатураКонтрагентов.Ссылка
		|ГДЕ
		|	ПоручениеЭкспедитору.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", СсылкаАкт);
	Возврат РСК_ВызовСервера.ЗапросВСтруктуру(Запрос);	
КонецФункции

&НаКлиенте
//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17534
//e1cib/data/Документ.Задание?ref=a3eb9b03bd1c14b84aeaf2f6294e0848
Процедура ВсеВыданныеПоКонтрактуПриИзменении(Элемент)
	Элементы.ПериодВыборки.Доступность = НЕ ВсеВыданныеПоКонтракту;
КонецПроцедуры


 
