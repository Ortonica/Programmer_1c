#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17534
//e1cib/data/Документ.Задание?ref=a3eb9b03bd1c14b84aeaf2f6294e0848
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	ПроверкаНаПовторноеУчастиеАктаВОтчете(Отказ);
	ПроверкаНаПринадлежностьАктовКЗаказуКлиента(Отказ);
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17534
//e1cib/data/Документ.Задание?ref=a3eb9b03bd1c14b84aeaf2f6294e0848
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Ответственный) Тогда
		Ответственный = ПараметрыСеанса.ТекущийПользователь;
	КонецЕсли;
	СуммаДокумента = Реестр.Итог("Сумма");
	УстановкаСтатусаВыданВУдалённыхПорученияхЭкспедитору(Отказ);
	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17534
//e1cib/data/Документ.Задание?ref=a3eb9b03bd1c14b84aeaf2f6294e0848
Процедура ПриКопировании(ОбъектКопирования)
	Ответственный = Неопределено;
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17534
//e1cib/data/Документ.Задание?ref=a3eb9b03bd1c14b84aeaf2f6294e0848
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	//Реализована на форме
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17534
//e1cib/data/Документ.Задание?ref=a3eb9b03bd1c14b84aeaf2f6294e0848
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	ПроведениеПоРегиструСостоянияЗаказовПоВыдачеТСР(Отказ);
КонецПроцедуры

Процедура ПроверкаНаПовторноеУчастиеАктаВОтчете(Отказ)
	//++ РС Консалт: Трофимов Евгений 14.11.2022 Тикет 21892
	//e1cib/data/Документ.Задание?ref=a2f9f48080c3996a4a0f6ce4f9476921
	//Чмилева Екатерина: Не должно быть два одинаковых акта в разных отчётах
	Если НЕ РегистрыСведений.реа_ПредопределенныеЗначения.Значение("ВыдачаТСР_ПроверкаНаПовторноеУчастиеАктаВОтчете", Истина) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РСК_ОтчетПоВыдачеТСРРеестр.Ссылка КАК Ссылка,
		|	РСК_ОтчетПоВыдачеТСРРеестр.НомерСтроки КАК НомерСтроки,
		|	ПРЕДСТАВЛЕНИЕ(РСК_ОтчетПоВыдачеТСРРеестр.Акт) КАК Акт,
		|	ПРЕДСТАВЛЕНИЕ(РСК_ОтчетПоВыдачеТСРРеестр.Заявитель) КАК Заявитель,
		|	ПРЕДСТАВЛЕНИЕ(РСК_ОтчетПоВыдачеТСРРеестр.Ссылка) КАК Отчет,
		|	РСК_ОтчетПоВыдачеТСРРеестр.Акт КАК АктСсылка
		|ИЗ
		|	Документ.РСК_ОтчетПоВыдачеТСР.Реестр КАК РСК_ОтчетПоВыдачеТСРРеестр
		|ГДЕ
		|	РСК_ОтчетПоВыдачеТСРРеестр.Ссылка <> &Ссылка
		|	И РСК_ОтчетПоВыдачеТСРРеестр.Ссылка.Проведен
		|	И РСК_ОтчетПоВыдачеТСРРеестр.Акт В(&Акты)";
	
	Запрос.УстановитьПараметр("Акты", Реестр.Выгрузить().ВыгрузитьКолонку("Акт"));
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ОбщегоНазначения.СообщитьПользователю(
			СтрШаблон(
				"%1 заявителя %2 уже был представлен в документе %3",
				Выборка.Акт,
				Выборка.Заявитель,
				Выборка.Отчет
			),
			Выборка.Ссылка,
			"Реестр["+XMLСтрока(Выборка.НомерСтроки-1)+"].Акт",,
			Отказ
		);
		
		НайденныеСтроки = Реестр.НайтиСтроки(Новый Структура("Акт", Выборка.АктСсылка));
		
		ОбщегоНазначения.СообщитьПользователю(
			"В этом документе тот-же акт указан в строке "+НайденныеСтроки[0].НомерСтроки,,
			"Реестр["+XMLСтрока(НайденныеСтроки[0].НомерСтроки - 1)+"].Акт",
			"Объект",
			Отказ
		);
		
	КонецЦикла;
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПоручениеЭкспедитору.Ссылка КАК АктСсылка,
		|	ВЫРАЗИТЬ(ДополнительныеСведения.Значение КАК СТРОКА(50)) КАК ОтчётПоВыдачеУТ,
		|	ПРЕДСТАВЛЕНИЕ(ПоручениеЭкспедитору.Ссылка) КАК Акт
		|ИЗ
		|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоручениеЭкспедитору КАК ПоручениеЭкспедитору
		|		ПО ДополнительныеСведения.Объект = ПоручениеЭкспедитору.Ссылка
		|ГДЕ
		|	ВЫРАЗИТЬ(ДополнительныеСведения.Объект КАК Документ.ПоручениеЭкспедитору) В (&Акты)
		|	И ДополнительныеСведения.Свойство = &ДопСвойство
		|	И (ВЫРАЗИТЬ(ДополнительныеСведения.Значение КАК СТРОКА(50))) <> """"
		|";
	Запрос.УстановитьПараметр("ДопСвойство", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ОтчётПоВыдачеУТ"));
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НайденныеСтроки = Реестр.НайтиСтроки(Новый Структура("Акт", Выборка.АктСсылка));
		ОбщегоНазначения.СообщитьПользователю(
			Выборка.Акт + " уже был представлен в УТ в Отчёте по выдаче "+Выборка.ОтчётПоВыдачеУТ,,
			"Реестр["+XMLСтрока(НайденныеСтроки[0].НомерСтроки - 1)+"].Акт",
			"Объект",
			Отказ
		);
	КонецЦикла;	
	//-- КонецТикета 21892
КонецПроцедуры

Процедура ПроверкаНаПринадлежностьАктовКЗаказуКлиента(Отказ)
	//++ РС Консалт: Трофимов Евгений 15.11.2022 Тикет 21892
	//e1cib/data/Документ.Задание?ref=a2f9f48080c3996a4a0f6ce4f9476921
	Если НЕ РегистрыСведений.реа_ПредопределенныеЗначения.Значение("ВыдачаТСР_ПроверкаНаПринадлежностьАктовКЗаказуКлиента", Истина)
		ИЛИ НЕ ЗначениеЗаполнено(Контракт) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПоручениеЭкспедитору.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ПоручениеЭкспедитору КАК ПоручениеЭкспедитору
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПоручениеЭкспедитору.Основания КАК ПоручениеЭкспедиторуОснования
		|		ПО ПоручениеЭкспедитору.Ссылка = ПоручениеЭкспедиторуОснования.Ссылка
		|			И (ПоручениеЭкспедиторуОснования.Основание = &ЗаказКлиента)
		|ГДЕ
		|	ПоручениеЭкспедитору.Ссылка В(&Акты)
		|	И ПоручениеЭкспедиторуОснования.Ссылка ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("Акты", Реестр.Выгрузить().ВыгрузитьКолонку("Акт"));
	Запрос.УстановитьПараметр("ЗаказКлиента", Контракт);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НайденныеСтроки = Реестр.НайтиСтроки(Новый Структура("Акт", Выборка.Ссылка));
		ОбщегоНазначения.СообщитьПользователю(
			""+ Выборка.Ссылка + " не привязан к документу "+Контракт,,
			"Реестр["+XMLСтрока(НайденныеСтроки[0].НомерСтроки - 1)+"].Акт",
			"Объект",
			Отказ
		);
	КонецЦикла;
	//-- КонецТикета 21892	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17534
//e1cib/data/Документ.Задание?ref=a3eb9b03bd1c14b84aeaf2f6294e0848
Процедура ПроведениеПоРегиструСостоянияЗаказовПоВыдачеТСР(Отказ)

	Движения.РСК_СостояниеЗаказовКлиентовПоВыдаче.Очистить();
	Движения.РСК_СостояниеЗаказовКлиентовПоВыдаче.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РСК_ОтчетПоВыдачеТСРРеестр.Ссылка.Дата КАК Период,
		|	РСК_ОтчетПоВыдачеТСРРеестр.Ссылка.Контракт КАК Заказ,
		|	РСК_ОтчетПоВыдачеТСРРеестр.Цена КАК Цена,
		|	РСК_ОтчетПоВыдачеТСРРеестр.Количество КАК КВключениюВОтчет,
		|	РСК_ОтчетПоВыдачеТСРРеестр.Акт КАК Акт,
		|	РСК_ОтчетПоВыдачеТСРРеестр.ТСР КАК ТСР,
		|	РСК_ОтчетПоВыдачеТСРРеестр.Номенклатура КАК Номенклатура,
		|	РСК_ОтчетПоВыдачеТСРРеестр.ХарактеристикаНоменклатуры КАК Характеристика,
		|	ПоручениеЭкспедитору.РСК_Серия КАК Серия
		|ИЗ
		|	Документ.РСК_ОтчетПоВыдачеТСР.Реестр КАК РСК_ОтчетПоВыдачеТСРРеестр
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоручениеЭкспедитору КАК ПоручениеЭкспедитору
		|		ПО РСК_ОтчетПоВыдачеТСРРеестр.Акт = ПоручениеЭкспедитору.Ссылка
		|ГДЕ
		|	РСК_ОтчетПоВыдачеТСРРеестр.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Движение = Движения.РСК_СостояниеЗаказовКлиентовПоВыдаче.ДобавитьРасход();
		ЗаполнитьЗначенияСвойств(Движение, Выборка);
		
		оАкт = Выборка.Акт.ПолучитьОбъект();
		оАкт.РСК_СтатусАктаВыдачиТСР = Перечисления.РСК_СтатусыАктовВыдачи.ВОтчете;
		оАкт.Записать(РежимЗаписиДокумента.Проведение);
		
	КонецЦикла;
		
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17534
//e1cib/data/Документ.Задание?ref=a3eb9b03bd1c14b84aeaf2f6294e0848
Процедура УстановкаСтатусаВыданВУдалённыхПорученияхЭкспедитору(Отказ)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РСК_ОтчетПоВыдачеТСРРеестр.Акт КАК Акт
		|ИЗ
		|	Документ.РСК_ОтчетПоВыдачеТСР.Реестр КАК РСК_ОтчетПоВыдачеТСРРеестр
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоручениеЭкспедитору КАК ПоручениеЭкспедитору
		|		ПО РСК_ОтчетПоВыдачеТСРРеестр.Акт = ПоручениеЭкспедитору.Ссылка
		|ГДЕ
		|	РСК_ОтчетПоВыдачеТСРРеестр.Ссылка = &Ссылка
		|	И НЕ РСК_ОтчетПоВыдачеТСРРеестр.Акт В (&МассивНесохранённыхАктов)
		|	И ПоручениеЭкспедитору.РСК_СтатусАктаВыдачиТСР = Значение(Перечисление.РСК_СтатусыАктовВыдачи.ВОтчете)";
	
	Запрос.УстановитьПараметр("МассивНесохранённыхАктов", Реестр.Выгрузить().ВыгрузитьКолонку("Акт"));
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ДокументАкт = Выборка.Акт.ПолучитьОбъект();
		ДокументАкт.РСК_СтатусАктаВыдачиТСР = Перечисления.РСК_СтатусыАктовВыдачи.Выдан;
		Попытка
			ДокументАкт.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ОбщегоНазначения.СообщитьПользователю(
				"Не удалось записать документ "+Строка(Выборка.Акт)+ " со статусом «Выдан». " +ОписаниеОшибки(),
				Выборка.Акт,,,
				Отказ
			);
			Возврат;
		КонецПопытки;
		
	КонецЦикла;

КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17534
//e1cib/data/Документ.Задание?ref=a3eb9b03bd1c14b84aeaf2f6294e0848
Процедура ОбработкаУдаленияПроведения(Отказ)
	Для каждого ТекСтр из Реестр Цикл
		Об = ТекСтр.Акт.ПолучитьОбъект();
		Об.РСК_СтатусАктаВыдачиТСР = Перечисления.РСК_СтатусыАктовВыдачи.Выдан;
		Об.Записать(РежимЗаписиДокумента.Проведение);
	КонецЦикла;
КонецПроцедуры

#КонецЕсли