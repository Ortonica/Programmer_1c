
&После("ОбработкаЗаполнения")
Процедура РСК_ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	//++ РС Консалт: Трофимов Евгений 08.02.2023 Задача 23710
	//e1cib/data/Документ.Задание?ref=ba9df9b91fdc6f184025abfa4761a13d
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
		ЗаполнитьНаОснованииЗаказаКлиента(ДанныеЗаполнения);
	КонецЕсли;
	//-- КонецЗадачи 23710	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 08.02.2023 Задача 23710
//e1cib/data/Документ.Задание?ref=ba9df9b91fdc6f184025abfa4761a13d
Процедура ЗаполнитьНаОснованииЗаказаКлиента(ДанныеЗаполнения)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаказКлиента.Организация КАК Организация,
		|	ЗаказКлиента.Подразделение КАК Подразделение,
		|	ЗаказКлиента.НалогообложениеНДС КАК ВыпускПодДеятельность,
		|	ЗаказКлиента.Валюта КАК Валюта
		|ИЗ
		|	Документ.ЗаказКлиента КАК ЗаказКлиента
		|ГДЕ
		|	ЗаказКлиента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ЗаказКлиентаТовары.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|			ТОГДА ЗаказКлиентаТовары.Ссылка.Склад
		|		ИНАЧЕ ЗаказКлиентаТовары.Склад
		|	КОНЕЦ КАК Получатель,
		|	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
		|	ЗаказКлиентаТовары.Характеристика КАК Характеристика,
		|	ЗаказКлиентаТовары.Серия КАК Серия,
		|	ЗаказКлиентаТовары.Упаковка КАК Упаковка,
		|	ЗаказКлиентаТовары.Количество КАК Количество,
		|	ЗаказКлиентаТовары.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ВЫБОР
		|		КОГДА ЗаказКлиентаТовары.Обособленно
		|			ТОГДА ЗаказКлиентаТовары.Ссылка.Назначение
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК Назначение,
		|	ЗаказКлиентаТовары.КодСтроки КАК КодСтроки,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад) КАК НаправлениеВыпуска,
		|	3 КАК НомерГруппыЗатрат
		|ИЗ
		|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
		|ГДЕ
		|	ЗаказКлиентаТовары.Ссылка = &Ссылка
		|	И НЕ ЗаказКлиентаТовары.Отменено
		|";
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	Результаты = Запрос.ВыполнитьПакет();
	Шапка = Результаты[0].Выбрать();
	Шапка.Следующий();
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Шапка);
	ДокументОснование = ДанныеЗаполнения;

	ГруппировкаЗатрат = Перечисления.ГруппировкиЗатратВПроизводствеБезЗаказа.ПоПродукции;
	РаспоряжениеДляТрудозатрат = Истина;
	Автор = ПараметрыСеанса.ТекущийПользователь;
	
	ИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	
	ВыходныеИзделия.Загрузить(Результаты[1].Выгрузить());
	Для Каждого Стр Из ВыходныеИзделия Цикл
		ЗаполнитьСериюПоУмолчанию(Стр.Серия, Стр.Получатель, Стр.Номенклатура, Стр.Характеристика); 
		Если ИспользоватьСерииНоменклатуры И ЗначениеЗаполнено(Стр.Серия) Тогда
			НоваяСтрокаСерии = ВыходныеИзделияСерии.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаСерии, Стр);
		КонецЕсли;
	КонецЦикла;
	
	Если ИспользоватьСерииНоменклатуры Тогда
		ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ПроизводствоБезЗаказа);
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ЭтотОбъект, ПараметрыУказанияСерий.ВыходныеИзделия);
		НоменклатураСервер.ОчиститьНеиспользуемыеСерии(ЭтотОбъект, ПараметрыУказанияСерий.ВыходныеИзделия);
	КонецЕсли;	

КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 08.02.2023 Задача 23710
//e1cib/data/Документ.Задание?ref=ba9df9b91fdc6f184025abfa4761a13d
Процедура ЗаполнитьСериюПоУмолчанию(Серия, Склад, Номенклатура, Характеристика)
	
	Если ЗначениеЗаполнено(Серия) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ВариантПолучениеДанныхИзРегистров = "ВсеСерииНоменклатуры";
	
	ТекстЗапроса = Обработки.ПодборСерийВДокументы.ТекстЗапросаФормированияТаблицыДанныеРегистров(ВариантПолучениеДанныхИзРегистров, Склад);
	
	ТекстЗапроса = ТекстЗапроса + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистров.Серия КАК Серия,
	|	ДанныеРегистров.СвободныйОстаток КАК СвободныйОстаток
	|ИЗ
	|	ДанныеРегистров КАК ДанныеРегистров";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
		Серия = Выборка.Серия;
	КонецЕсли;

КонецПроцедуры

&Перед("ПередЗаписью")
Процедура РСК_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	//++ РС Консалт: Минаков А.С. Задача 25314
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьУказатьОсновнуюКомплектующую(Отказ, РежимЗаписи, РежимПроведения)
	//++ РС Консалт: Минаков А.С. Задача 25314
	
КонецПроцедуры 

&ИзменениеИКонтроль("ПроверитьИзменениеТаблицыМатериалыИРаботы")
Функция РСК_ПроверитьИзменениеТаблицыМатериалыИРаботы(МенеджерВременныхТаблиц)

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаТоваров.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	#Вставка
	//++ РС Консалт: Минаков А.С. Задача 25314
	|		ТаблицаТоваров.ОсновнаяКомплектующая	КАК ОсновнаяКомплектующая,
	//++ РС Консалт: Минаков А.С. Задача 25314
	#КонецВставки
	|		ТаблицаТоваров.СтатьяКалькуляции			КАК СтатьяКалькуляции,
	|		ТаблицаТоваров.НомерГруппыЗатрат			КАК НомерГруппыЗатрат,
	|		ТаблицаТоваров.Склад						КАК Склад,
	|		ТаблицаТоваров.Подразделение				КАК Подразделение,
	|		ТаблицаТоваров.Количество					КАК Количество
	|	ИЗ
	|		ТаблицаТоваров КАК ТаблицаТоваров
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	#Вставка
	//++ РС Консалт: Минаков А.С. Задача 25314
	|		ТаблицаВидыЗапасов.ОсновнаяКомплектующая	КАК ОсновнаяКомплектующая,
	//++ РС Консалт: Минаков А.С. Задача 25314
	#КонецВставки
	|		ТаблицаВидыЗапасов.СтатьяКалькуляции			КАК СтатьяКалькуляции,
	|		ТаблицаВидыЗапасов.НомерГруппыЗатрат			КАК НомерГруппыЗатрат,
	|		ТаблицаВидыЗапасов.СкладОтгрузки				КАК СкладОтгрузки,
	|		ТаблицаВидыЗапасов.Подразделение				КАК Подразделение,
	|		-ТаблицаВидыЗапасов.Количество					КАК Количество
	|	ИЗ
	|		ТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов) КАК ТаблицаТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры,
	#Вставка
	//++ РС Консалт: Минаков А.С. Задача 25314
	|	ТаблицаТоваров.ОсновнаяКомплектующая,
	//++ РС Консалт: Минаков А.С. Задача 25314
	#КонецВставки
	|	ТаблицаТоваров.СтатьяКалькуляции,
	|	ТаблицаТоваров.Подразделение,
	|	ТаблицаТоваров.НомерГруппыЗатрат,
	|	ТаблицаТоваров.Склад
	|
	|ИМЕЮЩИЕ
	|	НЕ СУММА(ТаблицаТоваров.Количество) = 0");

	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;

	РезультатЗапрос = Запрос.Выполнить();

	Возврат (Не РезультатЗапрос.Пустой());

КонецФункции

&ИзменениеИКонтроль("ВременныеТаблицыДанныхМатериалыИРаботы")
Функция РСК_ВременныеТаблицыДанныхМатериалыИРаботы()

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Дата			КАК Дата,
	|	&Организация	КАК Организация,
	|	НЕОПРЕДЕЛЕНО	КАК Партнер,
	|	НЕОПРЕДЕЛЕНО	КАК Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.СоглашенияСПоставщиками.ПустаяСсылка)	КАК Соглашение,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)		КАК Договор,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)					КАК Валюта,
	|	&ВыпускПодДеятельность КАК ВыпускПодДеятельность,
	|	&ВыпускПодДеятельность КАК НалогообложениеНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства)	КАК ХозяйственнаяОперация,
	|	ЛОЖЬ			КАК ЕстьСделкиВТабличнойЧасти,
	|	&Подразделение	КАК Подразделение,
	|	ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)	КАК Менеджер,
	|	ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка)	КАК Сделка,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыЗапасов.Товар)	КАК ТипЗапасов
	|ПОМЕСТИТЬ ТаблицаДанныхДокумента
	|ИЗ
	|	Справочник.Организации КАК Организации
	|
	|ГДЕ
	|	Организации.Ссылка = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки					КАК НомерСтроки,
	#Вставка
	//++ РС Консалт: Минаков А.С. Задача 25314
	|	ТаблицаТоваров.РСК_ОсновнаяКомплектующая	КАК ОсновнаяКомплектующая,
	//++ РС Консалт: Минаков А.С. Задача 25314
	#КонецВставки
	|	ТаблицаТоваров.Номенклатура					КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика				КАК Характеристика,
	|	ТаблицаТоваров.Назначение					КАК Назначение,
	|	ТаблицаТоваров.Серия						КАК Серия,
	|	ТаблицаТоваров.СтатусУказанияСерий			КАК СтатусУказанияСерий,
	|	ТаблицаТоваров.Склад						КАК Склад,
	|	ТаблицаТоваров.Подразделение				КАК Подразделение,
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТоваров.СтатьяКалькуляции			КАК СтатьяКалькуляции,
	|	ТаблицаТоваров.НомерГруппыЗатрат			КАК НомерГруппыЗатрат,
	|	ТаблицаТоваров.Количество					КАК Количество,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|				ИЛИ НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ) < &ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров
	|			ТОГДА 0
	|		ИНАЧЕ &ТекстПоляТаблицаТоваровКоличествоПоРНПТ_
	|	КОНЕЦ										КАК КоличествоПоРНПТ,
	|	&ТекстПоляТаблицаТоваровНомерГТД_			КАК НомерГТД
	|ПОМЕСТИТЬ ВтТаблицаТоваров
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|
	|ГДЕ
	|	ТаблицаТоваров.Количество > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки					КАК НомерСтроки,
	#Вставка
	//++ РС Консалт: Минаков А.С. Задача 25314
	|	ТаблицаТоваров.ОсновнаяКомплектующая		КАК ОсновнаяКомплектующая,
	//++ РС Консалт: Минаков А.С. Задача 25314
	#КонецВставки
	|	ТаблицаТоваров.Номенклатура					КАК Номенклатура,
	|	ТаблицаТоваров.Характеристика				КАК Характеристика,
	|	ТаблицаТоваров.Назначение					КАК Назначение,
	|	ТаблицаТоваров.Серия						КАК Серия,
	|	ТаблицаТоваров.СтатусУказанияСерий			КАК СтатусУказанияСерий,
	|	ТаблицаТоваров.Склад						КАК Склад,
	|	ТаблицаТоваров.Подразделение				КАК Подразделение,
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаТоваров.СтатьяКалькуляции			КАК СтатьяКалькуляции,
	|	ТаблицаТоваров.НомерГруппыЗатрат			КАК НомерГруппыЗатрат,
	|	ТаблицаТоваров.Количество					КАК Количество,
	|	ТаблицаТоваров.КоличествоПоРНПТ				КАК КоличествоПоРНПТ,
	|	ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)	КАК ГруппаПродукции,
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СписаниеРасходовНаПартииПроизводства) КАК ХозяйственнаяОперация,
	|	ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка)	КАК Сделка,
	|	ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)	КАК СтавкаНДС,
	|	0											КАК СуммаСНДС,
	|	0											КАК СуммаНДС,
	|	0											КАК СуммаВознаграждения,
	|	0											КАК СуммаНДСВознаграждения,
	|	ИСТИНА										КАК ПодбиратьВидыЗапасов,
	|	ТаблицаТоваров.НомерГТД						КАК НомерГТД
	|ПОМЕСТИТЬ ТаблицаТоваров
	|ИЗ
	|	ВтТаблицаТоваров КАК ТаблицаТоваров
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|	ПО ТаблицаТоваров.Номенклатура = СпрНоменклатура.Ссылка
	|ГДЕ
	|	НЕ СпрНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки					КАК НомерСтроки,
	#Вставка
	//++ РС Консалт: Минаков А.С. Задача 25314
	|	ТаблицаВидыЗапасов.РСК_ОсновнаяКомплектующая	КАК ОсновнаяКомплектующая,
	//++ РС Консалт: Минаков А.С. Задача 25314
	#КонецВставки
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.СтатьяКалькуляции			КАК СтатьяКалькуляции,
	|	ТаблицаВидыЗапасов.Подразделение				КАК Подразделение,
	|	ТаблицаВидыЗапасов.НомерГруппыЗатрат			КАК НомерГруппыЗатрат,
	|	ТаблицаВидыЗапасов.ВидЗапасов					КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.НомерГТД						КАК НомерГТД,
	|	ТаблицаВидыЗапасов.Количество					КАК Количество,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|				ИЛИ НАЧАЛОПЕРИОДА(&Дата, МЕСЯЦ) < &ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаВидыЗапасов.КоличествоПоРНПТ
	|	КОНЕЦ КАК КоличествоПоРНПТ,
	|	ЗНАЧЕНИЕ(Справочник.СделкиСКлиентами.ПустаяСсылка)	КАК Сделка,
	|	ЛОЖЬ											КАК ВидыЗапасовУказаныВручную
	|ПОМЕСТИТЬ ВтВидыЗапасов
	|ИЗ
	|	&ТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВидыЗапасов.НомерСтроки					КАК НомерСтроки,
	#Вставка
	//++ РС Консалт: Минаков А.С. Задача 25314
	|	ТаблицаВидыЗапасов.ОсновнаяКомплектующая	КАК ОсновнаяКомплектующая,
	//++ РС Консалт: Минаков А.С. Задача 25314
	#КонецВставки
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.СтатьяКалькуляции			КАК СтатьяКалькуляции,
	|	ТаблицаВидыЗапасов.Подразделение				КАК Подразделение,
	|	ТаблицаВидыЗапасов.НомерГруппыЗатрат			КАК НомерГруппыЗатрат,
	|	Аналитика.Номенклатура							КАК Номенклатура,
	|	Аналитика.Характеристика						КАК Характеристика,
	|	ТаблицаВидыЗапасов.ВидЗапасов					КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.НомерГТД						КАК НомерГТД,
	|	ТаблицаВидыЗапасов.Количество					КАК Количество,
	|	ТаблицаВидыЗапасов.КоличествоПоРНПТ				КАК КоличествоПоРНПТ,
	|	Аналитика.МестоХранения							КАК СкладОтгрузки,
	|	ТаблицаВидыЗапасов.Сделка						КАК Сделка,
	|	ТаблицаВидыЗапасов.ВидыЗапасовУказаныВручную	КАК ВидыЗапасовУказаныВручную
	|ПОМЕСТИТЬ ТаблицаВидыЗапасов
	|ИЗ
	|	ВтВидыЗапасов КАК ТаблицаВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры = Аналитика.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТВидыЗапасовИзделия.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	ВТВидыЗапасовИзделия.ВидЗапасов			КАК ВидЗапасов,
	|	ВТВидыЗапасовИзделия.НомерГТД			КАК НомерГТД,
	|	ВТВидыЗапасовИзделия.Количество			КАК Количество
	|ПОМЕСТИТЬ ВидыЗапасовВыходныеИзделия
	|ИЗ
	|	&ВидыЗапасовВыходныеИзделия КАК ВТВидыЗапасовИзделия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТВидыЗапасовИзделия.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	ВТВидыЗапасовИзделия.ВидЗапасов			КАК ВидЗапасов,
	|	ВТВидыЗапасовИзделия.НомерГТД			КАК НомерГТД,
	|	ВТВидыЗапасовИзделия.Количество			КАК Количество
	|ПОМЕСТИТЬ ВидыЗапасовПобочныеИзделия
	|ИЗ
	|	&ВидыЗапасовПобочныеИзделия КАК ВТВидыЗапасовИзделия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОНЕЦПЕРИОДА(&Дата, МЕСЯЦ)						КАК Период,
	|	&Организация									КАК Организация,
	|	ВТВидыЗапасовИзделия.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	ВТВидыЗапасовИзделия.ВидЗапасов					КАК ВидЗапасов,
	|	ВТВидыЗапасовИзделия.НомерГТД					КАК НомерГТД,
	|	СУММА(ВТВидыЗапасовИзделия.Количество)			КАК Количество,
	|	0												КАК КоличествоПоРНПТ
	|ПОМЕСТИТЬ ВТПриходуемыеВидыЗапасовИзделия
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТВидыЗапасовИзделия.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|		ВТВидыЗапасовИзделия.ВидЗапасов			КАК ВидЗапасов,
	|		ВТВидыЗапасовИзделия.НомерГТД			КАК НомерГТД,
	|		ВТВидыЗапасовИзделия.Количество			КАК Количество
	|	ИЗ
	|		ВидыЗапасовВыходныеИзделия КАК ВТВидыЗапасовИзделия
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТВидыЗапасовИзделия.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|		ВТВидыЗапасовИзделия.ВидЗапасов			КАК ВидЗапасов,
	|		ВТВидыЗапасовИзделия.НомерГТД			КАК НомерГТД,
	|		ВТВидыЗапасовИзделия.Количество			КАК Количество
	|	ИЗ
	|		ВидыЗапасовПобочныеИзделия КАК ВТВидыЗапасовИзделия
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВидыЗапасовМатериалыИРаботы.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|		ВидыЗапасовМатериалыИРаботы.ВидЗапасов					КАК ВидЗапасов,
	|		ВидыЗапасовМатериалыИРаботы.НомерГТД					КАК НомерГТД,
	|		ВидыЗапасовМатериалыИРаботы.Количество					КАК Количество
	|	ИЗ
	|		Документ.ПроизводствоБезЗаказа.ВидыЗапасовМатериалыИРаботы КАК ВидыЗапасовМатериалыИРаботы
	|	ГДЕ
	|		ВидыЗапасовМатериалыИРаботы.Ссылка = &СторнируемыйДокумент
	|	) КАК ВТВидыЗапасовИзделия
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТВидыЗапасовИзделия.АналитикаУчетаНоменклатуры,
	|	ВТВидыЗапасовИзделия.ВидЗапасов,
	|	ВТВидыЗапасовИзделия.НомерГТД
	|";

	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;

	Если ДополнительныеСвойства.Свойство("ТаблицыЗаполненияВидовЗапасовПриОбмене")
		И ДополнительныеСвойства.ТаблицыЗаполненияВидовЗапасовПриОбмене <> Неопределено Тогда

		ТаблицыДокумента = ДополнительныеСвойства.ТаблицыЗаполненияВидовЗапасовПриОбмене;

	Иначе
		ТаблицыДокумента = Документы.ПроизводствоБезЗаказа.КоллекцияТабличныхЧастейТоваров();

		ЗаполнитьЗначенияСвойств(ТаблицыДокумента, ЭтотОбъект);
	КонецЕсли;

	Запрос.УстановитьПараметр("Дата",					Дата);
	Запрос.УстановитьПараметр("Организация",			Организация);
	Запрос.УстановитьПараметр("Подразделение",			Подразделение);
	Запрос.УстановитьПараметр("ВыпускПодДеятельность",	ВыпускПодДеятельность);
	Запрос.УстановитьПараметр("ТаблицаТоваров",			ТаблицыДокумента.МатериалыИРаботы);
	Запрос.УстановитьПараметр("ТаблицаВидыЗапасов",		ВидыЗапасовМатериалыИРаботы);
	Запрос.УстановитьПараметр("СторнируемыйДокумент",	СторнируемыйДокумент);
	Запрос.УстановитьПараметр(
	"ИспользоватьРаздельныйУчетПоНалогообложению",
	НастройкиНалоговУчетныхПолитикПовтИсп.РаздельныйУчетТоваровПоНалогообложениюНДС(Организация, Дата));

	РеквизитыТЧ = "АналитикаУчетаНоменклатуры, ВидЗапасов, НомерГТД, Количество";
	Запрос.УстановитьПараметр("ВидыЗапасовВыходныеИзделия",		ВидыЗапасовВыходныеИзделия.Выгрузить(, РеквизитыТЧ));
	Запрос.УстановитьПараметр("ВидыЗапасовПобочныеИзделия",		ВидыЗапасовПобочныеИзделия.Выгрузить(, РеквизитыТЧ));

	УчетПрослеживаемыхТоваровЛокализация.УстановитьПараметрыИспользованияУчетаПрослеживаемыхТоваров(Запрос);

	ЗапасыСервер.ДополнитьВременныеТаблицыОбязательнымиКолонками(Запрос);

	ОбщегоНазначенияУТ.ЗаменитьОтсутствующиеПоляТаблицыЗначенийВТекстеЗапроса(
	ТаблицыДокумента.МатериалыИРаботы,
	Запрос.Текст,
	"&ТекстПоляТаблицаТоваровКоличествоПоРНПТ_",
	"ТаблицаТоваров",
	"КоличествоПоРНПТ",
	"ТаблицаТоваров.КоличествоПоРНПТ",
	"0");

	ОбщегоНазначенияУТ.ЗаменитьОтсутствующиеПоляТаблицыЗначенийВТекстеЗапроса(
	ТаблицыДокумента.МатериалыИРаботы,
	Запрос.Текст,
	"&ТекстПоляТаблицаТоваровНомерГТД_",
	"ТаблицаТоваров",
	"НомерГТД",
	"ТаблицаТоваров.НомерГТД",
	"ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)");

	Запрос.Выполнить();

	Возврат МенеджерВременныхТаблиц;

КонецФункции

&ИзменениеИКонтроль("ЗаполнитьДополнительныеПоляВидовЗапасовМатериалыИРаботы")
Процедура РСК_ЗаполнитьДополнительныеПоляВидовЗапасовМатериалыИРаботы(МенеджерВременныхТаблиц)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры			КАК АналитикаУчетаНоменклатуры,
	#Вставка
	//++ РС Консалт: Минаков А.С. Задача 25314
	|	ТаблицаТоваров.ОсновнаяКомплектующая				КАК ОсновнаяКомплектующая,
	//++ РС Консалт: Минаков А.С. Задача 25314
	#КонецВставки
	|	ТаблицаТоваров.СтатьяКалькуляции					КАК СтатьяКалькуляции,
	|	ТаблицаТоваров.Подразделение						КАК Подразделение,
	|	ТаблицаТоваров.НомерГруппыЗатрат					КАК НомерГруппыЗатрат,
	|	ТаблицаТоваров.Номенклатура.ГруппаФинансовогоУчета	КАК ГруппаФинансовогоУчета,
	|	СУММА(ТаблицаТоваров.Количество)					КАК Количество
	|ИЗ
	|	ТаблицаТоваров КАК ТаблицаТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТоваров.СтатьяКалькуляции,
	#Вставка
	//++ РС Консалт: Минаков А.С. Задача 25314
	|	ТаблицаТоваров.ОсновнаяКомплектующая,
	//++ РС Консалт: Минаков А.С. Задача 25314
	#КонецВставки
	|	ТаблицаТоваров.Подразделение,
	|	ТаблицаТоваров.НомерГруппыЗатрат,
	|	ТаблицаТоваров.АналитикаУчетаНоменклатуры,
	|	ТаблицаТоваров.Номенклатура.ГруппаФинансовогоУчета
	|
	|УПОРЯДОЧИТЬ ПО
	|	АналитикаУчетаНоменклатуры,
	|	СтатьяКалькуляции,
	|	Подразделение,
	|	НомерГруппыЗатрат,
	|	Количество УБЫВ";

	МассивВЗ = ВидыЗапасовМатериалыИРаботы.Выгрузить(, "ВидЗапасов").ВыгрузитьКолонку("ВидЗапасов");
	ТипыЗапасов = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивВЗ, "ТипЗапасов");

	ВыборкаТовары = Запрос.Выполнить().Выбрать();

	ОтборТоваров = Новый Структура("АналитикаУчетаНоменклатуры");

	Пока ВыборкаТовары.Следующий() Цикл

		КоличествоТоваров = ВыборкаТовары.Количество;

		ЗаполнитьЗначенияСвойств(ОтборТоваров, ВыборкаТовары);

		Для Каждого СтрокаЗапасов Из ВидыЗапасовМатериалыИРаботы.НайтиСтроки(ОтборТоваров) Цикл

			Если СтрокаЗапасов.Количество = 0 Тогда
				Продолжить;
			КонецЕсли;

			Количество = Мин(КоличествоТоваров, СтрокаЗапасов.Количество);

			НоваяСтрока = ВидыЗапасовМатериалыИРаботы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗапасов);

			Если ТипыЗапасов[СтрокаЗапасов.ВидЗапасов] = Перечисления.ТипыЗапасов.КомиссионныйТовар Тогда
				НоваяСтрока.ВидЗапасовПолучателя = Справочники.ВидыЗапасов.ВидЗапасовДокумента(Организация, , ВыборкаТовары);
			Иначе
				НоваяСтрока.ВидЗапасовПолучателя = НоваяСтрока.ВидЗапасов;
			КонецЕсли;
			
			#Вставка
			//++ РС Консалт: Минаков А.С. Задача 25314
			НоваяСтрока.РСК_ОсновнаяКомплектующая = ВыборкаТовары.ОсновнаяКомплектующая;
			//++ РС Консалт: Минаков А.С. Задача 25314
			#КонецВставки
			НоваяСтрока.СтатьяКалькуляции	= ВыборкаТовары.СтатьяКалькуляции;
			НоваяСтрока.Подразделение		= ВыборкаТовары.Подразделение;
			НоваяСтрока.НомерГруппыЗатрат	= ВыборкаТовары.НомерГруппыЗатрат;
			НоваяСтрока.Количество			= Количество;
			НоваяСтрока.КоличествоПоРНПТ	= Количество * СтрокаЗапасов.КоличествоПоРНПТ / СтрокаЗапасов.Количество;

			СтрокаЗапасов.Количество		= СтрокаЗапасов.Количество - НоваяСтрока.Количество;
			СтрокаЗапасов.КоличествоПоРНПТ	= СтрокаЗапасов.КоличествоПоРНПТ - НоваяСтрока.КоличествоПоРНПТ;

			КоличествоТоваров = КоличествоТоваров - НоваяСтрока.Количество;

			Если КоличествоТоваров = 0 Тогда
				Прервать;
			КонецЕсли;

		КонецЦикла;

	КонецЦикла;

	ПараметрыПоиска = Новый Структура("Количество", 0);
	МассивУдаляемыхСтрок = ВидыЗапасовМатериалыИРаботы.НайтиСтроки(ПараметрыПоиска);

	Для Каждого СтрокаТаблицы Из МассивУдаляемыхСтрок Цикл
		ВидыЗапасовМатериалыИРаботы.Удалить(СтрокаТаблицы);
	КонецЦикла;

КонецПроцедуры

&ИзменениеИКонтроль("ЗаполнитьВидыЗапасов")
Процедура РСК_ЗаполнитьВидыЗапасов(Отказ)

	УстановитьПривилегированныйРежим(Истина);
	
	#Вставка
	//++ РС Консалт: Минаков А.С. Задача 25314
	ДозаполнитьГТДВПродукции = Ложь;
	//++ РС Консалт: Минаков А.С. Задача 25314
	#КонецВставки
	ПерезаполнитьВидыЗапасов =
	ЗапасыСервер.ПроверитьНеобходимостьПерезаполненияВидовЗапасовДокумента(ЭтотОбъект)
	Или ПроверитьИзменениеРеквизитовДокумента();

	#Область ВидыЗапасовВыходныеИзделия

	МенеджерВременныхТаблиц = ВременныеТаблицыДанныхВыходныеИзделия();

	Если Не Проведен
		Или ПерезаполнитьВидыЗапасов
		Или ПроверитьИзменениеТаблицыВыходныеИзделия(МенеджерВременныхТаблиц) Тогда

		ТаблицаВидыЗапасовИзделий = ТаблицаДляЗаполненияВидыЗапасовВыходныеИзделия(МенеджерВременныхТаблиц);
		ЗапасыСервер.ЗаполнитьВидыЗапасовПоУмолчанию(МенеджерВременныхТаблиц, ТаблицаВидыЗапасовИзделий);
		ВидыЗапасовВыходныеИзделия.Загрузить(ТаблицаВидыЗапасовИзделий);
		#Вставка
		//++ РС Консалт: Минаков А.С. Задача 25314
		ДозаполнитьГТДВПродукции = Истина;
		//++ РС Консалт: Минаков А.С. Задача 25314
		#КонецВставки

	КонецЕсли;

	#КонецОбласти

	#Область ВидыЗапасовПобочныеИзделия

	МенеджерВременныхТаблиц = ВременныеТаблицыДанныхПобочныеИзделия();

	Если Не Проведен
		Или ПерезаполнитьВидыЗапасов
		Или ПроверитьИзменениеТаблицыПобочныеИзделия(МенеджерВременныхТаблиц) Тогда

		ТаблицаВидыЗапасовИзделий = ТаблицаДляЗаполненияВидыЗапасовПобочныеИзделия(МенеджерВременныхТаблиц);
		ЗапасыСервер.ЗаполнитьВидыЗапасовПоУмолчанию(МенеджерВременныхТаблиц, ТаблицаВидыЗапасовИзделий);
		ВидыЗапасовПобочныеИзделия.Загрузить(ТаблицаВидыЗапасовИзделий);

	КонецЕсли;

	#КонецОбласти

	#Область ВидыЗапасовМатериалы

	МенеджерВременныхТаблиц = ВременныеТаблицыДанныхМатериалыИРаботы();

	Если Не Проведен
		Или ПерезаполнитьВидыЗапасов
		Или ПроверитьИзменениеТаблицыМатериалыИРаботы(МенеджерВременныхТаблиц) Тогда

		ПараметрыЗаполнения = ПараметрыЗаполненияВидовЗапасов("МатериалыИРаботы");

		ЗапасыСервер.ЗаполнитьВидыЗапасовПоТоварамОрганизаций(ЭтотОбъект,
		МенеджерВременныхТаблиц,
		Отказ,
		ПараметрыЗаполнения);

		ВидыЗапасовМатериалыИРаботы.Свернуть("АналитикаУчетаНоменклатуры, ВидЗапасов, НомерГТД",
		"Количество, КоличествоПоРНПТ");

		Если Не Отказ Тогда
			ЗаполнитьДополнительныеПоляВидовЗапасовМатериалыИРаботы(МенеджерВременныхТаблиц);
		КонецЕсли;
		#Вставка
		//++ РС Консалт: Минаков А.С. Задача 25314
		ДозаполнитьГТДВПродукции = Истина;
		//++ РС Консалт: Минаков А.С. Задача 25314
		#КонецВставки

	КонецЕсли;
	#Вставка
	//++ РС Консалт: Минаков А.С. Задача 25314
	Если ДозаполнитьГТДВПродукции Тогда
		ЗаполнитьНомераГТДВПродукции()
	КонецЕсли;
	//++ РС Консалт: Минаков А.С. Задача 25314
	#КонецВставки

	#КонецОбласти

	Если Не Отказ Тогда
		СтрокаТЧ = "ВидыЗапасовВыходныеИзделия,ВидыЗапасовПобочныеИзделия,ВидыЗапасовМатериалыИРаботы";

		ОбщегоНазначенияУТ.ЗаполнитьИдентификаторыДокумента(ЭтотОбъект, СтрокаТЧ);
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьНомераГТДВПродукции()
	
	//++ РС Консалт: Минаков А.С. Задача 25314
	//механизм разработан под группировавку по продукции
	
	ВидыМатериалов = ВидыЗапасовМатериалыИРаботы.Выгрузить();
	
	Для Каждого Строка Из ВидыЗапасовВыходныеИзделия Цикл
		
		Если Не Строка.АналитикаУчетаНоменклатуры.Номенклатура.ВестиУчетПоГТД Тогда
			Продолжить
		КонецЕсли;
		
		ОсталосьРаспределить = Строка.Количество; 
		
		НайденныеСтроки = ВидыМатериалов.НайтиСтроки(Новый Структура("НомерГруппыЗатрат, РСК_ОсновнаяКомплектующая", Строка.НомерГруппыЗатрат, Истина));
		Если НайденныеСтроки.Количество() Тогда
			
			СтрокаМатериалов = НайденныеСтроки[0];	
			
			Распределить = Мин(СтрокаМатериалов.Количество, ОсталосьРаспределить);
			Если Распределить = ОсталосьРаспределить Тогда
				Строка.НомерГТД = СтрокаМатериалов.НомерГТД
			Иначе
				НоваяСтрока = ВидыЗапасовВыходныеИзделия.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
				
				НовоеКоличество = НоваяСтрока.Количество - Распределить;
				Кф = НовоеКоличество / НоваяСтрока.Количество;
				
				НоваяСтрока.Количество = НовоеКоличество;
				НоваяСтрока.ДоляСтоимости = НоваяСтрока.ДоляСтоимости * Кф;
				НоваяСтрока.Сумма = НоваяСтрока.Сумма * Кф; 
				
				Строка.Количество = Распределить;
				Строка.ДоляСтоимости = Строка.ДоляСтоимости - НоваяСтрока.ДоляСтоимости;
				Строка.Сумма = Строка.Сумма - НоваяСтрока.Сумма;
				Строка.НомерГТД = СтрокаМатериалов.НомерГТД
			КонецЕсли;
			
			СтрокаМатериалов.Количество = СтрокаМатериалов.Количество - Распределить;
			Если СтрокаМатериалов.Количество <= 0 Тогда
				ВидыМатериалов.Удалить(СтрокаМатериалов)
			КонецЕсли
			
		КонецЕсли
		
	КонецЦикла
	//++ РС Консалт: Минаков А.С. Задача 25314
	
КонецПроцедуры

Процедура ПроверитьУказатьОсновнуюКомплектующую(Отказ, РежимЗаписи, РежимПроведения)
	
	//++ РС Консалт: Минаков А.С. Задача 25314
	//механизм разработан под группировавку по продукции
	
	МассивОбработанныхГруппировок = Новый Массив;		
	
	Для Каждого Строка Из ВыходныеИзделия Цикл
		
		Если Не МассивОбработанныхГруппировок.Найти(Строка.НомерГруппыЗатрат) = Неопределено Тогда
			Продолжить
		КонецЕсли;
		
		МассивОбработанныхГруппировок.Добавить(Строка.НомерГруппыЗатрат);
		
		НужнаКомплектующая = Строка.Номенклатура.ВестиУчетПоГТД;
		КомплектующаяУказана = Ложь;
		КоличествоКомплектующихСГТД = 0;
		ПоследняяСтрокаСГТД = Неопределено;
		УказаныЗатраты = Ложь;
		
		НайденныеСтроки = МатериалыИРаботы.НайтиСтроки(Новый Структура("НомерГруппыЗатрат", Строка.НомерГруппыЗатрат));
		Для Каждого СтрокаМатериалов Из НайденныеСтроки Цикл
			
			УказаныЗатраты = Истина;
			
			Если Не НужнаКомплектующая Тогда
				СтрокаМатериалов.РСК_ОсновнаяКомплектующая = Ложь;
				Продолжить
			КонецЕсли;
			
			Если СтрокаМатериалов.Номенклатура.ВестиУчетПоГТД Тогда
				КоличествоКомплектующихСГТД = КоличествоКомплектующихСГТД + 1;
				ПоследняяСтрокаСГТД = СтрокаМатериалов
			Иначе
				СтрокаМатериалов.РСК_ОсновнаяКомплектующая = Ложь
			КонецЕсли;
			
			Если СтрокаМатериалов.РСК_ОсновнаяКомплектующая Тогда
				КомплектующаяУказана = Истина	
			КонецЕсли	
			
		КонецЦикла;
		
		Если НужнаКомплектующая  
			И Не КомплектующаяУказана
			И КоличествоКомплектующихСГТД = 1 Тогда
			
			КомплектующаяУказана = Истина;
			ПоследняяСтрокаСГТД.РСК_ОсновнаяКомплектующая = Истина
		КонецЕсли;
		
		Если НужнаКомплектующая 
			И Не КомплектующаяУказана Тогда
			
			ОбщегоНазначения.СообщитьПользователю(
			"Требуется указать основную комплектущую для продукции " + Строка.Номенклатура
			,
			, "ВыходныеИзделия[" + ВыходныеИзделия.Индекс(Строка) + "].Номенклатура"
			, "Объект"
			, Отказ);
			
			Если Не(РежимЗаписи = РежимЗаписиДокумента.Проведение И УказаныЗатраты) Тогда
				Отказ = Ложь
			КонецЕсли	
		КонецЕсли	
		
	КонецЦикла
	//++ РС Консалт: Минаков А.С. Задача 25314
	
КонецПроцедуры
