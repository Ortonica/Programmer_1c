
&После("ОбработкаЗаполнения")
Процедура РСК_ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	//++ РС Консалт: Трофимов Евгений 08.02.2023 Задача 23710
	//e1cib/data/Документ.Задание?ref=ba9df9b91fdc6f184025abfa4761a13d
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
		ЗаполнитьНаОснованииЗаказаКлиента(ДанныеЗаполнения);
	КонецЕсли;
	//-- КонецЗадачи 23710	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 08.02.2023 Задача 23710
//e1cib/data/Документ.Задание?ref=ba9df9b91fdc6f184025abfa4761a13d
Процедура ЗаполнитьНаОснованииЗаказаКлиента(ДанныеЗаполнения)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаказКлиента.Организация КАК Организация,
		|	ЗаказКлиента.Подразделение КАК Подразделение,
		|	ЗаказКлиента.НалогообложениеНДС КАК ВыпускПодДеятельность,
		|	ЗаказКлиента.Валюта КАК Валюта
		|ИЗ
		|	Документ.ЗаказКлиента КАК ЗаказКлиента
		|ГДЕ
		|	ЗаказКлиента.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ЗаказКлиентаТовары.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|			ТОГДА ЗаказКлиентаТовары.Ссылка.Склад
		|		ИНАЧЕ ЗаказКлиентаТовары.Склад
		|	КОНЕЦ КАК Получатель,
		|	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
		|	ЗаказКлиентаТовары.Характеристика КАК Характеристика,
		|	ЗаказКлиентаТовары.Серия КАК Серия,
		|	ЗаказКлиентаТовары.Упаковка КАК Упаковка,
		|	ЗаказКлиентаТовары.Количество КАК Количество,
		|	ЗаказКлиентаТовары.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ВЫБОР
		|		КОГДА ЗаказКлиентаТовары.Обособленно
		|			ТОГДА ЗаказКлиентаТовары.Ссылка.Назначение
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК Назначение,
		|	ЗаказКлиентаТовары.КодСтроки КАК КодСтроки,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад) КАК НаправлениеВыпуска,
		|	3 КАК НомерГруппыЗатрат
		|ИЗ
		|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
		|ГДЕ
		|	ЗаказКлиентаТовары.Ссылка = &Ссылка
		|	И НЕ ЗаказКлиентаТовары.Отменено
		|";
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	Результаты = Запрос.ВыполнитьПакет();
	Шапка = Результаты[0].Выбрать();
	Шапка.Следующий();
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Шапка);
	ДокументОснование = ДанныеЗаполнения;

	ГруппировкаЗатрат = Перечисления.ГруппировкиЗатратВПроизводствеБезЗаказа.ПоПродукции;
	РаспоряжениеДляТрудозатрат = Истина;
	Автор = ПараметрыСеанса.ТекущийПользователь;
	
	ИспользоватьСерииНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры");
	
	ВыходныеИзделия.Загрузить(Результаты[1].Выгрузить());
	Для Каждого Стр Из ВыходныеИзделия Цикл
		ЗаполнитьСериюПоУмолчанию(Стр.Серия, Стр.Получатель, Стр.Номенклатура, Стр.Характеристика); 
		Если ИспользоватьСерииНоменклатуры И ЗначениеЗаполнено(Стр.Серия) Тогда
			НоваяСтрокаСерии = ВыходныеИзделияСерии.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаСерии, Стр);
		КонецЕсли;
	КонецЦикла;
	
	Если ИспользоватьСерииНоменклатуры Тогда
		ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ЭтотОбъект, Документы.ПроизводствоБезЗаказа);
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ЭтотОбъект, ПараметрыУказанияСерий.ВыходныеИзделия);
		НоменклатураСервер.ОчиститьНеиспользуемыеСерии(ЭтотОбъект, ПараметрыУказанияСерий.ВыходныеИзделия);
	КонецЕсли;	

КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 08.02.2023 Задача 23710
//e1cib/data/Документ.Задание?ref=ba9df9b91fdc6f184025abfa4761a13d
Процедура ЗаполнитьСериюПоУмолчанию(Серия, Склад, Номенклатура, Характеристика)
	
	Если ЗначениеЗаполнено(Серия) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ВариантПолучениеДанныхИзРегистров = "ВсеСерииНоменклатуры";
	
	ТекстЗапроса = Обработки.ПодборСерийВДокументы.ТекстЗапросаФормированияТаблицыДанныеРегистров(ВариантПолучениеДанныхИзРегистров, Склад);
	
	ТекстЗапроса = ТекстЗапроса + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистров.Серия КАК Серия,
	|	ДанныеРегистров.СвободныйОстаток КАК СвободныйОстаток
	|ИЗ
	|	ДанныеРегистров КАК ДанныеРегистров";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 1 Тогда
		Выборка.Следующий();
		Серия = Выборка.Серия;
	КонецЕсли;

КонецПроцедуры

