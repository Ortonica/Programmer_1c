   
&НаСервере
Процедура ИзменитьФорму()
	
	//++ РС Консалт: Минаков А.С. Задача 20226
	Элементы.Товары.Подвал = Истина;
	Элементы.ТоварыКоличествоУпаковок.ПутьКДаннымПодвала = "Объект.Товары.ИтогКоличествоУпаковок";
	
	МассивДобавить = Новый Массив(2);
	МассивДобавить[0] = Новый РеквизитФормы("РСК_Вес", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(30,ДопустимаяДлина.Переменная)), );
	МассивДобавить[1] = Новый РеквизитФормы("РСК_Кубатура", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(30,ДопустимаяДлина.Переменная)));
	ИзменитьРеквизиты(МассивДобавить);
	
	НовыйЭлементГруппа = Элементы.Добавить("ГруппаИтоги", Тип("ГруппаФормы"), Элементы.Подвал);
	НовыйЭлементГруппа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлементГруппа.ОтображатьЗаголовок = Ложь;
	НовыйЭлементГруппа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	НовыйЭлементГруппа.ЦветФона = ЦветаСтиля.ИтогиФон;
	
	НовыйЭлементГруппа = Элементы.Добавить("ГруппаВес", Тип("ГруппаФормы"), Элементы.ГруппаИтоги);
	НовыйЭлементГруппа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлементГруппа.ОтображатьЗаголовок = Ложь;
	
	НовыйЭлемент = Элементы.Добавить("РСК_Вес", Тип("ПолеФормы"), НовыйЭлементГруппа);	
	НовыйЭлемент.Заголовок = "Вес всего/к отгрузке";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "РСК_Вес";
	НовыйЭлемент.ТолькоПросмотр = Истина;
	НовыйЭлемент.Ширина = 12;
	НовыйЭлемент.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	НовыйЭлемент.РастягиватьПоГоризонтали = Ложь;
	
	НовыйЭлементГруппа = Элементы.Вставить("ГруппаОбъем", Тип("ГруппаФормы"), Элементы.ГруппаИтоги, НовыйЭлементГруппа);
	НовыйЭлементГруппа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	НовыйЭлементГруппа.ОтображатьЗаголовок = Ложь;
	
	НовыйЭлемент = Элементы.Добавить("РСК_Кубатура", Тип("ПолеФормы"), НовыйЭлементГруппа);	
	НовыйЭлемент.Заголовок = "Объем всего/к отгрузке";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
	НовыйЭлемент.ПутьКДанным = "РСК_Кубатура";
	НовыйЭлемент.ТолькоПросмотр = Истина;
	НовыйЭлемент.Ширина = 12;
	НовыйЭлемент.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	НовыйЭлемент.РастягиватьПоГоризонтали = Ложь
	//++ РС Консалт: Минаков А.С. Задача 20226
	
КонецПроцедуры  

&НаСервере
&После("ОбновитьКолонкуДоступноСервер")
Процедура РСК_ОбновитьКолонкуДоступноСервер()
	
	//++ РС Консалт: Минаков А.С. Задача 20226
	РСК_РассчитатьВГХ()
	//++ РС Консалт: Минаков А.С. Задача 20226
	
КонецПроцедуры

&НаСервере
Процедура РСК_РассчитатьВГХ() 
	
	//++ РС Консалт: Минаков А.С. Задача 20226
	Запрос = Новый Запрос(	
	"ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить) КАК КОтгрузке,
	|	ТаблицаТовары.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ТаблицаТовары.Упаковка КАК Упаковка,
	|	ТаблицаТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК ПустаяУпаковка
	|ПОМЕСТИТЬ ВтТаблицаТовары
	|ИЗ
	|	&ТаблицаТовары КАК ТаблицаТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтТаблицаТовары.КОтгрузке КАК КОтгрузке,
	|	СУММА(ВтТаблицаТовары.КоличествоУпаковок * ЕСТЬNULL(ВЫБОР
	|				КОГДА ВтТаблицаТовары.ПустаяУпаковка
	|					ТОГДА ШтучныйВесОбъем.Вес
	|				ИНАЧЕ УпаковкиЕдиницыИзмерения.Вес
	|			КОНЕЦ, 0)) КАК Вес,
	|	СУММА(ВтТаблицаТовары.КоличествоУпаковок * ЕСТЬNULL(ВЫБОР
	|				КОГДА ВтТаблицаТовары.ПустаяУпаковка
	|					ТОГДА ШтучныйВесОбъем.Объем
	|				ИНАЧЕ УпаковкиЕдиницыИзмерения.Объем
	|			КОНЕЦ, 0)) КАК Объем
	|ИЗ
	|	ВтТаблицаТовары КАК ВтТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиЕдиницыИзмерения
	|		ПО ВтТаблицаТовары.Упаковка = УпаковкиЕдиницыИзмерения.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			Упаковки.Владелец КАК Номенклатура,
	|			МИНИМУМ(Упаковки.Вес) КАК Вес,
	|			МИНИМУМ(Упаковки.Объем) КАК Объем
	|		ИЗ
	|			Справочник.УпаковкиЕдиницыИзмерения КАК Упаковки
	|		ГДЕ
	|			Упаковки.Владелец В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						ВтТаблицаТовары.Номенклатура
	|					ИЗ
	|						ВтТаблицаТовары КАК ВтТаблицаТовары
	|					ГДЕ
	|						ВтТаблицаТовары.ПустаяУпаковка)
	|			И НЕ Упаковки.ПометкаУдаления
	|			И Упаковки.Числитель = 1
	|			И Упаковки.Знаменатель = 1
	|		
	|		СГРУППИРОВАТЬ ПО
	|			Упаковки.Владелец) КАК ШтучныйВесОбъем
	|		ПО ВтТаблицаТовары.Номенклатура = ШтучныйВесОбъем.Номенклатура
	|
	|СГРУППИРОВАТЬ ПО
	|	ВтТаблицаТовары.КОтгрузке
	|
	|УПОРЯДОЧИТЬ ПО
	|	КОтгрузке");
	Запрос.УстановитьПараметр("ТаблицаТовары", Объект.Товары.Выгрузить(, "Номенклатура,КоличествоУпаковок,Упаковка,ВариантОбеспечения"));
	
	ТаблицаВГХ = Запрос.Выполнить().Выгрузить();
	Если ТаблицаВГХ.Количество() = 1 Тогда
		ЭтаФорма.РСК_Вес = Строка("" + Окр(ТаблицаВГХ[0].Вес, 3) + "/" + ?(ТаблицаВГХ[0].КОтгрузке, Окр(ТаблицаВГХ[0].Вес, 3), "0"));
		ЭтаФорма.РСК_Кубатура = Строка("" + Окр(ТаблицаВГХ[0].Объем, 2) + "/" + ?(ТаблицаВГХ[0].КОтгрузке, Окр(ТаблицаВГХ[0].Объем, 2), "0"))
	ИначеЕсли ТаблицаВГХ.Количество() = 2 Тогда
		ЭтаФорма.РСК_Вес = Строка("" + Окр(ТаблицаВГХ[0].Вес, 3) + "/" + Окр(ТаблицаВГХ[1].Вес, 3));
		ЭтаФорма.РСК_Кубатура = Строка("" + Окр(ТаблицаВГХ[0].Объем, 2) + "/" + Окр(ТаблицаВГХ[1].Объем, 2))
	Иначе
		ЭтаФорма.РСК_Вес = "0/0";
		ЭтаФорма.РСК_Кубатура = "0/0"
	КонецЕсли
	//++ РС Консалт: Минаков А.С. Задача 20226
	
КонецПроцедуры

&НаСервере
&После("ПриЧтенииСозданииНаСервере")
Процедура РСК_ПриЧтенииСозданииНаСервере()
	
	//++ РС Консалт: Минаков А.С. Задача 20226
	ИзменитьФорму();
	РСК_РассчитатьВГХ()
	//++ РС Консалт: Минаков А.С. Задача 20226
	
КонецПроцедуры

&НаКлиенте
&После("ТоварыПриОкончанииРедактирования")
Процедура РСК_ТоварыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	//++ РС Консалт: Минаков А.С. Задача 20226
	РСК_РассчитатьВГХ()
	//++ РС Консалт: Минаков А.С. Задача 20226	
	
КонецПроцедуры





