
&НаКлиенте
Процедура РСК_ОбработкаОповещенияВместо(ИмяСобытия, Параметр, Источник)
	//++ РС Консалт: Трофимов Евгений 04.08.2022 Задача 19394
	//e1cib/data/Документ.Задание?ref=a08e5944efdf2c724d9f9d3c61ee1f3b
	//Алгоритм перенесён из УТ 
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" Тогда
			РСК_РаботаСоСканером.УстановитьВремяПоследнегоСобытия();
			МассивШК = ПодключаемоеОборудованиеУТКлиент.ПреобразоватьДанныеСоСканераВМассив(Параметр);

			Если МассивШК.Количество() = 1 и Лев(МассивШК[0].Штрихкод,2) = "20" Тогда // Это пользователь, добавляем его в ответственные
				ДобавитьОтветственногоПоШК(МассивШК[0].Штрихкод);
				СтатусПриИзмененииСервер();
				Элементы.Страницы.ТекущаяСтраница = Элементы.Страницы.ПодчиненныеЭлементы.ГруппаИсполнители;
			ИначеЕсли МассивШК.Количество() = 1 и Лев(МассивШК[0].Штрихкод,2) = "21" Тогда // Это операция
				ВыполнитьСкладскуюОперациюПоШтрихкоду(МассивШК[0].Штрихкод);
				СтатусПриИзмененииСервер();
			КонецЕсли;							
			
		КонецЕсли;
	КонецЕсли;
	//-- КонецЗадачи 19394	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 04.08.2022 Задача 19394
//e1cib/data/Документ.Задание?ref=a08e5944efdf2c724d9f9d3c61ee1f3b
&НаСервере
Процедура ДобавитьОтветственногоПоШК(ШК)
	УстановитьПривилегированныйРежим(Истина);
	НайденныйПользователь = РегистрыСведений.ИдентификационныеДанныеПользователей.ПользовательПоШтрихкоду(ШК);
	Если ЗначениеЗаполнено(НайденныйПользователь) Тогда
		ПараметрыОтбораПользователя = Новый Структура("Исполнитель",НайденныйПользователь);
		СтрокиСПользователями = Объект.Исполнители.НайтиСтроки(ПараметрыОтбораПользователя);
		Если СтрокиСПользователями.Количество() = 0 Тогда
			НовыйИсполнитель = Объект.Исполнители.Добавить();
			НовыйИсполнитель.Исполнитель = НайденныйПользователь;
			Модифицированность = Истина;
		КонецЕсли;
		//++ РС Консалт: Трофимов Евгений 21.11.2022 Задача 22047
		//e1cib/data/Документ.Задание?ref=a515ccfce1d3c13d434ec899e80ac585
		Если НЕ ЗначениеЗаполнено(Объект.Исполнитель) Тогда
			Объект.Исполнитель = НайденныйПользователь;
			Модифицированность = Истина;
		КонецЕсли;
		//-- КонецЗадачи 22047
	Иначе
		Сообщить("Пользователь со штрихкодом " + ШК + " не найден!");
	КонецЕсли;
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 04.08.2022 Задача 19394
//e1cib/data/Документ.Задание?ref=a08e5944efdf2c724d9f9d3c61ee1f3b
&НаКлиенте
Процедура ВыполнитьСкладскуюОперациюПоШтрихкоду(ШК)
	РСК_РаботаСоСканером.УстановитьВремяПоследнегоСобытия();
	НайденнаяОперация = РСК_РаботаСоСканером.ОперацияПоШтрихкоду(ШК);
	Если НЕ ЗначениеЗаполнено(НайденнаяОперация) Тогда
		Сообщить("Операция со штрихкодом " + ШК + " не найдена!");
		Возврат;
	КонецЕсли;
	Если ЭтоПереводСтатусаВнизКладовщиком(НайденнаяОперация) Тогда
		Сообщить("В рабочем месте кладовщика статус документа можно менять только на больший");
		Возврат;
	КонецЕсли;	
	Если НайденнаяОперация = РСК_РаботаСоСканером.ПредопределённоеЗначение("Завершено") Тогда
		Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыОтборовРазмещенийТоваров.ВыполненоБезОшибок");
		Модифицированность = Истина;
		СтатусПриИзмененииСервер();
		Записать(Новый Структура("РежимЗаписи,РежимПроведения",РежимЗаписиДокумента.Проведение,РежимПроведенияДокумента.Неоперативный));
		
		//Меняем статус в распоряжении
		Если ТипЗнч(Объект.Распоряжение) = Тип("ДокументСсылка.РасходныйОрдерНаТовары") Тогда
			УстановитьВРаспоряжениеСтатусВФоне();
		КонецЕсли;
		
		ПодключитьОбработчикОжидания("ЗакрытьФорму", 0.1, Истина);

	ИначеЕсли НайденнаяОперация = РСК_РаботаСоСканером.ПредопределённоеЗначение("ЗавершеноСОшибками") Тогда
		Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыОтборовРазмещенийТоваров.ВыполненоСОшибками");
		Модифицированность = Истина;
		СтатусПриИзмененииСервер();
	ИначеЕсли НайденнаяОперация = РСК_РаботаСоСканером.ПредопределённоеЗначение("ВРаботе") Тогда
		Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыОтборовРазмещенийТоваров.ВРаботе");
		Модифицированность = Истина;
		СтатусПриИзмененииСервер();		
		Записать(Новый Структура("РежимЗаписи,РежимПроведения",РежимЗаписиДокумента.Проведение,РежимПроведенияДокумента.Неоперативный));
		ПодключитьОбработчикОжидания("ЗакрытьФорму", 0.1, Истина);
	Иначе
		Сообщить("Просканированная операция не может быть выполнена в Отборе(Размещении) товаров!");
	КонецЕсли;	
КонецПроцедуры

// Установить в распоряжение статус в фоне
//++ РС Консалт: Трофимов Евгений 04.11.2022 Задача 21690
//e1cib/data/Документ.Задание?ref=a94cfb7ab14ed9464fc6486de1b61c04
//
// Параметры:
//  СтруктураПараметров		 - Структура - Ключи: Распоряжение,Статус 
//  УникальныйИдентификатор	 - УникальныйИдентификатор - Уникальный идентификатор формы
//
&НаСервере
Процедура УстановитьВРаспоряжениеСтатусВФоне()
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Распоряжение", Объект.Распоряжение);
	СтруктураПараметров.Вставить("Товары", Объект.ТоварыОтбор.Выгрузить(,"Номенклатура,Характеристика,Серия,Упаковка,Количество,КоличествоУпаковок"));

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение = 0; // запускать сразу
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Смена статуса в распоряжении'");
	ПараметрыВыполнения.ЗапуститьНеВФоне = Ложь;
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
		"РСК_РаботаСоСканером.УстановитьВРаспоряжениеСтатус",
		СтруктураПараметров, 
		ПараметрыВыполнения
	);
	Если РезультатВыполнения.Статус = "Ошибка" Тогда
		ВызватьИсключение РезультатВыполнения.ПодробноеПредставлениеОшибки;
	КонецЕсли;

КонецПроцедуры // УстановитьВРаспоряжениеСтатусВФоне()

//++ РС Консалт: Трофимов Евгений 04.08.2022 Задача 19394
//e1cib/data/Документ.Задание?ref=a08e5944efdf2c724d9f9d3c61ee1f3b
&НаСервере
Функция ЭтоПереводСтатусаВнизКладовщиком(Операция)	
	Если ПараметрыСеанса.РСК_АвторизовавшийсяКладовщик.Пустая() Тогда // это оператор
		Возврат Ложь;
	КонецЕсли;	
	Если (Объект.Статус = Перечисления.СтатусыОтборовРазмещенийТоваров.ВыполненоСОшибками 
		или Объект.Статус = Перечисления.СтатусыОтборовРазмещенийТоваров.ВыполненоБезОшибок)
		и Операция = Справочники.РСК_СкладскиеОперации.ВРаботе() Тогда 
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
КонецФункции

//++ РС Консалт: Трофимов Евгений 04.08.2022 Задача 19394
//e1cib/data/Документ.Задание?ref=a08e5944efdf2c724d9f9d3c61ee1f3b
&НаКлиенте
Процедура ЗакрытьФорму() Экспорт
	Закрыть();
КонецПроцедуры	

