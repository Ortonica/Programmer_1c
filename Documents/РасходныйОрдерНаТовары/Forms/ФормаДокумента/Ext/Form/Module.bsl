 
&НаСервере
&ИзменениеИКонтроль("ПриЧтенииНаСервере")
Процедура РСК_ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	// Обработчик механизма "ДатыЗапретаИзменения"
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	#Вставка
	//++ РС Консалт: Минаков А.С. Задача 20226
	Если ЗначениеЗаполнено(ТекущийОбъект.Ссылка) 
		И ТекущийОбъект.Статус = Перечисления.СтатусыРасходныхОрдеров.Отгружен
		И НачалоДня(ТекущийОбъект.ДатаОтгрузки) < НачалоДня(ТекущаяДатаСеанса())
		И Не УправлениеДоступом.ЕстьРоль("РСК_РедактированиеСкладскихОрдеровПрошлыхПериодов") Тогда		
		ТолькоПросмотр = Истина
	КонецЕсли;	
	//++ РС Консалт: Минаков А.С. Задача 20226
	#КонецВставки

	ПриЧтенииСозданииНаСервере();

	УпаковочныеЛистыСервер.ПриЧтенииСозданииФормыСУпаковочнымиЛистами(ЭтаФорма,
	Объект.ОтгружаемыеТовары,
	Объект.Ссылка.Метаданные().ПредставлениеОбъекта);

	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства

	МодификацияКонфигурацииПереопределяемый.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаКлиенте
Процедура РСК_ОбработкаОповещенияВместо(ИмяСобытия, Параметр, Источник)
	
	// Подсистема "Свойства"
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтаФорма, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" Тогда // И МенеджерОборудованияУТКлиент.ЕстьНеобработанноеСобытие() Тогда
			//ОбработатьШтрихкоды(МенеджерОборудованияУТКлиент.ПреобразоватьДанныеСоСканераВМассив(Параметр));
			//++ РС Консалт: Трофимов Евгений 05.08.2022 Задача 19394
			//e1cib/data/Документ.Задание?ref=a08e5944efdf2c724d9f9d3c61ee1f3b
			РСК_РаботаСоСканером.УстановитьВремяПоследнегоСобытия();
			МассивШК = ПодключаемоеОборудованиеУТКлиент.ПреобразоватьДанныеСоСканераВМассив(Параметр);
			Если МассивШК.Количество() = 1 и Лев(МассивШК[0].Штрихкод,2) = "20" Тогда // Это пользователь
				ДобавитьОтветственногоПоШК(МассивШК[0].Штрихкод);
				Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСтраницы.ПодчиненныеЭлементы.ГруппаОтветственные;
			ИначеЕсли МассивШК.Количество() = 1 и Лев(МассивШК[0].Штрихкод,2) = "21" Тогда // Это операция
				ВыполнитьСкладскуюОперациюПоШтрихкоду(МассивШК[0].Штрихкод)
			Иначе	
				//ОбработатьШтрихкоды(МассивШК); Каширин отключил, потому что УТ думает, что ей сканируют только товары и добавляет в тч ненужные строки
			КонецЕсли;				
			
			//-- КонецЗадачи 19394			
		КонецЕсли;
	КонецЕсли;
	// Конец ПодключаемоеОборудование

	// Неизвестные штрихкоды
	Если Источник = "ПодключаемоеОборудование"
		И ИмяСобытия = "НеизвестныеШтрихкоды"
		И Параметр.ФормаВладелец = УникальныйИдентификатор Тогда
		
		КэшированныеЗначения.Штрихкоды.Очистить();
		ДанныеШтрихкодов = Новый Массив;
		Для Каждого ПолученныйШтрихкод Из Параметр.ПолученыНовыеШтрихкоды Цикл
			ДанныеШтрихкодов.Добавить(ПолученныйШтрихкод);
		КонецЦикла;
		Для Каждого ПолученныйШтрихкод Из Параметр.ЗарегистрированныеШтрихкоды Цикл
			ДанныеШтрихкодов.Добавить(ПолученныйШтрихкод);
		КонецЦикла;
		
		ОбработатьШтрихкоды(ДанныеШтрихкодов);
		
	КонецЕсли;
	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 05.08.2022 Задача 19394
//e1cib/data/Документ.Задание?ref=a08e5944efdf2c724d9f9d3c61ee1f3b
&НаСервере
Процедура ДобавитьОтветственногоПоШК(ШК)
	УстановитьПривилегированныйРежим(Истина);
	НайденныйПользователь = РегистрыСведений.ИдентификационныеДанныеПользователей.ПользовательПоШтрихкоду(ШК);
	Если ЗначениеЗаполнено(НайденныйПользователь) Тогда
		Отбор = Новый Структура;
		Отбор.Вставить("Пользователь", НайденныйПользователь);
		Если Объект.Ответственные.НайтиСтроки(Отбор).Количество() = 0 Тогда 
			НовСтр = Объект.Ответственные.Добавить();	
			НовСтр.Пользователь = НайденныйПользователь;
		КонецЕсли;
	Иначе
		Сообщить("Пользователь со штрихкодом " + ШК + " не найден!");
	КонецЕсли;
КонецПроцедуры	

//++ РС Консалт: Трофимов Евгений 05.08.2022 Задача 19394
//e1cib/data/Документ.Задание?ref=a08e5944efdf2c724d9f9d3c61ee1f3b
&НаКлиенте
Процедура ВыполнитьСкладскуюОперациюПоШтрихкоду(ШК)
	НайденнаяОперация = РСК_РаботаСоСканером.ОперацияПоШтрихкоду(ШК);
	Если не ЗначениеЗаполнено(НайденнаяОперация) Тогда
		Сообщить("Операция со штрихкодом " + ШК + " не найдена!");
		Возврат;
	КонецЕсли;
	Если НайденнаяОперация = ПредопределенноеЗначение("Справочник.РСК_СкладскиеОперации.Завершено") Тогда
		ТекущийСтатус = Объект.Статус;
		Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыРасходныхОрдеров.Отгружен");
		ИзменитьСтатусНаАдресномСкладе();
		Если Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыРасходныхОрдеров.Отгружен") Тогда
			Записать();
			Если не Модифицированность Тогда
				ПодключитьОбработчикОжидания("ЗакрытьФорму", 0.1, Истина);
			КонецЕсли;	
		КонецЕсли;	
	Иначе
		Сообщить("Просканированная операция не может быть выполнена в Расходном ордере на товары");
	КонецЕсли;	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 05.08.2022 Задача 19394
//e1cib/data/Документ.Задание?ref=a08e5944efdf2c724d9f9d3c61ee1f3b
&НаКлиенте
Процедура ЗакрытьФорму()
	Закрыть();
КонецПроцедуры	


