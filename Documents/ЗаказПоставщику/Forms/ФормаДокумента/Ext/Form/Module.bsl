
//++ РС Консалт: Трофимов Евгений 11.07.2022 Задача 18725
//e1cib/data/Документ.Задание?ref=82a4670c1ac44f3949c1b038bf806fda
&НаСервере
Процедура ЗаполнитьПартииПоставки()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РСК_ПартияПоставкиТовары.Ссылка КАК ПартияПоставки,
		|	РСК_ПартияПоставки.Номер КАК НомерПартии,
		|	РСК_ПартияПоставки.Дата КАК ДатаПартии,
		|	РСК_ПартияПоставкиТовары.КодСтроки КАК КодСтроки,
		|	РСК_ПартияПоставкиТовары.НомерКонтейнера КАК НомерКонтейнера,
		|	ЕСТЬNULL(РСК_ПартияПоставкиКонтейнеры.СтатусКонтейнера, ЗНАЧЕНИЕ(Перечисление.РСК_СтатусыПартийПоставки.ПустаяСсылка)) КАК СтатусКонтейнера,
		|	РСК_ПартияПоставки.ДатаЕТА КАК ДатаЕТА
		|ИЗ
		|	Документ.РСК_ПартияПоставки.Товары КАК РСК_ПартияПоставкиТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РСК_ПартияПоставки КАК РСК_ПартияПоставки
		|		ПО РСК_ПартияПоставкиТовары.Ссылка = РСК_ПартияПоставки.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РСК_ПартияПоставки.Контейнеры КАК РСК_ПартияПоставкиКонтейнеры
		|		ПО РСК_ПартияПоставкиТовары.Ссылка = РСК_ПартияПоставкиКонтейнеры.Ссылка
		|			И РСК_ПартияПоставкиТовары.НомерКонтейнера = РСК_ПартияПоставкиКонтейнеры.НомерКонтейнера
		|ГДЕ
		|	РСК_ПартияПоставкиТовары.ЗаказПоставщику = &Ссылка
		|	И НЕ РСК_ПартияПоставки.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	СтруктураПустыхЗначений = Новый Структура(
		"ПартияПоставки,НомерПартии,ДатаПартии,НомерКонтейнера"
	);
	СтароеЗначениеПоступлениеОднойДатой = Объект.ПоступлениеОднойДатой;
	Для Каждого Стр Из Объект.Товары Цикл
		Выборка.Сбросить();
		
		Если Выборка.НайтиСледующий(Новый Структура("КодСтроки", Стр.КодСтроки)) Тогда
			ЗаполнитьЗначенияСвойств(Стр, Выборка);
			Если НЕ ПустаяСтрока(Выборка.НомерКонтейнера) Тогда
				Стр.Статус = Выборка.СтатусКонтейнера;
			КонецЕсли;
			//Если хотябы одна строчка привязана к партии то нужно снимать флаг ПоступлениеОднойДатой 
			//так как дату ETA для неё нужно оставить неизменной (заморозить при вводе длительности перевозки внизу формы)
			Объект.ПоступлениеОднойДатой = Ложь;
		Иначе
			ЗаполнитьЗначенияСвойств(Стр, СтруктураПустыхЗначений);
		КонецЕсли;
	КонецЦикла;
	
	РассчитатьДлительностьПеревозкиЕТА();
	Если СтароеЗначениеПоступлениеОднойДатой <> Объект.ПоступлениеОднойДатой Тогда
		ПоступлениеОднойДатойПриИзмененииСервер();
	КонецЕсли;

КонецПроцедуры // ЗаполнитьПартииПоставки()

&НаСервере
Процедура РСК_ПриЧтенииНаСервереПосле(ТекущийОбъект)
	//++ РС Консалт: Трофимов Евгений 11.07.2022 Задача 18725
	//e1cib/data/Документ.Задание?ref=82a4670c1ac44f3949c1b038bf806fda
	ЗаполнитьПартииПоставки();
	УстановитьВидимостьЭлементов();
	//-- КонецЗадачи 18725
КонецПроцедуры

&НаКлиенте
Процедура РСК_ПриОткрытииПосле(Отказ)
	//++ РС Консалт: Трофимов Евгений 11.07.2022 Задача 18725
	//e1cib/data/Документ.Задание?ref=82a4670c1ac44f3949c1b038bf806fda
	ЗаполнитьПартииПоставки();
	//-- КонецЗадачи 18725	
КонецПроцедуры

&НаСервере
Процедура РСК_ПослеЗаписиНаСервереПосле(ТекущийОбъект, ПараметрыЗаписи)
	//++ РС Консалт: Трофимов Евгений 11.07.2022 Задача 18725
	//e1cib/data/Документ.Задание?ref=82a4670c1ac44f3949c1b038bf806fda
	ЗаполнитьПартииПоставки();
	//-- КонецЗадачи 18725	
КонецПроцедуры

&НаКлиенте
Процедура РСК_ТоварыВыборПосле(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	//++ РС Консалт: Трофимов Евгений 11.07.2022 Задача 18725
	//e1cib/data/Документ.Задание?ref=82a4670c1ac44f3949c1b038bf806fda
	ОбработкаВыбораКолонокПартииПоставки(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка);
	//-- КонецЗадачи 18725
	
	//++ РС Консалт: Трофимов Евгений 09.01.2023 Тикет 22629
	//e1cib/data/Документ.Задание?ref=9e439c07dd47beac4ed73427e39f11fb
	ОбработкаВыбораКолонокДатОтгрузкиПоступления(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка);                  
	//-- КонецТикета 22629	
	
КонецПроцедуры

&НаКлиенте
//++ РС Консалт: Трофимов Евгений 11.07.2022 Задача 18725
//e1cib/data/Документ.Задание?ref=82a4670c1ac44f3949c1b038bf806fda
Процедура ОбработкаВыбораКолонокПартииПоставки(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	КолонкиПартии = Новый Массив;
	КолонкиПартии.Добавить("ТоварыНомерПартии");
	КолонкиПартии.Добавить("ТоварыДатаПартии");
	КолонкиПартии.Добавить("ТоварыПартияПоставки");
	
	Если КолонкиПартии.Найти(Поле.Имя) <> Неопределено Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеСтроки = Элемент.ДанныеСтроки(ВыбраннаяСтрока);
		Если ЗначениеЗаполнено(ДанныеСтроки.ПартияПоставки) Тогда
			ПараметрыОткрытия = Новый Структура;
			ПараметрыОткрытия.Вставить("Ключ", ДанныеСтроки.ПартияПоставки);
			ФормаПартии = ПолучитьФорму("Документ.РСК_ПартияПоставки.ФормаОбъекта", ПараметрыОткрытия);
			ФормаПартии.Элементы.Страницы.ТекущаяСтраница = ФормаПартии.Элементы.ГруппаТовары;
			ФормаПартии.Открыть();
			Строки = ФормаПартии.Объект.Товары.НайтиСтроки(
				Новый Структура("ЗаказПоставщику,КодСтроки", Объект.Ссылка, ДанныеСтроки.КодСтроки)
			);
			Если Строки.Количество() > 0 Тогда
				ФормаПартии.Элементы.Товары.ТекущаяСтрока = Строки[0].ПолучитьИдентификатор();
			КонецЕсли;
		Иначе
			//++ РС Консалт: Трофимов Евгений 24.10.2022 Задача 21304
			//e1cib/data/Документ.Задание?ref=95ba1fec1efd0de4442f386a0a00044a
			ДопПарам = Новый Структура();
			ДопПарам.Вставить("КодСтроки", ДанныеСтроки.КодСтроки);
			ДопПарам.Вставить("Поставщик", Объект.Партнер);
			ДопПарам.Вставить("ЗаказПоставщику", Объект.Ссылка);
			ДопПарам.Вставить("ИдентификаторСтроки", ДанныеСтроки.ПолучитьИдентификатор());
			Если Модифицированность Тогда
				ТекстВопроса = СтрШаблон(
					"В документе имеются несохранённые данные. %1 заказ поставщику?",
					?(Объект.Проведен, "Провести", "Записать")
				);
				ПоказатьВопрос(
					Новый ОписаниеОповещения("ПослеВопросаОЗаписиЗаказаПоставщику", ЭтаФорма, ДопПарам), 
					ТекстВопроса, 
					РежимДиалогаВопрос.ДаНет
				);
			Иначе
				ПредложитьСоздатьНовуюПартиюИлиПривязатьСтрокуКСуществующейПартии(ДопПарам);
			КонецЕсли;
			//-- КонецЗадачи 21304			
		КонецЕсли;
		
	КонецЕсли;	

КонецПроцедуры // ОбработкаВыбораКолонокПартииПоставки()

&НаКлиенте
//Не даёт изменять даты в позициях, привязанных к контейнеру.
//++ РС Консалт: Трофимов Евгений 09.01.2023 Тикет 22629
//e1cib/data/Документ.Задание?ref=9e439c07dd47beac4ed73427e39f11fb
Процедура ОбработкаВыбораКолонокДатОтгрузкиПоступления(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	ДанныеСтроки = Элемент.ДанныеСтроки(ВыбраннаяСтрока);
	
	Если НЕ ПустаяСтрока(ДанныеСтроки.НомерКонтейнера) Тогда
		Если Поле.Имя = "ТоварыДатаОтгрузки" Тогда
			СтандартнаяОбработка = Ложь;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Позиция уже привязана к контейнеру "+ДанныеСтроки.НомерКонтейнера
				+", поэтому дату отгрузки нужно указывать в документе «Партия поставки» в табличной части «Контейнеры» в колонке «Дата загрузки контейнера»"
			);
		ИначеЕсли Поле.Имя = "ТоварыДатаПоступления" Тогда
			СтандартнаяОбработка = Ложь;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Позиция уже привязана к контейнеру "+ДанныеСтроки.НомерКонтейнера
				+", поэтому дату поступления нужно указывать в документе «Партия поставки» в табличной части «Контейнеры» в колонке «Дата поступления на склад»"
			);
		КонецЕсли;
	КонецЕсли;
	
	//++ РС Консалт: Трофимов Евгений 15.03.2023 Задача 23985
	//e1cib/data/Документ.Задание?ref=b37a5a78714d889647e1e0192b7e7134
	Если ЗначениеЗаполнено(ДанныеСтроки.ПартияПоставки) Тогда
		Если Поле.Имя = "ТоварыДатаЕТА" Тогда
			СтандартнаяОбработка = Ложь;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				"Позиция уже привязана к партии поставки "+ДанныеСтроки.НомерПартии
				+", поэтому дату ЕТА нужно указывать в документе «Партия поставки»",
				ДанныеСтроки.ПартияПоставки,
				"ДатаЕТА"
			);
		КонецЕсли;
	КонецЕсли;
	//-- КонецЗадачи 23985	

КонецПроцедуры // ОбработкаВыбораКолонокДатОтгрузкиПоступления()


//++ РС Консалт: Трофимов Евгений 24.10.2022 Задача 21304
//e1cib/data/Документ.Задание?ref=95ba1fec1efd0de4442f386a0a00044a
&НаКлиенте
Процедура ПослеВопросаОЗаписиЗаказаПоставщику(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.Проведен Тогда
		ПараметрыЗаписи = Новый Структура(
			"РежимЗаписи,РежимПроведения",
			РежимЗаписиДокумента.Проведение,
			РежимПроведенияДокумента.Неоперативный
		);
	Иначе
		ПараметрыЗаписи = Новый Структура(
			"РежимЗаписи",
			РежимЗаписиДокумента.Запись
		);
	КонецЕсли;
	
	Записать(ПараметрыЗаписи);
	Модифицированность = Ложь;
	ПредложитьСоздатьНовуюПартиюИлиПривязатьСтрокуКСуществующейПартии(ДополнительныеПараметры);
	
КонецПроцедуры

// Предложить создать новую партию или привязать строку к существующей партии поставки.
//++ РС Консалт: Трофимов Евгений 24.10.2022 Задача 21304
//e1cib/data/Документ.Задание?ref=95ba1fec1efd0de4442f386a0a00044a
//
&НаКлиенте
Процедура ПредложитьСоздатьНовуюПартиюИлиПривязатьСтрокуКСуществующейПартии(ДополнительныеПараметры)
	
	ОткрытьФорму(
		"Документ.РСК_ПартияПоставки.Форма.ВыборПартииДляВключенияЗаказаВСостав",
		ДополнительныеПараметры,
		ЭтаФорма,,,, 
		Новый ОписаниеОповещения("ПослеВключенияВПартию", ЭтаФорма, ДополнительныеПараметры), 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
	);
	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 24.10.2022 Задача 21304
//e1cib/data/Документ.Задание?ref=95ba1fec1efd0de4442f386a0a00044a
&НаКлиенте
Процедура ПослеВключенияВПартию(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		ДанныеСтроки = Объект.Товары.НайтиПоИдентификатору(ДополнительныеПараметры.ИдентификаторСтроки);
		ДанныеСтроки.ПартияПоставки = Результат.ПартияПоставки;
		ДанныеСтроки.НомерПартии = Результат.НомерПартии;
		ДанныеСтроки.ДатаПартии = Результат.ДатаПартии;
	КонецЕсли;

КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 24.10.2022 Задача 21304
//e1cib/data/Документ.Задание?ref=95ba1fec1efd0de4442f386a0a00044a
&НаКлиенте
Процедура РСК_ОбработкаОповещенияПосле(ИмяСобытия, Параметр, Источник)
	//++ РС Консалт: Трофимов Евгений 24.10.2022 Задача 21304
	//e1cib/data/Документ.Задание?ref=95ba1fec1efd0de4442f386a0a00044a
	Если ИмяСобытия = "ПослеЗаписиПартииПоставки" Тогда
		ЗаполнитьПартииПоставки();
	КонецЕсли;
	//-- КонецЗадачи 21304			
КонецПроцедуры

#Область Проформы_ТрофимовЕВ_13_07_2022_ПартияПоставки18725

&НаКлиенте
Процедура РСК_РСК_ПроформыПриОкончанииРедактированияПосле(Элемент, НоваяСтрока, ОтменаРедактирования)
	УстановитьВидимостьЭлементов();
КонецПроцедуры

&НаКлиенте
Процедура РСК_РСК_ПроформыПослеУдаленияПосле(Элемент)
	УстановитьВидимостьЭлементов();
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементов()

	//Проформы
	ВыборПроформыВТабличнойЧасти = Объект.РСК_Проформы.Количество() > 1;
	Элементы.ТоварыНомерПроформы.Видимость = ВыборПроформыВТабличнойЧасти;
	Элементы.ТоварыДатаПроформы.Видимость = ВыборПроформыВТабличнойЧасти;
	Элементы.ТоварыЗаполнитьНомерИДатуПроформы.Видимость = ВыборПроформыВТабличнойЧасти;
	
	//Команды заполнения
	Элементы.ТоварыЗаполнитьДатуОтгрузки.Видимость = НЕ Объект.ПоступлениеОднойДатой;
	Элементы.ТоварыЗаполнитьДатуПоступления.Видимость = НЕ Объект.ПоступлениеОднойДатой И Объект.ДлительностьДоставки > 0;
	
	//++ РС Консалт: Трофимов Евгений 16.03.2023 Задача 23985
	//e1cib/data/Документ.Задание?ref=b37a5a78714d889647e1e0192b7e7134
	//Добавлено так как условное оформление почему-то не работает
	МассивВПути = Новый Массив;
	МассивВПути.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути);
	МассивВПути.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути);
	Элементы.ДлительностьПеревозкиETA.Видимость = МассивВПути.Найти(Объект.ХозяйственнаяОперация) <> Неопределено;
	//-- КонецЗадачи 23985	

КонецПроцедуры // УстановитьВидимостьПроформ()

&НаКлиенте
Процедура РСК_ЗаполнитьНомерИДатуПроформыПосле(Команда)
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АдресХраненияПроформ", ПолучитьАдресХраненияПроформ());
	
	ОткрытьФорму(
		"Документ.ЗаказПоставщику.Форма.ВыборПроформы", 
		ПараметрыФормы, 
		ЭтаФорма,,,, 
		Новый ОписаниеОповещения("ПослеВыбораПроформы", ЭтаФорма), 
	);
КонецПроцедуры

&НаКлиенте
Процедура РСК_ТоварыНомерПроформыНачалоВыбораПосле(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ДанныеСтроки = Элементы.Товары.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Номер", ДанныеСтроки.НомерПроформы);
	ПараметрыФормы.Вставить("Дата", ДанныеСтроки.ДатаПроформы);
	ПараметрыФормы.Вставить("АдресХраненияПроформ", ПолучитьАдресХраненияПроформ());
	
	ОткрытьФорму(
		"Документ.ЗаказПоставщику.Форма.ВыборПроформы", 
		ПараметрыФормы, 
		ЭтаФорма,,,, 
		Новый ОписаниеОповещения("ПослеВыбораПроформы", ЭтаФорма), 
	);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьАдресХраненияПроформ()

	Возврат ПоместитьВоВременноеХранилище(Объект.РСК_Проформы.Выгрузить(), УникальныйИдентификатор);	

КонецФункции // ПолучитьАдресХраненияПроформ()

//++ РС Консалт: Трофимов Евгений 12.07.2022 Задача 18725
//e1cib/data/Документ.Задание?ref=82a4670c1ac44f3949c1b038bf806fda
&НаКлиенте
Процедура ПослеВыбораПроформы(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Для Каждого ВыделеннаяСтрока Из Элементы.Товары.ВыделенныеСтроки Цикл
		ДанныеСтроки = Элементы.Товары.ДанныеСтроки(ВыделеннаяСтрока);
		ДанныеСтроки.НомерПроформы = Результат.Номер;
		ДанныеСтроки.ДатаПроформы = Результат.Дата;
		Модифицированность = Истина;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

//++ РС Консалт: Трофимов Евгений 29.10.2022 Задача 21486
//e1cib/data/Документ.Задание?ref=98069037b5c9860445d876207616cfcb
&НаКлиенте
Процедура РСК_ТоварыСтатусНачалоВыбораПосле(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ТД = Элементы.Товары.ТекущиеДанные;
	Если ТД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТД.НомерКонтейнера) Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьПредупреждение(,"Товарная позиция заказа уже привязана к контейнеру "+ТД.НомерКонтейнера+".
		|Для смены статуса используйте документ «Партия поставки» вкладку «Контейнеры».");
	КонецЕсли;
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 29.10.2022 Задача 21486
//e1cib/data/Документ.Задание?ref=98069037b5c9860445d876207616cfcb
&НаКлиенте
Процедура РСК_ЗаполнитьСтатусПосле(Команда)
	ТекущееЗначение = ПредопределенноеЗначение("Перечисление.РСК_СтатусыПартийПоставки.НаСогласовании");
	ТД = Элементы.Товары.ТекущиеДанные;
	Если ТД <> Неопределено И ЗначениеЗаполнено(ТД.Статус) Тогда
		ТекущееЗначение = ТД.Статус;
	КонецЕсли;
	ПоказатьВводЗначения(
		Новый ОписаниеОповещения("ПослеВыбораСтатуса", ЭтаФорма), 
		ТекущееЗначение,
		"Статус",
		Тип("ПеречислениеСсылка.РСК_СтатусыПартийПоставки")
	);
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 29.10.2022 Задача 21486
//e1cib/data/Документ.Задание?ref=98069037b5c9860445d876207616cfcb
&НаКлиенте
Процедура ПослеВыбораСтатуса(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Для Каждого ВыделеннаяСтрока Из Элементы.Товары.ВыделенныеСтроки Цикл
		ДанныеСтроки = Элементы.Товары.ДанныеСтроки(ВыделеннаяСтрока);
		Если ПустаяСтрока(ДанныеСтроки.НомерКонтейнера) Тогда
			ДанныеСтроки.Статус = Результат;
			Модифицированность = Истина;
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон(
					"Позиция [%1] пропущена так как уже прикреплена к контейнеру %2. Изменять статус нужно во вкладке «Контейнеры» документа «Партия поставки» в колонке «Статус контейнера».",
					ДанныеСтроки.НомерСтроки,
					ДанныеСтроки.НомерКонтейнера
				)
			);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 26.12.2022 Тикет 22629
//e1cib/data/Документ.Задание?ref=9e439c07dd47beac4ed73427e39f11fb
&НаКлиенте
Процедура РСК_ЗаполнитьДатуОтгрузкиПосле(Команда)
	ТекущееЗначение = '0001.01.01';
	ТД = Элементы.Товары.ТекущиеДанные;
	Если ТД <> Неопределено И ЗначениеЗаполнено(ТД.ДатаОтгрузки) Тогда
		ТекущееЗначение = ТД.ДатаОтгрузки;
	КонецЕсли;
	ПоказатьВводДаты(Новый ОписаниеОповещения("ПослеВводаДатыОтгрузки", ЭтаФорма), ТекущееЗначение,"Дата отгрузки",ЧастиДаты.Дата);
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 26.12.2022 Тикет 22629
//e1cib/data/Документ.Задание?ref=9e439c07dd47beac4ed73427e39f11fb
&НаКлиенте
Процедура ПослеВводаДатыОтгрузки(Дата, ДополнительныеПараметры) Экспорт
	
	Если Дата = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Для Каждого ВыделеннаяСтрока Из Элементы.Товары.ВыделенныеСтроки Цикл
		ДанныеСтроки = Элементы.Товары.ДанныеСтроки(ВыделеннаяСтрока);
		Если ПустаяСтрока(ДанныеСтроки.НомерКонтейнера) Тогда
			ДанныеСтроки.ДатаОтгрузки = Дата;
			Модифицированность = Истина;
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон(
					"Позиция [%1] пропущена так как уже прикреплена к контейнеру %2. Изменять дату отгрузки нужно во вкладке «Контейнеры» документа «Партия поставки» в колонке «Дата загрузки контейнера».",
					ДанныеСтроки.НомерСтроки,
					ДанныеСтроки.НомерКонтейнера
				)
			);
		КонецЕсли;
	КонецЦикла;
	РассчитатьДлительностьПеревозкиЕТА();
	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 27.12.2022 Тикет 22629
//e1cib/data/Документ.Задание?ref=9e439c07dd47beac4ed73427e39f11fb
&НаКлиенте
Процедура РСК_ЗаполнитьДатуПоступленияПосле(Команда)
	ТекущееЗначение = '0001.01.01';
	ТД = Элементы.Товары.ТекущиеДанные;
	Если ТД <> Неопределено И ЗначениеЗаполнено(ТД.ДатаПоступления) Тогда
		ТекущееЗначение = ТД.ДатаПоступления;
	КонецЕсли;
	ПоказатьВводДаты(Новый ОписаниеОповещения("ПослеВводаДатыПоступления", ЭтаФорма), ТекущееЗначение,"Дата поступления",ЧастиДаты.Дата);
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 27.12.2022 Тикет 22629
//e1cib/data/Документ.Задание?ref=9e439c07dd47beac4ed73427e39f11fb
&НаКлиенте
Процедура ПослеВводаДатыПоступления(Дата, ДополнительныеПараметры) Экспорт
	
	Если Дата = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Для Каждого ВыделеннаяСтрока Из Элементы.Товары.ВыделенныеСтроки Цикл
		ДанныеСтроки = Элементы.Товары.ДанныеСтроки(ВыделеннаяСтрока);
		Если ПустаяСтрока(ДанныеСтроки.НомерКонтейнера) Тогда
			ДанныеСтроки.ДатаПоступления = Дата;
			Модифицированность = Истина;
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				СтрШаблон(
					"Позиция [%1] пропущена так как уже прикреплена к контейнеру %2. Изменять дату поступления нужно во вкладке «Контейнеры» документа «Партия поставки» в колонке «Дата поступления на склад».",
					ДанныеСтроки.НомерСтроки,
					ДанныеСтроки.НомерКонтейнера
				)
			);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 14.03.2023 Задача 23985
//e1cib/data/Документ.Задание?ref=b37a5a78714d889647e1e0192b7e7134
&НаКлиенте
Процедура РСК_ДлительностьПеревозкиETAПриИзмененииПосле(Элемент)
	
	Для Каждого Стр Из Объект.Товары Цикл
		Если ЗначениеЗаполнено(Стр.ПартияПоставки) Тогда
			Продолжить;
		КонецЕсли;
		Если Объект.ПоступлениеОднойДатой Тогда
			Если ЗначениеЗаполнено(Объект.ДатаОтгрузки) Тогда
				Стр.ДатаЕТА = Объект.ДатаОтгрузки + ДлительностьПеревозкиETA * 86400;
				Модифицированность = Истина;
			КонецЕсли;
		Иначе
			Если ЗначениеЗаполнено(Стр.ДатаОтгрузки) Тогда
				Стр.ДатаЕТА = Стр.ДатаОтгрузки + ДлительностьПеревозкиETA * 86400;
				Модифицированность = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ТоварыДатаЕТАПриИзмененииСервер();
	
КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("ДлительностьДоставкиПриИзмененииСервер")
Процедура РСК_ДлительностьДоставкиПриИзмененииСервер()
	#Вставка
	//++ РС Консалт: Трофимов Евгений 16.03.2023 Задача 23985
	//e1cib/data/Документ.Задание?ref=b37a5a78714d889647e1e0192b7e7134
	МассивВПути = Новый Массив;
	МассивВПути.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути);
	МассивВПути.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути);
	ЭтоТоварыВПути = МассивВПути.Найти(Объект.ХозяйственнаяОперация) <> Неопределено;
	//-- КонецЗадачи 23985	
	#КонецВставки
	Если НЕ Объект.ПоступлениеОднойДатой Тогда
		Для Каждого Стр Из Объект.Товары Цикл
			#Удаление
			Если ЗначениеЗаполнено(Стр.ДатаОтгрузки) Тогда
				Стр.ДатаПоступления = Стр.ДатаОтгрузки + Объект.ДлительностьДоставки * 86400;
			КонецЕсли;
			#КонецУдаления
			#Вставка
			//++ РС Консалт: Трофимов Евгений 16.03.2023 Задача 23985
			//e1cib/data/Документ.Задание?ref=b37a5a78714d889647e1e0192b7e7134
			Если ЭтоТоварыВПути Тогда
				Если ПустаяСтрока(Стр.НомерКонтейнера) И ЗначениеЗаполнено(Стр.ДатаЕТА) Тогда
					Стр.ДатаПоступления = Стр.ДатаЕТА + Объект.ДлительностьДоставки * 86400;
				КонецЕсли;
			ИначеЕсли ЗначениеЗаполнено(Стр.ДатаОтгрузки) Тогда //Типовой расчёт
				Стр.ДатаПоступления = Стр.ДатаОтгрузки + Объект.ДлительностьДоставки * 86400;
			КонецЕсли;
			//-- КонецЗадачи 23985
			#КонецВставки
		КонецЦикла;
	Иначе
		Если ЗначениеЗаполнено(Объект.ДатаОтгрузки) Тогда
			Объект.ДатаПоступления = Объект.ДатаОтгрузки + Объект.ДлительностьДоставки * 86400;
			#Вставка
			//++ РС Консалт: Трофимов Евгений 16.03.2023 Задача 23985
			//e1cib/data/Документ.Задание?ref=b37a5a78714d889647e1e0192b7e7134
			Если ЭтоТоварыВПути Тогда
				Объект.ДатаПоступления = Объект.ДатаОтгрузки + (Объект.ДлительностьДоставки + ДлительностьПеревозкиETA) * 86400;
			КонецЕсли;
			//-- КонецЗадачи 23985			
			#КонецВставки
		КонецЕсли;
		ЗаполнитьДатыПоступленияСервер(Объект.ДатаПоступления);
	КонецЕсли;

	УстановитьВидимостьЭлементовФормыДатПоступления();
КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("ПоступлениеОднойДатойПриИзмененииСервер")
Процедура РСК_ПоступлениеОднойДатойПриИзмененииСервер()
	#Вставка
	//++ РС Консалт: Трофимов Евгений 27.12.2022 Тикет 22629
	//e1cib/data/Документ.Задание?ref=9e439c07dd47beac4ed73427e39f11fb
	//Не давать устанавливать флажок «Отгрузка одной датой», если даты стали разными после прикрепления позиций к контейнеру.
	Если Объект.ПоступлениеОднойДатой Тогда
		ЕстьПартии = Ложь;
		Для Каждого Стр Из Объект.Товары Цикл
			Если ЗначениеЗаполнено(Стр.ПартияПоставки) Тогда
				ЕстьПартии = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если ЕстьПартии Тогда
			Объект.ПоступлениеОднойДатой = Ложь;
			ОбщегоНазначения.СообщитьПользователю("Запрещено устанавливать отгрузку одной датой так как уже имеются позиции, привязанные к партии. 
			|При этом привязанные даты отгрузки/поступления (в случаях привязки к контейнеру) и даты ЕТА (в случае привязки к партии)
			|в строках табличной части «Товары» ""замораживаются"", а непривязанные - остаются свободными для редактирования.");
			Возврат;
		КонецЕсли;
	КонецЕсли;
	//-- КонецТикета 22629
	//++ РС Консалт: Трофимов Евгений 16.03.2023 Задача 23985
	//e1cib/data/Документ.Задание?ref=b37a5a78714d889647e1e0192b7e7134
	МассивВПути = Новый Массив;
	МассивВПути.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути);
	МассивВПути.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути);
	ЭтоТоварыВПути = МассивВПути.Найти(Объект.ХозяйственнаяОперация) <> Неопределено;
	//-- КонецЗадачи 23985	
	#КонецВставки

	Если Объект.ПоступлениеОднойДатой Тогда

		Объект.ДатаОтгрузки = МаксимальнаяДатаОтгрузки();
		Объект.ДатаПоступления = Объект.ДатаОтгрузки + 86400 * Объект.ДлительностьДоставки;
		#Вставка
		//++ РС Консалт: Трофимов Евгений 16.03.2023 Задача 23985
		//e1cib/data/Документ.Задание?ref=b37a5a78714d889647e1e0192b7e7134
		Если ЭтоТоварыВПути Тогда
			Объект.ДатаПоступления = Объект.ДатаОтгрузки + 86400 * (Объект.ДлительностьДоставки + ДлительностьПеревозкиETA);
		КонецЕсли;
		//-- КонецЗадачи 23985	
		#КонецВставки
		ЗаполнитьДатыПоступленияСервер(Объект.ДатаПоступления);
		ЗаполнитьДатыОтгрузкиСервер(Объект.ДатаОтгрузки);
		ВзаиморасчетыСервер.ФормаПриИзмененииРеквизитов(ЭтаФорма, "Товары.ДатаОтгрузки");

	КонецЕсли;

	УстановитьВидимостьЭлементовФормыДатПоступления();

КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 27.12.2022 Тикет 22629
//e1cib/data/Документ.Задание?ref=9e439c07dd47beac4ed73427e39f11fb
&НаКлиенте
Процедура РСК_ПоступлениеОднойДатойПриИзмененииПосле(Элемент)
	УстановитьВидимостьЭлементов();
КонецПроцедуры

&НаКлиенте
Процедура РСК_ДатаВходящегоДокументаПриИзмененииПосле(Элемент)
	//++ РС Консалт: Трофимов Евгений 07.03.2023 Задача 23985
	//e1cib/data/Документ.Задание?ref=b37a5a78714d889647e1e0192b7e7134
	//Синхронизация даты размещения в производство с этапами оплат
	Если (Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути") 
		ИЛИ Объект.ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути"))
		И ЕстьВариантОтсчетаВСоглашении(Объект.Соглашение, ПредопределенноеЗначение("Перечисление.ВариантыОтсчетаДатыПлатежа.ДоДатыРазмещения")) Тогда
		
		ДатаВходящегоДокументаПриИзмененииНаСервере();
		
	КонецЕсли;
	//-- КонецЗадачи 23985	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 07.03.2023 Задача 23985
//e1cib/data/Документ.Задание?ref=b37a5a78714d889647e1e0192b7e7134
&НаСервере
Процедура ДатаВходящегоДокументаПриИзмененииНаСервере()

	ВзаиморасчетыСервер.ФормаПриИзмененииРеквизитов(ЭтаФорма, "Дата");

КонецПроцедуры // ДатаВходящегоДокументаПриИзмененииНаСервере()


// Проверка на использование варианта отсчёта в соглашении
//++ РС Консалт: Трофимов Евгений 07.03.2023 Задача 23985
//e1cib/data/Документ.Задание?ref=b37a5a78714d889647e1e0192b7e7134
//
// Параметры:
//  Соглашение  	- СправочникСсылка.СоглашенияСПоставщиками - Соглашение с поставщиками, в котором ищем вариант отсчета
//  ВариантОтсчета  - ПеречислениеСсылка.ВариантыОтсчетаДатыПлатежа - Вариант отсчета
//
// Возвращаемое значение:
//   Булево   - 
//
&НаСервереБезКонтекста
Функция ЕстьВариантОтсчетаВСоглашении(Соглашение, ВариантОтсчета)

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СоглашенияСПоставщикамиЭтапыГрафикаОплаты.Сдвиг КАК Сдвиг
		|ИЗ
		|	Справочник.СоглашенияСПоставщиками.ЭтапыГрафикаОплаты КАК СоглашенияСПоставщикамиЭтапыГрафикаОплаты
		|ГДЕ
		|	СоглашенияСПоставщикамиЭтапыГрафикаОплаты.Ссылка = &Соглашение
		|	И СоглашенияСПоставщикамиЭтапыГрафикаОплаты.ВариантОтсчета = &ВариантОтсчета
		|";
	Запрос.Параметры.Вставить("ВариантОтсчета", ВариантОтсчета); // <Варианты отсчета даты платежа>[], Перечисления.ВариантыОтсчетаДатыПлатежа.
	Запрос.Параметры.Вставить("Соглашение", Соглашение); // <Соглашение об условиях закупок>[], Справочники.СоглашенияСПоставщиками.ПустаяСсылка();
	Результат = Запрос.Выполнить();
	Возврат НЕ Результат.Пустой();

КонецФункции // ЕстьВариантОтсчетаВСоглашении()

&НаСервере
&После("УстановитьУсловноеОформление")
Процедура РСК_УстановитьУсловноеОформление()

	//++ РС Консалт: Трофимов Евгений 14.03.2023 Задача 23985
	//e1cib/data/Документ.Задание?ref=b37a5a78714d889647e1e0192b7e7134
	//Колонка Дата ETA
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыДатаЕТА.Имя);
	//это почему-то не работает, поэтому добавил исчезновение кодом (см. УстановитьВидимостьЭлементов())
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДлительностьПеревозкиETA.Имя);  
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.Использование = Истина;
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ХозяйственнаяОперация");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке;
	СписокИсключений = Новый СписокЗначений;
	СписокИсключений.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути);
	СписокИсключений.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути);
	ОтборЭлемента.ПравоеЗначение = СписокИсключений;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//-- КонецЗадачи 23985	
	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 14.03.2023 Задача 23985
//e1cib/data/Документ.Задание?ref=b37a5a78714d889647e1e0192b7e7134
&НаКлиенте
Процедура РСК_ТоварыДатаЕТАПриИзмененииПосле(Элемент)
	ТоварыДатаЕТАПриИзмененииСервер();
КонецПроцедуры

&НаСервере
Процедура ТоварыДатаЕТАПриИзмененииСервер()

	ДлительностьДоставкиПриИзмененииСервер();
	ВзаиморасчетыСервер.ФормаПриИзмененииРеквизитов(ЭтаФорма, "Товары.ДатаОтгрузки");
	РассчитатьДлительностьПеревозкиЕТА();

КонецПроцедуры


//++ РС Консалт: Трофимов Евгений 14.03.2023 Задача 23985
//e1cib/data/Документ.Задание?ref=b37a5a78714d889647e1e0192b7e7134
&НаСервере
Процедура РассчитатьДлительностьПеревозкиЕТА()
	
	МассивВПути = Новый Массив;
	МассивВПути.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути);
	МассивВПути.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути);
	
	Если МассивВПути.Найти(Объект.ХозяйственнаяОперация) = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ДлительностьПеревозкиETA = 0;
	Объект.ДлительностьДоставки = 0;
	Для Каждого Стр Из Объект.Товары Цикл
		Если НЕ ЗначениеЗаполнено(Стр.ДатаЕТА) Тогда
			Продолжить;
		КонецЕсли;
		Если Объект.ПоступлениеОднойДатой Тогда
			Если ЗначениеЗаполнено(Объект.ДатаОтгрузки) Тогда
				ДлительностьПеревозкиETA = Макс(ДлительностьПеревозкиETA, (Стр.ДатаЕТА - Объект.ДатаОтгрузки) / 86400);
			КонецЕсли;
			Если ЗначениеЗаполнено(Объект.ДатаПоступления) Тогда
				Объект.ДлительностьДоставки = Макс(Объект.ДлительностьДоставки, (Объект.ДатаПоступления - Стр.ДатаЕТА) / 86400);
			КонецЕсли;
		Иначе
			Если ЗначениеЗаполнено(Стр.ДатаОтгрузки) Тогда
				ДлительностьПеревозкиETA = Макс(ДлительностьПеревозкиETA, (Стр.ДатаЕТА - Стр.ДатаОтгрузки) / 86400);
			КонецЕсли;
			Если ЗначениеЗаполнено(Стр.ДатаПоступления) Тогда
				Объект.ДлительностьДоставки = Макс(Объект.ДлительностьДоставки, (Стр.ДатаПоступления - Стр.ДатаЕТА) / 86400);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры



//++ РС Консалт: Трофимов Евгений 15.03.2023 Задача 23985
//e1cib/data/Документ.Задание?ref=b37a5a78714d889647e1e0192b7e7134
&НаСервере
&После("ЗаполнитьПустыеДатыПоступленияСервер")
Процедура РСК_ЗаполнитьПустыеДатыПоступленияСервер()
	Для Каждого СтрокаТЧ Из Объект.Товары Цикл
		Если Не ЗначениеЗаполнено(СтрокаТЧ.ДатаЕТА) Тогда
			СтрокаТЧ.ДатаЕТА = СтрокаТЧ.ДатаОтгрузки + ДлительностьПеревозкиETA * 86400;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 16.03.2023 Задача 23985
//e1cib/data/Документ.Задание?ref=b37a5a78714d889647e1e0192b7e7134
&НаКлиенте
Процедура РСК_ХозяйственнаяОперацияПриИзмененииПосле(Элемент)
	УстановитьВидимостьЭлементов();
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 16.03.2023 Задача 23985
//e1cib/data/Документ.Задание?ref=b37a5a78714d889647e1e0192b7e7134
&НаСервере
&После("УстановитьВидимостьЭлементовФормыДатПоступления")
Процедура РСК_УстановитьВидимостьЭлементовФормыДатПоступления()
	
	МассивВПути = Новый Массив;
	МассивВПути.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути);
	МассивВПути.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути);
	
	Если МассивВПути.Найти(Объект.ХозяйственнаяОперация) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ТоварыДатаПоступления.Видимость = НЕ Объект.ПоступлениеОднойДатой;
	Элементы.ДатаПоступления.Видимость = Объект.ПоступлениеОднойДатой;
	
КонецПроцедуры

&НаКлиенте
Процедура РСК_ТоварыДатаОтгрузкиПриИзмененииВместо(Элемент)
	//++ РС Консалт: Трофимов Евгений 16.03.2023 Задача 23985
	//e1cib/data/Документ.Задание?ref=b37a5a78714d889647e1e0192b7e7134
	МассивВПути = Новый Массив;
	МассивВПути.Добавить(ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути"));
	МассивВПути.Добавить(ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути"));
	ЭтоТоварыВПути = МассивВПути.Найти(Объект.ХозяйственнаяОперация) <> Неопределено;
	Если НЕ ЭтоТоварыВПути Тогда
		ПродолжитьВызов(Элемент);
		Возврат;
	КонецЕсли;
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ТекущиеДанные.ДатаЕТА = ТекущиеДанные.ДатаОтгрузки + 86400 * ДлительностьПеревозкиETA;
		ТекущиеДанные.ДатаПоступления = ТекущиеДанные.ДатаОтгрузки + 86400 * (Объект.ДлительностьДоставки + ДлительностьПеревозкиETA);
		ТоварыДатаОтгрузкиПриИзмененииСервер();
	КонецЕсли;
	//-- КонецЗадачи 23985	
КонецПроцедуры
