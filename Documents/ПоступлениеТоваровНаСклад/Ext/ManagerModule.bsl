
&Вместо("ДанныеДокументаДляПроведения")
Функция РСК_ДанныеДокументаДляПроведения(Документ, Регистры, ДопПараметры)
	
	ТаблицыДляДвижений = ПродолжитьВызов(Документ, Регистры, ДопПараметры);
	
	//++РС Консалт: Минаков А.С. Задача 17205
	Если ЗначениеЗаполнено(Документ)
		И ЗначениеЗаполнено(Документ.Распоряжение)
		И (ТипЗнч(Документ.Распоряжение) = Тип("ДокументСсылка.ЗаказПоставщику")
		Или ТипЗнч(Документ.Распоряжение) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг")) Тогда
		
		СкладНакладной = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "Распоряжение.Склад");
		
		Если ТаблицыДляДвижений.Свойство("ТаблицаЗапасыИПотребности") Тогда		
			Для Каждого СтрокаТч Из ТаблицыДляДвижений.ТаблицаЗапасыИПотребности Цикл
				Если СтрокаТч.ЗакрытьГрафикПоступления > 0 Тогда
					СтрокаТч.Склад = СкладНакладной	
				КонецЕсли
			КонецЦикла	
		КонецЕсли;
		
		Если ТаблицыДляДвижений.Свойство("ТаблицаРаспределениеЗапасовДвижения") Тогда		
			Для Каждого СтрокаТч Из ТаблицыДляДвижений.ТаблицаРаспределениеЗапасовДвижения Цикл
				Если СтрокаТч.ЗакрытьГрафикПоступления > 0 Тогда
					СтрокаТч.Склад = СкладНакладной	
				КонецЕсли
			КонецЦикла	
		КонецЕсли;
		
		Если ТаблицыДляДвижений.Свойство("ТаблицаТоварыКПоступлению") Тогда		
			Для Каждого СтрокаТч Из ТаблицыДляДвижений.ТаблицаТоварыКПоступлению Цикл
				
				Если СтрокаТч.ВидДвижения = ВидДвиженияНакопления.Приход Тогда
					Продолжить
				КонецЕсли;
				
				Если СтрокаТч.КОформлениюПоступленийПоРаспоряжению > 0
					Или СтрокаТч.КОформлениюПоступленийПоНакладным > 0 Тогда
					
					СтрокаТч.Склад = СкладНакладной
				ИначеЕсли СтрокаТч.КОформлениюПоступленийПоОрдерам > 0 Тогда
					НоваяСтрока = ТаблицыДляДвижений.ТаблицаТоварыКПоступлению.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока
					,СтрокаТч
					,
					, "ВидДвижения, Серия, КОформлениюНакладныхПоРаспоряжению, 
					|КОформлениюОрдеров, КОформлениюПоступленийПоНакладным, 
					|КОформлениюПоступленийПоОрдерам, КОформлениюПоступленийПоРаспоряжению");
					НоваяСтрока.ВидДвижения = ВидДвиженияНакопления.Приход;
					НоваяСтрока.КОформлениюОрдеров = СтрокаТч.КОформлениюПоступленийПоОрдерам
				КонецЕсли
			КонецЦикла	
		КонецЕсли
	КонецЕсли;
	//++РС Консалт: Минаков А.С. Задача 17205
	
	Возврат ТаблицыДляДвижений
	
КонецФункции 

&ИзменениеИКонтроль("ТекстЗапросаТаблицаТоварыОрганизаций")
Функция РСК_ТекстЗапросаТаблицаТоварыОрганизаций(Запрос, ТекстыЗапроса, Регистры)
	
	#Вставка
	//++РС Консалт: Минаков А.С. Задача 17205
	//Все вставки и удаления без комментария относятся к этой задаче
	#КонецВставки
	ИмяРегистра = "ТоварыОрганизаций";

	Если НЕ ПроведениеДокументов.ТребуетсяТаблицаДляДвижений(ИмяРегистра, Регистры) Тогда
		Возврат "";
	КонецЕсли; 

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)      КАК ВидДвижения,
	|	&Период                                     КАК Период,
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры    КАК АналитикаУчетаНоменклатуры,
	|	&Организация                                КАК Организация,
	|	ТаблицаТовары.ВидЗапасов                    КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка) КАК НомерГТД,
	|	ТаблицаТовары.Количество                    КАК Количество,
	|	0                                           КАК КоличествоПоРНПТ,
	|	&ХозяйственнаяОперация                      КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                КАК ОрганизацияОтгрузки,
	|	ИСТИНА                                      КАК Первичное
	|ИЗ
	|	Документ.ПоступлениеТоваровНаСклад.Товары КАК ТаблицаТовары
	|
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И (ТаблицаТовары.Ссылка.ХозяйственнаяОперация В
	|		(ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСПоступлениеИзТоваровВПути),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпортуПоступлениеИзТоваровВПути),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаПоступлениеИзТоваровВПути))
	|	)
	|	И НЕ (ТаблицаТовары.АналитикаУчетаНоменклатуры.Номенклатура.ВестиУчетПоГТД
	// Серии по себестоимости (14).
	|		И (ТаблицаТовары.СтатусУказанияСерий = 14
	|		И НЕ (ЕСТЬNULL(ТаблицаТовары.АналитикаУчетаНоменклатуры.Номенклатура.ВидНоменклатуры.ПолитикаУчетаСерий.УчетСерийВНеотфактурованныхПоставкахТоваров, ЛОЖЬ)
	#Удаление
	|			ИЛИ ЕСТЬNULL(ТаблицаТовары.АналитикаУчетаНоменклатуры.Номенклатура.ВидНоменклатуры.ПолитикаУчетаСерий.УчетТоваровВПутиОтПоставщикаПоСериям, ЛОЖЬ)))
	#КонецУдаления
	#Вставка
	|			ИЛИ ЕСТЬNULL(ТаблицаТовары.АналитикаУчетаНоменклатуры.Номенклатура.ВидНоменклатуры.ПолитикаУчетаСерий.УчетТоваровВПутиОтПоставщикаПоСериям, ЛОЖЬ))
	|		ИЛИ &РазличаютсяСклады)
	#КонецВставки
	|	)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)      КАК ВидДвижения,
	|	&Период                                     КАК Период,
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры    КАК АналитикаУчетаНоменклатуры,
	|	&Организация                                КАК Организация,
	|	ТаблицаТовары.ВидЗапасовНаСкладе            КАК ВидЗапасов,
	#Удаление
	|	ТаблицаТовары.НомерГТД                      КАК НомерГТД,
	#КонецУдаления
	#Вставка
	|	ТаблицаТовары.НомерГТДНакладной             КАК НомерГТД,
	#КонецВставки
	|	МАКСИМУМ(ТаблицаТовары.Количество)          КАК Количество,
	|	МАКСИМУМ(ТаблицаТовары.КоличествоПоРНПТ)    КАК КоличествоПоРНПТ,
	|	&ХозяйственнаяОперация                      КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                КАК ОрганизацияОтгрузки,
	|	ИСТИНА                                      КАК Первичное
	|ИЗ
	|	Документ.ПоступлениеТоваровНаСклад.ВидыЗапасов КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровНаСклад.Товары КАК ДанныеСтатусовСерий
	|		ПО ТаблицаТовары.Ссылка = ДанныеСтатусовСерий.Ссылка
	|			И ТаблицаТовары.АналитикаУчетаНоменклатуры.Номенклатура = ДанныеСтатусовСерий.Номенклатура
	|
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И (ТаблицаТовары.Ссылка.ХозяйственнаяОперация В
	|		(ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСПоступлениеИзТоваровВПути),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпортуПоступлениеИзТоваровВПути),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаПоступлениеИзТоваровВПути))
	|	)
	|	И ТаблицаТовары.АналитикаУчетаНоменклатуры.Номенклатура.ВестиУчетПоГТД
	// Серии по себестоимости (14).
	|	И (ДанныеСтатусовСерий.СтатусУказанияСерий = 14
	|		И НЕ (ЕСТЬNULL(ТаблицаТовары.АналитикаУчетаНоменклатуры.Номенклатура.ВидНоменклатуры.ПолитикаУчетаСерий.УчетСерийВНеотфактурованныхПоставкахТоваров, ЛОЖЬ)
	#Удаление
	|			ИЛИ ЕСТЬNULL(ТаблицаТовары.АналитикаУчетаНоменклатуры.Номенклатура.ВидНоменклатуры.ПолитикаУчетаСерий.УчетТоваровВПутиОтПоставщикаПоСериям, ЛОЖЬ)))
	#КонецУдаления
	#Вставка
	|			ИЛИ ЕСТЬNULL(ТаблицаТовары.АналитикаУчетаНоменклатуры.Номенклатура.ВидНоменклатуры.ПолитикаУчетаСерий.УчетТоваровВПутиОтПоставщикаПоСериям, ЛОЖЬ))
	|		ИЛИ &РазличаютсяСклады)
	#КонецВставки
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры,
	|	ТаблицаТовары.ВидЗапасовНаСкладе,
	#Удаление
	|	ТаблицаТовары.НомерГТД
	#КонецУдаления
	#Вставка
	|	ТаблицаТовары.НомерГТДНакладной
	#КонецВставки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// В пути, компенсация приобретения для ГТД с сериями по себестоимости.
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)      КАК ВидДвижения,
	|	&Период                                     КАК Период,
	|	Аналитики.КлючАналитики КАК АналитикаУчетаНоменклатуры,
	|	&Организация                                КАК Организация,
	|	ТаблицаТовары.ВидЗапасов                    КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка) КАК НомерГТД,
	|	ТаблицаТовары.Количество                    КАК Количество,
	|	0                                           КАК КоличествоПоРНПТ,
	|	&ХозяйственнаяОперация                      КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                КАК ОрганизацияОтгрузки,
	|	ИСТИНА                                      КАК Первичное
	|ИЗ
	|	Документ.ПоступлениеТоваровНаСклад.Товары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитики	
	|		ПО ТаблицаТовары.АналитикаУчетаНоменклатуры.Номенклатура = Аналитики.Номенклатура
	|			И ТаблицаТовары.АналитикаУчетаНоменклатуры.Характеристика = Аналитики.Характеристика
	#Удаление
	|			И ТаблицаТовары.АналитикаУчетаНоменклатуры.МестоХранения = Аналитики.МестоХранения
	#КонецУдаления
	#Вставка
	|			И ТаблицаТовары.АналитикаУчетаНоменклатурыНакладной.МестоХранения = Аналитики.МестоХранения
	#КонецВставки
	|			И ТаблицаТовары.АналитикаУчетаНоменклатуры.Назначение = Аналитики.Назначение
	|			И ТаблицаТовары.АналитикаУчетаНоменклатуры.СтатьяКалькуляции = Аналитики.СтатьяКалькуляции
	#Удаление
	|			И ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) = Аналитики.Серия
	#КонецУдаления
	#Вставка
	|			И ТаблицаТовары.АналитикаУчетаНоменклатурыНакладной.Серия = Аналитики.Серия
	#КонецВставки
	|
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И (ТаблицаТовары.Ссылка.ХозяйственнаяОперация В
	|		(ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСПоступлениеИзТоваровВПути),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпортуПоступлениеИзТоваровВПути),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаПоступлениеИзТоваровВПути))
	|	)
	|	И ТаблицаТовары.АналитикаУчетаНоменклатуры.Номенклатура.ВестиУчетПоГТД
	// Серии по себестоимости (14).
	|	И (ТаблицаТовары.СтатусУказанияСерий = 14
	|		И НЕ (ЕСТЬNULL(ТаблицаТовары.АналитикаУчетаНоменклатуры.Номенклатура.ВидНоменклатуры.ПолитикаУчетаСерий.УчетСерийВНеотфактурованныхПоставкахТоваров, ЛОЖЬ)
	#Удаление
	|			ИЛИ ЕСТЬNULL(ТаблицаТовары.АналитикаУчетаНоменклатуры.Номенклатура.ВидНоменклатуры.ПолитикаУчетаСерий.УчетТоваровВПутиОтПоставщикаПоСериям, ЛОЖЬ)))
	#КонецУдаления
	#Вставка
	|			ИЛИ ЕСТЬNULL(ТаблицаТовары.АналитикаУчетаНоменклатуры.Номенклатура.ВидНоменклатуры.ПолитикаУчетаСерий.УчетТоваровВПутиОтПоставщикаПоСериям, ЛОЖЬ))
	|		ИЛИ &РазличаютсяСклады)
	#КонецВставки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// В пути, сторно приобретения для ГТД (включая РНПТ) с сериями по себестоимости.
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)      КАК ВидДвижения,
	|	&Период                                     КАК Период,
	|	Аналитики.КлючАналитики КАК АналитикаУчетаНоменклатуры,
	|	&Организация                                КАК Организация,
	|	ТаблицаТовары.ВидЗапасовНаСкладе            КАК ВидЗапасов,
	#Удаление
	|	ТаблицаТовары.НомерГТД                      КАК НомерГТД,
	#КонецУдаления
	#Вставка
	|	ТаблицаТовары.НомерГТДНакладной             КАК НомерГТД,
	#КонецВставки
	|	МАКСИМУМ(ТаблицаТовары.Количество)          КАК Количество,
	|	МАКСИМУМ(ТаблицаТовары.КоличествоПоРНПТ)    КАК КоличествоПоРНПТ,
	|	&ХозяйственнаяОперация                      КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                КАК ОрганизацияОтгрузки,
	|	ИСТИНА                                      КАК Первичное
	|ИЗ
	|	Документ.ПоступлениеТоваровНаСклад.ВидыЗапасов КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитики
	|		ПО ТаблицаТовары.АналитикаУчетаНоменклатуры.Номенклатура = Аналитики.Номенклатура
	|			И ТаблицаТовары.АналитикаУчетаНоменклатуры.Характеристика = Аналитики.Характеристика
	#Удаление
	|			И ТаблицаТовары.АналитикаУчетаНоменклатуры.МестоХранения = Аналитики.МестоХранения
	#КонецУдаления
	#Вставка
	|			И ТаблицаТовары.АналитикаУчетаНоменклатурыНакладной.МестоХранения = Аналитики.МестоХранения
	#КонецВставки
	|			И ТаблицаТовары.АналитикаУчетаНоменклатуры.Назначение = Аналитики.Назначение
	|			И ТаблицаТовары.АналитикаУчетаНоменклатуры.СтатьяКалькуляции = Аналитики.СтатьяКалькуляции
	#Удаление
	|			И ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) = Аналитики.Серия
	#КонецУдаления
	#Вставка
	|			И ТаблицаТовары.АналитикаУчетаНоменклатурыНакладной.Серия = Аналитики.Серия
	#КонецВставки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровНаСклад.Товары КАК ДанныеСтатусовСерий
	|		ПО ТаблицаТовары.Ссылка = ДанныеСтатусовСерий.Ссылка
	|			И ТаблицаТовары.АналитикаУчетаНоменклатуры.Номенклатура = ДанныеСтатусовСерий.Номенклатура
	|
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И (ТаблицаТовары.Ссылка.ХозяйственнаяОперация В
	|		(ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСПоступлениеИзТоваровВПути),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпортуПоступлениеИзТоваровВПути),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаПоступлениеИзТоваровВПути))
	|	)
	|	И ТаблицаТовары.АналитикаУчетаНоменклатуры.Номенклатура.ВестиУчетПоГТД
	// Серии по себестоимости (14).
	|	И (ДанныеСтатусовСерий.СтатусУказанияСерий = 14
	|		И НЕ (ЕСТЬNULL(ТаблицаТовары.АналитикаУчетаНоменклатуры.Номенклатура.ВидНоменклатуры.ПолитикаУчетаСерий.УчетСерийВНеотфактурованныхПоставкахТоваров, ЛОЖЬ)
	#Удаление
	|			ИЛИ ЕСТЬNULL(ТаблицаТовары.АналитикаУчетаНоменклатуры.Номенклатура.ВидНоменклатуры.ПолитикаУчетаСерий.УчетТоваровВПутиОтПоставщикаПоСериям, ЛОЖЬ)))
	#КонецУдаления
	#Вставка
	|			ИЛИ ЕСТЬNULL(ТаблицаТовары.АналитикаУчетаНоменклатуры.Номенклатура.ВидНоменклатуры.ПолитикаУчетаСерий.УчетТоваровВПутиОтПоставщикаПоСериям, ЛОЖЬ))
	|		ИЛИ &РазличаютсяСклады)
	#КонецВставки
	|
	|СГРУППИРОВАТЬ ПО
	|	Аналитики.КлючАналитики,
	|	ТаблицаТовары.ВидЗапасовНаСкладе,
	#Удаление
	|	ТаблицаТовары.НомерГТД
	#КонецУдаления
	#Вставка
	|	ТаблицаТовары.НомерГТДНакладной
	#КонецВставки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Товары в пути.
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)      КАК ВидДвижения,
	|	&Период                                     КАК Период,
	|	ТаблицаТовары.АналитикаУчетаНоменклатурыТоварыУПартнеров КАК АналитикаУчетаНоменклатуры,
	|	&Организация                                КАК Организация,
	|	ТаблицаТовары.ВидЗапасов                    КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка) КАК НомерГТД,
	|	ТаблицаТовары.Количество                    КАК Количество,
	|	0                                           КАК КоличествоПоРНПТ,
	|	&ХозяйственнаяОперация                      КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                КАК ОрганизацияОтгрузки,
	|	ЛОЖЬ                                        КАК Первичное
	|ИЗ
	|	Документ.ПоступлениеТоваровНаСклад.ВидыЗапасов КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.Ссылка.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСПоступлениеИзТоваровВПути),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпортуПоступлениеИзТоваровВПути),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаПоступлениеИзТоваровВПути)
	|	)
	|
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	// Неотфактуровка.
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)      КАК ВидДвижения,
	|	&Период                                     КАК Период,
	|	ТаблицаТовары.АналитикаУчетаНоменклатурыТоварыУПартнеров КАК АналитикаУчетаНоменклатуры,
	|	&Организация                                КАК Организация,
	|	ТаблицаТовары.ВидЗапасов                    КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка) КАК НомерГТД,
	|	ТаблицаТовары.Количество                    КАК Количество,
	|	0                                           КАК КоличествоПоРНПТ,
	|	&ХозяйственнаяОперация                      КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                КАК ОрганизацияОтгрузки,
	|	ЛОЖЬ                                        КАК Первичное
	|ИЗ
	|	Документ.ПоступлениеТоваровНаСклад.ВидыЗапасов КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.Ссылка.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСНеотфактурованнаяПоставка),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаНеотфактурованнаяПоставка)
	|	)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)      КАК ВидДвижения,
	|	&Период                                     КАК Период,
	|	ТаблицаТовары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	&Организация                                КАК Организация,
	|	ТаблицаТовары.ВидЗапасов                    КАК ВидЗапасов,
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка) КАК НомерГТД,
	|	ТаблицаТовары.Количество                    КАК Количество,
	|	0                                           КАК КоличествоПоРНПТ,
	|	&ХозяйственнаяОперация                      КАК ХозяйственнаяОперация,
	|	НЕОПРЕДЕЛЕНО                                КАК ОрганизацияОтгрузки,
	|	ЛОЖЬ                                        КАК Первичное
	|ИЗ
	|	Документ.ПоступлениеТоваровНаСклад.Товары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &Ссылка
	|	И ТаблицаТовары.Ссылка.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСНеотфактурованнаяПоставка),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаНеотфактурованнаяПоставка)
	|	)
	|";

	ТекстыЗапроса.Добавить(ТекстЗапроса, ИмяРегистра);
	Возврат ТекстЗапроса;

КонецФункции

&После("ЗаполнитьПараметрыИнициализации")
Процедура РСК_ЗаполнитьПараметрыИнициализации(Запрос, ДокументСсылка)
	
	//++РС Консалт: Минаков А.С. Задача 17205
	Если ЗначениеЗаполнено(ДокументСсылка)
		И ЗначениеЗаполнено(ДокументСсылка.Распоряжение)
		И (ТипЗнч(ДокументСсылка.Распоряжение) = Тип("ДокументСсылка.ЗаказПоставщику")
		Или ТипЗнч(ДокументСсылка.Распоряжение) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг")) Тогда
		РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "Распоряжение.Склад, Склад");
		
		РазличаютсяСклады = Не РеквизитыДокумента.РаспоряжениеСклад = РеквизитыДокумента.Склад		
	Иначе
		РазличаютсяСклады = Ложь
	КонецЕсли;
    Запрос.УстановитьПараметр("РазличаютсяСклады", РазличаютсяСклады)
	//++РС Консалт: Минаков А.С. Задача 17205
	
КонецПроцедуры

