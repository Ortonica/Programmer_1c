
&НаСервере
&Вместо("УстановитьПараметрыВыбораРеквизитаСклад")
Процедура РСК_УстановитьПараметрыВыбораРеквизитаСклад(СкладыРаспоряжения)
	
	//++РС Консалт: Минаков А.С. Задача 17205
	Возврат
	//++РС Консалт: Минаков А.С. Задача 17205

КонецПроцедуры

&НаКлиенте
&ИзменениеИКонтроль("РаспоряжениеПредставлениеОбработкаНавигационнойСсылки")
Процедура РСК_РаспоряжениеПредставлениеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	Если НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьРаспоряжение" Тогда
		ПоказатьЗначение(, Объект.Распоряжение)
	Иначе

		// &ЗамерПроизводительности
		ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ПоступлениеТоваровНаСклад.Форма.ВыборРаспоряжения");

		ПараметрыФормыВыбораРаспоряжения = Новый Структура;
		ПараметрыФормыВыбораРаспоряжения.Вставить("Партнер",				Объект.Партнер);
		ПараметрыФормыВыбораРаспоряжения.Вставить("Контрагент",				Объект.Контрагент);
		ПараметрыФормыВыбораРаспоряжения.Вставить("Соглашение",				Объект.Соглашение);
		ПараметрыФормыВыбораРаспоряжения.Вставить("ХозяйственнаяОперация",	Объект.ХозяйственнаяОперация);
		ПараметрыФормыВыбораРаспоряжения.Вставить("Организация",			Объект.Организация);
		ПараметрыФормыВыбораРаспоряжения.Вставить("Договор",				Объект.Договор);
		#Удаление
		ПараметрыФормыВыбораРаспоряжения.Вставить("Склад",					Объект.Склад);
		#КонецУдаления

		ОписаниеОповещения = Новый ОписаниеОповещения("РаспоряжениеВыборКлиент", ЭтотОбъект);

		ОткрытьФорму("Документ.ПоступлениеТоваровНаСклад.Форма.ВыборРаспоряжения",
		ПараметрыФормыВыбораРаспоряжения,
		ЭтотОбъект,
		,
		,
		,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;

КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("ОчиститьРаспоряжение")
Процедура РСК_ОчиститьРаспоряжение()

	Если Не ЗначениеЗаполнено(Объект.Распоряжение) Тогда
		Возврат;
	КонецЕсли;

	ИменаРеквизитов = "Партнер, Контрагент, ХозяйственнаяОперация, Организация";

	Если ТипЗнч(Объект.Распоряжение) <> Тип("СправочникСсылка.СоглашенияСПоставщиками")
		И ТипЗнч(Объект.Распоряжение) <> Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда

		ИменаРеквизитов = ИменаРеквизитов + ", Соглашение, Договор";

	КонецЕсли;
	
	#Удаление
	Если ТипЗнч(Объект.Распоряжение) <> Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
		ИменаРеквизитов = ИменаРеквизитов + ", Склад";
	КонецЕсли;
	#КонецУдаления

	РеквизитыРаспоряжения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Распоряжение, ИменаРеквизитов);

	Если Объект.Партнер <> РеквизитыРаспоряжения.Партнер
		И ЗначениеЗаполнено(Объект.Партнер)

		Или Объект.Контрагент <> РеквизитыРаспоряжения.Контрагент
		И ЗначениеЗаполнено(РеквизитыРаспоряжения.Контрагент)
		И ЗначениеЗаполнено(Объект.Контрагент)

		Или Объект.Организация <> РеквизитыРаспоряжения.Организация
		И ЗначениеЗаполнено(РеквизитыРаспоряжения.Организация)
		И ЗначениеЗаполнено(Объект.Организация)

		Или РеквизитыРаспоряжения.Свойство("Склад")
		И Объект.Склад <> РеквизитыРаспоряжения.Склад
		И ЗначениеЗаполнено(РеквизитыРаспоряжения.Склад)
		И ЗначениеЗаполнено(Объект.Склад)
		И ЗначениеНастроекПовтИсп.ИерархияГрупп(Объект.Склад).Найти(РеквизитыРаспоряжения.Склад) = Неопределено Тогда

		Объект.Распоряжение = Неопределено;

	КонецЕсли;

	Если ЗначениеЗаполнено(Объект.Распоряжение) Тогда

		Если ТипЗнч(Объект.Распоряжение) = Тип("СправочникСсылка.СоглашенияСПоставщиками") Тогда

			ОсновнаяОперацияРаспоряжения = ЗакупкиСервер.ОсновнаяХозяйственнаяОперацияРаздельнойЗакупки(
			РеквизитыРаспоряжения.ХозяйственнаяОперация);

			Если (Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСНеотфактурованнаяПоставка
				Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСПоступлениеИзТоваровВПути)
				И ОсновнаяОперацияРаспоряжения <> Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС

				Или (Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаНеотфактурованнаяПоставка
				Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаПоступлениеИзТоваровВПути)
				И ОсновнаяОперацияРаспоряжения <> Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика

				Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуПоступлениеИзТоваровВПути
				И ОсновнаяОперацияРаспоряжения <> Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпорту Тогда

				Объект.Распоряжение = Неопределено;

			КонецЕсли;

			Если Объект.Соглашение <> Объект.Распоряжение Тогда
				Объект.Распоряжение = Неопределено;
			КонецЕсли;

		ИначеЕсли ТипЗнч(Объект.Распоряжение) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда

			ПарыОперацийРаздельнойЗакупки = ЗакупкиСервер.ПарыОперацийРаздельнойЗакупки();

			Если ПарыОперацийРаздельнойЗакупки.Получить(Объект.ХозяйственнаяОперация) <> РеквизитыРаспоряжения.ХозяйственнаяОперация Тогда
				Объект.Распоряжение = Неопределено;
			КонецЕсли;

			Если Объект.Договор <> Объект.Распоряжение Тогда
				Объект.Распоряжение = Неопределено;
			КонецЕсли;

		Иначе

			Если Объект.Договор <> РеквизитыРаспоряжения.Договор
				И ЗначениеЗаполнено(Объект.Договор)

				Или Объект.Соглашение <> РеквизитыРаспоряжения.Соглашение
				И ЗначениеЗаполнено(РеквизитыРаспоряжения.Соглашение) Тогда

				Объект.Распоряжение = Неопределено;

			КонецЕсли;

		КонецЕсли;

	КонецЕсли;

	ПоказатьПредставлениеРаспоряжения();

КонецПроцедуры

&НаСервере
&Вместо("ОчиститьПараметрыВыбораПоляСклад")
Процедура РСК_ОчиститьПараметрыВыбораПоляСклад()
	
	//++РС Консалт: Минаков А.С. Задача 17205
	Возврат
	//++РС Консалт: Минаков А.С. Задача 17205
	
КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("РаспоряжениеВыборСервер")
Процедура РСК_РаспоряжениеВыборСервер(ВыбранноеЗначение)

	ИсключаемыеСвойства = "";

	Если ТипЗнч(ВыбранноеЗначение.Распоряжение) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
		ИсключаемыеСвойства = "Склад, Валюта";
	КонецЕсли;

	ЗаполнитьЗначенияСвойств(Объект, ВыбранноеЗначение, , ИсключаемыеСвойства);

	СкладыРаспоряжения = СкладыРаспоряжения(Истина);
	
	#Удаление
	Если ВыбранноеЗначение.ЭтоГруппаСкладов		
		И ЗначениеЗаполнено(Объект.Склад)
		И СкладыРаспоряжения.Найти(Объект.Склад) = Неопределено Тогда
		
		Объект.Склад = Справочники.Склады.ПустаяСсылка();

	КонецЕсли;
	#КонецУдаления

	ХозяйственнаяОперацияДоИзменения = Объект.ХозяйственнаяОперация;

	ПриИзмененииХозяйственнойОперацииСервер(Ложь);
	ПриИзмененииДоговораСервер(Ложь);
	ПоказатьПредставлениеРаспоряжения();
	УстановитьПараметрыВыбораРеквизитаСклад(СкладыРаспоряжения);

	Если ТипЗнч(Объект.Распоряжение) <> Тип("СправочникСсылка.СоглашенияСПоставщиками") Тогда
		ЗакупкиСервер.УстановитьДоступностьДоговора(Объект,
		Элементы.Договор.Доступность,
		Элементы.Договор.Видимость,
		Объект.Договор);
	КонецЕсли;

	ПарыОперацийРаздельнойЗакупки = ЗакупкиСервер.ПарыОперацийРаздельнойЗакупки(Ложь);
	ХозяйственнаяОперацияПриемки = ПарыОперацийРаздельнойЗакупки.Получить(Объект.ХозяйственнаяОперация);

	ДанныеЗаполнения = Новый Структура();
	ДанныеЗаполнения.Вставить("ДокументОснование", Объект.Распоряжение);	
	ДанныеЗаполнения.Вставить("Склад", Объект.Склад);
	ДанныеЗаполнения.Вставить("ЗаполнятьПоНакладной", Истина);
	ДанныеЗаполнения.Вставить("ХозяйственнаяОперацияПриемки", ХозяйственнаяОперацияПриемки);

	Объект.Товары.Очистить();

	Значение = ДанныеФормыВЗначение(Объект, Тип("ДокументОбъект.ПоступлениеТоваровНаСклад"));
	Значение.Заполнить(ДанныеЗаполнения);
	ЗначениеВДанныеФормы(Значение, Объект);

	ВалютаДокумента = Объект.Валюта;
	Склад = Объект.Склад;

	СтруктураНаименованийТабличныхЧастей = Новый Структура("Товары");
	ЗаполнитьСлужебныеРеквизитыПоНоменклатуре(СтруктураНаименованийТабличныхЧастей);

	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(Объект, ПараметрыУказанияСерий);

	РассчитатьИтоговыеПоказатели(ЭтаФорма);

КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("СкладыРаспоряжения")
Функция РСК_СкладыРаспоряжения(ОчиститьСклад)

	СкладыРаспоряжения = Новый Массив;

	Если ТипЗнч(Объект.Распоряжение) = Тип("ДокументСсылка.ЗаказПоставщику")
		Или ТипЗнч(Объект.Распоряжение) = Тип("ДокументСсылка.ПриобретениеТоваровУслуг") Тогда

		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТоварыКПоступлению.Склад КАК Склад
		|ИЗ
		|	РегистрНакопления.ТоварыКПоступлению КАК ТоварыКПоступлению
		|ГДЕ
		|	ТоварыКПоступлению.ДокументПоступления = &Распоряжение";

		Запрос.УстановитьПараметр("Распоряжение", Объект.Распоряжение);

		УстановитьПривилегированныйРежим(Истина);
		Выборка = Запрос.Выполнить().Выбрать();
		УстановитьПривилегированныйРежим(Ложь);

		Пока Выборка.Следующий() Цикл
			СкладыРаспоряжения.Добавить(Выборка.Склад);
		КонецЦикла;

		#Удаление
		Если ОчиститьСклад
			И ЗначениеЗаполнено(Объект.Склад)
			И СкладыРаспоряжения.Найти(Объект.Склад) = Неопределено Тогда

			Объект.Склад = Неопределено;

		КонецЕсли;
		#КонецУдаления

	КонецЕсли;

	Возврат СкладыРаспоряжения;

КонецФункции
