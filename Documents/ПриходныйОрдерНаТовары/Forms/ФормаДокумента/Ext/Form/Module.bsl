 
 &НаСервере
&ИзменениеИКонтроль("ПриЧтенииНаСервере")
Процедура РСК_ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	// Обработчик механизма "ДатыЗапретаИзменения"
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	#Вставка
	//++ РС Консалт: Минаков А.С. Задача 20226
	Если ЗначениеЗаполнено(ТекущийОбъект.Ссылка)
		И ТекущийОбъект.Статус = Перечисления.СтатусыПриходныхОрдеров.Принят
		И НачалоДня(ТекущийОбъект.Дата) < НачалоДня(ТекущаяДатаСеанса())
		И Не УправлениеДоступом.ЕстьРоль("РСК_РедактированиеСкладскихОрдеровПрошлыхПериодов") Тогда		
		ТолькоПросмотр = Истина
	КонецЕсли;	
	//++ РС Консалт: Минаков А.С. Задача 20226
	#КонецВставки

	СобытияФорм.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);

	ПриходныйОрдерНаТоварыЛокализация.УстановитьПризнакИспользованияМаркируемойПродукции(ЭтаФорма);
	УстановитьВидимостьЭлементовУпаковкиПриМаркировке();

	ПриЧтенииСозданииНаСервере();

	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства

	МодификацияКонфигурацииПереопределяемый.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаКлиенте
Процедура РСК_ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	// Подсистема "Свойства"
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтаФорма, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" Тогда // И МенеджерОборудованияУТКлиент.ЕстьНеобработанноеСобытие()
			//ОбработатьШтрихкоды(МенеджерОборудованияУТКлиент.ПреобразоватьДанныеСоСканераВМассив(Параметр));
			//++ РС Консалт: Трофимов Евгений 05.08.2022 Задача 19394
			//e1cib/data/Документ.Задание?ref=a08e5944efdf2c724d9f9d3c61ee1f3b
			РСК_РаботаСоСканером.УстановитьВремяПоследнегоСобытия();
			МассивШК = ПодключаемоеОборудованиеУТКлиент.ПреобразоватьДанныеСоСканераВМассив(Параметр);
			Если МассивШК.Количество() = 1 и Лев(МассивШК[0].Штрихкод,2) = "20" Тогда // Это пользователь
				ДобавитьОтветственногоПоШК(МассивШК[0].Штрихкод);
				Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСтраницы.ПодчиненныеЭлементы.ГруппаОтветственные;
			ИначеЕсли МассивШК.Количество() = 1 и Лев(МассивШК[0].Штрихкод,2) = "21" Тогда // Это операция
				ВыполнитьСкладскуюОперациюПоШтрихкоду(МассивШК[0].Штрихкод)
			Иначе	
				//ОбработатьШтрихкоды(МассивШК); Каширин отключил, потому что УТ думает, что ей сканируют только товары и добавляет в тч ненужные строки
			КонецЕсли;	
			//-- КонецЗадачи 19394			
		КонецЕсли;
	КонецЕсли;
	// Конец ПодключаемоеОборудование
	
	// Изменение номенклатуры
	Если Врег(ИмяСобытия) = Врег("Запись_Номенклатура") Тогда
		Если Элементы.Товары.ТекущиеДанные <> Неопределено
			И Элементы.Товары.ТекущиеДанные.Номенклатура = Источник Тогда
			НоменклатураПриИзмененииСервер(КэшированныеЗначения); 
		КонецЕсли;		
	КонецЕсли;	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 05.08.2022 Задача 19394
//e1cib/data/Документ.Задание?ref=a08e5944efdf2c724d9f9d3c61ee1f3b
&НаСервере
Процедура ДобавитьОтветственногоПоШК(ШК)
	
	//++ РС Консалт: Трофимов Евгений 10.01.2023 Задача 22873
	//e1cib/data/Документ.Задание?ref=a228da5aa43829244389d2f8624bf3c6
	//Если Объект.Статус <> Перечисления.СтатусыПриходныхОрдеров.КПоступлению Тогда
	//	Сообщить("Добавление отвественных за приёмку возможно только в статусе К поступлению!");
	//	Возврат;
	//КонецЕсли;	
	Если Объект.Статус <> Перечисления.СтатусыПриходныхОрдеров.ТребуетсяОбработка Тогда
		Сообщить("Добавление отвественных за приёмку возможно только в статусе «Требуется обработка»!");
		Возврат;
	КонецЕсли;	
	//-- КонецЗадачи 22873	
	УстановитьПривилегированныйРежим(Истина);
	НайденныйПользователь = РегистрыСведений.ИдентификационныеДанныеПользователей.ПользовательПоШтрихкоду(ШК);
	Если ЗначениеЗаполнено(НайденныйПользователь) Тогда
		Отбор = Новый Структура;
		Отбор.Вставить("Пользователь", НайденныйПользователь);
		Если Объект.Ответственные.НайтиСтроки(Отбор).Количество() = 0 Тогда 
			НовСтр = Объект.Ответственные.Добавить();	
			НовСтр.Пользователь = НайденныйПользователь;
		КонецЕсли;
	Иначе
		Сообщить("Пользователь со штрихкодом " + ШК + " не найден!");
	КонецЕсли;
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 05.08.2022 Задача 19394
//e1cib/data/Документ.Задание?ref=a08e5944efdf2c724d9f9d3c61ee1f3b
&НаКлиенте
Процедура ВыполнитьСкладскуюОперациюПоШтрихкоду(ШК)
	Если Объект.Статус <> ПредопределенноеЗначение("Перечисление.СтатусыПриходныхОрдеров.ТребуетсяОбработка") и не ЭтоАРМКладовщика() Тогда
		Сообщить("Документ должен быть в статусе «Требуется обработка»");
		Возврат;
	КонецЕсли;
	НайденнаяОперация = РСК_РаботаСоСканером.ОперацияПоШтрихкоду(ШК);
	Если не ЗначениеЗаполнено(НайденнаяОперация) Тогда
		Сообщить("Операция со штрихкодом " + ШК + " не найдена!");
		Возврат;
	КонецЕсли;
	Если НайденнаяОперация = РСК_РаботаСоСканером.ПредопределённоеЗначение("Завершено") Тогда
		Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыПриходныхОрдеров.Принят");
		СтатусПриИзменении("");
		Записать();
		Если не Модифицированность и Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыПриходныхОрдеров.Принят") Тогда
			ПодключитьОбработчикОжидания("ЗакрытьФорму", 0.1, Истина);
		КонецЕсли;	

	ИначеЕсли НайденнаяОперация = РСК_РаботаСоСканером.ПредопределённоеЗначение("ЗавершеноСОшибками") Тогда
		Если ЭтоАРМКладовщика() Тогда
			Сообщить("Ставить статус Завершено с ошибками может только оператор");
			Возврат;
		КонецЕсли;
		Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыПриходныхОрдеров.ТребуетсяОбработка");
		СтатусПриИзменении("");
		Модифицированность = Истина;
		
	Иначе
		Сообщить("Просканированная операция не может быть выполнена в Приходном ордере на товары. Допускается переключать ПО только в статус «Завершено»");
	КонецЕсли;	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 05.08.2022 Задача 19394
//e1cib/data/Документ.Задание?ref=a08e5944efdf2c724d9f9d3c61ee1f3b
&НаСервере
Функция ЭтоАРМКладовщика()
	Возврат не ПараметрыСеанса.РСК_АвторизовавшийсяКладовщик.Пустая();
КонецФункции

//++ РС Консалт: Трофимов Евгений 05.08.2022 Задача 19394
//e1cib/data/Документ.Задание?ref=a08e5944efdf2c724d9f9d3c61ee1f3b
&НаКлиенте
Процедура ЗакрытьФорму()
	Закрыть();
КонецПроцедуры	

//++ РС Консалт: Трофимов Евгений 08.02.2023 Задача 23710
//e1cib/data/Документ.Задание?ref=ba9df9b91fdc6f184025abfa4761a13d
&НаКлиенте
Процедура ОбновитьВидимостьЭлементовПослеЗаполненияДокумента() Экспорт

	ПоказатьПредставлениеРаспоряжения(РаспоряжениеПредставление, Объект.Распоряжение);
	ОбновитьВидимостьЭлементовПослеЗаполненияДокументаНаСервере();

КонецПроцедуры // ПереоткрытьДокумент()

//++ РС Консалт: Трофимов Евгений 08.02.2023 Задача 23710
//e1cib/data/Документ.Задание?ref=ba9df9b91fdc6f184025abfa4761a13d
&НаСервере
Процедура ОбновитьВидимостьЭлементовПослеЗаполненияДокументаНаСервере()
	
	#Область КодПроцедуры_ПриСозданииНаСервере
	Если Не СкладыСервер.ИспользоватьСтатусыОрдеров(Объект.Склад,Истина,Ложь) Тогда
		Объект.Статус = Перечисления.СтатусыПриходныхОрдеров.Принят; 
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(Объект, ПараметрыУказанияСерий);
	Иначе
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаТовары;
	КонецЕсли;
	
	ОбновитьАвтоотметкуНезаполненного(Объект.Статус, Элементы.Товары);
	#КонецОбласти

КонецПроцедуры // ОбновитьВидимостьЭлементовПослеЗаполненияДокументаНаСервере()
