//Форма вызывается в событии ПередУдалением строк табличной части Маршруты документа ЗаданиеНаПеревозку
//++ РС Консалт: Трофимов Евгений 09.02.2023 Тикет 23385
//e1cib/data/Документ.Задание?ref=a4138b831ba66f9148449511f2e680ff
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	МассивИсключаемыхРО = Новый Массив;
	Параметры.Свойство("МассивИсключаемыхРО", МассивИсключаемыхРО);
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("МассивИсключаемыхРО", МассивИсключаемыхРО);
	СтруктураПараметров.Вставить("СписокРезультатов", СписокРезультатов.Выгрузить());
	АдресХранилищаПараметров = ПоместитьВоВременноеХранилище(СтруктураПараметров, Новый УникальныйИдентификатор);
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 09.02.2023 Тикет 23385
//e1cib/data/Документ.Задание?ref=a4138b831ba66f9148449511f2e680ff
&НаКлиенте
Процедура ПриОткрытии(Отказ)
    РезультатВыполнения = ВыполнитьДлительнуюОбработкуНаСервере(АдресХранилищаПараметров, УникальныйИдентификатор);
    ИдентификаторЗадания = РезультатВыполнения.ИдентификаторЗадания;
     
    ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
    ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
    ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
    ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеВыполнения", ЭтаФорма);
     
    ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполнения, ОповещениеОЗавершении, ПараметрыОжидания);  	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 09.02.2023 Тикет 23385
//e1cib/data/Документ.Задание?ref=a4138b831ba66f9148449511f2e680ff
&НаСервереБезКонтекста
Функция ВыполнитьДлительнуюОбработкуНаСервере(АдресХранилищаПараметров, УникальныйИдентификатор)
	
	СтруктураПараметров = ПолучитьИзВременногоХранилища(АдресХранилищаПараметров);
	УдалитьИзВременногоХранилища(АдресХранилищаПараметров);

    ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
    ПараметрыВыполнения.ОжидатьЗавершение = 0; // запускать сразу
    ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Изменение статусов РО при исключении из МЛ'");
    ПараметрыВыполнения.ЗапуститьНеВФоне = Ложь;
     
    РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
        "Документы.РасходныйОрдерНаТовары.ИзменениеСтатусовРОприИсключенииИзМЛ",
        СтруктураПараметров, 
        ПараметрыВыполнения
    );
     
    Если РезультатВыполнения.Статус = "Ошибка" Тогда
        ВызватьИсключение РезультатВыполнения.КраткоеПредставлениеОшибки;
    КонецЕсли;
     
    Возврат РезультатВыполнения;	

КонецФункции // ВыполнитьДлительнуюОбработкуНаСервере()

//++ РС Консалт: Трофимов Евгений 09.02.2023 Тикет 23385
//e1cib/data/Документ.Задание?ref=a4138b831ba66f9148449511f2e680ff
&НаКлиенте
Процедура ПослеВыполнения(Результат, ДополнительныеПараметры) Экспорт
     
    Если Результат = Неопределено Тогда
        Возврат;
    КонецЕсли;
     
    Если Результат.Статус = "Выполнено" Тогда
        ЗагрузитьПодготовленныеДанные(Результат.АдресРезультата);
    ИначеЕсли Результат.Статус = "Ошибка" Тогда
        ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
    КонецЕсли;
     
КонецПроцедуры
 
//++ РС Консалт: Трофимов Евгений 09.02.2023 Тикет 23385
//e1cib/data/Документ.Задание?ref=a4138b831ba66f9148449511f2e680ff
&НаСервере
Процедура ЗагрузитьПодготовленныеДанные(АдресРезультата)
    СтруктураРезультата = ПолучитьИзВременногоХранилища(АдресРезультата);
    Если СтруктураРезультата = Неопределено Тогда
        Возврат;
	КонецЕсли;
	СписокРезультатов.Загрузить(СтруктураРезультата.СписокРезультатов);
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 09.02.2023 Тикет 23385
//e1cib/data/Документ.Задание?ref=a4138b831ba66f9148449511f2e680ff
&НаКлиенте
Процедура СписокРезультатовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
    Если Элементы[Поле.Имя].ТолькоПросмотр Тогда
        ДанныеСтроки = Элемент.ДанныеСтроки(ВыбраннаяСтрока);
        ИмяПоля = Сред(Поле.Имя, СтрДлина(Элемент.Имя)+1);
        ПоказатьЗначение(, ДанныеСтроки[ИмяПоля]);
    КонецЕсли;
КонецПроцедуры



