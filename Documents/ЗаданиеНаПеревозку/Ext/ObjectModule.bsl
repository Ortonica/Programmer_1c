
&После("ОбработкаЗаполнения")
Процедура РСК_ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17531
//e1cib/data/Документ.Задание?ref=bb163964a2ac42cd49116e65acb2bf5c
	Если РСК_ВызовСервера.ЕстьРоль("РСК_МенеджерПоВыдачеТСР") Тогда
		РСК_ВыдачаТСР = Истина;
		
		//Если НЕ ЗначениеЗаполнено(Приоритет) Тогда
		//	Приоритет = Справочники.Приоритеты.НайтиПоНаименованию("Средний", Истина);
		//КонецЕсли;
	//++ РС Консалт: Трофимов Евгений 13.10.2022 Тикет 20965
	//e1cib/data/Документ.Задание?ref=bdc83380f664d5bb4898b38b648fea97
	//5. При создании Задания и при открытии формы Выбор транспорта для
	//создания заданий на перевозку - при установке типа исполнителя перевозки 
	//по умолчанию ставить "Перевозчик"
	Иначе
		ЗаданиеВыполняет = Перечисления.ТипыИсполнителейЗаданийНаПеревозку.Перевозчик;
	//-- КонецТикета 20965
	КонецЕсли;
//-- КонецЗадачи 17531	
КонецПроцедуры

&После("ПередЗаписью")
Процедура РСК_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПересчитатьВесОбъемПоУпаковкам();
	ИзменитьСтатусНаОтгруженВАктахТСР();
	//++ РС Консалт: Минаков А.С. Задача 20226
	ПроверитьЗаполнениеНакладных(Отказ)
	//++ РС Консалт: Минаков А.С. Задача 20226
	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 18.11.2022 Задача 21979
//e1cib/data/Документ.Задание?ref=960157bd91fdb1dd40c034a9ff351dad
Процедура ПересчитатьВесОбъемПоУпаковкам() Экспорт

	Если НЕ РСК_ВыдачаТСР Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Распоряжения.Распоряжение КАК Распоряжение,
		|	Распоряжения.КлючСвязи КАК КлючСвязи
		|ПОМЕСТИТЬ Распоряжения
		|ИЗ
		|	&Распоряжения КАК Распоряжения
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Распоряжения.Распоряжение КАК Распоряжение,
		|	Распоряжения.КлючСвязи КАК КлючСвязи,
		|	ПоручениеЭкспедитору.РСК_Номенклатура КАК Номенклатура,
		|	ПоручениеЭкспедитору.РСК_Количество КАК КоличествоУпаковок,
		|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка
		|ИЗ
		|	Распоряжения КАК Распоряжения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоручениеЭкспедитору КАК ПоручениеЭкспедитору
		|		ПО Распоряжения.Распоряжение = ПоручениеЭкспедитору.Ссылка";
	
	Запрос.УстановитьПараметр("Распоряжения", Распоряжения.Выгрузить(,"Распоряжение,КлючСвязи"));
	ТаблицаУпаковок = Запрос.Выполнить().Выгрузить();
	ТаблицаВесОбъем = РСК_ВызовСервера.ПолучитьВГХпоДаннымУпаковкиИзТаблицы(ТаблицаУпаковок);
	Фильтр = Новый Структура("Номенклатура,КоличествоУпаковок,Упаковка");
	Для Каждого Стр Из Распоряжения Цикл
		СтрокаУпаковки = ТаблицаУпаковок.Найти(Стр.Распоряжение,"Распоряжение");
		Если СтрокаУпаковки = Неопределено Тогда
			Стр.Вес = 0;
			Стр.Объем = 0;
			Продолжить;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(Фильтр, СтрокаУпаковки);
		ВесОбъем = ТаблицаВесОбъем.НайтиСтроки(Фильтр)[0];
		Стр.Вес = ВесОбъем.Вес / 1000;
		Стр.Объем = ВесОбъем.Объем;
	КонецЦикла;
	
	Для Каждого Стр Из Маршрут Цикл
		ТаблицаМаршрута = Распоряжения.Выгрузить(Новый Структура("КлючСвязи", Стр.КлючСвязи));
		Стр.Вес = ТаблицаМаршрута.Итог("Вес");
		Стр.Объем = ТаблицаМаршрута.Итог("Объем");
	КонецЦикла;
	
	Вес = Распоряжения.Итог("Вес");
	Объем = Распоряжения.Итог("Объем");
	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17531
//e1cib/data/Документ.Задание?ref=bb163964a2ac42cd49116e65acb2bf5c
Процедура ИзменитьСтатусНаОтгруженВАктахТСР()

	Если НЕ РСК_ВыдачаТСР Тогда
		Возврат;
	КонецЕсли;
	
	Если Статус = Перечисления.СтатусыЗаданийНаПеревозку.Отправлено Тогда
		
		ИзменяемыеСтатусыАктов = Новый Массив;
		ИзменяемыеСтатусыАктов.Добавить(Перечисления.РСК_СтатусыАктовВыдачи.ПустаяСсылка());
		ИзменяемыеСтатусыАктов.Добавить(Перечисления.РСК_СтатусыАктовВыдачи.Создан);
		ИзменяемыеСтатусыАктов.Добавить(Перечисления.РСК_СтатусыАктовВыдачи.Подготовлен);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПоручениеЭкспедитору.Ссылка КАК Акт
			|ИЗ
			|	Документ.ПоручениеЭкспедитору КАК ПоручениеЭкспедитору
			|ГДЕ
			|	ПоручениеЭкспедитору.Ссылка В(&МассивАктов)
			|	И ПоручениеЭкспедитору.РСК_СтатусАктаВыдачиТСР В(&ИзменяемыеСтатусыАктов)
			|	И НЕ ПоручениеЭкспедитору.ПометкаУдаления";
		
		Запрос.УстановитьПараметр("МассивАктов", Распоряжения.Выгрузить().ВыгрузитьКолонку("Распоряжение"));
		Запрос.УстановитьПараметр("ИзменяемыеСтатусыАктов", ИзменяемыеСтатусыАктов);
	
		МассивАктов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Акт");
		
		Для Каждого Акт Из МассивАктов Цикл
			оАкт = Акт.ПолучитьОбъект();
			оАкт.РСК_СтатусАктаВыдачиТСР = Перечисления.РСК_СтатусыАктовВыдачи.Отгружен;
			оАкт.Записать(РежимЗаписиДокумента.Проведение);
		КонецЦикла;
	
	КонецЕсли;

КонецПроцедуры

Процедура ПроверитьЗаполнениеНакладных(Отказ)
	
	//++ РС Консалт: Минаков А.С. Задача 20226
	Если РСК_ВыдачаТСР Тогда
		Возврат
	КонецЕсли;	
	
	Если Не Статус = Перечисления.СтатусыЗаданийНаПеревозку.Отправлено
		И Не Статус = Перечисления.СтатусыЗаданийНаПеревозку.Закрыто Тогда
		Возврат
	КонецЕсли;	
	
	Для Каждого СтрокаТч Из Распоряжения Цикл
		Если Не ЗначениеЗаполнено(СтрокаТч.РСК_Накладная) Тогда
			ОбщегоНазначения.СообщитьПользователю("Для " + СтрокаТч.Распоряжение + " не указана накладная", СтрокаТч.Распоряжение,,, Отказ)
		КонецЕсли
	КонецЦикла
	//++ РС Консалт: Минаков А.С. Задача 20226
	
КонецПроцедуры	

&После("ПриЗаписи")
Процедура РСК_ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	//++ РС Консалт: Трофимов Евгений 22.02.2023 Задача 23698
	//e1cib/data/Документ.Задание?ref=a1c07de7eccf0a5942be9c5a46bb066d
	//
	// +------------------------------------------------------------------------------------------------------------------+
	// | ПЕРЕНЕСЕНО В ПРОЦЕДУРУ ПРОВЕДЕНИЯ, так как фоновое запускалось слишком быстро и документ не успевал проводиться  |
	// +------------------------------------------------------------------------------------------------------------------+
	//
	////++ РС Консалт: Трофимов Евгений 17.02.2023 Задача 23698
	////e1cib/data/Документ.Задание?ref=a1c07de7eccf0a5942be9c5a46bb066d
	//Если РегистрыСведений.реа_ПредопределенныеЗначения.Значение("Функционал задачи № 243812", Истина)
	//	И НЕ ДополнительныеСвойства.Свойство("БезПроверкиДопустимостиСтатуса")
	//	И СкладыСервер.ИспользоватьОрдернуюСхемуПриОтгрузке(Склад, Дата) Тогда
	//	
	//	ТаблицыСверкиСтатусовРО = Документы.ЗаданиеНаПеревозку.ПолучитьТаблицыСверкиСтатусовРО(Новый Структура("Ссылка", Ссылка));
	//	СообщитьОбОшибкахСтатуса(ТаблицыСверкиСтатусовРО, Отказ);
	//	
	//КонецЕсли;
	////-- КонецЗадачи 23698	
	//
	////++ РС Консалт: Минаков А.С. Задача 20226
	//Если Отказ Тогда
	//	Возврат
	//КонецЕсли;
	//
	//РСК_ВызовСервера.ЗапускВыполненияФоновогоИсключенияРасходногоОрдера(Ссылка);
	//ДополнительныеСвойства.Вставить("ОтказОтТиповогоЗаполненияРО"); //обработается при проведении
	////++ РС Консалт: Минаков А.С. Задача 20226
	//-- КонецЗадачи 23698	
	
КонецПроцедуры

&После("ОбработкаПроведения")
Процедура РСК_ОбработкаПроведения(Отказ, РежимПроведения)
	
	//++ РС Консалт: Минаков А.С. Задача 20226
	Перем ПометитьНеДоставленные;
	
	Если Отказ Тогда
		Возврат
	КонецЕсли;	
	
	Если ДополнительныеСвойства.Свойство("ПометитьНеДоставленные", ПометитьНеДоставленные)
		И ПометитьНеДоставленные Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		ДатаОтгрузки = ?(ЗначениеЗаполнено(ДатаВремяРейсаПланС), НачалоДня(ДатаВремяРейсаПланС), НачалоДня(ТекущаяДата()));
		
		МассивРаспоряжений = Новый Массив;
		МассивНакладных = Новый Массив; 
		Для Каждого СтрокаТч Из Маршрут Цикл
			Если Не СтрокаТч.Доставлено Тогда
				НайденныеСтроки = Распоряжения.НайтиСтроки(Новый Структура("КлючСвязи", СтрокаТч.КлючСвязи));
				Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
					МассивРаспоряжений.Добавить(НайденнаяСтрока.Распоряжение);
					
					Если ЗначениеЗаполнено(НайденнаяСтрока.РСК_Накладная) Тогда
						МассивНакладных.Добавить(НайденнаяСтрока.РСК_Накладная)
					КонецЕсли
				КонецЦикла
			КонецЕсли
		КонецЦикла;
		
		Для Каждого Накладная Из МассивНакладных Цикл
			ДокументОбъект = Накладная.ПолучитьОбъект();
			ДокументОбъект.ДополнительныеСвойства.Вставить("ОтключитьКонтрольПриПроведении", Истина);
			ДокументОбъект.УстановитьПометкуУдаления(Истина)
		КонецЦикла;
			
		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка КАК Ордер,
		|	ЕСТЬNULL(ОтборРазмещениеТоваров.Ссылка, ЗНАЧЕНИЕ(Документ.ОтборРазмещениеТоваров.ПустаяСсылка)) КАК Отбор
		|ИЗ
		|	Документ.РасходныйОрдерНаТовары.ТоварыПоРаспоряжениям КАК РасходныйОрдерНаТоварыТоварыПоРаспоряжениям
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтборРазмещениеТоваров КАК ОтборРазмещениеТоваров
		|		ПО РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка = ОтборРазмещениеТоваров.Распоряжение
		|			И (НЕ ОтборРазмещениеТоваров.ПометкаУдаления)
		|ГДЕ
		|	РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Распоряжение В(&Распоряжения)
		|	И НЕ РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка.ПометкаУдаления
		|	И РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка.ЗаданиеНаПеревозку = &Ссылка");
		
		Запрос.УстановитьПараметр("Распоряжения", МассивРаспоряжений);
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ДокументОбъект = Выборка.Ордер.ПолучитьОбъект();
			ДокументОбъект.ДополнительныеСвойства.Вставить("ОтключитьКонтрольПриПроведении", Истина);
			ДокументОбъект.УстановитьПометкуУдаления(Истина);
			
			Если Не Выборка.Отбор.Пустая() Тогда
				ДокументОбъект = Выборка.Отбор.ПолучитьОбъект();
				ДокументОбъект.ДополнительныеСвойства.Вставить("ОтключитьКонтрольПриПроведении", Истина);
				ДокументОбъект.УстановитьПометкуУдаления(Истина);
			КонецЕсли	
			
		КонецЦикла;
		
		Для Каждого Накладная Из МассивНакладных Цикл
			
			НовыйОрдер = Документы.ПриходныйОрдерНаТовары.СоздатьДокумент();
			НовыйОрдер.Заполнить(Неопределено);
			НовыйОрдер.Дата = ДатаОтгрузки;
			Если СкладыСервер.ИспользоватьАдресноеХранение(НовыйОрдер.Склад, НовыйОрдер.Помещение, НовыйОрдер.Дата) Тогда
				НовыйОрдер.ЗонаПриемки = Справочники.СкладскиеЯчейки.ЗонаПриемкиПоУмолчанию(НовыйОрдер.Склад, НовыйОрдер.Помещение, НовыйОрдер.ЗонаПриемки)
			КонецЕсли; 
			НовыйОрдер.СкладскаяОперация = Перечисления.СкладскиеОперации.ВозвратНеПринятыхТоваров;
			НовыйОрдер.ХозяйственнаяОперация = Накладная.ХозяйственнаяОперация;
			
			Если ТипЗнч(Накладная) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
				НовыйОрдер.Склад = Накладная.Склад;
				НовыйОрдер.Отправитель = Накладная.Партнер
			ИначеЕсли ТипЗнч(Накладная) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
				НовыйОрдер.Склад = Накладная.СкладПолучатель;
				НовыйОрдер.Отправитель = Накладная.СкладОтправитель
			КонецЕсли;
			
			Для Каждого СтрокаТч Из Накладная.Товары Цикл
				ЗаполнитьЗначенияСвойств(НовыйОрдер.Товары.Добавить(), СтрокаТч)
			КонецЦикла;	
			
			Попытка
				Если НовыйОрдер.ПроверитьЗаполнение() Тогда
					НовыйОрдер.Записать(РежимЗаписиДокумента.Проведение)
				Иначе
					НовыйОрдер.Записать()
				КонецЕсли;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Создан документ " + НовыйОрдер.Ссылка, НовыйОрдер.Ссылка)
			Исключение
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не удалось создать приходный ордер по накладной " + Накладная + " по причине " + ОписаниеОшибки(), Накладная) 
			КонецПопытки			
			
		КонецЦикла;
		
		УстановитьПривилегированныйРежим(Ложь)
	КонецЕсли
	//++ РС Консалт: Минаков А.С. Задача 20226

КонецПроцедуры

&После("ОбработкаПроверкиЗаполнения")
Процедура РСК_ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	//++ РС Консалт: Трофимов Евгений 01.11.2022 Тикет 21573
	//e1cib/data/Документ.Задание?ref=8cabafb2e2bb17df406f974e2f52c112
	Если РСК_ВызовСервера.ЕстьРоль("РСК_МенеджерПоВыдачеТСР") Тогда
		РеквизитПриоритет = ПроверяемыеРеквизиты.Найти("Приоритет");
		Если РеквизитПриоритет <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(РеквизитПриоритет);
		КонецЕсли;
			
	КонецЕсли;
	//-- КонецТикета 21573
	
	//++ РС Консалт: Трофимов Евгений 17.02.2023 Задача 23698
	//e1cib/data/Документ.Задание?ref=a1c07de7eccf0a5942be9c5a46bb066d
	Если РегистрыСведений.реа_ПредопределенныеЗначения.Значение("Функционал задачи № 243812", Истина) Тогда
		Если Статус = Перечисления.СтатусыЗаданийНаПеревозку.Отправлено И НЕ РСК_ВыдачаТСР Тогда
			ПроверяемыеРеквизиты.Добавить("РСК_ОтветственныйЗаОтгрузку");
		КонецЕсли;
	КонецЕсли;
	//-- КонецЗадачи 23698	
КонецПроцедуры


//++ РС Консалт: Трофимов Евгений 20.02.2023 Задача 23698
//e1cib/data/Документ.Задание?ref=a1c07de7eccf0a5942be9c5a46bb066d
Процедура СообщитьОбОшибкахСтатуса(ТаблицыСверкиСтатусовРО, Отказ = Ложь) Экспорт

	Если ТаблицыСверкиСтатусовРО.РаспоряженияБезРО.Количество() > 0 Тогда
		ОбщегоНазначения.СообщитьПользователю("Нельзя устанавливать статус "+Статус+" т.к. имеются распоряжения без расходных ордеров:",Ссылка,,,Отказ);
		Для Каждого Стр Из ТаблицыСверкиСтатусовРО.РаспоряженияБезРО Цикл
			ОбщегоНазначения.СообщитьПользователю(""+Стр.Распоряжение,Стр.Распоряжение);
		КонецЦикла;
	КонецЕсли;
	Если ТаблицыСверкиСтатусовРО.НедопустимыеСтатусыРО.Количество() > 0 Тогда
		ОбщегоНазначения.СообщитьПользователю("Нельзя устанавливать статус "+Статус+" т.к. имеются расходные ордера со статусами ниже чем «К отгрузке»:",Ссылка,,,Отказ);
		Для Каждого Стр Из ТаблицыСверкиСтатусовРО.НедопустимыеСтатусыРО Цикл
			ОбщегоНазначения.СообщитьПользователю(""+Стр.РО+" имеет статус "+Стр.СтатусРО,Стр.РО,"Статус",,Отказ);
		КонецЦикла;
	КонецЕсли;

	//++ РС Консалт: Трофимов Евгений 06.04.2023 Тикет 24985
	//e1cib/data/Документ.Задание?ref=9a3c01eb245c24dd4ef5d9b0182f94ff
	Если НЕ РСК_ВызовСервера.ЕстьРоль("РСК_РедактированиеСкладскихОрдеровПрошлыхПериодов") Тогда
		ЕстьРОвПрошедшемПериоде = Ложь;
		Для Каждого Стр Из ТаблицыСверкиСтатусовРО.КСменеСтатуса Цикл
			Если Стр.ТребуемыйСтатусРО <> Перечисления.СтатусыРасходныхОрдеров.Отгружен
				И Стр.СтатусРО = Перечисления.СтатусыРасходныхОрдеров.Отгружен
				И Стр.ДатаОтгрузки < НачалоДня(ТекущаяДатаСеанса()) Тогда
				
				ЕстьРОвПрошедшемПериоде = Истина;
				ОбщегоНазначения.СообщитьПользователю(""+Стр.РО+" находится в прошедшем периоде",Стр.РО,,,Отказ);
				
			КонецЕсли;
		КонецЦикла;
		Если ЕстьРОвПрошедшемПериоде Тогда
			ОбщегоНазначения.СообщитьПользователю("Запрещено снимать РО со статуса «Отгружен» в прошедшем периоде! Поэтому проводить МЛ в статусе, отличным от «Отправлено» или «Закрыто», тоже запрещено (Тикет 849)");
		КонецЕсли;
	КонецЕсли;
	//-- КонецТикета 24985	
	
КонецПроцедуры


&ИзменениеИКонтроль("ОбработкаПроведения")
Процедура РСК_ОбработкаПроведения1(Отказ, РежимПроведения)

	ПроведенПередЗаписью = ДополнительныеСвойства.ПроведенПередЗаписью;
	СтатусПередЗаписью = ДополнительныеСвойства.СтатусПередЗаписью;

	// Проверка можно ли устанавливать статус
	Если Операция = Перечисления.ВидыДоставки.СоСклада
		И (Статус = Перечисления.СтатусыЗаданийНаПеревозку.Отправлено 
		Или Статус = Перечисления.СтатусыЗаданийНаПеревозку.Закрыто) Тогда

		// Проверка на все ли количество по заданию на перевозку оформлены расходные ордера,
		// только для складов с ФО НачинатьОтгрузкуПослеФормированияЗаданияНаПеревозку.
		КонтрольОформленныхОрдеровПоЗаданиюНаПеревозку(Ссылка, Отказ);

		// Проверка готовности расходных ордеров
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходныйОрдерНаТовары.Ссылка
		|ИЗ
		|	Документ.РасходныйОрдерНаТовары КАК РасходныйОрдерНаТовары
		|ГДЕ
		|	РасходныйОрдерНаТовары.ЗаданиеНаПеревозку = &ЗаданиеНаПеревозку
		|	И НЕ(РасходныйОрдерНаТовары.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРасходныхОрдеров.КОтгрузке)
		|				ИЛИ РасходныйОрдерНаТовары.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРасходныхОрдеров.Отгружен))
		|	И РасходныйОрдерНаТовары.Проведен";
		Запрос.УстановитьПараметр("ЗаданиеНаПеревозку", Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл

			ТекстСообщения = НСтр("ru = 'Расходный ордер ""%Ссылка%"" по заданию на перевозку не готов к отгрузке.';
			|en = 'Goods issue note ""%Ссылка%"" for delivery order is not ready to ship.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Выборка.Ссылка);

			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,Выборка.Ссылка,,,Отказ);

		КонецЦикла;

		// Запуск автоматического создания расходных ордеров
		// для складов с ФО НачинатьОтгрузкуПослеФормированияЗаданияНаПеревозку.
	ИначеЕсли Операция = Перечисления.ВидыДоставки.СоСклада
		И Константы.РежимФормированияРасходныхОрдеров.Получить() = Перечисления.РежимыФормированияРасходныхОрдеров.Автоматически

		И (		Статус = Перечисления.СтатусыЗаданийНаПеревозку.КПогрузке
		И Не ПроведенПередЗаписью

		Или 

		Статус = Перечисления.СтатусыЗаданийНаПеревозку.КПогрузке
		И СтатусПередЗаписью = Перечисления.СтатусыЗаданийНаПеревозку.Формируется

		Или

		Статус = Перечисления.СтатусыЗаданийНаПеревозку.Формируется
		И СтатусПередЗаписью <> Перечисления.СтатусыЗаданийНаПеревозку.Формируется
		И ПроведенПередЗаписью

		Или
		СтатусПередЗаписью <> Перечисления.СтатусыЗаданийНаПеревозку.Формируется
		И ДополнительныеСвойства.ИзмененияСоставаРаспоряжений.Количество() > 0

		) Тогда

		Документы.ЗаданиеНаПеревозку.СформироватьОчередьПереоформленияРасходныхОрдеров(Ссылка,
		Отказ,
		ДополнительныеСвойства.ИзмененияСоставаРаспоряжений);

	КонецЕсли;

	Если ДополнительныеСвойства.ЭтоНовый
		Или Отказ Тогда
		Возврат;
	КонецЕсли;
	#Вставка
	//++ РС Консалт: Трофимов Евгений 22.02.2023 Задача 23698
	//e1cib/data/Документ.Задание?ref=a1c07de7eccf0a5942be9c5a46bb066d
	//Если ДополнительныеСвойства.Свойство("ОтказОтТиповогоЗаполненияРО") Тогда
	//	Возврат;
	//КонецЕсли;
	Если РегистрыСведений.реа_ПредопределенныеЗначения.Значение("Функционал задачи № 243812", Истина)
		И СкладыСервер.ИспользоватьОрдернуюСхемуПриОтгрузке(Склад, Дата) Тогда
		
		Если НЕ ДополнительныеСвойства.Свойство("БезПроверкиДопустимостиСтатуса") Тогда
			ТаблицыСверкиСтатусовРО = Документы.ЗаданиеНаПеревозку.ПолучитьТаблицыСверкиСтатусовРО(Новый Структура("Ссылка", Ссылка));
			СообщитьОбОшибкахСтатуса(ТаблицыСверкиСтатусовРО, Отказ);
		КонецЕсли;
		
	КонецЕсли;
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	РСК_ВызовСервера.ЗапускВыполненияФоновогоИсключенияРасходногоОрдера(Ссылка);
	Возврат; // - отказ от дальнейшего выполнения процедуры так как там идёт подобная фоновая обработка (проверить при обновлении релиза!!!);
	//-- КонецЗадачи 23698	
	#КонецВставки

	// Изменить статус расходных ордеров на "Отгружен" по отправленным заданиям на перевозку,
	// если все ордера оформлены и находятся в статусе не ниже КОтгрузке (это проверяется выше).
	Если Операция = Перечисления.ВидыДоставки.СоСклада
		И (Статус = Перечисления.СтатусыЗаданийНаПеревозку.Закрыто
		ИЛИ Статус = Перечисления.СтатусыЗаданийНаПеревозку.Отправлено)
		И (Не ПроведенПередЗаписью
		Или 
		(СтатусПередЗаписью <> Перечисления.СтатусыЗаданийНаПеревозку.Закрыто
		И СтатусПередЗаписью <> Перечисления.СтатусыЗаданийНаПеревозку.Отправлено
		И Статус <> СтатусПередЗаписью)) Тогда 

		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	РасходныйОрдерНаТовары.Ссылка
		|ИЗ
		|	Документ.РасходныйОрдерНаТовары КАК РасходныйОрдерНаТовары
		|ГДЕ
		|	РасходныйОрдерНаТовары.ЗаданиеНаПеревозку = &ЗаданиеНаПеревозку
		|	И РасходныйОрдерНаТовары.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыРасходныхОрдеров.КОтгрузке)
		|	И РасходныйОрдерНаТовары.Проведен";
		Запрос.УстановитьПараметр("ЗаданиеНаПеревозку", Ссылка);
		Выборка = Запрос.Выполнить().Выбрать();

		Если Не Запрос.Выполнить().Пустой() Тогда

			СкладыСервер.ЗапускВыполненияФоновогоПереводаСтатусаРасходногоОрдераВОтгружено(Ссылка);

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

