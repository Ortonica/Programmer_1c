
&НаСервере
Процедура РСК_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("СтрокиКОтражению") Тогда
		АдресХранилищаСтрокиКОтражению = Параметры.СтрокиКОтражению;	
	КонецЕсли;
КонецПроцедуры


&НаСервере
Процедура ЗаписатьОплатуУслугСоисполнителя(ПервичноеОтражение,ПараметрыЗаписи)
	
	Запрос = Новый Запрос;
	Если ПервичноеОтражение Тогда 
		Запрос.Текст 	 = ТекстЗапросаПервичноеОтражениеУслуг();
		Запрос.УстановитьПараметр("СтрокиКОтражению",ПолучитьИзВременногоХранилища(АдресХранилищаСтрокиКОтражению)); 
	Иначе 
		Запрос.Текст 	 = ТекстЗапросаОтражениеУслуг();
	КонецЕсли;
	Запрос.УстановитьПараметр("ДанныеДокумента",Объект.Расходы.Выгрузить());
	Запрос.УстановитьПараметр("Активно",ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение);
	Запрос.УстановитьПараметр("ДокументОтражения",Объект.Ссылка); 
	
	Результат = Запрос.Выполнить().Выгрузить(); 
	
	Набор = РегистрыСведений.РСК_ОтраженныеУслугиСоисполнителей.СоздатьНаборЗаписей();
	Набор.Отбор.ДокументОтражения.Установить(Объект.Ссылка);
	Набор.Загрузить(Результат);
	Набор.Записать();
	
КонецПроцедуры  

Функция ТекстЗапросаПервичноеОтражениеУслуг() 
	
	Текст = 
	"ВЫБРАТЬ
	|	СтрокиКОтражению.ЗаданиеТорговомуПредставителю КАК ЗаданиеТорговомуПредставителю,
	|	СтрокиКОтражению.Услуга КАК Услуга,
	|	СтрокиКОтражению.Количество КАК Количество,
	|	СтрокиКОтражению.РСК_ИдентификаторСтрокиЗадания КАК РСК_ИдентификаторСтрокиЗадания
	|ПОМЕСТИТЬ ВТ_СтрокиКОтражению
	|ИЗ
	|	&СтрокиКОтражению КАК СтрокиКОтражению
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.РСК_ИдентификаторСтрокиЗадания КАК РСК_ИдентификаторСтрокиЗадания,
	|	ДанныеДокумента.Количество КАК Количество
	|ПОМЕСТИТЬ ВТ_ДанныеДокумента
	|ИЗ
	|	&ДанныеДокумента КАК ДанныеДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДокумента.РСК_ИдентификаторСтрокиЗадания КАК ИдентификаторСтроки,
	|	ВТ_ДанныеДокумента.Количество КАК Количество,
	|	ВТ_СтрокиКОтражению.ЗаданиеТорговомуПредставителю КАК ЗаданиеТорговомуПредставителю,
	|	ВТ_СтрокиКОтражению.Услуга КАК Услуга,
	|	&Активно КАК Активно,
	|	&ДокументОтражения КАК ДокументОтражения
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтрокиКОтражению КАК ВТ_СтрокиКОтражению
	|		ПО ВТ_ДанныеДокумента.РСК_ИдентификаторСтрокиЗадания = ВТ_СтрокиКОтражению.РСК_ИдентификаторСтрокиЗадания";
	
	Возврат Текст;   
	
КонецФункции   

Функция ТекстЗапросаОтражениеУслуг() 
	
	Текст = 
	"ВЫБРАТЬ
	|	РСК_ОтраженныеУслугиСоисполнителей.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	РСК_ОтраженныеУслугиСоисполнителей.Количество КАК Количество,
	|	РСК_ОтраженныеУслугиСоисполнителей.ДокументОтражения КАК ДокументОтражения,
	|	РСК_ОтраженныеУслугиСоисполнителей.Услуга КАК Услуга,
	|	РСК_ОтраженныеУслугиСоисполнителей.ЗаданиеТорговомуПредставителю КАК ЗаданиеТорговомуПредставителю
	|ПОМЕСТИТЬ ВТ_СтрокиКОтражению
	|ИЗ
	|	РегистрСведений.РСК_ОтраженныеУслугиСоисполнителей КАК РСК_ОтраженныеУслугиСоисполнителей
	|ГДЕ
	|	РСК_ОтраженныеУслугиСоисполнителей.ДокументОтражения = &ДокументОтражения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокумента.РСК_ИдентификаторСтрокиЗадания КАК РСК_ИдентификаторСтрокиЗадания,
	|	ДанныеДокумента.Количество КАК Количество
	|ПОМЕСТИТЬ ВТ_ДанныеДокумента
	|ИЗ
	|	&ДанныеДокумента КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.РСК_ИдентификаторСтрокиЗадания <> """"
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДокумента.РСК_ИдентификаторСтрокиЗадания КАК ИдентификаторСтроки,
	|	ВТ_ДанныеДокумента.Количество КАК Количество,
	|	&Активно КАК Активно,
	|	ВТ_СтрокиКОтражению.ДокументОтражения КАК ДокументОтражения,
	|	ВТ_СтрокиКОтражению.Услуга КАК Услуга,
	|	ВТ_СтрокиКОтражению.ЗаданиеТорговомуПредставителю КАК ЗаданиеТорговомуПредставителю
	|ИЗ
	|	ВТ_ДанныеДокумента КАК ВТ_ДанныеДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтрокиКОтражению КАК ВТ_СтрокиКОтражению
	|		ПО ВТ_ДанныеДокумента.РСК_ИдентификаторСтрокиЗадания = ВТ_СтрокиКОтражению.ИдентификаторСтроки";
	
	Возврат Текст;   
	
КонецФункции

&НаСервере
&После("ПослеЗаписиНаСервере")
Процедура РСК_ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ЗаписатьОплатуУслугСоисполнителя(ЗначениеЗаполнено(АдресХранилищаСтрокиКОтражению),ПараметрыЗаписи);	
КонецПроцедуры



