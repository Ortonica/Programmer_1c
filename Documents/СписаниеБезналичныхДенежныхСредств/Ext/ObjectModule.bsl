

&ИзменениеИКонтроль("ОбработкаПроверкиЗаполнения")
Процедура РСК_ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	Перем МассивВсехРеквизитов;
	Перем МассивРеквизитовОперации;

	ФлагОбменСБанками = Ложь;
	ОшибкиЗаполнения = "";
	РасшифровкаБезРазбиения = Ложь;

	Если ДополнительныеСвойства.Свойство("ОбменСБанками")
		И ДополнительныеСвойства.ОбменСБанками Тогда
		ФлагОбменСБанками = Истина;
	КонецЕсли;

	Если ДополнительныеСвойства.Свойство("РасшифровкаБезРазбиения")
		И ДополнительныеСвойства.РасшифровкаБезРазбиения Тогда
		РасшифровкаБезРазбиения = Истина;
	КонецЕсли;

	МассивНепроверяемыхРеквизитов = Новый Массив;
	Документы.СписаниеБезналичныхДенежныхСредств.ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(
	ЭтотОбъект, МассивВсехРеквизитов, МассивРеквизитовОперации);
	ОбщегоНазначенияУТКлиентСервер.ЗаполнитьМассивНепроверяемыхРеквизитов(
	МассивВсехРеквизитов, МассивРеквизитовОперации, МассивНепроверяемыхРеквизитов);

	МассивНепроверяемыхРеквизитов.Добавить("ЗаявкаНаРасходованиеДенежныхСредств");

	ИспользоватьЗаявки = ПолучитьФункциональнуюОпцию("ИспользоватьЗаявкиНаРасходованиеДенежныхСредств");
	НеобязательноУказаниеЗаявки = (ДенежныеСредстваСервер.РазрешеныПлатежиБезУказанияЗаявок(ЭтотОбъект)
	Или Не ИспользоватьЗаявки Или НеКонтролироватьЗаполнениеЗаявки)
	И Не ОплатаПоЗаявкам;

	Если НеобязательноУказаниеЗаявки Тогда
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.ЗаявкаНаРасходованиеДенежныхСредств");
	КонецЕсли;

	Если Документы.СписаниеБезналичныхДенежныхСредств.МассивОперацийБезРасшифровкиПлатежа().Найти(ХозяйственнаяОперация) <> Неопределено
		И НеобязательноУказаниеЗаявки Тогда
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа");
	КонецЕсли;

	Если ТипПлатежногоДокумента = Перечисления.ТипыПлатежныхДокументов.ПлатежноеПоручение Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДатаВходящегоДокумента");
		МассивНепроверяемыхРеквизитов.Добавить("НомерВходящегоДокумента");
	КонецЕсли;

	Если Не ПроведеноБанком Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДатаПроведенияБанком");
	КонецЕсли;

	Если Не ЗначениеЗаполнено(БанковскийСчет) Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Организация");
		МассивНепроверяемыхРеквизитов.Добавить("Валюта");
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.ЗаявкаНаРасходованиеДенежныхСредств");
	КонецЕсли;

	Если РасшифровкаБезРазбиения Тогда
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.Сумма");
	КонецЕсли;

	Если СписокФизЛиц Тогда

		МассивНепроверяемыхРеквизитов.Добавить("ПодотчетноеЛицо");
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств");

	Иначе

		МассивНепроверяемыхРеквизитов.Добавить("ЛицевыеСчетаСотрудников");
		МассивНепроверяемыхРеквизитов.Добавить("ЛицевыеСчетаСотрудников.ФизическоеЛицо");
		МассивНепроверяемыхРеквизитов.Добавить("ЛицевыеСчетаСотрудников.ЛицевойСчет");
		МассивНепроверяемыхРеквизитов.Добавить("ЛицевыеСчетаСотрудников.Сумма");

		Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику Тогда
			МассивНепроверяемыхРеквизитов.Добавить("СтатьяДвиженияДенежныхСредств");
		КонецЕсли;

	КонецЕсли;

	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику Тогда

		МассивНепроверяемыхРеквизитов.Добавить("Подразделение");
		МассивНепроверяемыхРеквизитов.Добавить("ДатаАвансовогоОтчета");
       #Удаление
		Если Не ЗначениеЗаполнено(ДатаАвансовогоОтчета) Тогда
			ТекстОшибки = НСтр("ru = 'Поле ""Отчитаться"" не заполнено';
			|en = '""Report"" is required'");
			Если ФлагОбменСБанками Тогда
				ДенежныеСредстваСервер.ДобавитьОшибкуЗаполнения(ОшибкиЗаполнения, ТекстОшибки);
			Иначе
				ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,, "ПериодАвансовогоОтчета",, Отказ);
			КонецЕсли;
		КонецЕсли;
        #КонецУдаления
		#Вставка 
		//РСК+ Кикинева А.Г. заявка 255873 проверка на заполнение Даты отчета по Заявке на командировку
		Если Не ЗначениеЗаполнено(ДатаАвансовогоОтчета)
			Тогда
			ЭтотОбъект.ДатаАвансовогоОтчета=Дата(ЭтотОбъект.Дата+7*24*3600);
		КонецЕсли;  
		ЭтотОбъект.НазначениеПлатежа="";
		ТекстСумма = "Сумма " + Формат(ЭтотОбъект.СуммаДокумента, "ЧДЦ=2; ЧРД=-; ЧН=0-00; ЧГ=");
		ТекстВалюта =СокрЛП(ЭтотОбъект.Валюта.Наименование);
		ТекстСуммаНДС = "Без налога (НДС)";
		ТекстНазначение = "Перечисление подотчет "+ ТекстСумма+" "+ТекстВалюта + Символы.ПС + ТекстСуммаНДС;  
		ЭтотОбъект.НазначениеПлатежа = ТекстНазначение;
		
		ЭтотОбъект.КодВидаДохода="2";
		//РСК- Кикинева А.Г. заявка 255873 проверка на заполнение Даты отчета по Заявке на командировку
		#КонецВставки
	КонецЕсли;

	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет Тогда
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.Подразделение");
	КонецЕсли;

	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.КонвертацияВалюты Тогда
		МассивНепроверяемыхРеквизитов.Добавить("БанковскийСчетКонтрагента");
	КонецЕсли;

	ДенежныеСредстваСервер.ДобавитьНепроверяемыеРеквизитыПоВыплатеЗаработнойПлаты(ЭтотОбъект, МассивНепроверяемыхРеквизитов);

	Если ПеречислениеСотрудникуЧерезБанк Тогда

		КонтрольныйМассив = Новый Массив;
		КонтрольныйМассив.Добавить("Контрагент");
		КонтрольныйМассив.Добавить("БанковскийСчетКонтрагента");

		Для Каждого СтрокаМассива Из КонтрольныйМассив Цикл

			ИскомыйЭлемент = МассивНепроверяемыхРеквизитов.Найти(СтрокаМассива);

			Если ИскомыйЭлемент <> Неопределено Тогда
				МассивНепроверяемыхРеквизитов.Удалить(ИскомыйЭлемент);
			КонецЕсли;

		КонецЦикла;

	Иначе

		Если (ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику
			ИЛИ ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаЗаймаСотруднику)
			И МассивНепроверяемыхРеквизитов.Найти("Контрагент") = Неопределено Тогда
			МассивНепроверяемыхРеквизитов.Добавить("Контрагент");
		КонецЕсли;

	КонецЕсли;

	Если ФлагОбменСБанками Тогда
		ДополнительныеПараметры = Новый Структура("ПрограммнаяПроверка", Истина);
		ПланыВидовХарактеристик.СтатьиРасходов.ПроверитьЗаполнениеАналитик(
		ЭтотОбъект, Новый Структура("РасшифровкаПлатежа"), МассивНепроверяемыхРеквизитов, Отказ, ДополнительныеПараметры);

		Для каждого Ошибка Из ДополнительныеПараметры.Ошибки.СписокОшибок Цикл
			ДенежныеСредстваСервер.ДобавитьОшибкуЗаполнения(
			ОшибкиЗаполнения, Ошибка.ТекстДляНесколькихОшибок);
		КонецЦикла;
	КонецЕсли;

	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет
		И Не ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихАктивовПассивов") Тогда
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.СтатьяРасходов");
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.АналитикаРасходов");
		МассивНепроверяемыхРеквизитов.Добавить("РасшифровкаПлатежа.АналитикаАктивовПассивов");
	КонецЕсли;

	Если Не ФлагОбменСБанками Тогда
		ДенежныеСредстваСервер.ПроверитьЗаполнениеПартнера(
		ЭтотОбъект, ХозяйственнаяОперация, МассивНепроверяемыхРеквизитов, РасшифровкаБезРазбиения, Отказ);
	КонецЕсли;

	МассивНепроверяемыхРеквизитов.Добавить("СтатьяРасходов");
	Если ОтражатьКомиссию И СуммаКомиссии<>0 И Не ЗначениеЗаполнено(СтатьяРасходов) Тогда
		ТекстОшибки = НСтр("ru = 'Поле ""Статья расходов"" по эквайринговой комиссии не заполнено';
		|en = 'The ""Expense item"" field for the acquiring commission is required'");
		Если ФлагОбменСБанками Тогда
			ДенежныеСредстваСервер.ДобавитьОшибкуЗаполнения(ОшибкиЗаполнения, ТекстОшибки);
		Иначе
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, "СтатьяРасходов",, Отказ);
		КонецЕсли;
	КонецЕсли;

	Если ЗначениеЗаполнено(БанковскийСчет) Тогда
		ДенежныеСредстваСервер.ПроверитьДоступностьБанковскогоСчета(
		ЭтотОбъект, БанковскийСчет, "БанковскийСчет", ФлагОбменСБанками, ОшибкиЗаполнения);
	КонецЕсли;

	Если ЗначениеЗаполнено(БанковскийСчетКонтрагента)
		И МассивРеквизитовОперации.Найти("БанковскийСчетКонтрагента") <> Неопределено Тогда
		ДенежныеСредстваСервер.ПроверитьДоступностьБанковскогоСчета(
		ЭтотОбъект, БанковскийСчетКонтрагента, "БанковскийСчетКонтрагента", ФлагОбменСБанками, ОшибкиЗаполнения);
	КонецЕсли;

	Если ЗначениеЗаполнено(БанковскийСчетПолучатель)
		И МассивРеквизитовОперации.Найти("БанковскийСчетПолучатель") <> Неопределено Тогда
		ДенежныеСредстваСервер.ПроверитьДоступностьБанковскогоСчета(
		ЭтотОбъект, БанковскийСчетПолучатель, "БанковскийСчетПолучатель", ФлагОбменСБанками, ОшибкиЗаполнения);
	КонецЕсли;

	Если ЗначениеЗаполнено(БанковскийСчетСписанияКомиссии)
		И МассивРеквизитовОперации.Найти("БанковскийСчетСписанияКомиссии") <> Неопределено Тогда
		ДенежныеСредстваСервер.ПроверитьДоступностьБанковскогоСчета(
		ЭтотОбъект, БанковскийСчетСписанияКомиссии, "БанковскийСчетСписанияКомиссии", ФлагОбменСБанками, ОшибкиЗаполнения);
	КонецЕсли;

	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);

	ПараметрыВыбораСтатейИАналитик = Документы.СписаниеБезналичныхДенежныхСредств.ПараметрыВыбораСтатейИАналитик(ХозяйственнаяОперация);
	ДоходыИРасходыСервер.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, ПараметрыВыбораСтатейИАналитик);

	ИсправлениеДокументов.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);

	ДенежныеСредстваСервер.ПроверитьЗаполнениеРасшифровкиПлатежа(
	ЭтотОбъект, СуммаДокумента, ХозяйственнаяОперация, Отказ, ФлагОбменСБанками, ОшибкиЗаполнения);

	Если Не ФлагОбменСБанками Тогда
		ДенежныеСредстваСервер.ПроверитьЗаявкуНаРасходованиеДенежныхСредств(ЭтотОбъект, Отказ);
		ДенежныеСредстваСервер.ПроверитьРаспоряжениеНаПеремещениеДенежныхСредств(ЭтотОбъект, Отказ);
	КонецЕсли;

	ДенежныеСредстваСервер.ПроверитьБанковскийСчетПолучатель(ЭтотОбъект, Отказ, ФлагОбменСБанками, ОшибкиЗаполнения);
	ДенежныеСредстваСервер.ПроверитьВалютуКонвертации(ЭтотОбъект, Отказ, ФлагОбменСБанками, ОшибкиЗаполнения);

	ДенежныеСредстваСервер.ПроверитьЛицевыеСчетаСотрудниковПоБанку(ЭтотОбъект, Отказ, ФлагОбменСБанками, ОшибкиЗаполнения);

	Если ОплатаПоЗаявкам
		И ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику
		И СписокФизЛиц Тогда

		Заявки = РасшифровкаПлатежа.ВыгрузитьКолонку("ЗаявкаНаРасходованиеДенежныхСредств");

		Если Заявки.Количество() <> 0 Тогда
			ДенежныеСредстваСервер.ПроверитьЗаполнениеЛицевыхСчетовПоИсточникам(
			ЭтотОбъект, "ЛицевыеСчетаСотрудников", Заявки, Отказ, ФлагОбменСБанками, ОшибкиЗаполнения);
		КонецЕсли;

	КонецЕсли;

	СписаниеБезналичныхДенежныхСредствЛокализация.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);

	Если РасшифровкаБезРазбиения И Не ФлагОбменСБанками Тогда
		ДенежныеСредстваСервер.ПроверитьЗаполнениеРасшифровкиБезРазбиения(
		ЭтотОбъект, ПроверяемыеРеквизиты, "РасшифровкаПлатежа", "РасшифровкаБезРазбиения", Отказ);
	КонецЕсли;

	Если ФлагОбменСБанками Тогда
		ДенежныеСредстваСервер.ПроверитьЗаполнениеОбязательныхРеквизитов(ЭтотОбъект, ПроверяемыеРеквизиты, ОшибкиЗаполнения);
		ДополнительныеСвойства.Вставить("ОшибкиЗаполнения", СокрЛП(ОшибкиЗаполнения));
		ПроверяемыеРеквизиты.Очистить();
	КонецЕсли;

КонецПроцедуры

