
&ИзменениеИКонтроль("ОбработкаЗаполнения")
Процедура РСК_ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Автор = Пользователи.ТекущийПользователь();

	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда

		Если ДанныеЗаполнения.Свойство("Основание") Тогда

			ЗаполнитьПоПриобретениюТоваров(ДанныеЗаполнения);

		ИначеЕсли ДанныеЗаполнения.Свойство("МассивЗаказов") Тогда

			ЗаполнитьПоЗаказу(ДанныеЗаполнения);

		ИначеЕсли ДанныеЗаполнения.Свойство("АктОРасхождениях") 
			И ДанныеЗаполнения.Свойство("ПеремещениеТоваров") Тогда

			ЗаполнитьДокументНаОснованииАктаОРасхожденияхПослеПеремещения(ДанныеЗаполнения);

		ИначеЕсли ДанныеЗаполнения.Свойство("Товары") Тогда

			ЗаполнитьДокументПоОтбору(ДанныеЗаполнения);

		КонецЕсли;

		Если ДанныеЗаполнения.Свойство("Склад") Тогда
			СкладОтправитель = ДанныеЗаполнения.Склад;
		КонецЕсли;

	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступлениеТоваровНаСклад") Тогда
		ЗаполнитьПоПоступлениюТоваров(ДанныеЗаполнения);

	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда

		СтатусОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения, "Статус");
		Если СтатусОснования <> Перечисления.СтатусыПеремещенийТоваров.Принято Тогда
			ВызватьИсключение СтрШаблон(НСтр(
			"ru = 'Исправление может вводиться только на основании документа в статусе ""%1""';
			|en = 'Correction can only be entered based on the document in ""%1"" status'"), 
			Перечисления.СтатусыПеремещенийТоваров.Принято);
		КонецЕсли;

		ИсправлениеДокументов.ЗаполнитьИсправление(ЭтотОбъект, ДанныеЗаполнения);

	КонецЕсли;

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьОбособленныеПодразделенияВыделенныеНаБаланс") Тогда
		ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПеремещениеТоваров;
	КонецЕсли;

	ПеремещениеТоваровЛокализация.ОбработкаЗаполнения(ЭтотОбъект, ДанныеЗаполнения, СтандартнаяОбработка);

	СтруктураПараметров = ДенежныеСредстваСервер.ПараметрыЗаполненияБанковскогоСчетаОрганизацииПоУмолчанию();
	СтруктураПараметров.Организация = Организация;
	БанковскийСчетОрганизации = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетОрганизацииПоУмолчанию(СтруктураПараметров);

	ЗаполнениеОбъектовПоСтатистике.ЗаполнитьРеквизитыОбъекта(ЭтотОбъект, ДанныеЗаполнения);
	#Вставка
	//++ РС Консалт: Трофимов Евгений 26.01.2023 Тикет 23361
	//e1cib/data/Документ.Задание?ref=84ae847c82d145c74ffef40725e4a17e
	//Трофимов:реквизит «Ответственный» в перемещениях заполняется по статистике использования. Это типовой механизм. Как нужно сделать? Заполнять текущим пользователем?
	//Соболева Ольга: Да, пусть будет текущий пользователь
	Ответственный = ПараметрыСеанса.ТекущийПользователь;
	//-- КонецТикета 23361	
	#КонецВставки

	Документы.ЗаказНаПеремещение.ПроверитьИОчиститьОрганизацию(ЭтотОбъект, ОрганизацияПолучатель);
	Документы.ЗаказНаПеремещение.ПроверитьИОчиститьОрганизацию(ЭтотОбъект, Организация);

	ИнициализироватьДокумент(ДанныеЗаполнения);

	ДополнительныеСвойства.Вставить("НеобходимостьЗаполненияСчетаПриФОИспользоватьНесколькоСчетовЛожь", Ложь);

КонецПроцедуры
