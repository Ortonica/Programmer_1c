
&После("ПередЗаписью")
Процедура РСК_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	//++ РС Консалт: Трофимов Евгений 27.11.2022 Тикет 22072
	//e1cib/data/Документ.Задание?ref=97ca727a83736946450ccf89d8cad748
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	АвтозаполнениеБанковскихДокументов();
	//-- КонецТикета 22072	
КонецПроцедуры

// Заполняет реквизиты по содержимому назначения платежа
//++ РС Консалт: Трофимов Евгений 27.11.2022 Тикет 22072
//e1cib/data/Документ.Задание?ref=97ca727a83736946450ccf89d8cad748
//
Процедура АвтозаполнениеБанковскихДокументов()
	
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ХозяйственнаяОперация <> Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	ПартнерыДополнительныеРеквизиты.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Партнеры.ДополнительныеРеквизиты КАК ПартнерыДополнительныеРеквизиты
		|ГДЕ
		|	ПартнерыДополнительныеРеквизиты.Свойство = &СвойствоB2G
		|	И ПартнерыДополнительныеРеквизиты.Значение В ИЕРАРХИИ(&ГруппаB2G)
		|	И ПартнерыДополнительныеРеквизиты.Ссылка = &Партнер
		|";
	
	Запрос.УстановитьПараметр("Партнер", Партнер);
	Запрос.УстановитьПараметр("СвойствоB2G", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту(
		"Имя",
		"КатегорияB2G_f053eb8ad35a446c9345cdcfa6032883"
	));
	Запрос.УстановитьПараметр("ГруппаB2G", РегистрыСведений.реа_ПредопределенныеЗначения.Значение(
		"Группа доп.реквизита гос.заказчиков b2g",
		Справочники.ЗначенияСвойствОбъектов.НайтиПоНаименованию("ГЗ",Истина,,Запрос.Параметры.СвойствоB2G)
	));
	Если Запрос.Выполнить().Пустой() Тогда
		Возврат; //Это не государственный заказчик
	КонецЕсли;

	ЗапретАвтозамены = РегистрыСведений.реа_ПредопределенныеЗначения.Значение("Запретить автозаполнение банковских документов", Ложь);
	Если ЗапретАвтозамены Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		ЗапретАвтозамены = УправлениеСвойствами.ЗначениеСвойства(Ссылка, "ЗапретитьАвтозаполнение");
		Если ТипЗнч(ЗапретАвтозамены) = Тип("Булево") И ЗапретАвтозамены Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	СтруктураЗаполнения = ПолучитьСтруктуруЗаполнения(нРег(НазначениеПлатежа));
	ХозяйственнаяОперация = СтруктураЗаполнения.ХозяйственнаяОперация;
	
	Если СтруктураЗаполнения.Свойство("ОснованиеПлатежа") Тогда
		Для Каждого Стр Из РасшифровкаПлатежа Цикл
			Стр.ОснованиеПлатежа = СтруктураЗаполнения.ОснованиеПлатежа;
			СтруктураЗаполнения.Свойство("ОбъектРасчетов", Стр.ОбъектРасчетов);
		КонецЦикла;
	КонецЕсли;
	СтруктураЗаполнения.Свойство("Договор", Договор);

КонецПроцедуры


// Получить структуру заполнения
//++ РС Консалт: Трофимов Евгений 27.11.2022 Тикет 22072
//e1cib/data/Документ.Задание?ref=97ca727a83736946450ccf89d8cad748
//
// Параметры:
//  НазнПлатежа	 - Строка - Назначение платежа
// 
// Возвращаемое значение:
//   - Структура - 
//		* НомерПаттерна 		- Число - Номер паттерна, который был использован для определения номера и даты
//		* ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации - Рекомендуемая хозяйственная операция
//		* Номер 				- Строка - Распознанный номер реализации
//		* Дата					- Дата - Распознанная дата реализации
//		* ОснованиеПлатежа		- ДокументСсылка.РеализацияТоваровУслуг - Реализация рекомендуемая для основания платежа
//		* Договор				- СправочникСсылка.ДоговорыКонтрагентов - Договор из реализации
//		* ОбъектРасчетов		- СправочникСсылка.ОбъектыРасчетов - Рекомендуемый объект расчетов для расшифровки платежа, 
//								  найденный по реализации
//
Функция ПолучитьСтруктуруЗаполнения(НазнПлатежа)

	Ответ = Новый Структура;
	Ответ.Вставить("НомерПаттерна",0);
	Если СтрНайти(нРег(НазнПлатежа),"возврат") Тогда
		Ответ.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика);
		Возврат Ответ;
	КонецЕсли;
	Ответ.Вставить("ХозяйственнаяОперация", Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента);

	//Паттерн 1  счету № 4721 от 05.05.2022
	М = РСК_РаботаСоСтроками.Совпадение(НазнПлатежа, "счету?\s*(-фактура)?№*N*\s*(UT-)?\d{1,11}\/*\d*\s*(от)\s*\d{1,2}\.\d{1,2}\.\d{2,4}");
	ДатуИНомерВОтвет(Ответ, М, 1);
	
	//Паттерн 2  счету от 26.05.22 № 5841
	Если НЕ Ответ.Свойство("Номер")	Тогда
		М = РСК_РаботаСоСтроками.Совпадение(НазнПлатежа, "(счет)у*\s*(от)\s*\d{1,2}\.\d{1,2}\.\d{2,4}\s*№*\s*\d{1,11}\/*\d*");
		ДатуИНомерВОтвет(Ответ, М, 2);
	КонецЕсли;

	//Паттерн 3  сч№3915/1 от 21.06.22
	Если НЕ Ответ.Свойство("Номер")	Тогда
		М = РСК_РаботаСоСтроками.Совпадение(НазнПлатежа, "(сч)\.*ф*\s*№*N*\s*\d{2,11}\/*\d*\s*(от)\s*\d{1,2}\.\d{1,2}\.\d{2,4}");
		ДатуИНомерВОтвет(Ответ, М, 3);
	КонецЕсли;
	
	//Паттерн 4  Акт № 6739 от 15.06.2022
	Если НЕ Ответ.Свойство("Номер")	Тогда
		М = РСК_РаботаСоСтроками.Совпадение(НазнПлатежа, "акт№*\s*N*№*\s*\d{2,11}\/*\d*\s*от\s*\d{1,2}\.\d{1,2}\.\d{2,4}");
		ДатуИНомерВОтвет(Ответ, М, 4);
	КонецЕсли;

	//Паттерн 5  УПД 758 от 16.09.22
	Если НЕ Ответ.Свойство("Номер")	Тогда
		М = РСК_РаботаСоСтроками.Совпадение(НазнПлатежа, "УПД\s*№*N*\s*\d{2,11}\/*\d*\s*(от)\s*\d{1,2}\.\d{1,2}\.\d{2,4}");
		ДатуИНомерВОтвет(Ответ, М, 5);
	КонецЕсли;
	
	//Паттерн 6  счет на оплату 901 от 11.10.22
	Если НЕ Ответ.Свойство("Номер")	Тогда
		М = РСК_РаботаСоСтроками.Совпадение(НазнПлатежа, "сч(ет)?у?\.?\s*на опл\.*(ату)*\s*№*N*\s*(UT-)?\d{2,11}\/*\d*\s*(от)\s*\d{1,2}\.\d{1,2}\.\d{2,4}");
		ДатуИНомерВОтвет(Ответ, М, 6);
	КонецЕсли;
	
	//Паттерн 7  документ о приемке(счет-фактура) 9674 от09.08.22
	Если НЕ Ответ.Свойство("Номер")	Тогда
		М = РСК_РаботаСоСтроками.Совпадение(НазнПлатежа, "док\.?(умент)?у?\s*о приемке(\(счет-фактура\))?\s*№*N*\s*(UT-)*\d{1,11}\/*\d*\s*(от)\s*\d{1,2}\.\d{1,2}\.\d{2,4}");
		ДатуИНомерВОтвет(Ответ, М, 7);
	КонецЕсли;
	
	//Паттерн 8  накл. 8095 от 19.07.2022
	Если НЕ Ответ.Свойство("Номер")	Тогда
		М = РСК_РаботаСоСтроками.Совпадение(НазнПлатежа, нРег("накл(ад)?(ной)?\.*\s*№*N*\s*(от-)?(UT-)?\d{2,11}\/*\d*\s*(от)?\s*\d{1,2}\.\d{1,2}\.\d{2,4}"));
		ДатуИНомерВОтвет(Ответ, М, 8);
	КонецЕсли;
	
	Если НЕ Ответ.Свойство("Номер") ИЛИ СтрДлина(Ответ.Номер) > 6 Тогда
		Возврат Ответ;
	КонецЕсли;

	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеализацияТоваровУслуг.Номер КАК Номер,
		|	РеализацияТоваровУслуг.Дата КАК Дата,
		|	РеализацияТоваровУслуг.Ссылка КАК ОснованиеПлатежа,
		|	РеализацияТоваровУслуг.Договор КАК Договор,
		|	ЕСТЬNULL(ОбъектыРасчетов.Ссылка, ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)) КАК ОбъектРасчетов
		|ИЗ
		|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
		|		ПО РеализацияТоваровУслуг.Ссылка = ОбъектыРасчетов.Объект
		|			И (ОбъектыРасчетов.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом))
		|ГДЕ
		|	НАЧАЛОПЕРИОДА(РеализацияТоваровУслуг.Дата, ГОД) = НАЧАЛОПЕРИОДА(&Дата, ГОД)
		|	И РеализацияТоваровУслуг.Номер ПОДОБНО &Номер
		|	И РеализацияТоваровУслуг.Контрагент = &Контрагент
		|";
	
	Запрос.УстановитьПараметр("Дата", Ответ.Дата);
	Запрос.УстановитьПараметр("Номер", "%"+Прав("000000",6-СтрДлина(Ответ.Номер))+Ответ.Номер);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Ответ.Вставить("ОснованиеПлатежа", Выборка.ОснованиеПлатежа);
		Ответ.Вставить("Договор", Выборка.Договор);
		Ответ.Вставить("ОбъектРасчетов", Выборка.ОбъектРасчетов);
	КонецЕсли;
	
	Возврат Ответ;

КонецФункции // ПолучитьСтруктуруЗаполнения()

// Заполняет дату и номер в структуру ответа
//++ РС Консалт: Трофимов Евгений 27.11.2022 Тикет 22072
//e1cib/data/Документ.Задание?ref=97ca727a83736946450ccf89d8cad748
//
// Параметры:
//  Ответ			 - Структура - см. функцию ПолучитьСтруктуруЗаполнения()
//  МассивСовпадений - Массив - Массив, возвращаемый функцией РСК_РаботаСоСтроками.Совпадение()
//  НомерПаттерна	 - Число - Номер текущего паттерна
//
Процедура ДатуИНомерВОтвет(Ответ, МассивСовпадений, НомерПаттерна)
	
	Если МассивСовпадений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
    НомерИДата = РСК_РаботаСоСтроками.Заменить(МассивСовпадений[0], "\/\d*"); //избавимся от дробей в номере
	М = РСК_РаботаСоСтроками.Совпадение(НомерИДата, "\d{1,2}\.\d{1,2}\.\d{2,4}");
	Если М.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	Ответ.Вставить("Дата", РСК_РаботаСоСтроками.СтрокуВДату(М[0])); 
	Ответ.Вставить("Номер", РСК_РаботаСоСтроками.ПолучитьТолькоЦифры(СтрЗаменить(НомерИДата, М[0], "")));
	Ответ.НомерПаттерна = НомерПаттерна;

КонецПроцедуры // ДатаПослеНомера()
