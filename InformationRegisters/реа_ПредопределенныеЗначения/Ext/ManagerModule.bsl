// Возвращает предопределённое значение по идентификатору.
// Если такого идентификатора нет, то создаст новую запись со значением по умолчанию и вернёт значение по умолчанию.
// ++ РС Консалт: Трофимов Евгений 17.08.2022
//
// Параметры:
//  Идентификатор	 - Строка - Уникальный идентификатор предопределённого значения
//  ПоУмолчанию		 - Любое - Значение по умолчанию
// 
Функция Значение(Идентификатор, ПоУмолчанию = Ложь) Экспорт
	
    УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	реа_ПредопределенныеЗначения.Значение КАК Значение
		|ИЗ
		|	РегистрСведений.реа_ПредопределенныеЗначения КАК реа_ПредопределенныеЗначения
		|ГДЕ
		|	реа_ПредопределенныеЗначения.Идентификатор = &Идентификатор";
	
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Значение;
	КонецЕсли;
	
	Запись = СоздатьМенеджерЗаписи();
	Запись.Идентификатор = Идентификатор;
	Запись.Значение = ПоУмолчанию;
	Запись.Записать();
	
	Возврат ПоУмолчанию;

КонецФункции // Значение()
