
&НаКлиенте
//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17531
//e1cib/data/Документ.Задание?ref=bb163964a2ac42cd49116e65acb2bf5c
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ПараметрыВыполненияКоманды.Источник.ИмяФормы = "Документ.ЗаданиеНаПеревозку.Форма.ФормаДокумента" 
		И ПараметрыВыполненияКоманды.Источник.Модифицированность = Истина Тогда
		
		ПоказатьПредупреждение(,"Нужно записать изменения в документе");
		Возврат;
		
	КонецЕсли;
	
	ЗаданиеПроведено = РСК_ВызовСервера.ЗначениеРеквизитаОбъекта(ПараметрКоманды, "Проведен");
	Если НЕ ЗаданиеПроведено Тогда
		
		ПоказатьПредупреждение(,"Нужно провести документ");
		Возврат;
		
	КонецЕсли;
	
	НезаполненныеАкты = Новый Массив;
	СписокЗаказов = РСК_ВызовСервера.ПолучитьСписокЗаказов(ПараметрКоманды, НезаполненныеАкты);
	
	Отказ = Ложь;
	Для Каждого НезаполненныйАкт Из НезаполненныеАкты Цикл
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			"Необходимо заполнить номенклатуру в Акте выдачи ТСР",
			НезаполненныйАкт,
			"РСК_Номенклатура",,
			Отказ
		);
	КонецЦикла;
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если СписокЗаказов.Количество() = 0 Тогда
		ПоказатьПредупреждение(,"В задании на перевозку нет заполненных строк");
		Возврат;
	КонецЕсли;
	
	Организации = ПолучитьОрганизацииЗаказов(ПараметрКоманды);
	Для Каждого Организация Из Организации Цикл
	
		ФормаЗаказаНаПеремещение = ПолучитьФорму(
			"Документ.ЗаказНаПеремещение.ФормаОбъекта",,
			ПараметрыВыполненияКоманды.Источник,
			Истина, //Не искать ранее открытую форму
			ПараметрыВыполненияКоманды.Окно, 
			ПараметрыВыполненияКоманды.НавигационнаяСсылка
		);
		
		ДанныеФормы = ФормаЗаказаНаПеремещение.Объект;
		ЗаполнитьФормуНаСервере(ДанныеФормы, ПараметрКоманды, Организация);
		КопироватьДанныеФормы(ДанныеФормы, ФормаЗаказаНаПеремещение.Объект);
		ФормаЗаказаНаПеремещение.ОбновитьВидимостьЭлементовПриЗаполненииНаОсновании();
		ФормаЗаказаНаПеремещение.Открыть();
	
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17531
//e1cib/data/Документ.Задание?ref=bb163964a2ac42cd49116e65acb2bf5c
Процедура ЗаполнитьФормуНаСервере(ДанныеФормы, ЗаданиеНаПеревозку, Организация)

	оДок = Документы.ЗаказНаПеремещение.СоздатьДокумент();
	оДок.ДополнительныеСвойства.Вставить("Организация", Организация);
	оДок.Заполнить(ЗаданиеНаПеревозку);
	ЗначениеВДанныеФормы(оДок, ДанныеФормы);

КонецПроцедуры

&НаСервере
Функция ПолучитьОрганизацииЗаказов(ЗаданиеНаПеревозку)

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаказКлиента.Организация КАК Организация
		|ИЗ
		|	Документ.ЗаданиеНаПеревозку.Распоряжения КАК ЗаданиеНаПеревозкуРаспоряжения
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоручениеЭкспедитору.Основания КАК ПоручениеЭкспедиторуОснования
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ЗаказКлиента
		|			ПО ПоручениеЭкспедиторуОснования.Основание = ЗаказКлиента.Ссылка
		|		ПО ЗаданиеНаПеревозкуРаспоряжения.Распоряжение = ПоручениеЭкспедиторуОснования.Ссылка
		|ГДЕ
		|	ЗаданиеНаПеревозкуРаспоряжения.Ссылка = &Ссылка
		|";
	Запрос.УстановитьПараметр("Ссылка", ЗаданиеНаПеревозку);
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Организация");

КонецФункции // ПолучитьОрганизацииЗаказов()

