
&НаКлиенте
//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17531
//e1cib/data/Документ.Задание?ref=bb163964a2ac42cd49116e65acb2bf5c
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ПараметрыВыполненияКоманды.Источник.ИмяФормы = "Документ.ЗаданиеНаПеревозку.Форма.ФормаДокумента" 
		И ПараметрыВыполненияКоманды.Источник.Модифицированность = Истина Тогда
		
		ПоказатьПредупреждение(,"Нужно записать изменения в документе");
		Возврат;
		
	КонецЕсли;
	
	ЗаданиеПроведено = РСК_ВызовСервера.ЗначениеРеквизитаОбъекта(ПараметрКоманды, "Проведен");
	Если НЕ ЗаданиеПроведено Тогда
		
		ПоказатьПредупреждение(,"Нужно провести документ");
		Возврат;
		
	КонецЕсли;
	
	НезаполненныеАкты = Новый Массив;
	СписокЗаказов = РСК_ВызовСервера.ПолучитьСписокЗаказов(ПараметрКоманды, НезаполненныеАкты);
	
	Отказ = Ложь;
	Для Каждого НезаполненныйАкт Из НезаполненныеАкты Цикл
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			"Необходимо заполнить номенклатуру в Акте выдачи ТСР",
			НезаполненныйАкт,
			"РСК_Номенклатура",,
			Отказ
		);
	КонецЦикла;
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если СписокЗаказов.Количество() = 0 Тогда
		ПоказатьПредупреждение(,"В задании на перевозку нет заполненных строк");
		Возврат;
	КонецЕсли;
	
	ФормаЗаказаНаПеремещение = ПолучитьФорму(
		"Документ.ЗаказНаПеремещение.ФормаОбъекта",,
		ПараметрыВыполненияКоманды.Источник,
		ПараметрыВыполненияКоманды.Уникальность,
		ПараметрыВыполненияКоманды.Окно, 
		ПараметрыВыполненияКоманды.НавигационнаяСсылка
	);
	
	ДанныеФормы = ФормаЗаказаНаПеремещение.Объект;
	ЗаполнитьФормуНаСервере(ДанныеФормы, ПараметрКоманды, СписокЗаказов);
	КопироватьДанныеФормы(ДанныеФормы, ФормаЗаказаНаПеремещение.Объект);
	ФормаЗаказаНаПеремещение.Открыть();
	
	
КонецПроцедуры

&НаСервере
//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17531
//e1cib/data/Документ.Задание?ref=bb163964a2ac42cd49116e65acb2bf5c
Процедура ЗаполнитьФормуНаСервере(ДанныеФормы, ЗаданиеНаПеревозку, ЗаказыКлиента)

	оДок = Документы.ЗаказНаПеремещение.СоздатьДокумент();
	оДок.ДополнительныеСвойства.Вставить("ЗаказыКлиента", ЗаказыКлиента);
	оДок.Заполнить(ЗаданиеНаПеревозку);
	ЗначениеВДанныеФормы(оДок, ДанныеФормы);

КонецПроцедуры

