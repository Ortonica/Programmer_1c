//++ РС Консалт: Трофимов Евгений 08.02.2023 Задача 23710
//e1cib/data/Документ.Задание?ref=ba9df9b91fdc6f184025abfa4761a13d
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	Если ПараметрыВыполненияКоманды.Источник.ИмяФормы = "Документ.ПроизводствоБезЗаказа.Форма.ФормаДокумента" 
		И ПараметрыВыполненияКоманды.Источник.Модифицированность = Истина Тогда
		
		ПоказатьПредупреждение(,"Нужно записать изменения в документе");
		Возврат;
		
	КонецЕсли;

	ФормаЗаполняемогоДокумента = ПолучитьФорму(
		"Документ.ПриходныйОрдерНаТовары.ФормаОбъекта",,
		ПараметрыВыполненияКоманды.Источник,
		ПараметрыВыполненияКоманды.Уникальность,
		ПараметрыВыполненияКоманды.Окно, 
		ПараметрыВыполненияКоманды.НавигационнаяСсылка
	);
	ДанныеФормы = ФормаЗаполняемогоДокумента.Объект;
	ЗаполнитьФормуНаСервере(ДанныеФормы, ПараметрКоманды);
	КопироватьДанныеФормы(ДанныеФормы, ФормаЗаполняемогоДокумента.Объект);
	ФормаЗаполняемогоДокумента.Открыть();
	ФормаЗаполняемогоДокумента.ОбновитьВидимостьЭлементовПослеЗаполненияДокумента();
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 08.02.2023 Задача 23710
//e1cib/data/Документ.Задание?ref=ba9df9b91fdc6f184025abfa4761a13d
&НаСервере
Процедура ЗаполнитьФормуНаСервере(ДанныеФормы, СсылкаНаДокументОснование)
	УстановитьПривилегированныйРежим(Истина);
	оДок = Документы.ПриходныйОрдерНаТовары.СоздатьДокумент();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЕСТЬNULL(ПроизводствоБезЗаказаВыходныеИзделия.Получатель, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)) КАК Склад,
		|	ПроизводствоБезЗаказа.Ссылка КАК Распоряжение,
		|	ПроизводствоБезЗаказа.Дата КАК ДатаПоступления,
		|	ПроизводствоБезЗаказа.Подразделение КАК Отправитель,
		|	ПроизводствоБезЗаказа.Дата КАК ДатаВходящегоДокумента,
		|	ПроизводствоБезЗаказа.Номер КАК НомерВходящегоДокумента,
		|	ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПриемкаПродукцииИзПроизводства) КАК СкладскаяОперация,
		|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции) КАК ХозяйственнаяОперация
		|ИЗ
		|	Документ.ПроизводствоБезЗаказа КАК ПроизводствоБезЗаказа
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ПроизводствоБезЗаказаВыходныеИзделия
		|		ПО ПроизводствоБезЗаказа.Ссылка = ПроизводствоБезЗаказаВыходныеИзделия.Ссылка
		|			И (ПроизводствоБезЗаказаВыходныеИзделия.НомерСтроки = 1)
		|ГДЕ
		|	ПроизводствоБезЗаказа.Ссылка = &Ссылка
		|";
	Запрос.УстановитьПараметр("Ссылка", СсылкаНаДокументОснование);	
	ДанныеЗаполнения = РСК_ВызовСервера.ЗапросВСтруктуру(Запрос);
	
	оДок.Заполнить(ДанныеЗаполнения);
	оДок.Дата = ТекущаяДатаСеанса();
	ЗначениеВДанныеФормы(оДок, ДанныеФормы);
КонецПроцедуры

