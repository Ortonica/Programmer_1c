
&НаКлиенте
//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17531
//e1cib/data/Документ.Задание?ref=bb163964a2ac42cd49116e65acb2bf5c
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если ПараметрыВыполненияКоманды.Источник.ИмяФормы = "Документ.ЗаданиеНаПеревозку.Форма.ФормаДокумента" 
		И ПараметрыВыполненияКоманды.Источник.Модифицированность = Истина Тогда
		
		ПоказатьПредупреждение(,"Нужно записать изменения в документе");
		Возврат;
		
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ДокументОснование", ПараметрКоманды);
	НезаполненныеАкты = Новый Массив;
	СписокЗаказов = РСК_ВызовСервера.ПолучитьСписокЗаказов(ПараметрКоманды, НезаполненныеАкты);
	
	Отказ = Ложь;
	Для Каждого НезаполненныйАкт Из НезаполненныеАкты Цикл
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			"Необходимо заполнить номенклатуру в Акте выдачи ТСР",
			НезаполненныйАкт,
			"РСК_Номенклатура",,
			Отказ
		);
	КонецЦикла;
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если СписокЗаказов.Количество() = 0 Тогда
		ПоказатьПредупреждение(,"В задании на перевозку нет заполненных строк");
		Возврат;
	ИначеЕсли СписокЗаказов.Количество() = 1 Тогда
		ПараметрыФормы.Вставить("Контракт", СписокЗаказов[0].Значение);
		ОткрытьФорму(
			"Документ.РСК_ОтчетПоВыдачеТСР.ФормаОбъекта", 
			ПараметрыФормы, 
			ПараметрыВыполненияКоманды.Источник,
			ПараметрыВыполненияКоманды.Уникальность,
			ПараметрыВыполненияКоманды.Окно,
			ПараметрыВыполненияКоманды.НавигационнаяСсылка
		);
	Иначе
		ДопПараметры = Новый Структура;
		ДопПараметры.Вставить("ПараметрыФормы", ПараметрыФормы);
		ДопПараметры.Вставить("ПараметрыВыполненияКоманды", ПараметрыВыполненияКоманды);
		Оповещение = Новый ОписаниеОповещения(
			"ПослеВыбораЗаказаПриВводеОтчётаОВыдачеТСР",
			ЭтотОбъект,
			ДопПараметры
		);
		СписокЗаказов.ПоказатьВыборЭлемента(Оповещение, "Выбери заказ"); 
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 17531
//e1cib/data/Документ.Задание?ref=bb163964a2ac42cd49116e65acb2bf5c
Процедура ПослеВыбораЗаказаПриВводеОтчётаОВыдачеТСР(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт

	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ПараметрыФормы = ДополнительныеПараметры.ПараметрыФормы;
	ПараметрыВыполненияКоманды = ДополнительныеПараметры.ПараметрыВыполненияКоманды;
	
	ПараметрыФормы.Вставить("Контракт", ВыбранныйЭлемент.Значение);
	ОткрытьФорму(
		"Документ.РСК_ОтчетПоВыдачеТСР.ФормаОбъекта", 
		ПараметрыФормы, 
		ПараметрыВыполненияКоманды.Источник,
		ПараметрыВыполненияКоманды.Уникальность,
		ПараметрыВыполненияКоманды.Окно,
		ПараметрыВыполненияКоманды.НавигационнаяСсылка
	);

КонецПроцедуры
