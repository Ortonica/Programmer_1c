#Область ТрофимовЕВ_05_07_2022_ВыдачаТСР
//Добавляет возможность редактировать макеты внешних печатных форм в пользовательском режиме
//++ РС Консалт: Трофимов Евгений 05.07.2022 Задача 18246
//e1cib/data/Документ.Задание?ref=8d179e4a99812cbd4be919dded1b36c7

&НаСервере
Процедура РСК_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	Параметры.Свойство("ВариантМакета", РСК_ВариантМакета);
КонецПроцедуры

&НаКлиенте
Процедура РСК_ЗаписатьИЗакрытьВместо(Команда)
	
	Если ЗначениеЗаполнено(РСК_ВариантМакета) Тогда
		ЗаписатьВариантМакета(РСК_ВариантМакета, ТабличныйДокумент);
		Если ТипЗнч(ВладелецФормы) = Тип("ФормаКлиентскогоПриложения") 
			И ВладелецФормы.ИмяФормы = "Справочник.РСК_ВариантыМакетовПечатныхФорм.Форма.ФормаЭлемента" Тогда
			ВладелецФормы.Прочитать();
		КонецЕсли;
		ЗаписьВыполнена = Истина;
		Модифицированность = Ложь;
		Закрыть();
		Возврат;
	КонецЕсли;
	
	ПродолжитьВызов(Команда);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьВариантМакета(ВариантМакета, ТабДок)

	о = ВариантМакета.ПолучитьОбъект();
	о.Макет = Новый ХранилищеЗначения(ТабДок);
	о.Записать();

КонецПроцедуры // ЗаписатьВариантМакета()

&НаКлиенте
Процедура РСК_ЗаписатьВместо(Команда)
	Если ЗначениеЗаполнено(РСК_ВариантМакета) Тогда
		ЗаписатьВариантМакета(РСК_ВариантМакета, ТабличныйДокумент);
		ЗаписьВыполнена = Истина;
		Модифицированность = Ложь;
		Возврат;
	КонецЕсли;
	
	ПродолжитьВызов(Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура РСК_ПередЗакрытиемВместо(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(РСК_ВариантМакета) Тогда
		Если ЗавершениеРаботы Тогда
			Возврат;
		КонецЕсли;
		Если Модифицированность Тогда
			Отказ = Истина;
			ПоказатьВопрос(
				Новый ОписаниеОповещения("ПослеВопросаОСохраненииИзменений", ЭтаФорма), 
				"Записать изменения?", 
				РежимДиалогаВопрос.ДаНетОтмена
			);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	ПродолжитьВызов(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ПослеВопросаОСохраненииИзменений(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		РСК_ЗаписатьИЗакрытьВместо(Неопределено);
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

//-- КонецЗадачи 18246
#КонецОбласти