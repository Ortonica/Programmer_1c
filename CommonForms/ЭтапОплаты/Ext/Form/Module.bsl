
&НаСервере
Процедура РСК_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	//++ РС Консалт: Трофимов Евгений 03.03.2023 Задача 23985
	//e1cib/data/Документ.Задание?ref=b37a5a78714d889647e1e0192b7e7134
	Если Параметры.Свойство("ХозяйственнаяОперация") 
		И (Параметры.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути
		ИЛИ Параметры.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути) Тогда
		Элементы.ВариантОтсчета.СписокВыбора.Добавить(Перечисления.ВариантыОтсчетаДатыПлатежа.ДоДатыРазмещения);
		Элементы.ВариантОтсчета.СписокВыбора.Добавить(Перечисления.ВариантыОтсчетаДатыПлатежа.ДоДатыОкончанияПроизводства);
		Элементы.ВариантОтсчета.СписокВыбора.Добавить(Перечисления.ВариантыОтсчетаДатыПлатежа.ОтДатыОкончанияПроизводства);
		Элементы.ВариантОтсчета.СписокВыбора.Добавить(Перечисления.ВариантыОтсчетаДатыПлатежа.ДоДатыПрибытияВПорт);
	КонецЕсли;
	Параметры.Свойство("ДатаРазмещения", ДатаРазмещения)
	//-- КонецЗадачи 23985
КонецПроцедуры

&НаСервере
&ИзменениеИКонтроль("УстановитьВидимостьЭлементов")
Процедура РСК_УстановитьВидимостьЭлементов()

	Элементы.ДатаПлатежа.Видимость = ВариантОтсчета = Перечисления.ВариантыОтсчетаДатыПлатежа.ОтДатыЗаказа
	#Вставка
	//++ РС Консалт: Трофимов Евгений 03.03.2023 Задача 23985
	//e1cib/data/Документ.Задание?ref=b37a5a78714d889647e1e0192b7e7134
	ИЛИ ВариантОтсчета = Перечисления.ВариантыОтсчетаДатыПлатежа.ДоДатыРазмещения
	//-- КонецЗадачи 23985
	#КонецВставки
	ИЛИ ВариантОтсчета = Перечисления.ВариантыОтсчетаДатыПлатежа.ОтДатыСогласования;

	Если НЕ Элементы.ДатаПлатежа.Видимость Тогда
		ДатаПлатежа = Дата(1,1,1);
	КонецЕсли;

	Элементы.СуммаЗалогаЗаТару.Видимость = ЕстьТара;
	Элементы.ПроцентЗалогаЗаТару.Видимость = ЕстьТара;
	Элементы.ВалютаЗалогаЗаТару.Видимость = ЕстьТара;

	Если ВариантОтсчета = Перечисления.ВариантыОтсчетаДатыПлатежа.ОтДатыОтгрузки
		#Вставка
		//++ РС Консалт: Трофимов Евгений 03.03.2023 Задача 23985
		//e1cib/data/Документ.Задание?ref=b37a5a78714d889647e1e0192b7e7134
		ИЛИ ВариантОтсчета = Перечисления.ВариантыОтсчетаДатыПлатежа.ДоДатыОкончанияПроизводства
		ИЛИ ВариантОтсчета = Перечисления.ВариантыОтсчетаДатыПлатежа.ОтДатыОкончанияПроизводства
		ИЛИ ВариантОтсчета = Перечисления.ВариантыОтсчетаДатыПлатежа.ДоДатыПрибытияВПорт
		//-- КонецЗадачи 23985
		#КонецВставки
		ИЛИ ВариантОтсчета = Перечисления.ВариантыОтсчетаДатыПлатежа.ОтДатыПереходаПраваСобственности Тогда
		Элементы.ВариантОплаты.ТолькоПросмотр = Истина;
	Иначе
		Элементы.ВариантОплаты.ТолькоПросмотр = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура РСК_СдвигПриИзмененииПеред(Элемент)
	//++ РС Консалт: Трофимов Евгений 03.03.2023 Задача 23985
	//e1cib/data/Документ.Задание?ref=b37a5a78714d889647e1e0192b7e7134
	Если ВариантОтсчета = ПредопределенноеЗначение("Перечисление.ВариантыОтсчетаДатыПлатежа.ДоДатыРазмещения") Тогда
		ДатаПереходаПраваСобственности = ДатаРазмещения;
	КонецЕсли;
	//-- КонецЗадачи 23985
КонецПроцедуры

&НаКлиенте
Процедура РСК_ВариантОтсчетаПриИзмененииПеред(Элемент)
	//++ РС Консалт: Трофимов Евгений 03.03.2023 Задача 23985
	//e1cib/data/Документ.Задание?ref=b37a5a78714d889647e1e0192b7e7134
	Если ВариантОтсчета = ПредопределенноеЗначение("Перечисление.ВариантыОтсчетаДатыПлатежа.ДоДатыРазмещения") Тогда
		ДатаПереходаПраваСобственности = ДатаРазмещения;
	КонецЕсли;
	//-- КонецЗадачи 23985
КонецПроцедуры

&НаКлиенте
Процедура РСК_ПеренестиВместо(Команда)

	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("ВариантОплаты",         ВариантОплаты);
	Если ВариантОтсчета = ПредопределенноеЗначение("Перечисление.ВариантыОтсчетаДатыПлатежа.ОтДатыЗаказа")
		//++ РС Консалт: Трофимов Евгений 03.03.2023 Задача 23985
		//e1cib/data/Документ.Задание?ref=b37a5a78714d889647e1e0192b7e7134
		ИЛИ ВариантОтсчета = ПредопределенноеЗначение("Перечисление.ВариантыОтсчетаДатыПлатежа.ДоДатыРазмещения")
		//-- КонецЗадачи 23985
		ИЛИ ВариантОтсчета = ПредопределенноеЗначение("Перечисление.ВариантыОтсчетаДатыПлатежа.ОтДатыСогласования") Тогда
		СтруктураРезультата.Вставить("ДатаПлатежа",           ДатаПлатежа);
	Иначе
		СтруктураРезультата.Вставить("ДатаПлатежа",           Дата(1,1,1));
	КонецЕсли;
	СтруктураРезультата.Вставить("ПроцентПлатежа",        ПроцентПлатежа);
	СтруктураРезультата.Вставить("СуммаПлатежа",          СуммаПлатежа);
	СтруктураРезультата.Вставить("ВариантОтсчета",        ВариантОтсчета);
	СтруктураРезультата.Вставить("Сдвиг",                 Сдвиг);
	СтруктураРезультата.Вставить("СуммаЗалогаЗаТару",     СуммаЗалогаЗаТару);
	СтруктураРезультата.Вставить("ПроцентЗалогаЗаТару",   ПроцентЗалогаЗаТару);

	Если Операция = "Изменение" Тогда
		СтруктураРезультата.Вставить("ВариантОплатыПередИзменением",         ВариантОплатыПередИзменением);
		СтруктураРезультата.Вставить("ВариантОтсчетаПередИзменением",        ВариантОтсчетаПередИзменением);
		СтруктураРезультата.Вставить("СдвигПередИзменением",                 СдвигПередИзменением);
	КонецЕсли;

	Закрыть(СтруктураРезультата);

КонецПроцедуры
