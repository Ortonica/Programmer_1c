//++ РС Консалт: Трофимов Евгений 04.08.2022 Задача 19394
//e1cib/data/Документ.Задание?ref=a08e5944efdf2c724d9f9d3c61ee1f3b
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбщегоНазначенияУТ.НастроитьПодключаемоеОборудование(ЭтаФорма);
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 04.08.2022 Задача 19394
//e1cib/data/Документ.Задание?ref=a08e5944efdf2c724d9f9d3c61ee1f3b
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(Неопределено, ЭтаФорма, "СканерШтрихкода");
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 04.08.2022 Задача 19394
//e1cib/data/Документ.Задание?ref=a08e5944efdf2c724d9f9d3c61ee1f3b
&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтаФорма);
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 04.08.2022 Задача 19394
//e1cib/data/Документ.Задание?ref=a08e5944efdf2c724d9f9d3c61ee1f3b
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если Источник = "ПодключаемоеОборудование" Тогда
		Если ИмяСобытия = "ScanData" Тогда
			//Слушаем только при закрытых окнах
			Если НЕ РСК_Клиент.ВсеОкнаЗакрыты() Тогда
				Возврат;
			КонецЕсли;
			ОчиститьСообщения();
			
			РСК_РаботаСоСканером.УстановитьВремяПоследнегоСобытия();
			МассивШК = ПодключаемоеОборудованиеУТКлиент.ПреобразоватьДанныеСоСканераВМассив(Параметр);
			Если МассивШК.Количество() = 1 Тогда
				Если Лев(МассивШК[0].Штрихкод,2) = "20" Тогда // Это пользователь
					АвторизованныйКладовщик = ПользовательПоШК(МассивШК[0].Штрихкод);
					РСК_РаботаСоСканером.УстановитьПараметрСеансаАвторизованныйКладовщик(АвторизованныйКладовщик);
					Если ЗначениеЗаполнено(АвторизованныйКладовщик) Тогда
						ОткрытьФорму("ЖурналДокументов.СкладскиеОрдера.ФормаСписка");
					Иначе
						Сообщить("Пользователь по штрихкоду "+МассивШК[0].Штрихкод+" не найден!");
					КонецЕсли;
				Иначе
					СсылкаНаДокумент = СсылкаНаДокументПоШтрихкоду(МассивШК[0].Штрихкод);
					Если ЗначениеЗаполнено(СсылкаНаДокумент) Тогда
						Отказ = Ложь;
						ПроверитьВозможностьИзменятьОткрываемыйДокумент(СсылкаНаДокумент, Отказ);
						Если Отказ Тогда
							Возврат;
						КонецЕсли;
						Если ТипЗнч(СсылкаНаДокумент) = Тип("ДокументСсылка.ЗаданиеНаПеревозку") Тогда
							Парам = Новый Структура;
							Парам.Вставить("Ключ", СсылкаНаДокумент);
							Парам.Вставить("РС_ФормаОткрытаПоШтрихкоду", Истина);
							ОткрытьФорму("Документ.ЗаданиеНаПеревозку.ФормаОбъекта", Парам);
						КонецЕсли;
					Иначе
						Сообщить("Документ по штрихкоду "+МассивШК[0].Штрихкод+" не найден!");
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;							
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 04.08.2022 Задача 19394
//e1cib/data/Документ.Задание?ref=a08e5944efdf2c724d9f9d3c61ee1f3b
&НаСервере
Функция ПользовательПоШК(ШК) 
	УстановитьПривилегированныйРежим(Истина);
	Возврат РегистрыСведений.ИдентификационныеДанныеПользователей.ПользовательПоШтрихкоду(ШК);
КонецФункции

&НаКлиенте
Функция СсылкаНаДокументПоШтрихкоду(Штрихкод)
	
	Менеджеры = Новый Массив();
	Менеджеры.Добавить(ПредопределенноеЗначение("Документ.ЗаданиеНаПеревозку.ПустаяСсылка"));
	МассивСсылок = ШтрихкодированиеПечатныхФормКлиент.ПолучитьСсылкуПоШтрихкодуТабличногоДокумента(Штрихкод, Менеджеры);
	Если МассивСсылок.Количество() = 0 Тогда
		Возврат Неопределено;
	Иначе
		Возврат МассивСсылок[0];
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ПроверитьВозможностьИзменятьОткрываемыйДокумент(СсылкаНаДокумент, Отказ = Ложь)
	
	Если ТипЗнч(СсылкаНаДокумент) = Тип("ДокументСсылка.ЗаданиеНаПеревозку") Тогда
		
		//Проверка на статус
		ТекущийСтатус = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаДокумент, "Статус");
		Если ТекущийСтатус <> Перечисления.СтатусыЗаданийНаПеревозку.КПогрузке Тогда
			ОбщегоНазначения.СообщитьПользователю(
				"Для установки статуса «Отправлено» к работе принимаются только документы в статусе «К погрузке».
				|У документа "+СсылкаНаДокумент+" на данный момент установлен статус «"+ТекущийСтатус+"»",
				СсылкаНаДокумент,
				"Статус",,
				Отказ
			);
		КонецЕсли;
		
		//Проверка на наличие накладных
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЗаданиеНаПеревозкуРаспоряжения.Распоряжение КАК Распоряжение
			|ИЗ
			|	Документ.ЗаданиеНаПеревозку.Распоряжения КАК ЗаданиеНаПеревозкуРаспоряжения
			|ГДЕ
			|	ЗаданиеНаПеревозкуРаспоряжения.Ссылка = &Ссылка
			|	И ЗаданиеНаПеревозкуРаспоряжения.РСК_Накладная В(&ПустоеЗначение)";
		
		ПустоеЗначение = Новый Массив;
		ПустоеЗначение.Добавить(Неопределено);
		ПустоеЗначение.Добавить(Документы.РеализацияТоваровУслуг.ПустаяСсылка());
		ПустоеЗначение.Добавить(Документы.ПеремещениеТоваров.ПустаяСсылка());
		Запрос.УстановитьПараметр("ПустоеЗначение", ПустоеЗначение);
		Запрос.УстановитьПараметр("Ссылка", СсылкаНаДокумент);
		
		Рез = Запрос.Выполнить();
		Если НЕ Рез.Пустой() Тогда
			ОбщегоНазначения.СообщитьПользователю(
				"Переводить " + СсылкаНаДокумент + " в статус «Отправлено» нельзя!",
				СсылкаНаДокумент,,,
				Отказ
			);
			
			Выборка = Рез.Выбрать();
			Пока Выборка.Следующий() Цикл
				ОбщегоНазначения.СообщитьПользователю(
					"Для "+Выборка.Распоряжение+" не создана накладная",
					Выборка.Распоряжение,,,
					Отказ
				);
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры // ПроверитьВозможностьИзменятьОткрываемыйДокумент()
