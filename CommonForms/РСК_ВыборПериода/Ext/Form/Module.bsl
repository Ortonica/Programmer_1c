#Область ТрофимовЕВ_05_07_2022
//Просто удобный выбор периода

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Параметры.Свойство("ДатаНачала", ДатаНачала);
	Параметры.Свойство("ДатаОкончания", ДатаОкончания);
КонецПроцедуры

&НаКлиенте
Процедура ДатаВыбораПриАктивизацииДаты(Элемент)
	Даты = Элементы.ДатаВыбора.ВыделенныеДаты;
	Если ЭтоВторойКлик Тогда
		ДатаОкончания = Даты[Даты.ВГраница()];
		Если ДатаОкончания < ДатаНачала Тогда
			ВременнаяДата = ДатаОкончания;
			ДатаОкончания = ДатаНачала;
			ДатаНачала = ВременнаяДата;
		КонецЕсли;
		ВыделитьУказанныйДиапазон();
		ЭтоВторойКлик = Ложь;
	Иначе
		ДатаНачала = Даты[0];
		ДатаОкончания = Даты[Даты.ВГраница()];
		Если ДатаНачала = ДатаОкончания Тогда
			ЭтоВторойКлик = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыделитьУказанныйДиапазон()

	Элементы.ДатаВыбора.ВыделенныеДаты.Очистить();
	ВременнаяДата = ДатаНачала;
	Пока ВременнаяДата <= ДатаОкончания Цикл
		Элементы.ДатаВыбора.ВыделенныеДаты.Добавить(ВременнаяДата);
		ВременнаяДата = ВременнаяДата + 86400;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ЗначениеЗаполнено(ДатаНачала) И ЗначениеЗаполнено(ДатаОкончания) Тогда
		ВыделитьУказанныйДиапазон();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	СП = Новый СтандартныйПериод;
	СП.ДатаНачала = ДатаНачала;
	СП.ДатаОкончания = КонецДня(ДатаОкончания);
	ОповеститьОВыборе(СП);
КонецПроцедуры

#КонецОбласти