
&ИзменениеИКонтроль("ТекстыЗапросовПроведенияДокументов")
Функция РСК_ТекстыЗапросовПроведенияДокументов(ПолноеИмяДокумента, ИмяРегистра)

	Регистры = Новый Структура(ИмяРегистра);
	ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	ДопПараметры.ПолучитьТекстыЗапроса = Истина;
	ИмяДокумента = СтрРазделить(ПолноеИмяДокумента, ".")[1];

	//++ Локализация
	Если ПолноеИмяДокумента = "Документ.МаркировкаТоваровГИСМ" Тогда

		ТекстыЗапроса = МаркировкаТоваровГИСМУТ.ДанныеДокументаДляПроведения(
		Тип("ДокументСсылка.МаркировкаТоваровГИСМ"), Регистры, ДопПараметры);

	ИначеЕсли ПолноеИмяДокумента = "Документ.ПеремаркировкаТоваровГИСМ" Тогда

		ТекстыЗапроса = МаркировкаТоваровГИСМУТ.ДанныеДокументаДляПроведения(
		Тип("ДокументСсылка.ПеремаркировкаТоваровГИСМ"), Регистры, ДопПараметры);

	Иначе
		//-- Локализация

		#Вставка
		//++ РС Консалт: Трофимов Евгений 13.01.2023 Тикет 22980
		//e1cib/data/Документ.Задание?ref=81529cd3f0b97b57465a80b14c432f11
		//Выявляем документы без функции ДанныеДокументаДляПроведения
		Попытка
		//-- КонецТикета 22980		
		#КонецВставки
		ТекстыЗапроса = Документы[ИмяДокумента].ДанныеДокументаДляПроведения(Неопределено, Регистры, ДопПараметры);
		#Вставка
		//++ РС Консалт: Трофимов Евгений 13.01.2023 Тикет 22980
		//e1cib/data/Документ.Задание?ref=81529cd3f0b97b57465a80b14c432f11
		Исключение
			ЗаписьЖурналаРегистрации("Ошибка обновления",УровеньЖурналаРегистрации.Ошибка,,,
				"В модуле менеждера документа "+ИмяДокумента+" отсуствует функция «ДанныеДокументаДляПроведения»"
			);
			Возврат "";
		КонецПопытки;
		//-- КонецТикета 22980		
		#КонецВставки

		//++ Локализация
	КонецЕсли;
	//-- Локализация

	Тексты = Новый Массив();
	Для Каждого Элемент Из ТекстыЗапроса Цикл
		Если Элемент.Представление = ИмяРегистра Тогда
			Тексты.Добавить(Элемент.Значение);
		КонецЕсли;
	КонецЦикла;

	ТекстЗапроса = СтрСоединить(Тексты, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ".Ссылка) = &Ссылка", ".Ссылка.Проведен)");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ".Ссылка В (&Ссылка)", ".Ссылка.Проведен");
	Возврат ТекстЗапроса;

КонецФункции
