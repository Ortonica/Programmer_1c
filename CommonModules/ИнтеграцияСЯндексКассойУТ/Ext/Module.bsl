
&ИзменениеИКонтроль("ТекстЗапросаЗаказКлиента")
Функция РСК_ТекстЗапросаЗаказКлиента()

	Возврат

	"ВЫБРАТЬ
	|	ЗаказКлиента.Ссылка КАК Основание,
	|	ЗаказКлиента.НалогообложениеНДС КАК НалогообложениеНДС	
	|ПОМЕСТИТЬ ОснованияПлатежа
	|ИЗ
	|	Документ.ЗаказКлиента КАК ЗаказКлиента
	|ГДЕ
	|	ЗаказКлиента.Ссылка = &Ссылка	
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.НомерСтроки            КАК НомерСтроки,    
	|	ТаблицаДокумента.Номенклатура           КАК Номенклатура,  
	#Вставка
	|	ТаблицаДокумента.НоменклатураПартнера   КАК НоменклатураПартнера, 
	#КонецВставки
	|	ТаблицаДокумента.Характеристика         КАК Характеристика,
	|	ТаблицаДокумента.Упаковка               КАК Упаковка,
	|	ТаблицаДокумента.КоличествоУпаковок     КАК Количество,
	|	ТаблицаДокумента.Цена                   КАК Цена,
	|	ТаблицаДокумента.СтавкаНДС              КАК СтавкаНДС,
	|	ТаблицаДокумента.СуммаСНДС              КАК Сумма,
	|	ТаблицаДокумента.Ссылка.Валюта          КАК Валюта,
	|	ТаблицаДокумента.Ссылка.ЦенаВключаетНДС КАК ЦенаВключаетНДС
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И НЕ ТаблицаДокумента.Отменено
	|	И (ТаблицаДокумента.Номенклатура.ВидНоменклатуры.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|			ИЛИ ТаблицаДокумента.Ссылка.ТребуетсяЗалогЗаТару)";

КонецФункции

&ИзменениеИКонтроль("ТекстЗапросаТоваровДокумента")
Функция РСК_ТекстЗапросаТоваровДокумента(ДокументСсылка)

	ТекстСводныеДанные = 

	"ВЫБРАТЬ
	|	ДокументОснованиеПлатежа.Номер КАК Номер,
	|	ДокументОснованиеПлатежа.Дата КАК Дата,
	|	ДокументОснованиеПлатежа.Организация КАК Организация,
	|	ДокументОснованиеПлатежа.Контрагент КАК Контрагент,
	|	ДокументОснованиеПлатежа.Контрагент.ИНН КАК КонтрагентИНН,
	|	ДокументОснованиеПлатежа.Контрагент.НаименованиеПолное КАК КонтрагентНаименованиеПолное,
	|	ДокументОснованиеПлатежа.Организация.НаименованиеПолное КАК ОрганизацияНаименованиеПолное,
	|	ДокументОснованиеПлатежа.Организация.ИНН КАК ОрганизацияИНН,
	|	ДокументОснованиеПлатежа.Организация.КПП КАК ОрганизацияКПП,
	|	ДокументОснованиеПлатежа.БанковскийСчет.НомерСчета КАК БанковскийСчетНомерСчета,
	|	ДокументОснованиеПлатежа.БанковскийСчет.Банк.Наименование КАК БанкНаименование,
	|	ДокументОснованиеПлатежа.БанковскийСчет.Банк.Код КАК БанкБИК,
	|	ДокументОснованиеПлатежа.БанковскийСчет.Банк.КоррСчет КАК БанкКоррСчет,
	|	ДокументОснованиеПлатежа.СуммаДокумента КАК Сумма,
	|	ДокументОснованиеПлатежа.БанковскийСчет КАК БанковскийСчет,
	|	ДокументОснованиеПлатежа.Валюта КАК Валюта
	|ИЗ
	|	&ДокументОснованиеПлатежа КАК ДокументОснованиеПлатежа
	|ГДЕ
	|	ДокументОснованиеПлатежа.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	УчетнаяПолитикаОрганизацийСрезПоследних.ПрименяетсяУСН КАК ПрименяетсяУСН,
	|	НастройкиУчетаУСНСрезПоследних.ПрименяетсяУСНДоходыМинусРасходы КАК ПрименяетсяУСНДоходыМинусРасходы
	|ПОМЕСТИТЬ УчетнаяПолитика
	|ИЗ
	|	РегистрСведений.НастройкиСистемыНалогообложения.СрезПоследних(&Период, Организация = ВЫРАЗИТЬ (&Организация КАК Справочник.Организации).ГоловнаяОрганизация) КАК УчетнаяПолитикаОрганизацийСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаУСН.СрезПоследних(&Период, Организация = ВЫРАЗИТЬ (&Организация КАК Справочник.Организации).ГоловнаяОрганизация) КАК НастройкиУчетаУСНСрезПоследних
	|		ПО УчетнаяПолитикаОрганизацийСрезПоследних.Организация = НастройкиУчетаУСНСрезПоследних.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(УчетнаяПолитика.ПрименяетсяУСН, ЛОЖЬ) КАК ПрименяетсяУСН,
	|	ЕСТЬNULL(УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы, ЛОЖЬ) КАК ПрименяетсяУСНДоходыМинусРасходы,
	|	ЕСТЬNULL(ОснованияПлатежа.НалогообложениеНДС, НЕОПРЕДЕЛЕНО) КАК НалогообложениеНДС
	|ИЗ
	|	ОснованияПлатежа КАК ОснованияПлатежа
	|		ЛЕВОЕ СОЕДИНЕНИЕ УчетнаяПолитика КАК УчетнаяПолитика
	|		ПО (ИСТИНА)
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 
	|ВЫБРАТЬ
	#Удаление
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА ТаблицаНоменклатуры.Номенклатура.НаименованиеПолное = """"
	|				ТОГДА ТаблицаНоменклатуры.Номенклатура.Наименование
	|			ИНАЧЕ ТаблицаНоменклатуры.Номенклатура.НаименованиеПолное
	|		КОНЕЦ, """") КАК Наименование,
	#КонецУдаления  
	#Вставка
	|ВЫБОР КОГДА &ЭтоЗаказ ТОГДА выбор когда ТаблицаНоменклатуры.НоменклатураПартнера = Значение(Справочник.НоменклатураКонтрагентов.ПустаяСсылка) тогда
	|ЕСТЬNULL(ВЫБОР
	|			КОГДА ТаблицаНоменклатуры.Номенклатура.НаименованиеПолное = """"
	|				ТОГДА ТаблицаНоменклатуры.Номенклатура.Наименование
	|			ИНАЧЕ ТаблицаНоменклатуры.Номенклатура.НаименованиеПолное
	|		КОНЕЦ, """") иначе ЕСТЬNULL(ТаблицаНоменклатуры.НоменклатураПартнера.Наименование, """") + "" ("" + СТРОКА(ТаблицаНоменклатуры.Номенклатура.Наименование) + "")"" конец ИНАЧЕ
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА ТаблицаНоменклатуры.Номенклатура.НаименованиеПолное = """"
	|				ТОГДА ТаблицаНоменклатуры.Номенклатура.Наименование
	|			ИНАЧЕ ТаблицаНоменклатуры.Номенклатура.НаименованиеПолное
	|		КОНЕЦ, """") КОНЕЦ КАК Наименование,
	#КонецВставки
	#Удаление
	|	ЕСТЬNULL(ТаблицаНоменклатуры.Характеристика.НаименованиеПолное, """") КАК Характеристика,
	#КонецУдаления 
	#Вставка
	|ВЫБОР КОГДА &ЭтоЗаказ ТОГДА выбор когда ТаблицаНоменклатуры.НоменклатураПартнера = Значение(Справочник.НоменклатураКонтрагентов.ПустаяСсылка) тогда ЕСТЬNULL(ТаблицаНоменклатуры.Характеристика.НаименованиеПолное, """") иначе """" конец ИНАЧЕ 
	|	ЕСТЬNULL(ТаблицаНоменклатуры.Характеристика.НаименованиеПолное, """") КОНЕЦ КАК Характеристика,  	
	#КонецВставки
	|	ТаблицаНоменклатуры.Количество КАК Количество,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ТаблицаНоменклатуры.Количество = 0
	|				ТОГДА ТаблицаНоменклатуры.Сумма
	|			ИНАЧЕ ТаблицаНоменклатуры.Сумма / ТаблицаНоменклатуры.Количество
	|		КОНЕЦ КАК ЧИСЛО(31, 2)) КАК Цена,
	|	ТаблицаНоменклатуры.Сумма КАК Сумма,
	|	ТаблицаНоменклатуры.СтавкаНДС КАК СтавкаНДССсылка,	
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаНоменклатуры.СтавкаНДС) КАК СтавкаНДС,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаНоменклатуры.Валюта) КАК Валюта,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.Номенклатура.ПодакцизныйТовар 
	|			ТОГДА 2	
	|		КОГДА ТаблицаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
	|			ТОГДА 1
	|		КОГДА ТаблицаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|			ТОГДА 3
	|		КОГДА ТаблицаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)
	|			ТОГДА 4
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ПредметРасчета,
	|	ЕСТЬNULL(ТаблицаНоменклатуры.Номенклатура.Наименование, """") КАК НаименованиеПолное,
	|	ТаблицаНоменклатуры.Номенклатура.Артикул КАК Артикул,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаНоменклатуры.Номенклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмерения,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаНоменклатуры.Номенклатура.ВидНоменклатуры) КАК ВидНоменклатуры,
	|	ПРЕДСТАВЛЕНИЕ(ТаблицаНоменклатуры.Номенклатура.Родитель) КАК Родитель,
	|	ТаблицаНоменклатуры.НомерСтроки КАК НомерСтроки,
	|	"""" КАК КодТовара,
	|	"""" КАК КодСтраныПроисхожденияТовара,
	|	"""" КАК НомерТаможеннойДекларации,
	|	"""" КАК СуммаАкциза
	|ИЗ
	|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаНоменклатуры.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.НомерСтроки КАК НомерСтроки,
	|	ШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод
	|ИЗ
	|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
	|		ПО ТаблицаНоменклатуры.Номенклатура = ШтрихкодыНоменклатуры.Номенклатура
	|			И ТаблицаНоменклатуры.Характеристика = ШтрихкодыНоменклатуры.Характеристика
	|			И ТаблицаНоменклатуры.Упаковка = ШтрихкодыНоменклатуры.Упаковка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОрганизацииКонтактнаяИнформация.Вид КАК Вид,
	|	ОрганизацииКонтактнаяИнформация.Представление КАК Представление,
	|	ОрганизацииКонтактнаяИнформация.НомерТелефона КАК НомерТелефона
	|ИЗ
	|	Справочник.Организации.КонтактнаяИнформация КАК ОрганизацииКонтактнаяИнформация
	|ГДЕ
	|	ОрганизацииКонтактнаяИнформация.Ссылка = &Организация
	|	И ОрганизацииКонтактнаяИнформация.Вид В(&ВидыКонтактнойИнформации)";

	Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ЗаказКлиента") Тогда
		ТексНоменклатураДокумента = ТекстЗапросаЗаказКлиента(); 
		#Удаление
		ТекстСводныеДанные = СтрЗаменить(ТекстСводныеДанные, "&ДокументОснованиеПлатежа", "Документ.ЗаказКлиента") 
		#КонецУдаления
		#Вставка 
		ТекстСводныеДанные = СтрЗаменить(ТекстСводныеДанные, "&ДокументОснованиеПлатежа", "Документ.ЗаказКлиента");
		ТекстСводныеДанные = СтрЗаменить(ТекстСводныеДанные, "&ЭтоЗаказ", "ИСТИНА");
		#КонецВставки
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.СчетНаОплатуКлиенту") Тогда
		ТексНоменклатураДокумента = ТекстЗапросаСчетНаОплатуКлиенту();  
		#Удаление
		ТекстСводныеДанные = СтрЗаменить(ТекстСводныеДанные, "&ДокументОснованиеПлатежа", "Документ.СчетНаОплатуКлиенту")    
		#КонецУдаления   
		#Вставка 
		ТекстСводныеДанные = СтрЗаменить(ТекстСводныеДанные, "&ДокументОснованиеПлатежа", "Документ.СчетНаОплатуКлиенту");
		ТекстСводныеДанные = СтрЗаменить(ТекстСводныеДанные, "&ЭтоЗаказ", "ЛОЖЬ");
		#КонецВставки
	КонецЕсли;

	Возврат ТексНоменклатураДокумента + " ; " + ТекстСводныеДанные;

КонецФункции

&ИзменениеИКонтроль("ЗаполнитьКонтактнуюИнформациюОснованияПлатежа")
Процедура РСК_ЗаполнитьКонтактнуюИнформациюОснованияПлатежа(Знач ОснованиеПлатежа, КонтактнаяИнформация)
    #Удаление
	Партнер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОснованиеПлатежа, "Партнер");
    #КонецУдаления
	
	#Вставка
	Партнер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОснованиеПлатежа, "Контрагент");
    #КонецВставки
	Если НЕ ЗначениеЗаполнено(Партнер) Тогда
		Возврат;
	КонецЕсли; 

	////////////////////////////////////////////////////////////////////////////////

	ИсточникиКонтактнойИнформации = Новый Массив;
	ИсточникиКонтактнойИнформации.Добавить(Партнер);

	ВидыКонтактнойИнформации = Новый Массив; 
	#Удаление
	ВидыКонтактнойИнформации.Добавить(Справочники.ВидыКонтактнойИнформации.ТелефонПартнера);
	ВидыКонтактнойИнформации.Добавить(Справочники.ВидыКонтактнойИнформации.EmailПартнера); 
	#КонецУдаления
	
	#Вставка
	ВидыКонтактнойИнформации.Добавить(Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента);
	ВидыКонтактнойИнформации.Добавить(Справочники.ВидыКонтактнойИнформации.EmailКонтрагента);
    #КонецВставки
	
	ДанныеПоКонтактнойИнформации = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъектов(
	ИсточникиКонтактнойИнформации,, ВидыКонтактнойИнформации, ТекущаяДатаСеанса());

	Для каждого ЭлементКоллекции Из ДанныеПоКонтактнойИнформации Цикл
		#Удаление
		Если ЭлементКоллекции.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонПартнера Тогда      
		#КонецУдаления 
		#Вставка
		Если ЭлементКоллекции.Вид = Справочники.ВидыКонтактнойИнформации.ТелефонКонтрагента Тогда
		#КонецВставки
			Телефон10Знаков = РозничныеПродажиКлиентСервер.НомерТелефонаВФормате10Знаков(ЭлементКоллекции.Представление);
			Телефон = РозничныеПродажиКлиентСервер.ОтформатироватьНомерТелефона(Телефон10Знаков);
			КонтактнаяИнформация.Телефоны.Добавить(Телефон);  
		#Удаление
		ИначеЕсли ЭлементКоллекции.Вид = Справочники.ВидыКонтактнойИнформации.EmailПартнера Тогда
		#КонецУдаления 
		#Вставка
		ИначеЕсли ЭлементКоллекции.Вид = Справочники.ВидыКонтактнойИнформации.EmailКонтрагента Тогда
		#КонецВставки
			КонтактнаяИнформация.ЭлектроннаяПочта.Добавить(ЭлементКоллекции.Представление);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&Вместо("ПреобразоватьСтавкуНДС")
Функция РСК_ПреобразоватьСтавкуНДС(СтавкаНДС)
	СтавкаНДСЧислом = 1;
	
	Если СтавкаНДС = Справочники.СтавкиНДС.БезНДС Тогда
		СтавкаНДСЧислом = 1;
	ИначеЕсли СтавкаНДС = УчетНДСЛокализация.СтавкаНДСПоПеречислению(Перечисления.СтавкиНДС.НДС0) Тогда
		СтавкаНДСЧислом = 2;
	ИначеЕсли СтавкаНДС = УчетНДСЛокализация.СтавкаНДСПоПеречислению(Перечисления.СтавкиНДС.НДС10) Тогда
		СтавкаНДСЧислом = 3;
	ИначеЕсли СтавкаНДС = УчетНДСЛокализация.СтавкаНДСПоПеречислению(Перечисления.СтавкиНДС.НДС18) 
		ИЛИ СтавкаНДС = УчетНДСЛокализация.СтавкаНДСПоПеречислению(Перечисления.СтавкиНДС.НДС20) Тогда
		СтавкаНДСЧислом = 4;
	ИначеЕсли СтавкаНДС = УчетНДСЛокализация.СтавкаНДСПоПеречислению(Перечисления.СтавкиНДС.НДС10_110) Тогда
		СтавкаНДСЧислом = 5;
	ИначеЕсли СтавкаНДС = УчетНДСЛокализация.СтавкаНДСПоПеречислению(Перечисления.СтавкиНДС.НДС18_118)
		ИЛИ СтавкаНДС = УчетНДСЛокализация.СтавкаНДСПоПеречислению(Перечисления.СтавкиНДС.НДС20_120) Тогда
		СтавкаНДСЧислом = 6;
	КонецЕсли;
	
	Возврат СтавкаНДСЧислом;
	
КонецФункции

&ИзменениеИКонтроль("ПриСозданииФормыНастроекЯндексКассы")
Процедура РСК_ПриСозданииФормыНастроекЯндексКассы(Форма, Группа, Префикс, ДополнительныеНастройки)

	ИспользуемыеНастройки = ОписаниеИспользуемыхНастроек();

	ДобавляемыеРеквизиты = Новый Массив;

	Для каждого ЭлементКоллекции Из ИспользуемыеНастройки Цикл

		Реквизит = Новый РеквизитФормы(
		Префикс + ЭлементКоллекции.Настройка,
		ЭлементКоллекции.ТипЗначения,,
		ЭлементКоллекции.Представление,
		Истина);

		ДобавляемыеРеквизиты.Добавить(Реквизит);
		ДополнительныеНастройки.Вставить(ЭлементКоллекции.Настройка, Реквизит.Имя);
	КонецЦикла;

	Реквизит = Новый РеквизитФормы(Префикс + "ДополнительныеПараметрыФормы", Новый ОписаниеТипов);
	ДобавляемыеРеквизиты.Добавить(Реквизит);

	Реквизит = Новый РеквизитФормы(Префикс + "АналитикаРасходовОбязательна", Новый ОписаниеТипов("Булево"));
	ДобавляемыеРеквизиты.Добавить(Реквизит);

	Реквизит = Новый РеквизитФормы(Префикс + "АналитикаРасходовЗаказРеализация", Новый ОписаниеТипов("Булево"));
	ДобавляемыеРеквизиты.Добавить(Реквизит);

	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);

	// Элементы формы
	РасположениеПолей = Новый Структура;
	РаспределитьРеквизитыПоГруппам(Форма, Группа, Префикс, РасположениеПолей);

	// События элементов
	СобытияЭлементов = Новый Структура;
	СобытияЭлементов.Вставить("СтатьяДвиженияДенежныхСредствОплаты",   "НачалоВыбора");
	СобытияЭлементов.Вставить("СтатьяДвиженияДенежныхСредствВозвраты", "НачалоВыбора");
	СобытияЭлементов.Вставить("АналитикаРасходов",                     "НачалоВыбора, ОбработкаВыбора");
	СобытияЭлементов.Вставить("СтатьяРасходов",                        "ПриИзменении, НачалоВыбора, Очистка");
	СобытияЭлементов.Вставить("Эквайер",                               "Создание");

	// Связи параметров выбора
	СвязиПараметровВыбора = Новый Структура;
	Связи = Новый Массив;
	Связи.Добавить(Новый СвязьПараметраВыбора("Отбор.Владелец", "Организация"));
	СвязиПараметровВыбора.Вставить("БанковскийСчет", Связи);
	Связи = Новый Массив; 
	#Удаление
	Связи.Добавить(Новый СвязьПараметраВыбора("Отбор.Организация", "Организация"));
	#КонецУдаления
	#Вставка 
	Связи.Добавить(Новый СвязьПараметраВыбора("Отбор.Организация", "Объект.Организация"));
	#КонецВставки
	Связи.Добавить(Новый СвязьПараметраВыбора("Отбор.Контрагент", Префикс + "Эквайер"));
	СвязиПараметровВыбора.Вставить("ДоговорЭквайринга", Связи);

	// Проверка заполнения
	РеквизитыДляПроверкиЗаполнения = РеквизитыДляПроверкиЗаполнения();

	Элементы = Форма.Элементы;
	ТипПолеФормы = Тип("ПолеФормы");

	Для каждого ЭлементКоллекции Из ИспользуемыеНастройки Цикл

		ИмяРеквизита = Префикс + ЭлементКоллекции.Настройка;

		Поле = Элементы.Добавить(ИмяРеквизита, ТипПолеФормы, РасположениеПолей[ЭлементКоллекции.Настройка]);
		Поле.ПутьКДанным = ИмяРеквизита;
		Поле.Вид = ВидПоляФормы.ПолеВвода;
		Поле.Подсказка = ЭлементКоллекции.Подсказка;
		Поле.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;

		Если РеквизитыДляПроверкиЗаполнения.НайтиПоЗначению(ЭлементКоллекции.Настройка) <> Неопределено Тогда
			Поле.АвтоОтметкаНезаполненного = Истина;
		КонецЕсли;

		Если СвязиПараметровВыбора.Свойство(ЭлементКоллекции.Настройка) Тогда
			Поле.СвязиПараметровВыбора = Новый ФиксированныйМассив(СвязиПараметровВыбора[ЭлементКоллекции.Настройка]); 
		КонецЕсли;

		Если СобытияЭлементов.Свойство(ЭлементКоллекции.Настройка) Тогда
			МассивСобытий = СтрРазделить(СобытияЭлементов[ЭлементКоллекции.Настройка], ",");
			Для каждого Событие Из МассивСобытий Цикл
				Поле.УстановитьДействие(СокрЛП(Событие), "Подключаемый_Элемент" + СокрЛП(Событие));
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;

	ЗаполнитьРеквизитыФормы(Форма, Префикс);

	ПараметрыВыбораСтатейИАналитик = ПараметрыВыбораСтатейИАналитик(Префикс);
	ДоходыИРасходыСервер.ПриСозданииНаСервере(Форма, ПараметрыВыбораСтатейИАналитик);
	Если ЗначениеЗаполнено(Форма.Объект.Ссылка) Тогда
		// Не переопределения события ПриЧтенииНаСервере, поэтому вызывается здесь.
		ДоходыИРасходыСервер.ПриЧтенииНаСервере(Форма, ПараметрыВыбораСтатейИАналитик);
	КонецЕсли;

КонецПроцедуры

//&ИзменениеИКонтроль("ПреобразоватьСтавкуНДС")
//Функция РСК_ПреобразоватьСтавкуНДС(СтавкаНДС)
//    #Удаление
//	СтавкаНДСЧислом = 1;
//    #КонецУдаления  
//	#Вставка
//	СтавкаНДСЧислом = 0;     
//	#КонецВставки

//	Если СтавкаНДС = Справочники.СтавкиНДС.БезНДС Тогда       
//		#Удаление
//		СтавкаНДСЧислом = 1;     
//		#КонецУдаления 
//		#Вставка
//		СтавкаНДСЧислом = 0;     
//		#КонецВставки
//	ИначеЕсли СтавкаНДС = УчетНДСЛокализация.СтавкаНДСПоПеречислению(Перечисления.СтавкиНДС.НДС0) Тогда
//		#Удаление
//		СтавкаНДСЧислом = 2;     
//		#КонецУдаления 
//		#Вставка
//		СтавкаНДСЧислом = 1;     
//		#КонецВставки
//	ИначеЕсли СтавкаНДС = УчетНДСЛокализация.СтавкаНДСПоПеречислению(Перечисления.СтавкиНДС.НДС10) Тогда
//		#Удаление
//		СтавкаНДСЧислом = 3;     
//		#КонецУдаления 
//		#Вставка
//		СтавкаНДСЧислом = 2;     
//		#КонецВставки
//	ИначеЕсли СтавкаНДС = УчетНДСЛокализация.СтавкаНДСПоПеречислению(Перечисления.СтавкиНДС.НДС18) 
//		ИЛИ СтавкаНДС = УчетНДСЛокализация.СтавкаНДСПоПеречислению(Перечисления.СтавкиНДС.НДС20) Тогда
//		#Удаление
//		СтавкаНДСЧислом = 4;     
//		#КонецУдаления 
//		#Вставка
//		СтавкаНДСЧислом = 3;     
//		#КонецВставки
//	ИначеЕсли СтавкаНДС = УчетНДСЛокализация.СтавкаНДСПоПеречислению(Перечисления.СтавкиНДС.НДС10_110) Тогда
//		#Удаление
//		СтавкаНДСЧислом = 5;     
//		#КонецУдаления 
//		#Вставка
//		СтавкаНДСЧислом = 4;     
//		#КонецВставки
//	ИначеЕсли СтавкаНДС = УчетНДСЛокализация.СтавкаНДСПоПеречислению(Перечисления.СтавкиНДС.НДС18_118)
//		ИЛИ СтавкаНДС = УчетНДСЛокализация.СтавкаНДСПоПеречислению(Перечисления.СтавкиНДС.НДС20_120) Тогда
//		#Удаление
//		СтавкаНДСЧислом = 6;     
//		#КонецУдаления 
//		#Вставка
//		СтавкаНДСЧислом = 5;     
//		#КонецВставки
//	КонецЕсли;

//	Возврат СтавкаНДСЧислом;

//КонецФункции
