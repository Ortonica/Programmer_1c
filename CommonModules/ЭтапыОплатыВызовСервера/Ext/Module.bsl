
&ИзменениеИКонтроль("ДатаПлатежаПоКалендарю")
Функция РСК_ДатаПлатежаПоКалендарю(Параметры)

	Если НЕ ЗначениеЗаполнено(Параметры.ДатаСогласования) Тогда
		Параметры.ДатаСогласования = Параметры.ДатаЗаказа;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(Параметры.ДатаОтгрузки) Тогда
		Параметры.ДатаОтгрузки = Параметры.ДатаЗаказа;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(Параметры.ВариантОтсчета) Тогда
		ДатаПлатежа = Дата(1,1,1);
	ИначеЕсли Параметры.ВариантОтсчета = Перечисления.ВариантыОтсчетаДатыПлатежа.ОтДатыЗаказа И ЗначениеЗаполнено(Параметры.ДатаЗаказа) Тогда
		ДатаПлатежа = ДатаПоКалендарю(Параметры.Календарь, Параметры.ДатаЗаказа, Параметры.Сдвиг);
	ИначеЕсли Параметры.ВариантОтсчета = Перечисления.ВариантыОтсчетаДатыПлатежа.ОтДатыСогласования И ЗначениеЗаполнено(Параметры.ДатаСогласования) Тогда
		ДатаПлатежа = ДатаПоКалендарю(Параметры.Календарь, Параметры.ДатаСогласования, Параметры.Сдвиг);
	ИначеЕсли Параметры.ВариантОтсчета = Перечисления.ВариантыОтсчетаДатыПлатежа.ДоДатыОтгрузки И ЗначениеЗаполнено(Параметры.ДатаОтгрузки) Тогда
		ДатаПлатежа = ДатаПоКалендарю(Параметры.Календарь, Параметры.ДатаОтгрузки, -Параметры.Сдвиг);
	ИначеЕсли Параметры.ВариантОтсчета = Перечисления.ВариантыОтсчетаДатыПлатежа.ОтДатыОтгрузки И ЗначениеЗаполнено(Параметры.ДатаОтгрузки) Тогда
		ДатаПлатежа = ДатаПоКалендарю(Параметры.Календарь, Параметры.ДатаОтгрузки, Параметры.Сдвиг);
	ИначеЕсли Параметры.ВариантОтсчета = Перечисления.ВариантыОтсчетаДатыПлатежа.ОтДатыПереходаПраваСобственности И ЗначениеЗаполнено(Параметры.ДатаПереходаПраваСобственности) Тогда
		ДатаПлатежа = ДатаПоКалендарю(Параметры.Календарь, Параметры.ДатаПереходаПраваСобственности, Параметры.Сдвиг);
	ИначеЕсли Параметры.ВариантОтсчета = Перечисления.ВариантыОтсчетаДатыПлатежа.ОтДатыПереходаПраваСобственности И ЗначениеЗаполнено(Параметры.ДатаОтгрузки) Тогда
		ДатаПлатежа = ДатаПоКалендарю(Параметры.Календарь, Параметры.ДатаОтгрузки + 86400*Параметры.СрокПереходаПраваСобственности, Параметры.Сдвиг);
	#Вставка
	//++ РС Консалт: Трофимов Евгений 03.03.2023 Задача 23985
	//e1cib/data/Документ.Задание?ref=b37a5a78714d889647e1e0192b7e7134
	ИначеЕсли Параметры.ВариантОтсчета = Перечисления.ВариантыОтсчетаДатыПлатежа.ДоДатыРазмещения И ЗначениеЗаполнено(Параметры.ДатаРазмещения) Тогда
		ДатаПлатежа = ДатаПоКалендарю(Параметры.Календарь, Параметры.ДатаРазмещения, -Параметры.Сдвиг);
	ИначеЕсли Параметры.ВариантОтсчета = Перечисления.ВариантыОтсчетаДатыПлатежа.ДоДатыПрибытияВПорт И ЗначениеЗаполнено(Параметры.ДатаПрибытияВПорт) Тогда
		ДатаПлатежа = ДатаПоКалендарю(Параметры.Календарь, Параметры.ДатаПрибытияВПорт, -Параметры.Сдвиг);
	ИначеЕсли Параметры.ВариантОтсчета = Перечисления.ВариантыОтсчетаДатыПлатежа.ДоДатыОкончанияПроизводства И ЗначениеЗаполнено(Параметры.ДатаОкончанияПроизводства) Тогда
		ДатаПлатежа = ДатаПоКалендарю(Параметры.Календарь, Параметры.ДатаОкончанияПроизводства, -Параметры.Сдвиг);
	ИначеЕсли Параметры.ВариантОтсчета = Перечисления.ВариантыОтсчетаДатыПлатежа.ОтДатыОкончанияПроизводства И ЗначениеЗаполнено(Параметры.ДатаОкончанияПроизводства) Тогда
		ДатаПлатежа = ДатаПоКалендарю(Параметры.Календарь, Параметры.ДатаОкончанияПроизводства, Параметры.Сдвиг);
	//-- КонецЗадачи 23985	
	#КонецВставки
	Иначе
		ДатаПлатежа = Дата(1,1,1);
	КонецЕсли;

	Возврат ДатаПлатежа

КонецФункции
