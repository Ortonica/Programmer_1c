
&ИзменениеИКонтроль("УстановитьБлокировкуРегистров")
Процедура РСК_УстановитьБлокировкуРегистров(ДокументОбъект, МенеджерВременныхТаблиц)
	
	#Вставка     
	Если ДокументОбъект.ДополнительныеСвойства.Свойство("НеИспользоватьБлокировкуРегистров") Тогда   
		Возврат;
	КонецЕсли;
	#КонецВставки
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтборыОстатков.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	|ИЗ
	|	ВтОтборыОстатковОрганизаций КАК ОтборыОстатков
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтборыОстатков.ВидЗапасов КАК ВидЗапасов
	|ИЗ
	|	ВтОтборыОстатковОрганизаций КАК ОтборыОстатков
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасов
	|		ПО ОтборыОстатков.ВидЗапасов = ВидыЗапасов.Ссылка
	|ГДЕ
	|	(ВидыЗапасов.ЭтоДубль
	|			ИЛИ ВидыЗапасов.Устаревший)";
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	РезультатЗапросаОтборОстатков			= РезультатЗапроса[0];
	РезультатЗапросаУстаревшиеВидыЗапасов	= РезультатЗапроса[1];
	
	ОбъектДвижения = ?(ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.Сторно"),
						ДокументОбъект.СторнируемыйДокумент.ПолучитьОбъект(),
						ДокументОбъект);
	
	Блокировка = Новый БлокировкаДанных;
	
	Если ЕстьДвижениеВКоллекции(ОбъектДвижения, "ТоварыОрганизаций") Тогда
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ТоварыОрганизаций");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = РезультатЗапросаОтборОстатков;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("АналитикаУчетаНоменклатуры", "АналитикаУчетаНоменклатуры");
		
		Если ЕстьДвижениеВКоллекции(ДокументОбъект,"РезервыТоваровОрганизаций") = Неопределено Тогда
			ТекстИсключения = НСтр("ru = 'Ошибка в описании движений документа: документ является регистратором %Регистр1% и не является регистратором %Регистр2%.';
									|en = 'An error occurred in the document record description: the document is the %Регистр1% recorder and is not the %Регистр2% recorder.'");
			ТекстИсключения = СтрЗаменить(ТекстИсключения, "%Регистр2%", "РезервыТоваровОрганизаций");
			ТекстИсключения = СтрЗаменить(ТекстИсключения, "%Регистр1%", "ТоварыОрганизаций");
			
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РезервыТоваровОрганизаций");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = РезультатЗапросаОтборОстатков;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("АналитикаУчетаНоменклатуры", "АналитикаУчетаНоменклатуры");
		
		Если Не РезультатЗапросаУстаревшиеВидыЗапасов.Пустой() Тогда
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.УстаревшиеВидыЗапасовСОстатками");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.ИсточникДанных = РезультатЗапросаУстаревшиеВидыЗапасов;
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ВидЗапасов", "ВидЗапасов");
		КонецЕсли;
		
	КонецЕсли;

	Если ЕстьДвижениеВКоллекции(ОбъектДвижения, "ТоварыКОформлениюОтчетовКомитенту") Тогда
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ТоварыКОформлениюОтчетовКомитенту");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = РезультатЗапросаОтборОстатков;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("АналитикаУчетаНоменклатуры", "АналитикаУчетаНоменклатуры");
		
	КонецЕсли;
	
	//++ НЕ УТ

	//++ Устарело_Переработка24
	Если ЕстьДвижениеВКоллекции(ОбъектДвижения, "ТоварыПереданныеПереработчику") Тогда
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ТоварыПереданныеПереработчику");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = РезультатЗапросаОтборОстатков;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("АналитикаУчетаНоменклатуры", "АналитикаУчетаНоменклатуры");
		
	КонецЕсли;
	//-- Устарело_Переработка24

	//-- НЕ УТ
	
	Если ЕстьДвижениеВКоллекции(ОбъектДвижения, "ТоварыПереданныеНаКомиссию") Тогда
		
		ЗапросПоКомиссии = Новый Запрос;
		ЗапросПоКомиссии.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОтборыОстатков.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
		|ИЗ
		|	ВтОтборыОстатковОрганизаций КАК ОтборыОстатков
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	АналитикаКомиссионера.КлючАналитики КАК АналитикаУчетаНоменклатуры
		|ИЗ
		|	ВтОтборыОстатковОрганизаций КАК ОтборыОстатков
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
		|		ПО Ключи.Ссылка = ОтборыОстатков.АналитикаУчетаНоменклатуры
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаКомиссионера
		|		ПО АналитикаКомиссионера.Номенклатура = Ключи.Номенклатура
		|			И АналитикаКомиссионера.Характеристика = Ключи.Характеристика
		|			И АналитикаКомиссионера.Серия = Ключи.Серия
		|			И АналитикаКомиссионера.МестоХранения = &Партнер
		|			И АналитикаКомиссионера.Назначение = Ключи.Назначение
		//++ НЕ УТ
		|			И АналитикаКомиссионера.СтатьяКалькуляции = Ключи.СтатьяКалькуляции
		//-- НЕ УТ
		|";
		
		ЗапросПоКомиссии.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		
		Если ТипЗнч(ОбъектДвижения) = Тип("ДокументОбъект.ОтчетПоКомиссииМеждуОрганизациями") Тогда
			ЗапросПоКомиссии.УстановитьПараметр("Партнер", ОбъектДвижения.Комиссионер);
		Иначе
			ЗапросПоКомиссии.УстановитьПараметр("Партнер", ОбъектДвижения.Партнер);
		КонецЕсли;
		
		РезультатЗапросаПоКомиссии = ЗапросПоКомиссии.Выполнить();
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ТоварыПереданныеНаКомиссию");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = РезультатЗапросаПоКомиссии;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("АналитикаУчетаНоменклатуры", "АналитикаУчетаНоменклатуры");
		
	КонецЕсли;
	
	Блокировка.Заблокировать();
	
КонецПроцедуры

&ИзменениеИКонтроль("ПроверкаОтрицательныхОстатковТоваровОрганизаций")
Процедура РСК_ПроверкаОтрицательныхОстатковТоваровОрганизаций(ПараметрыПроверки)

	СписокПолей = Новый СписокЗначений;
	СписокПолей.Добавить("Организация",					НСтр("ru = 'Организация';
	|en = 'Company'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("АналитикаУчетаНоменклатуры",	НСтр("ru = 'Аналитика номенклатуры';
	|en = 'Item dimension'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("ВидЗапасов",					НСтр("ru = 'Вид запасов';
	|en = 'Inventory owner attributes'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("НомерГТД",					НСтр("ru = 'Номер ГТД';
	|en = 'CCD number'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СписокПолей.Добавить("Количество",					НСтр("ru = 'Количество';
	|en = 'Quantity'", ОбщегоНазначения.КодОсновногоЯзыка()));

	ПараметрыРегистрации = ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыРегистрацииПроблемПроверки(
	"ОтрицательныеОстатки",
	НСтр("ru = 'Обнаружены отрицательные остатки товаров организации ""%1"" на конец периода %2';
	|en = 'Negative stock balance of the ""%1"" company goods is detected as of the end of the %2 period'", ОбщегоНазначения.КодОсновногоЯзыка()),
	СписокПолей);

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("НеконтролируемыеТипыЗапасов",             Перечисления.ТипыЗапасов.НеконтролируемыеПоТоварамОрганизацииТипыЗапасов());
	ДополнительныеПараметры.Вставить("ОперацииРаздельногоОформленияЗакупок",    ЗакупкиСервер.ХозяйственныеОперацииРаздельнойЗакупкиБезОтборов());
	ДополнительныеПараметры.Вставить("ИспользуетсяРаздельноеОформлениеЗакупок", ПолучитьФункциональнуюОпцию("ИспользоватьТоварыВПутиОтПоставщиков")  
	ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьНеотфактурованныеПоставки"));
	#Вставка
	ДополнительныеПараметры.Вставить("ТипЗапасовТоварыВПути", Перечисления.ТипыЗапасов.СобственныйТоварВПути);
	ДополнительныеПараметры.Вставить("СкладЗаказа", реа_ОбщийСерверПовтИсп.ПолучитьПредопределенноеЗначение("РС_Склад", "Склад заказа"));
	#КонецВставки

	ЗакрытиеМесяцаСервер.ЗарегистрироватьПроблемыВыполненияПроверки(ПараметрыПроверки,
	ПараметрыРегистрации,
	ЗапросОтрицательныеОстаткиТоваровОрганизаций(),
	ДополнительныеПараметры);

КонецПроцедуры

&ИзменениеИКонтроль("ЗапросОтрицательныеОстаткиТоваровОрганизаций")
Функция РСК_ЗапросОтрицательныеОстаткиТоваровОрганизаций()

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВидЗапасов.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТРазрешенныеВидыЗапасов
	|ИЗ
	|	Справочник.ВидыЗапасов КАК ВидЗапасов
	|ГДЕ
	|	НЕ ВидЗапасов.ТипЗапасов В (&НеконтролируемыеТипыЗапасов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	#Вставка
	|ВЫБРАТЬ
	|	ВидЗапасов.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТВидыЗапасовВПути
	|ИЗ
	|	Справочник.ВидыЗапасов КАК ВидЗапасов
	|ГДЕ
	|	ВидЗапасов.ТипЗапасов = &ТипЗапасовТоварыВПути
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	#КонецВставки
	|ВЫБРАТЬ
	|	ТоварыОрганизаций.Организация КАК Организация,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТоварыОрганизаций.ВидЗапасов КАК ВидЗапасов,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Номенклатура.ВестиУчетПоГТД КАК ВестиУчетПоГТД,
	|	ТоварыОрганизаций.НомерГТД КАК НомерГТД,
	|	&КонецПериода КАК Период,
	|	ТоварыОрганизаций.КоличествоОстаток КАК КоличествоОстаток,
	|	ТоварыОрганизаций.КОформлениюСписанияОстаток КАК КОформлениюСписанияОстаток
	|ПОМЕСТИТЬ ВТОстаткиТекущийПериодВсе
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.Остатки(
	|			&ГраницаКонецПериода,
	|			Организация В (&МассивОрганизаций)
	|				И ВидЗапасов В
	|					(ВЫБРАТЬ
	|						ВидЗапасов.Ссылка
	|					ИЗ
	|						ВТРазрешенныеВидыЗапасов КАК ВидЗапасов)) КАК ТоварыОрганизаций
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Резервы.Организация,
	|	Резервы.АналитикаУчетаНоменклатуры,
	|	Резервы.ВидЗапасов,
	|	Резервы.АналитикаУчетаНоменклатуры.Номенклатура.ВестиУчетПоГТД,
	|	Резервы.НомерГТД,
	|	&КонецПериода КАК Период,
	|	Резервы.КоличествоОстаток,
	|	0
	|ИЗ
	|	РегистрНакопления.РезервыТоваровОрганизаций.Остатки(
	|			&ГраницаКонецПериода,
	|			Организация В (&МассивОрганизаций)
	|				И ВидЗапасов В
	|					(ВЫБРАТЬ
	|						ВидЗапасов.Ссылка
	|					ИЗ
	|						ВТРазрешенныеВидыЗапасов КАК ВидЗапасов)) КАК Резервы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВестиУчетПоГТД,
	|	НомерГТД,
	|	Организация,
	|	АналитикаУчетаНоменклатуры,
	|	ВидЗапасов 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыОрганизаций.Организация КАК Организация,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТоварыОрганизаций.ВидЗапасов КАК ВидЗапасов,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Номенклатура.ВестиУчетПоГТД КАК ВестиУчетПоГТД,
	|	ТоварыОрганизаций.НомерГТД КАК НомерГТД,
	|	&КонецПериода КАК Период,
	|	ТоварыОрганизаций.КоличествоОстаток КАК КоличествоОстаток,
	|	ТоварыОрганизаций.КОформлениюСписанияОстаток КАК КОформлениюСписанияОстаток
	|ПОМЕСТИТЬ ВТОстаткиПрошлыйПериодВсе
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.Остатки(
	|			&ГраницаКонецПредыдущегоПериода,
	|			Организация В (&МассивОрганизаций)
	|				И ВидЗапасов В
	|					(ВЫБРАТЬ
	|						ВидЗапасов.Ссылка
	|					ИЗ
	|						ВТРазрешенныеВидыЗапасов КАК ВидЗапасов)) КАК ТоварыОрганизаций
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Резервы.Организация,
	|	Резервы.АналитикаУчетаНоменклатуры,
	|	Резервы.ВидЗапасов,
	|	Резервы.АналитикаУчетаНоменклатуры.Номенклатура.ВестиУчетПоГТД,
	|	Резервы.НомерГТД,
	|	&КонецПериода КАК Период,
	|	Резервы.КоличествоОстаток,
	|	0
	|ИЗ
	|	РегистрНакопления.РезервыТоваровОрганизаций.Остатки(
	|			&ГраницаКонецПредыдущегоПериода,
	|			Организация В (&МассивОрганизаций)
	|				И ВидЗапасов В
	|					(ВЫБРАТЬ
	|						ВидЗапасов.Ссылка
	|					ИЗ
	|						ВТРазрешенныеВидыЗапасов КАК ВидЗапасов)) КАК Резервы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВестиУчетПоГТД,
	|	НомерГТД,
	|	Организация,
	|	АналитикаУчетаНоменклатуры,
	|	ВидЗапасов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	#Удаление
	|ВЫБРАТЬ
	|	ВТОстаткиТекущийПериодВсе.Организация КАК Организация,
	|	ВТОстаткиТекущийПериодВсе.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВТОстаткиТекущийПериодВсе.ВидЗапасов КАК ВидЗапасов,
	|	ВТОстаткиТекущийПериодВсе.НомерГТД КАК НомерГТД,
	|	ВТОстаткиТекущийПериодВсе.Период КАК Период,
	|	ВТОстаткиТекущийПериодВсе.КоличествоОстаток + 
	|		ВЫБОР 
	|			КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА 1
	|			ИНАЧЕ -1
	|		КОНЕЦ * ЕСТЬNULL(ТоварыОрганизаций.Количество, 0) КАК КоличествоОстаток,
	|	ВТОстаткиТекущийПериодВсе.КОформлениюСписанияОстаток + 
	|		ВЫБОР 
	|			КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА 1
	|			ИНАЧЕ -1
	|		КОНЕЦ * ЕСТЬNULL(ТоварыОрганизаций.КОформлениюСписания, 0) КАК КОформлениюСписанияОстаток
	|ПОМЕСТИТЬ ВТОстаткиТекущийПериод
	|ИЗ 
	|	ВТОстаткиТекущийПериодВсе КАК ВТОстаткиТекущийПериодВсе
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
	|		ПО &ИспользуетсяРаздельноеОформлениеЗакупок
	|			И ВТОстаткиТекущийПериодВсе.ВестиУчетПоГТД
	|			И ВТОстаткиТекущийПериодВсе.НомерГТД = ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|			И ВТОстаткиТекущийПериодВсе.Организация = ТоварыОрганизаций.Организация
	|			И ВТОстаткиТекущийПериодВсе.АналитикаУчетаНоменклатуры = ТоварыОрганизаций.АналитикаУчетаНоменклатуры
	|			И ВТОстаткиТекущийПериодВсе.ВидЗапасов = ТоварыОрганизаций.ВидЗапасов
	|			И ТоварыОрганизаций.ХозяйственнаяОперация В (&ОперацииРаздельногоОформленияЗакупок)
	|			И ТоварыОрганизаций.Период <= &КонецПериода
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	АналитикаУчетаНоменклатуры,
	|	ВидЗапасов,
	|	НомерГТД
	#КонецУдаления
	#Вставка
	|ВЫБРАТЬ
	|	ВТОстаткиТекущийПериодВсе.Организация КАК Организация,
	|	ВТОстаткиТекущийПериодВсе.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВТОстаткиТекущийПериодВсе.ВидЗапасов КАК ВидЗапасов,
	|	ВТОстаткиТекущийПериодВсе.НомерГТД КАК НомерГТД,
	|	ВТОстаткиТекущийПериодВсе.Период КАК Период,
	|	ВТОстаткиТекущийПериодВсе.КоличествоОстаток + ЕСТЬNULL(ТоварыОрганизацийВПути.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|	ВТОстаткиТекущийПериодВсе.КОформлениюСписанияОстаток КАК КОформлениюСписанияОстаток
	|ПОМЕСТИТЬ ВТОстаткиТекущийПериод
	|ИЗ 
	|	ВТОстаткиТекущийПериодВсе КАК ВТОстаткиТекущийПериодВсе
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыОрганизаций.Остатки(
	|				&ГраницаКонецПериода,
	|				Организация В (&МассивОрганизаций)
	|				И ВидЗапасов В
	|					(ВЫБРАТЬ
	|						ВидЗапасов.Ссылка
	|					ИЗ
	|						ВТВидыЗапасовВПути КАК ВидЗапасов)) КАК ТоварыОрганизацийВПути
	|		ПО &ИспользуетсяРаздельноеОформлениеЗакупок
	|			И ВТОстаткиТекущийПериодВсе.ВестиУчетПоГТД
	|			И ВТОстаткиТекущийПериодВсе.НомерГТД = ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|			И ВТОстаткиТекущийПериодВсе.АналитикаУчетаНоменклатуры.Номенклатура = ТоварыОрганизацийВПути.АналитикаУчетаНоменклатуры.Номенклатура
	|			И ВТОстаткиТекущийПериодВсе.АналитикаУчетаНоменклатуры.Характеристика = ТоварыОрганизацийВПути.АналитикаУчетаНоменклатуры.Характеристика
	|			И ВТОстаткиТекущийПериодВсе.АналитикаУчетаНоменклатуры.Серия = ТоварыОрганизацийВПути.АналитикаУчетаНоменклатуры.Серия
	|			И ВТОстаткиТекущийПериодВсе.Организация = ТоварыОрганизацийВПути.Организация
	|			И ВТОстаткиТекущийПериодВсе.АналитикаУчетаНоменклатуры.МестоХранения = &СкладЗаказа
	|			И ТоварыОрганизацийВПути.КоличествоОстаток > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	АналитикаУчетаНоменклатуры,
	|	ВидЗапасов,
	|	НомерГТД
	#КонецВставки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТОстаткиТекущийПериодВсе
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	#Удаление
	|ВЫБРАТЬ
	|	ВТОстаткиПрошлыйПериодВсе.Организация КАК Организация,
	|	ВТОстаткиПрошлыйПериодВсе.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВТОстаткиПрошлыйПериодВсе.ВидЗапасов КАК ВидЗапасов,
	|	ВТОстаткиПрошлыйПериодВсе.НомерГТД КАК НомерГТД,
	|	ВТОстаткиПрошлыйПериодВсе.Период КАК Период,
	|	ВТОстаткиПрошлыйПериодВсе.КоличествоОстаток + 
	|		ВЫБОР 
	|			КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА 1
	|			ИНАЧЕ -1
	|		КОНЕЦ * ЕСТЬNULL(ТоварыОрганизаций.Количество, 0) КАК КоличествоОстаток,
	|	ВТОстаткиПрошлыйПериодВсе.КОформлениюСписанияОстаток + 
	|		ВЫБОР 
	|			КОГДА ТоварыОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА 1
	|			ИНАЧЕ -1
	|		КОНЕЦ * ЕСТЬNULL(ТоварыОрганизаций.КОформлениюСписания, 0) КАК КОформлениюСписанияОстаток
	|ПОМЕСТИТЬ ВТОстаткиПрошлыйПериод
	|ИЗ 
	|	ВТОстаткиПрошлыйПериодВсе КАК ВТОстаткиПрошлыйПериодВсе
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыОрганизаций КАК ТоварыОрганизаций
	|		ПО &ИспользуетсяРаздельноеОформлениеЗакупок
	|			И ВТОстаткиПрошлыйПериодВсе.ВестиУчетПоГТД
	|			И ВТОстаткиПрошлыйПериодВсе.НомерГТД = ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|			И ВТОстаткиПрошлыйПериодВсе.Организация = ТоварыОрганизаций.Организация
	|			И ВТОстаткиПрошлыйПериодВсе.АналитикаУчетаНоменклатуры = ТоварыОрганизаций.АналитикаУчетаНоменклатуры
	|			И ВТОстаткиПрошлыйПериодВсе.ВидЗапасов = ТоварыОрганизаций.ВидЗапасов
	|			И ТоварыОрганизаций.ХозяйственнаяОперация В (&ОперацииРаздельногоОформленияЗакупок)
	|			И ТоварыОрганизаций.Период <= &КонецПредыдущегоПериода
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	АналитикаУчетаНоменклатуры,
	|	ВидЗапасов,
	|	НомерГТД
	#КонецУдаления
	#Вставка
	|ВЫБРАТЬ
	|	ВТОстаткиПрошлыйПериодВсе.Организация КАК Организация,
	|	ВТОстаткиПрошлыйПериодВсе.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВТОстаткиПрошлыйПериодВсе.ВидЗапасов КАК ВидЗапасов,
	|	ВТОстаткиПрошлыйПериодВсе.НомерГТД КАК НомерГТД,
	|	ВТОстаткиПрошлыйПериодВсе.Период КАК Период,
	|	ВТОстаткиПрошлыйПериодВсе.КоличествоОстаток + ЕСТЬNULL(ТоварыОрганизацийВПути.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|	ВТОстаткиПрошлыйПериодВсе.КОформлениюСписанияОстаток КАК КОформлениюСписанияОстаток
	|ПОМЕСТИТЬ ВТОстаткиПрошлыйПериод
	|ИЗ 
	|	ВТОстаткиПрошлыйПериодВсе КАК ВТОстаткиПрошлыйПериодВсе
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыОрганизаций.Остатки(
	|				&ГраницаКонецПредыдущегоПериода,
	|				Организация В (&МассивОрганизаций)
	|				И ВидЗапасов В
	|					(ВЫБРАТЬ
	|						ВидЗапасов.Ссылка
	|					ИЗ
	|						ВТВидыЗапасовВПути КАК ВидЗапасов)) КАК ТоварыОрганизацийВПути
	|		ПО &ИспользуетсяРаздельноеОформлениеЗакупок
	|			И ВТОстаткиПрошлыйПериодВсе.ВестиУчетПоГТД
	|			И ВТОстаткиПрошлыйПериодВсе.НомерГТД = ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|			И ВТОстаткиПрошлыйПериодВсе.АналитикаУчетаНоменклатуры.Номенклатура = ТоварыОрганизацийВПути.АналитикаУчетаНоменклатуры.Номенклатура
	|			И ВТОстаткиПрошлыйПериодВсе.АналитикаУчетаНоменклатуры.Характеристика = ТоварыОрганизацийВПути.АналитикаУчетаНоменклатуры.Характеристика
	|			И ВТОстаткиПрошлыйПериодВсе.АналитикаУчетаНоменклатуры.Серия = ТоварыОрганизацийВПути.АналитикаУчетаНоменклатуры.Серия
	|			И ВТОстаткиПрошлыйПериодВсе.Организация = ТоварыОрганизацийВПути.Организация
	|			И ВТОстаткиПрошлыйПериодВсе.АналитикаУчетаНоменклатуры.МестоХранения = &СкладЗаказа
	|			И ТоварыОрганизацийВПути.КоличествоОстаток > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	АналитикаУчетаНоменклатуры,
	|	ВидЗапасов,
	|	НомерГТД
	#КонецВставки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТОстаткиПрошлыйПериодВсе
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 10
	|	ОстаткиТекущийПериод.Организация КАК Организация,
	|	ОстаткиТекущийПериод.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ОстаткиТекущийПериод.ВидЗапасов КАК ВидЗапасов,
	|	ОстаткиТекущийПериод.НомерГТД КАК НомерГТД,
	|	ОстаткиТекущийПериод.Период КАК Период,
	|	СУММА(ОстаткиТекущийПериод.КоличествоОстаток) КАК Количество,
	|	СУММА(ОстаткиТекущийПериод.КоличествоОстаток - ЕСТЬNULL(ОстаткиПрошлыйПериод.КоличествоОстаток, 0)) КАК Оборот
	|ПОМЕСТИТЬ ОтрицательныеОстатки
	|ИЗ
	|	ВТОстаткиТекущийПериод КАК ОстаткиТекущийПериод
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОстаткиПрошлыйПериод КАК ОстаткиПрошлыйПериод
	|		ПО ОстаткиТекущийПериод.Организация = ОстаткиПрошлыйПериод.Организация
	|			И ОстаткиТекущийПериод.АналитикаУчетаНоменклатуры = ОстаткиПрошлыйПериод.АналитикаУчетаНоменклатуры
	|			И ОстаткиТекущийПериод.ВидЗапасов = ОстаткиПрошлыйПериод.ВидЗапасов
	|			И ОстаткиТекущийПериод.НомерГТД = ОстаткиПрошлыйПериод.НомерГТД
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиТекущийПериод.Организация,
	|	ОстаткиТекущийПериод.АналитикаУчетаНоменклатуры,
	|	ОстаткиТекущийПериод.ВидЗапасов,
	|	ОстаткиТекущийПериод.НомерГТД,
	|	ОстаткиТекущийПериод.Период
	|
	|ИМЕЮЩИЕ
	|	СУММА(ОстаткиТекущийПериод.КоличествоОстаток) < 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 10
	|	ОстаткиТекущийПериод.Организация,
	|	ОстаткиТекущийПериод.АналитикаУчетаНоменклатуры,
	|	ОстаткиТекущийПериод.ВидЗапасов,
	|	ОстаткиТекущийПериод.НомерГТД,
	|	ОстаткиТекущийПериод.Период КАК Период,
	|	СУММА(ОстаткиТекущийПериод.КОформлениюСписанияОстаток),
	|	СУММА(ОстаткиТекущийПериод.КОформлениюСписанияОстаток - ЕСТЬNULL(ОстаткиПрошлыйПериод.КОформлениюСписанияОстаток, 0))
	|ИЗ
	|	ВТОстаткиТекущийПериод КАК ОстаткиТекущийПериод
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОстаткиПрошлыйПериод КАК ОстаткиПрошлыйПериод
	|		ПО ОстаткиТекущийПериод.Организация = ОстаткиПрошлыйПериод.Организация
	|			И ОстаткиТекущийПериод.АналитикаУчетаНоменклатуры = ОстаткиПрошлыйПериод.АналитикаУчетаНоменклатуры
	|			И ОстаткиТекущийПериод.ВидЗапасов = ОстаткиПрошлыйПериод.ВидЗапасов
	|			И ОстаткиТекущийПериод.НомерГТД = ОстаткиПрошлыйПериод.НомерГТД
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиТекущийПериод.Организация,
	|	ОстаткиТекущийПериод.АналитикаУчетаНоменклатуры,
	|	ОстаткиТекущийПериод.ВидЗапасов,
	|	ОстаткиТекущийПериод.НомерГТД,
	|	ОстаткиТекущийПериод.Период
	|
	|ИМЕЮЩИЕ
	|	СУММА(ОстаткиТекущийПериод.КОформлениюСписанияОстаток) < 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТРазрешенныеВидыЗапасов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТОстаткиПрошлыйПериод
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТОстаткиТекущийПериод";

	Возврат ТекстЗапроса;

КонецФункции

&ИзменениеИКонтроль("ТаблицаРезервыТоваровОрганизаций")
Функция РСК_ТаблицаРезервыТоваровОрганизаций(ДокументОбъект, МенеджерВременныхТаблиц, ПараметрыЗаполненияВидовЗапасов, ПередЗаписьюДокумента)

	ТекстЗапроса = 	
	"ВЫБРАТЬ
	|	СформированныеРезервы.ВидДвижения КАК ВидДвижения,
	|	СформированныеРезервы.Период КАК Период,
	|	СформированныеРезервы.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	СформированныеРезервы.Организация КАК Организация,
	|	СформированныеРезервы.ВидЗапасов КАК ВидЗапасов,
	|	СформированныеРезервы.НомерГТД КАК НомерГТД,
	|	СформированныеРезервы.Количество КАК Количество,
	|	ВЫРАЗИТЬ(СформированныеРезервы.КоличествоПоРНПТ КАК ЧИСЛО(23, 11)) КАК КоличествоПоРНПТ,
	|	СформированныеРезервы.КорОрганизация КАК КорОрганизация,
	|	СформированныеРезервы.КорВидЗапасов КАК КорВидЗапасов
	|ПОМЕСТИТЬ СформированныеРезервы
	|ИЗ
	|	&СформированныеРезервы КАК СформированныеРезервы
	#Вставка
	//++РС Консалт: Минаков А.С. Задача 25542
	|ГДЕ 
	|	НЕ СформированныеРезервы.ЭтоСторно
	//++РС Консалт: Минаков А.С. Задача 25542
	#КонецВставки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Период КАК Период,
	|	ВложенныйЗапрос.ВидДвижения КАК ВидДвижения,
	|	ВложенныйЗапрос.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.ВидЗапасов КАК ВидЗапасов,
	|	ВложенныйЗапрос.НомерГТД КАК НомерГТД,
	|	СУММА(ВложенныйЗапрос.Количество) КАК Количество,
	|	СУММА(ВложенныйЗапрос.КоличествоПоРНПТ) КАК КоличествоПоРНПТ,
	|	ВЫБОР
	|		КОГДА СУММА(ВложенныйЗапрос.Количество) < 0
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоСторно,
	|	ВложенныйЗапрос.КорОрганизация КАК КорОрганизация,
	|	ВложенныйЗапрос.КорВидЗапасов КАК КорВидЗапасов
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаНовыеРезервы.ВидДвижения КАК ВидДвижения,
	|		КОНЕЦПЕРИОДА(ТаблицаНовыеРезервы.Период, МЕСЯЦ) КАК Период,
	|		ТаблицаНовыеРезервы.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ТаблицаНовыеРезервы.Организация КАК Организация,
	|		ТаблицаНовыеРезервы.ВидЗапасов КАК ВидЗапасов,
	|		ТаблицаНовыеРезервы.НомерГТД КАК НомерГТД,
	|		ТаблицаНовыеРезервы.Количество КАК Количество,
	|		ВЫРАЗИТЬ(ТаблицаНовыеРезервы.КоличествоПоРНПТ КАК ЧИСЛО(23, 11)) КАК КоличествоПоРНПТ,
	|		ТаблицаНовыеРезервы.КорОрганизация КАК КорОрганизация,
	|		ТаблицаНовыеРезервы.КорВидЗапасов КАК КорВидЗапасов
	|	ИЗ
	|		ВТНовыеРезервы КАК ТаблицаНовыеРезервы
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаСторно.ВидДвижения,
	|		КОНЕЦПЕРИОДА(ТаблицаСторно.Период, МЕСЯЦ),
	|		ТаблицаСторно.АналитикаУчетаНоменклатуры,
	|		ТаблицаСторно.Организация,
	|		ТаблицаСторно.ВидЗапасов,
	|		ТаблицаСторно.НомерГТД,
	|		ТаблицаСторно.Количество,
	|		ВЫРАЗИТЬ(ТаблицаСторно.КоличествоПоРНПТ КАК ЧИСЛО(23, 11)),
	|		ТаблицаСторно.КорОрганизация,
	|		ТаблицаСторно.КорВидЗапасов
	|	ИЗ
	|		ВТСторноРезервов КАК ТаблицаСторно
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РезервыПоОрганизациямДляКонтроля.ВидДвижения,
	|		КОНЕЦПЕРИОДА(РезервыПоОрганизациямДляКонтроля.Период, МЕСЯЦ),
	|		РезервыПоОрганизациямДляКонтроля.АналитикаУчетаНоменклатуры,
	|		РезервыПоОрганизациямДляКонтроля.Организация,
	|		РезервыПоОрганизациямДляКонтроля.ВидЗапасов,
	|		РезервыПоОрганизациямДляКонтроля.НомерГТД,
	|		РезервыПоОрганизациямДляКонтроля.Количество,
	|		ВЫРАЗИТЬ(РезервыПоОрганизациямДляКонтроля.КоличествоПоРНПТ КАК ЧИСЛО(23, 11)),
	|		РезервыПоОрганизациямДляКонтроля.КорОрганизация,
	|		РезервыПоОрганизациямДляКонтроля.КорВидЗапасов
	|	ИЗ
	|		ВТРезервыПоОрганизациямДляКонтроля КАК РезервыПоОрганизациямДляКонтроля
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СформированныеРезервы.ВидДвижения,
	|		КОНЕЦПЕРИОДА(СформированныеРезервы.Период, МЕСЯЦ),
	|		СформированныеРезервы.АналитикаУчетаНоменклатуры,
	|		СформированныеРезервы.Организация,
	|		СформированныеРезервы.ВидЗапасов,
	|		СформированныеРезервы.НомерГТД,
	|		СформированныеРезервы.Количество,
	|		ВЫРАЗИТЬ(СформированныеРезервы.КоличествоПоРНПТ КАК ЧИСЛО(23, 11)),
	|		СформированныеРезервы.КорОрганизация,
	|		СформированныеРезервы.КорВидЗапасов
	|	ИЗ
	|		СформированныеРезервы КАК СформированныеРезервы
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияРезервыТоварыОрганизацийПередЗаписью.ВидДвижения,
	|		КОНЕЦПЕРИОДА(ДвиженияРезервыТоварыОрганизацийПередЗаписью.Период, МЕСЯЦ),
	|		ДвиженияРезервыТоварыОрганизацийПередЗаписью.АналитикаУчетаНоменклатуры,
	|		ДвиженияРезервыТоварыОрганизацийПередЗаписью.Организация,
	|		ДвиженияРезервыТоварыОрганизацийПередЗаписью.ВидЗапасов,
	|		ДвиженияРезервыТоварыОрганизацийПередЗаписью.НомерГТД,
	|		ДвиженияРезервыТоварыОрганизацийПередЗаписью.Количество,
	|		ВЫБОР
	|			КОГДА НЕ &ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|					ИЛИ НАЧАЛОПЕРИОДА(ДвиженияРезервыТоварыОрганизацийПередЗаписью.Период, МЕСЯЦ) < &ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров
	|				ТОГДА 0
	|			ИНАЧЕ ВЫРАЗИТЬ(ДвиженияРезервыТоварыОрганизацийПередЗаписью.КоличествоПоРНПТ КАК ЧИСЛО(23, 11))
	|		КОНЕЦ,
	|		ДвиженияРезервыТоварыОрганизацийПередЗаписью.КорОрганизация,
	|		ДвиженияРезервыТоварыОрганизацийПередЗаписью.КорВидЗапасов
	|	ИЗ
	|		РегистрНакопления.РезервыТоваровОрганизаций КАК ДвиженияРезервыТоварыОрганизацийПередЗаписью
	|	ГДЕ
	|		ДвиженияРезервыТоварыОрганизацийПередЗаписью.Регистратор = &Ссылка
	|		И &ПрибавлятьДвиженияРезервыПередЗаписью) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.АналитикаУчетаНоменклатуры,
	|	ВложенныйЗапрос.ВидЗапасов,
	|	ВложенныйЗапрос.НомерГТД,
	|	ВложенныйЗапрос.КорОрганизация,
	|	ВложенныйЗапрос.КорВидЗапасов,
	|	ВложенныйЗапрос.ВидДвижения,
	|	ВложенныйЗапрос.Период
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.Количество) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТНовыеРезервы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСторноРезервов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТРезервыПоОрганизациямДляКонтроля
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СформированныеРезервы";

	ПроверитьНаличиеВременныхТаблицРезервов(МенеджерВременныхТаблиц,
	"ВТНовыеРезервы, ВТСторноРезервов, ВТРезервыПоОрганизациямДляКонтроля");

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапроса;

	ИмяТаблицыРезервыТоваровОрганизаций = ИмяТаблицыРезервыТоваровОрганизаций();

	Если ДокументОбъект.ДополнительныеСвойства.Свойство(ИмяТаблицыРезервыТоваровОрганизаций) Тогда
		СформированныеРезервы = ДокументОбъект.ДополнительныеСвойства[ИмяТаблицыРезервыТоваровОрганизаций].Скопировать();
	Иначе
		СформированныеРезервы = РегистрыНакопления.РезервыТоваровОрганизаций.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	КонецЕсли;

	Запрос.УстановитьПараметр("СформированныеРезервы", СформированныеРезервы);
	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);
	Запрос.УстановитьПараметр("ПрибавлятьДвиженияРезервыПередЗаписью",
	// Резервы записываются вместе с остальными регистрами.
	Не ПараметрыЗаполненияВидовЗапасов.ДокументДелаетИПриходИРасход
	// Резервы записываются отдельно после формирования таблица резервов в обработке проведения.
	Или ДокументОбъект.ДополнительныеСвойства.Свойство(ИмяТаблицыРезервыТоваровОрганизаций)
	И Не ПередЗаписьюДокумента);

	УчетПрослеживаемыхТоваровЛокализация.УстановитьПараметрыИспользованияУчетаПрослеживаемыхТоваров(Запрос);

	РезультатТаблицаРезервы = Запрос.Выполнить().Выгрузить();

	Возврат РезультатТаблицаРезервы;

КонецФункции

&ИзменениеИКонтроль("СформироватьВТСторноРезервов")
Процедура РСК_СформироватьВТСторноРезервов(ДокументОбъект, МенеджерВременныхТаблиц, ПараметрыЗаполненияВидовЗапасов)

	ПроверитьНаличиеВременныхТаблицРезервов(МенеджерВременныхТаблиц, "ВТСторноРезервовПредыдущихПериодов");

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;

	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТТоварыОрганизацийИзменения.ВидЗапасов КАК ВидЗапасов
	|ИЗ
	|	ВТТоварыОрганизацийИзменения КАК ВТТоварыОрганизацийИзменения
	|ГДЕ
	|	НЕ ВТТоварыОрганизацийИзменения.ДляСторноРезерва
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	(&ПоВсемТипамЗапасов
	|			ИЛИ ВидыЗапасов.ТипЗапасов В (&ТипЗапасов))
	|	И (&ПоВсемНалогообложениямНДС
	|			ИЛИ ВидыЗапасов.НалогообложениеНДС В (&НалогообложениеНДС)
	|			ИЛИ ВидыЗапасов.НалогообложениеНДС = ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПустаяСсылка))
	|	И (&ПоВсемВладельцамТоваров
	|			ИЛИ ВидыЗапасов.ВладелецТовара В (&ВладелецТовара))
	|	И (&ПоВсемСоглашениям
	|			ИЛИ ВидыЗапасов.Соглашение В (&Соглашение))
	|	И (&ПоВсемВалютам
	|			ИЛИ ВидыЗапасов.Валюта В (&Валюта))
	|	И (&ПоВсемКонтрагентам
	|			ИЛИ ВидыЗапасов.Контрагент В (&Контрагент))
	|	И (&ПоВсемДоговорам
	|			ИЛИ ВидыЗапасов.Договор В (&Договор))
	|	И (&ПоВсемГруппамПродукции
	|			ИЛИ ВидыЗапасов.ГруппаПродукции В (&ГруппаПродукции))
	|	И (&ПоВсемВидамЦен
	|			ИЛИ ВидыЗапасов.ВидЦены В (&ВидЦены))
	|	И (&ПоВсемОрганизациям
	|			ИЛИ ВидыЗапасов.Организация В (&Организация))
	|	И НЕ ВидыЗапасов.РеализацияЗапасовДругойОрганизации
	|	И НЕ ВидыЗапасов.ЭтоДубль";

	ОтборыВидовЗапасов = ПараметрыЗаполненияВидовЗапасов.ОтборыВидовЗапасов;

	Запрос.УстановитьПараметр("ТипЗапасов", ОтборыВидовЗапасов.ТипЗапасов);
	Запрос.УстановитьПараметр("ПоВсемТипамЗапасов", ОтборыВидовЗапасов.ТипЗапасов = Неопределено);

	Запрос.УстановитьПараметр("НалогообложениеНДС", ОтборыВидовЗапасов.НалогообложениеНДС);
	Запрос.УстановитьПараметр("ПоВсемНалогообложениямНДС", ОтборыВидовЗапасов.НалогообложениеНДС = Неопределено);

	Запрос.УстановитьПараметр("ВладелецТовара", ОтборыВидовЗапасов.ВладелецТовара);
	Запрос.УстановитьПараметр("ПоВсемВладельцамТоваров", ЗначениеЗаполнено(ОтборыВидовЗапасов.ВладелецТовара));

	Запрос.УстановитьПараметр("Соглашение", ОтборыВидовЗапасов.Соглашение);
	Запрос.УстановитьПараметр("ПоВсемСоглашениям", ОтборыВидовЗапасов.Соглашение = Неопределено);

	Запрос.УстановитьПараметр("Валюта", ОтборыВидовЗапасов.Валюта);
	Запрос.УстановитьПараметр("ПоВсемВалютам", ОтборыВидовЗапасов.Валюта = Неопределено);

	Запрос.УстановитьПараметр("Контрагент", ОтборыВидовЗапасов.Контрагент);
	Запрос.УстановитьПараметр("ПоВсемКонтрагентам", ЗначениеЗаполнено(ОтборыВидовЗапасов.Контрагент));

	Запрос.УстановитьПараметр("Договор", ОтборыВидовЗапасов.Договор);
	Запрос.УстановитьПараметр("ПоВсемДоговорам", ЗначениеЗаполнено(ОтборыВидовЗапасов.Договор));

	Запрос.УстановитьПараметр("ГруппаПродукции", ОтборыВидовЗапасов.ГруппаПродукции);
	Запрос.УстановитьПараметр("ПоВсемГруппамПродукции", ОтборыВидовЗапасов.ГруппаПродукции = Неопределено);

	Запрос.УстановитьПараметр("ВидЦены", ОтборыВидовЗапасов.ВидЦены);
	Запрос.УстановитьПараметр("ПоВсемВидамЦен", ОтборыВидовЗапасов.ВидЦены = Неопределено);

	Запрос.УстановитьПараметр("Организация", ОтборыВидовЗапасов.Организация);
	Запрос.УстановитьПараметр("ПоВсемОрганизациям", ОтборыВидовЗапасов.Организация = Неопределено);

	Результаты = Запрос.ВыполнитьПакет();

	Запрос.УстановитьПараметр("КорВидыЗапасовДляСторно", Результаты[0].Выгрузить().ВыгрузитьКолонку("ВидЗапасов"));
	Запрос.УстановитьПараметр("ВидыЗапасовПолучателя", Результаты[1].Выгрузить().ВыгрузитьКолонку("Ссылка"));

	Запрос.Текст =
	"ВЫБРАТЬ
	|	СформированныеРезервы.Период КАК Период,
	|	СформированныеРезервы.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	СформированныеРезервы.Организация КАК Организация,
	|	СформированныеРезервы.ВидЗапасов КАК ВидЗапасов,
	|	СформированныеРезервы.НомерГТД КАК НомерГТД,
	|	СформированныеРезервы.КорОрганизация КАК КорОрганизация,
	|	СформированныеРезервы.КорВидЗапасов КАК КорВидЗапасов,
	|	СформированныеРезервы.Количество КАК Количество,
	|	ВЫРАЗИТЬ(СформированныеРезервы.КоличествоПоРНПТ КАК ЧИСЛО(23, 11)) КАК КоличествоПоРНПТ,
	|	СформированныеРезервы.ВидДвижения КАК ВидДвижения
	|ПОМЕСТИТЬ ВТСформированныеРезервы
	|ИЗ
	|	&СформированныеРезервы КАК СформированныеРезервы
	#Вставка
	//++РС Консалт: Минаков А.С. Задача 25542
	|ГДЕ 
	|	НЕ СформированныеРезервы.ЭтоСторно
	//++РС Консалт: Минаков А.С. Задача 25542
	#КонецВставки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры,
	|	Организация,
	|	ВидЗапасов,
	|	НомерГТД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТТоварыОрганизацийИзменения.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВТТоварыОрганизацийИзменения.Организация КАК Организация,
	|	ВТТоварыОрганизацийИзменения.ВидЗапасов КАК ВидЗапасов,
	|	ВТТоварыОрганизацийИзменения.НомерГТД КАК НомерГТД,
	|	ВТТоварыОрганизацийИзменения.Период КАК Период
	|ПОМЕСТИТЬ ВТОтборыРезервовДляСторно
	|ИЗ
	|	ВТТоварыОрганизацийИзменения КАК ВТТоварыОрганизацийИзменения
	|ГДЕ
	|	ВТТоварыОрганизацийИзменения.ДляСторноРезерва
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаНоменклатуры,
	|	Организация,
	|	ВидЗапасов,
	|	НомерГТД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ВложенныйЗапрос.Период, МЕСЯЦ) КАК Период,
	|	ВложенныйЗапрос.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВложенныйЗапрос.Организация КАК Организация,
	|	ВложенныйЗапрос.ВидЗапасов КАК ВидЗапасов,
	|	ВложенныйЗапрос.НомерГТД КАК НомерГТД,
	|	ВложенныйЗапрос.КорОрганизация КАК КорОрганизация,
	|	ВложенныйЗапрос.КорВидЗапасов КАК КорВидЗапасов,
	|	СУММА(ВложенныйЗапрос.Количество) КАК Количество,
	|	СУММА(ВложенныйЗапрос.КоличествоПоРНПТ) КАК КоличествоПоРНПТ,
	|	ВЫБОР
	|		КОГДА ВложенныйЗапрос.КорВидЗапасов В (&КорВидыЗапасовДляСторно)
	|			ТОГДА 0
	|		КОГДА ВложенныйЗапрос.КорВидЗапасов В (&ВидыЗапасовПолучателя)
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Приоритет
	|ИЗ
	|	(ВЫБРАТЬ
	|		РезервыТоваровОрганизаций.Период КАК Период,
	|		РезервыТоваровОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		РезервыТоваровОрганизаций.Организация КАК Организация,
	|		РезервыТоваровОрганизаций.ВидЗапасов КАК ВидЗапасов,
	|		РезервыТоваровОрганизаций.НомерГТД КАК НомерГТД,
	|		РезервыТоваровОрганизаций.КорОрганизация КАК КорОрганизация,
	|		РезервыТоваровОрганизаций.КорВидЗапасов КАК КорВидЗапасов,
	|		РезервыТоваровОрганизаций.Количество КАК Количество,
	|		ВЫБОР
	|			КОГДА НЕ &ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|					ИЛИ НАЧАЛОПЕРИОДА(РезервыТоваровОрганизаций.Период, МЕСЯЦ) < &ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров
	|				ТОГДА 0
	|			ИНАЧЕ ВЫРАЗИТЬ(РезервыТоваровОрганизаций.КоличествоПоРНПТ КАК ЧИСЛО(23, 11))
	|		КОНЕЦ КАК КоличествоПоРНПТ
	|	ИЗ
	|		РегистрНакопления.РезервыТоваровОрганизаций КАК РезервыТоваровОрганизаций
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК КорВидыЗапасов
	|			ПО РезервыТоваровОрганизаций.КорВидЗапасов = КорВидыЗапасов.Ссылка
	|	ГДЕ
	|		(РезервыТоваровОрганизаций.АналитикаУчетаНоменклатуры, РезервыТоваровОрганизаций.Организация, РезервыТоваровОрганизаций.ВидЗапасов, РезервыТоваровОрганизаций.НомерГТД) В
	|				(ВЫБРАТЬ
	|					ВТОтборыРезервовДляСторно.АналитикаУчетаНоменклатуры,
	|					ВТОтборыРезервовДляСторно.Организация,
	|					ВТОтборыРезервовДляСторно.ВидЗапасов,
	|					ВТОтборыРезервовДляСторно.НомерГТД
	|				ИЗ
	|					ВТОтборыРезервовДляСторно КАК ВТОтборыРезервовДляСторно)
	|		И РезервыТоваровОрганизаций.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СторноРезервов.Период,
	|		СторноРезервов.АналитикаУчетаНоменклатуры,
	|		СторноРезервов.Организация,
	|		СторноРезервов.ВидЗапасов,
	|		СторноРезервов.НомерГТД,
	|		СторноРезервов.КорОрганизация,
	|		СторноРезервов.КорВидЗапасов,
	|		СторноРезервов.Количество,
	|		ВЫРАЗИТЬ(СторноРезервов.КоличествоПоРНПТ КАК ЧИСЛО(23, 11))
	|	ИЗ
	|		ВТСторноРезервовПредыдущихПериодов КАК СторноРезервов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК КорВидыЗапасов
	|			ПО СторноРезервов.КорВидЗапасов = КорВидыЗапасов.Ссылка
	|	ГДЕ
	|		(СторноРезервов.АналитикаУчетаНоменклатуры, СторноРезервов.Организация, СторноРезервов.ВидЗапасов, СторноРезервов.НомерГТД) В
	|				(ВЫБРАТЬ
	|					ВТОтборыРезервовДляСторно.АналитикаУчетаНоменклатуры,
	|					ВТОтборыРезервовДляСторно.Организация,
	|					ВТОтборыРезервовДляСторно.ВидЗапасов,
	|					ВТОтборыРезервовДляСторно.НомерГТД
	|				ИЗ
	|					ВТОтборыРезервовДляСторно КАК ВТОтборыРезервовДляСторно)
	|		И СторноРезервов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СформированныеРезервы.Период,
	|		СформированныеРезервы.АналитикаУчетаНоменклатуры,
	|		СформированныеРезервы.Организация,
	|		СформированныеРезервы.ВидЗапасов,
	|		СформированныеРезервы.НомерГТД,
	|		СформированныеРезервы.КорОрганизация,
	|		СформированныеРезервы.КорВидЗапасов,
	|		СформированныеРезервы.Количество,
	|		ВЫРАЗИТЬ(СформированныеРезервы.КоличествоПоРНПТ КАК ЧИСЛО(23, 11))
	|	ИЗ
	|		ВТСформированныеРезервы КАК СформированныеРезервы
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК КорВидыЗапасов
	|			ПО СформированныеРезервы.КорВидЗапасов = КорВидыЗапасов.Ссылка
	|	ГДЕ
	|		(СформированныеРезервы.АналитикаУчетаНоменклатуры, СформированныеРезервы.Организация, СформированныеРезервы.ВидЗапасов, СформированныеРезервы.НомерГТД) В
	|				(ВЫБРАТЬ
	|					ВТОтборыРезервовДляСторно.АналитикаУчетаНоменклатуры,
	|					ВТОтборыРезервовДляСторно.Организация,
	|					ВТОтборыРезервовДляСторно.ВидЗапасов,
	|					ВТОтборыРезервовДляСторно.НомерГТД
	|				ИЗ
	|					ВТОтборыРезервовДляСторно КАК ВТОтборыРезервовДляСторно)
	|		И СформированныеРезервы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(ВложенныйЗапрос.Период, МЕСЯЦ),
	|	ВложенныйЗапрос.АналитикаУчетаНоменклатуры,
	|	ВложенныйЗапрос.Организация,
	|	ВложенныйЗапрос.ВидЗапасов,
	|	ВложенныйЗапрос.НомерГТД,
	|	ВложенныйЗапрос.КорОрганизация,
	|	ВложенныйЗапрос.КорВидЗапасов
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.Количество) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период,
	|	Приоритет
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ВТТоварыОрганизацийИзменения.Период, МЕСЯЦ) КАК Период,
	|	ВТТоварыОрганизацийИзменения.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВТТоварыОрганизацийИзменения.Организация КАК Организация,
	|	ВТТоварыОрганизацийИзменения.ВидЗапасов КАК ВидЗапасов,
	|	ВТТоварыОрганизацийИзменения.НомерГТД КАК НомерГТД,
	|	СУММА(ВТТоварыОрганизацийИзменения.Количество) КАК Количество
	|ИЗ
	|	ВТТоварыОрганизацийИзменения КАК ВТТоварыОрганизацийИзменения
	|ГДЕ
	|	ВТТоварыОрганизацийИзменения.ДляСторноРезерва
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(ВТТоварыОрганизацийИзменения.Период, МЕСЯЦ),
	|	ВТТоварыОрганизацийИзменения.АналитикаУчетаНоменклатуры,
	|	ВТТоварыОрганизацийИзменения.Организация,
	|	ВТТоварыОрганизацийИзменения.ВидЗапасов,
	|	ВТТоварыОрганизацийИзменения.НомерГТД
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период";

	ИмяТаблицыРезервыТоваровОрганизаций = ИмяТаблицыРезервыТоваровОрганизаций();

	Если ДокументОбъект.ДополнительныеСвойства.Свойство(ИмяТаблицыРезервыТоваровОрганизаций) Тогда
		СформированныеРезервы = ДокументОбъект.ДополнительныеСвойства[ИмяТаблицыРезервыТоваровОрганизаций].Скопировать();
	Иначе
		СформированныеРезервы = РегистрыНакопления.РезервыТоваровОрганизаций.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	КонецЕсли;

	Запрос.УстановитьПараметр("СформированныеРезервы", СформированныеРезервы);

	УчетПрослеживаемыхТоваровЛокализация.УстановитьПараметрыИспользованияУчетаПрослеживаемыхТоваров(Запрос);

	Результаты = Запрос.ВыполнитьПакет();
	РезервыДляСторно = Результаты[Результаты.ВГраница() - 1].Выгрузить();
	РезервыДляСторно.Индексы.Добавить("АналитикаУчетаНоменклатуры, Организация, ВидЗапасов, НомерГТД");

	ТоварыОрганизацийДляСторно	= Результаты[Результаты.ВГраница()].Выгрузить();
	ТаблицаСторноДвижений		= РегистрыНакопления.РезервыТоваровОрганизаций.СоздатьНаборЗаписей().ВыгрузитьКолонки();

	СтруктураПоиска = Новый Структура("АналитикаУчетаНоменклатуры, Организация, ВидЗапасов, НомерГТД");

	Для Каждого СтрокаТоварыОрганизацийДляСторно Из ТоварыОрганизацийДляСторно Цикл

		МожноОтсторнировать = СтрокаТоварыОрганизацийДляСторно.Количество;

		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаТоварыОрганизацийДляСторно);
		СтрокиРезервыДляСторно = РезервыДляСторно.НайтиСтроки(СтруктураПоиска);

		Для Каждого СтрокаРезервыДляСторно Из СтрокиРезервыДляСторно Цикл

			Если СтрокаРезервыДляСторно.Период < СтрокаТоварыОрганизацийДляСторно.Период Тогда
				Продолжить;
			КонецЕсли;

			Если МожноОтсторнировать = 0 Тогда
				Продолжить;
			КонецЕсли;

			КоличествоКСторнированию	= Мин(МожноОтсторнировать, СтрокаРезервыДляСторно.Количество);

			НоваяСтрока = ТаблицаСторноДвижений.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРезервыДляСторно);
			НоваяСтрока.Период				= КонецМесяца(СтрокаРезервыДляСторно.Период);
			НоваяСтрока.ВидДвижения			= ВидДвиженияНакопления.Приход;
			НоваяСтрока.Количество			= -КоличествоКСторнированию;
			НоваяСтрока.КоличествоПоРНПТ	= -?(СтрокаРезервыДляСторно.Количество <> 0,
			?(СтрокаРезервыДляСторно.Количество - КоличествоКСторнированию = 0,
			СтрокаРезервыДляСторно.КоличествоПоРНПТ,
			КоличествоКСторнированию * СтрокаРезервыДляСторно.КоличествоПоРНПТ / СтрокаРезервыДляСторно.Количество),
			0);
			НоваяСтрока.ЭтоСторно			= Истина;

			РегистрыНакопления.РезервыТоваровОрганизаций.ДобавитьВНаборРезерваКорЗапись(ТаблицаСторноДвижений,
			НоваяСтрока);

			СтрокаРезервыДляСторно.Количество		= СтрокаРезервыДляСторно.Количество - КоличествоКСторнированию;
			СтрокаРезервыДляСторно.КоличествоПоРНПТ	= СтрокаРезервыДляСторно.КоличествоПоРНПТ - НоваяСтрока.КоличествоПоРНПТ;

			МожноОтсторнировать = МожноОтсторнировать - КоличествоКСторнированию;

		КонецЦикла;

	КонецЦикла;

	Запрос.Текст = 
	"УНИЧТОЖИТЬ ВТОтборыРезервовДляСторно
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСформированныеРезервы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТСторноРезервовПредыдущихПериодов";

	Запрос.Выполнить();

	Если ТаблицаСторноДвижений.Количество() > 0 Тогда
		ГлубинаРекурсии = 0;

		ДобавитьВТаблицуСторноРезервовСторноПоРасходу(ДокументОбъект,
		ТаблицаСторноДвижений,
		ТаблицаСторноДвижений,
		ГлубинаРекурсии);
	КонецЕсли;

	Если ПараметрыЗаполненияВидовЗапасов.ТаблицаСторноДвижений <> Неопределено Тогда
		ПараметрыЗаполненияВидовЗапасов.Вставить("ТаблицаСторноДвижений", ТаблицаСторноДвижений);
	Иначе
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВТСторноРезервов.ВидДвижения КАК ВидДвижения,
		|	ВТСторноРезервов.Период КАК Период,
		|	ВТСторноРезервов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	ВТСторноРезервов.Организация КАК Организация,
		|	ВТСторноРезервов.ВидЗапасов КАК ВидЗапасов,
		|	ВТСторноРезервов.НомерГТД КАК НомерГТД,
		|	ВТСторноРезервов.Количество КАК Количество,
		|	ВТСторноРезервов.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
		|	ВТСторноРезервов.КорОрганизация КАК КорОрганизация,
		|	ВТСторноРезервов.КорВидЗапасов КАК КорВидЗапасов
		|ПОМЕСТИТЬ ВТСторноРезервов
		|ИЗ
		|	&ТаблицаСторноДвижений КАК ВТСторноРезервов";

		Запрос.УстановитьПараметр("ТаблицаСторноДвижений", ТаблицаСторноДвижений);

		Запрос.Выполнить();
	КонецЕсли;

КонецПроцедуры

&ИзменениеИКонтроль("ТаблицаОстатковПереданныхМеждуОрганизациямиТоваров")
Процедура РСК_ТаблицаОстатковПереданныхМеждуОрганизациямиТоваров(ДокументОбъект, МенеджерВременныхТаблиц, ПараметрыЗаполненияВидовЗапасов)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПереданныеТовары.Организация КАК Организация,
	|	ПереданныеТовары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ПереданныеТовары.АналитикаУчетаНоменклатурыОтгрузки КАК АналитикаУчетаНоменклатурыОтгрузки,
	|	ПереданныеТовары.ВидЗапасовОтгрузки КАК ВидЗапасовОтгрузки,
	|	ПереданныеТовары.ДокументПередачи КАК ДокументПередачи,
	|	ПереданныеТовары.ВидЗапасов КАК ВидЗапасов,
	#Удаление
	|	ПереданныеТовары.НомерГТД КАК НомерГТД,
	#КонецУдаления
	#Вставка
	//++РС Консалт: Минаков А.С. Задача 24557
	|	ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка) КАК НомерГТД,
	//++РС Консалт: Минаков А.С. Задача 24557
	#КонецВставки
	|	СУММА(ПереданныеТовары.Количество) КАК КоличествоОстаток,
	|	СУММА(ПереданныеТовары.КоличествоПоРНПТ) КАК КоличествоПоРНПТОстаток,
	|	0 КАК СуммаОстаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатурыОтгрузки,
	|		ВидыЗапасов.ВидЗапасов КАК ВидЗапасовОтгрузки,
	|		ВтОтборыОстатковОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		ВидыЗапасов.Ссылка.ОрганизацияПолучатель КАК Организация,
	|		ВидыЗапасов.ВидЗапасовПолучателя КАК ВидЗапасов,
	#Удаление
	|		ВидыЗапасов.НомерГТД КАК НомерГТД,
	#КонецУдаления
	|		ВидыЗапасов.Количество КАК Количество,
	|		ВЫБОР
	|			КОГДА НЕ &ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|					ИЛИ НАЧАЛОПЕРИОДА(ВидыЗапасов.Ссылка.Дата, МЕСЯЦ) < &ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров
	|				ТОГДА 0
	|			ИНАЧЕ ВЫРАЗИТЬ(ВидыЗапасов.КоличествоПоРНПТ КАК ЧИСЛО(23, 11))
	|		КОНЕЦ КАК КоличествоПоРНПТ,
	|		ВидыЗапасов.Ссылка КАК ДокументПередачи
	|	ИЗ
	|		Документ.ПередачаТоваровМеждуОрганизациями.ВидыЗапасов КАК ВидыЗапасов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокумента КАК ТаблицаДанныхДокумента
	|			ПО ВидыЗапасов.Ссылка.ХозяйственнаяОперация = ТаблицаДанныхДокумента.ХозяйственнаяОперацияПередачи
	|				И ВидыЗапасов.Ссылка.ОрганизацияПолучатель = ТаблицаДанныхДокумента.Организация
	|				И ВидыЗапасов.Ссылка.Организация = ТаблицаДанныхДокумента.ОрганизацияПолучатель
	|				И (ВидыЗапасов.Ссылка.Проведен)
	|				И (ВидыЗапасов.Ссылка.Дата <= КОНЕЦПЕРИОДА(ТаблицаДанныхДокумента.Дата, МЕСЯЦ))
	|			
	#Удаление
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТоваров КАК ТаблицаТоваров
	|			ПО ВидыЗапасов.Ссылка = ТаблицаТоваров.ДокументПередачи
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтОтборыОстатковОрганизаций КАК ВтОтборыОстатковОрганизаций
	#КонецУдаления
	#Вставка
	//++РС Консалт: Минаков А.С. Задача 24557
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|										ВтОтборыОстатковОрганизаций.АналитикаУчетаНоменклатуры
	|									ИЗ
	|										ВтОтборыОстатковОрганизаций КАК ВтОтборыОстатковОрганизаций) КАК ВтОтборыОстатковОрганизаций	
	//++РС Консалт: Минаков А.С. Задача 24557
	#КонецВставки
	|			ПО ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура = ВтОтборыОстатковОрганизаций.АналитикаУчетаНоменклатуры.Номенклатура
	|				И ВидыЗапасов.АналитикаУчетаНоменклатуры.Характеристика = ВтОтборыОстатковОрганизаций.АналитикаУчетаНоменклатуры.Характеристика
	|				И ВидыЗапасов.АналитикаУчетаНоменклатуры.Серия = ВтОтборыОстатковОрганизаций.АналитикаУчетаНоменклатуры.Серия
	|				И ВидыЗапасов.АналитикаУчетаНоменклатуры.Назначение = ВтОтборыОстатковОрганизаций.АналитикаУчетаНоменклатуры.Назначение
	#Вставка
	//++РС Консалт: Минаков А.С. Задача 24557
	|	ГДЕ 
	|		ВидыЗапасов.Ссылка В (ВЫБРАТЬ ТаблицаТоваров.ДокументПередачи ИЗ ТаблицаТоваров КАК ТаблицаТоваров)
	//++РС Консалт: Минаков А.С. Задача 24557
	#КонецВставки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВидыЗапасов.АналитикаУчетаНоменклатурыОтгрузки,
	#Удаление
	|		ВидыЗапасов.ВидЗапасовОтгрузки,
	#КонецУдаления
	#Вставка
	//++РС Консалт: Минаков А.С. Задача 24557
	|		ВидыЗапасов.ВидЗапасовПолучателя,
	//++РС Консалт: Минаков А.С. Задача 24557
	#КонецВставки
	|		ВидыЗапасов.АналитикаУчетаНоменклатуры,
	|		ВидыЗапасов.Ссылка.Организация,
	|		ВидыЗапасов.ВидЗапасов,
	#Удаление
	|		ВидыЗапасов.НомерГТД,
	|		ВидыЗапасов.Количество,
	|		ВЫБОР
	|			КОГДА НЕ &ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|					ИЛИ НАЧАЛОПЕРИОДА(ВидыЗапасов.Ссылка.Дата, МЕСЯЦ) < &ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров
	|				ТОГДА 0
	|			ИНАЧЕ ВЫРАЗИТЬ(ВидыЗапасов.КоличествоПоРНПТ КАК ЧИСЛО(23, 11))
	|		КОНЕЦ,
	#КонецУдаления 
	#Вставка
	//++РС Консалт: Минаков А.С. Задача 24557
	|		-ВидыЗапасов.Количество,
	|		ВЫБОР
	|			КОГДА НЕ &ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|					ИЛИ НАЧАЛОПЕРИОДА(ВидыЗапасов.Ссылка.Дата, МЕСЯЦ) < &ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров
	|				ТОГДА 0
	|			ИНАЧЕ -ВЫРАЗИТЬ(ВидыЗапасов.КоличествоПоРНПТ КАК ЧИСЛО(23, 11))
	|		КОНЕЦ,	
	//++РС Консалт: Минаков А.С. Задача 24557
	#КонецВставки
	|		ВидыЗапасов.ДокументПередачи
	|	ИЗ
	|		Документ.ВозвратТоваровМеждуОрганизациями.ВидыЗапасов КАК ВидыЗапасов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаДанныхДокумента КАК ТаблицаДанныхДокумента
	|			ПО ВидыЗапасов.Ссылка.ХозяйственнаяОперация = ТаблицаДанныхДокумента.ХозяйственнаяОперацияВозврата
	|				И ВидыЗапасов.Ссылка.Организация = ТаблицаДанныхДокумента.Организация
	|				И ВидыЗапасов.Ссылка.ОрганизацияПолучатель = ТаблицаДанныхДокумента.ОрганизацияПолучатель
	|				И (ВидыЗапасов.Ссылка.Проведен)
	|				И (ВидыЗапасов.Ссылка.Дата <= КОНЕЦПЕРИОДА(ТаблицаДанныхДокумента.Дата, МЕСЯЦ))
	|			
	#Удаление
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТоваров КАК ТаблицаТоваров
	|			ПО ВидыЗапасов.ДокументПередачи = ТаблицаТоваров.ДокументПередачи
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтОтборыОстатковОрганизаций КАК ВтОтборыОстатковОрганизаций
	#КонецУдаления
	#Вставка
	//++РС Консалт: Минаков А.С. Задача 24557
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|										ВтОтборыОстатковОрганизаций.АналитикаУчетаНоменклатуры
	|									ИЗ
	|										ВтОтборыОстатковОрганизаций КАК ВтОтборыОстатковОрганизаций) КАК ВтОтборыОстатковОрганизаций	
	//++РС Консалт: Минаков А.С. Задача 24557
	#КонецВставки
	|			ПО ВидыЗапасов.АналитикаУчетаНоменклатуры.Номенклатура = ВтОтборыОстатковОрганизаций.АналитикаУчетаНоменклатуры.Номенклатура
	|				И ВидыЗапасов.АналитикаУчетаНоменклатуры.Характеристика = ВтОтборыОстатковОрганизаций.АналитикаУчетаНоменклатуры.Характеристика
	|				И ВидыЗапасов.АналитикаУчетаНоменклатуры.Серия = ВтОтборыОстатковОрганизаций.АналитикаУчетаНоменклатуры.Серия
	|				И ВидыЗапасов.АналитикаУчетаНоменклатуры.Назначение = ВтОтборыОстатковОрганизаций.АналитикаУчетаНоменклатуры.Назначение
	|	
	|	ГДЕ
	|		ВидыЗапасов.Ссылка <> &Ссылка
	#Вставка 
	//++РС Консалт: Минаков А.С. Задача 24557
	|		И ВидыЗапасов.ДокументПередачи В (ВЫБРАТЬ ТаблицаТоваров.ДокументПередачи ИЗ ТаблицаТоваров КАК ТаблицаТоваров)
	//++РС Консалт: Минаков А.С. Задача 24557
	#КонецВставки
	|	
	|	) КАК ПереданныеТовары
	|
	#Удаление
	|ГДЕ
	|	ПереданныеТовары.Количество > 0
	#КонецУдаления
	|
	|СГРУППИРОВАТЬ ПО
	#Удаление
	|	ПереданныеТовары.НомерГТД,
	#КонецУдаления
	|	ПереданныеТовары.Организация,
	|	ПереданныеТовары.АналитикаУчетаНоменклатуры,
	|	ПереданныеТовары.АналитикаУчетаНоменклатурыОтгрузки,
	|	ПереданныеТовары.ВидЗапасовОтгрузки,
	|	ПереданныеТовары.ДокументПередачи,
	#Удаление
	|	ПереданныеТовары.ВидЗапасов";
	#КонецУдаления
	#Вставка
	//++РС Консалт: Минаков А.С. Задача 24557
	|	ПереданныеТовары.ВидЗапасов
	|
	|ИМЕЮЩИЕ
	|	СУММА(ПереданныеТовары.Количество) > 0";	
	//++РС Консалт: Минаков А.С. Задача 24557
	#КонецВставки

	Запрос.УстановитьПараметр("Ссылка", ДокументОбъект.Ссылка);

	УчетПрослеживаемыхТоваровЛокализация.УстановитьПараметрыИспользованияУчетаПрослеживаемыхТоваров(Запрос);

	РезультатЗапроса = Запрос.Выполнить();

	ТаблицаОстатковПереданныхТоваров = РезультатЗапроса.Выгрузить();

	СформироватьВТТаблицаОстатков("ТоварыОрганизаций",
	ДокументОбъект,
	МенеджерВременныхТаблиц,
	ПараметрыЗаполненияВидовЗапасов,
	Истина);

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаОстатков.Организация КАК Организация,
	|	ТаблицаОстатков.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаОстатков.Склад КАК Склад,
	|	ТаблицаОстатков.Номенклатура КАК Номенклатура,
	|	ТаблицаОстатков.Характеристика КАК Характеристика,
	|	ТаблицаОстатков.Серия КАК Серия,
	|	ТаблицаОстатков.Назначение КАК Назначение,
	|	ТаблицаОстатков.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаОстатков.НомерГТД КАК НомерГТД,
	|	ТаблицаОстатков.ДатаПоступления КАК ДатаПоступления,
	|	ТаблицаОстатков.КоличествоОстаток КАК КоличествоОстаток,
	|	ВЫРАЗИТЬ(ТаблицаОстатков.КоличествоПоРНПТОстаток КАК ЧИСЛО(23, 11)) КАК КоличествоПоРНПТОстаток,
	|	ТаблицаОстатков.СуммаОстаток КАК СуммаОстаток
	|ИЗ
	|	ТаблицаОстатков КАК ТаблицаОстатков";

	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаОстатковТоваровОрганизаций = РезультатЗапроса.Выгрузить();

	ТаблицаТоваров = ДокументОбъект.Товары.Выгрузить(,
	"АналитикаУчетаНоменклатуры, ДокументПередачи, НомерГТД,Количество,
	|КоличествоПоРНПТ");
	ТаблицаТоваров.Свернуть("АналитикаУчетаНоменклатуры, ДокументПередачи, НомерГТД", "Количество, КоличествоПоРНПТ");

	ВычестьИзОстатковПереданныеТовары(ТаблицаОстатковТоваровОрганизаций, ТаблицаОстатковПереданныхТоваров, ТаблицаТоваров);

	Запрос.Текст = 
	"УНИЧТОЖИТЬ ТаблицаОстатков
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиТоваровОрганизаций.Организация КАК Организация,
	|	ОстаткиТоваровОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ОстаткиТоваровОрганизаций.Склад КАК Склад,
	|	ОстаткиТоваровОрганизаций.Номенклатура КАК Номенклатура,
	|	ОстаткиТоваровОрганизаций.Характеристика КАК Характеристика,
	|	ОстаткиТоваровОрганизаций.Серия КАК Серия,
	|	ОстаткиТоваровОрганизаций.Назначение КАК Назначение,
	|	ОстаткиТоваровОрганизаций.ВидЗапасов КАК ВидЗапасов,
	|	ОстаткиТоваровОрганизаций.НомерГТД КАК НомерГТД,
	|	ОстаткиТоваровОрганизаций.ДатаПоступления КАК ДатаПоступления,
	|	ОстаткиТоваровОрганизаций.КоличествоОстаток КАК КоличествоОстаток,
	|	ВЫРАЗИТЬ(ОстаткиТоваровОрганизаций.КоличествоПоРНПТОстаток КАК ЧИСЛО(23, 11)) КАК КоличествоПоРНПТОстаток,
	|	ОстаткиТоваровОрганизаций.СуммаОстаток КАК СуммаОстаток
	|ПОМЕСТИТЬ ОстаткиТоваровОрганизаций
	|ИЗ
	|	&ТаблицаОстатковТоваровОрганизаций КАК ОстаткиТоваровОрганизаций
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиПереданныхТоваров.Организация КАК Организация,
	|	ОстаткиПереданныхТоваров.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ОстаткиПереданныхТоваров.АналитикаУчетаНоменклатурыОтгрузки КАК АналитикаУчетаНоменклатурыОтгрузки,
	|	ОстаткиПереданныхТоваров.ВидЗапасовОтгрузки КАК ВидЗапасовОтгрузки,
	|	ОстаткиПереданныхТоваров.ДокументПередачи КАК ДокументПередачи,
	|	ОстаткиПереданныхТоваров.ВидЗапасов КАК ВидЗапасов,
	|	ОстаткиПереданныхТоваров.НомерГТД КАК НомерГТД,
	|	ОстаткиПереданныхТоваров.КоличествоОстаток КАК КоличествоОстаток,
	|	ВЫРАЗИТЬ(ОстаткиПереданныхТоваров.КоличествоПоРНПТОстаток КАК ЧИСЛО(23, 11)) КАК КоличествоПоРНПТОстаток
	|ПОМЕСТИТЬ ОстаткиПереданныхТоваров
	|ИЗ
	|	&ТаблицаОстатковПереданныхТоваров КАК ОстаткиПереданныхТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиТоваровОрганизаций.Организация КАК Организация,
	|	ОстаткиТоваровОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка) КАК АналитикаУчетаНоменклатурыОтгрузки,
	|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК ВидЗапасовОтгрузки,
	|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасов,
	|	ОстаткиТоваровОрганизаций.Склад КАК Склад,
	|	ОстаткиТоваровОрганизаций.Номенклатура КАК Номенклатура,
	|	ОстаткиТоваровОрганизаций.Характеристика КАК Характеристика,
	|	ОстаткиТоваровОрганизаций.Серия КАК Серия,
	|	ОстаткиТоваровОрганизаций.Назначение КАК Назначение,
	|	ЗНАЧЕНИЕ(Документ.ПередачаТоваровМеждуОрганизациями.ПустаяСсылка) КАК ДокументПередачи,
	|	ОстаткиТоваровОрганизаций.ВидЗапасов КАК ВидЗапасов,
	|	ОстаткиТоваровОрганизаций.НомерГТД КАК НомерГТД,
	|	ОстаткиТоваровОрганизаций.ДатаПоступления КАК ДатаПоступления,
	|	ОстаткиТоваровОрганизаций.КоличествоОстаток КАК КоличествоОстаток,
	|	ОстаткиТоваровОрганизаций.КоличествоПоРНПТОстаток КАК КоличествоПоРНПТОстаток,
	|	ОстаткиТоваровОрганизаций.СуммаОстаток КАК СуммаОстаток
	|ПОМЕСТИТЬ ТаблицаОстатков
	|ИЗ
	|	ОстаткиТоваровОрганизаций КАК ОстаткиТоваровОрганизаций
	|
	|ГДЕ
	|	ОстаткиТоваровОрганизаций.КоличествоОстаток > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОстаткиПереданныхТоваров.Организация,
	|	ОстаткиПереданныхТоваров.АналитикаУчетаНоменклатуры,
	|	ОстаткиПереданныхТоваров.АналитикаУчетаНоменклатурыОтгрузки,
	|	ОстаткиПереданныхТоваров.ВидЗапасовОтгрузки,
	|	ЗНАЧЕНИЕ(Справочник.ВидыЗапасов.ПустаяСсылка) КАК КорВидЗапасов,
	|	КлючиАналитикиУчетаНоменклатуры.МестоХранения,
	|	КлючиАналитикиУчетаНоменклатуры.Номенклатура,
	|	КлючиАналитикиУчетаНоменклатуры.Характеристика,
	|	КлючиАналитикиУчетаНоменклатуры.Серия,
	|	КлючиАналитикиУчетаНоменклатуры.Назначение,
	|	ОстаткиПереданныхТоваров.ДокументПередачи,
	|	ОстаткиПереданныхТоваров.ВидЗапасов,
	|	ОстаткиПереданныхТоваров.НомерГТД,
	|	ДатыПоступленияТоваровОрганизаций.ДатаПоступления,
	|	ОстаткиПереданныхТоваров.КоличествоОстаток,
	|	ОстаткиПереданныхТоваров.КоличествоПоРНПТОстаток,
	|	0
	|ИЗ
	|	ОстаткиПереданныхТоваров КАК ОстаткиПереданныхТоваров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиАналитикиУчетаНоменклатуры
	|		ПО ОстаткиПереданныхТоваров.АналитикаУчетаНоменклатуры = КлючиАналитикиУчетаНоменклатуры.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыПоступленияТоваровОрганизаций КАК ДатыПоступленияТоваровОрганизаций
	|		ПО ОстаткиПереданныхТоваров.ВидЗапасов = ДатыПоступленияТоваровОрганизаций.ВидЗапасов
	|			И ОстаткиПереданныхТоваров.НомерГТД = ДатыПоступленияТоваровОрганизаций.НомерГТД
	|			И (КлючиАналитикиУчетаНоменклатуры.Номенклатура = ДатыПоступленияТоваровОрганизаций.Номенклатура)
	|			И (КлючиАналитикиУчетаНоменклатуры.Характеристика = ДатыПоступленияТоваровОрганизаций.Характеристика)
	|			И (КлючиАналитикиУчетаНоменклатуры.Серия = ДатыПоступленияТоваровОрганизаций.Серия)
	|			И (КлючиАналитикиУчетаНоменклатуры.Назначение = ДатыПоступленияТоваровОрганизаций.Назначение)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ОстаткиТоваровОрганизаций
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ОстаткиПереданныхТоваров";

	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТаблицаОстатковТоваровОрганизаций", ТаблицаОстатковТоваровОрганизаций);
	Запрос.УстановитьПараметр("ТаблицаОстатковПереданныхТоваров", ТаблицаОстатковПереданныхТоваров);

	Запрос.Выполнить();

КонецПроцедуры

&ИзменениеИКонтроль("ВычестьИзОстатковПереданныеТовары")
Процедура РСК_ВычестьИзОстатковПереданныеТовары(ТаблицаОстатковТоваровОрганизаций, ТаблицаОстатковПереданныхТоваров, ТаблицаТоваров)
	
	#Вставка
	//++РС Консалт: Минаков А.С. Задача 24557
	СтруктураПоискаТоваров	= Новый Структура("АналитикаУчетаНоменклатуры, ДокументПередачи");
	
	ТаблицаОстатковПереданныхТоваровКопия = ТаблицаОстатковПереданныхТоваров.СкопироватьКолонки();
	
	Для Каждого СтрТоваров Из ТаблицаТоваров Цикл
		
		ОсталосьРаспределить = СтрТоваров.Количество;
		
		ЗаполнитьЗначенияСвойств(СтруктураПоискаТоваров, СтрТоваров);
		
		НайденныеСтроки = ТаблицаОстатковПереданныхТоваров.НайтиСтроки(СтруктураПоискаТоваров);
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			
			Если ОсталосьРаспределить <= 0 Тогда
				Прервать
			КонецЕсли;
			
			Распределить = Мин(НайденнаяСтрока.КоличествоОстаток, ОсталосьРаспределить);
			
			НоваяСтрока = ТаблицаОстатковПереданныхТоваровКопия.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденнаяСтрока);
			
			КоличествоОстаток = НоваяСтрока.КоличествоОстаток;
			
			НоваяСтрока.КоличествоОстаток = Распределить;
			НоваяСтрока.КоличествоПоРНПТОстаток = ?(КоличествоОстаток = 0, 0, НоваяСтрока.КоличествоОстаток * НоваяСтрока.КоличествоПоРНПТОстаток / КоличествоОстаток);	
			НоваяСтрока.НомерГТД = СтрТоваров.НомерГТД;
			
			НайденнаяСтрока.КоличествоОстаток = НайденнаяСтрока.КоличествоОстаток - НоваяСтрока.КоличествоОстаток;
			НайденнаяСтрока.КоличествоПоРНПТОстаток = НайденнаяСтрока.КоличествоПоРНПТОстаток - НоваяСтрока.КоличествоПоРНПТОстаток;
			
			ОсталосьРаспределить = ОсталосьРаспределить - НоваяСтрока.КоличествоОстаток;
			
			Если НайденнаяСтрока.КоличествоОстаток <= 0 Тогда
				ТаблицаОстатковПереданныхТоваров.Удалить(НайденнаяСтрока)
			КонецЕсли	

		КонецЦикла	
		
	КонецЦикла;	
	
	ТаблицаОстатковПереданныхТоваров = ТаблицаОстатковПереданныхТоваровКопия;
	//++РС Консалт: Минаков А.С. Задача 24557
	#КонецВставки
	СтруктураПоискаОстатков	= Новый Структура("Организация, АналитикаУчетаНоменклатуры, ВидЗапасов, НомерГТД");
	СтруктураПоискаТоваров	= Новый Структура("АналитикаУчетаНоменклатуры, ДокументПередачи, НомерГТД");

	Для Каждого СтрПереданных Из ТаблицаОстатковПереданныхТоваров Цикл

		КоличествоКВычету = СтрПереданных.КоличествоОстаток;

		ЗаполнитьЗначенияСвойств(СтруктураПоискаТоваров, СтрПереданных);

		СтрокиТоваров = ТаблицаТоваров.НайтиСтроки(СтруктураПоискаТоваров);
		Если СтрокиТоваров.Количество() > 0 Тогда
			// Вычитаем не превышающее количество из таблицы товаров
			КоличествоКВычету = Мин(СтрокиТоваров[0].Количество, КоличествоКВычету);
		Иначе
			Продолжить;
		КонецЕсли;

		ЗаполнитьЗначенияСвойств(СтруктураПоискаОстатков, СтрПереданных);
		СтрокиОстатковТоваровОрганизаций = ТаблицаОстатковТоваровОрганизаций.НайтиСтроки(СтруктураПоискаОстатков);

		Для Каждого СтрОстатков Из СтрокиОстатковТоваровОрганизаций Цикл

			Если КоличествоКВычету = 0
				Или СтрОстатков.КоличествоОстаток = 0 Тогда

				Прервать;

			КонецЕсли;

			Если СтрОстатков.КоличествоОстаток > КоличествоКВычету Тогда
				КоличествоОстаток = СтрОстатков.КоличествоОстаток;

				СтрОстатков.КоличествоОстаток = СтрОстатков.КоличествоОстаток - КоличествоКВычету;
				СтрОстатков.КоличествоПоРНПТОстаток = СтрОстатков.КоличествоПоРНПТОстаток - (КоличествоКВычету
				* СтрОстатков.КоличествоПоРНПТОстаток / КоличествоОстаток);
			Иначе
				СтрОстатков.КоличествоОстаток = 0;
				СтрОстатков.КоличествоПоРНПТОстаток = 0;
			КонецЕсли;

		КонецЦикла;

		Если СтрокиТоваров.Количество() Тогда
			Количество = СтрокиТоваров[0].Количество;

			СтрокиТоваров[0].Количество = КоличествоКВычету;
			СтрокиТоваров[0].КоличествоПоРНПТ = ?(Количество = 0,
			0,
			КоличествоКВычету * СтрокиТоваров[0].КоличествоПоРНПТ / Количество);
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры
