
&ИзменениеИКонтроль("ПолучитьШтрихкодыНоменклатуры")
Функция РСК_ПолучитьШтрихкодыНоменклатуры(Структура)

	МассивШтрихкодов = Новый Массив();

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК КоличествоЕдиничныхУпаковок,
	|	УпаковкиНоменклатуры.Владелец КАК Владелец,
	|	УпаковкиНоменклатуры.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ УпаковкиНоменклатуры
	|ИЗ
	|	Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиНоменклатуры
	|ГДЕ
	|	УпаковкиНоменклатуры.ПометкаУдаления = ЛОЖЬ
	|	И (УпаковкиНоменклатуры.Владелец = &Владелец
	|		ИЛИ УпаковкиНоменклатуры.Владелец = &НаборУпаковок)
	|	И УпаковкиНоменклатуры.Владелец.ЕдиницаИзмерения = УпаковкиНоменклатуры.ЕдиницаИзмерения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УпаковкиНоменклатуры.КоличествоЕдиничныхУпаковок КАК КоличествоЕдиничныхУпаковок,
	|	УпаковкиНоменклатуры.Владелец КАК Владелец,
	|	УпаковкиНоменклатуры.Ссылка КАК Ссылка
	|ИЗ
	|	УпаковкиНоменклатуры КАК УпаковкиНоменклатуры
	|ГДЕ
	|	УпаковкиНоменклатуры.КоличествоЕдиничныхУпаковок = 1";

	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстЗапросаКоэффициентУпаковки",
	Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки("УпаковкиНоменклатуры", Неопределено));
	НаборУпаковок = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Структура.Номенклатура,"НаборУпаковок");
	Запрос.УстановитьПараметр("НаборУпаковок", НаборУпаковок);
	Запрос.УстановитьПараметр("Владелец", Структура.Номенклатура);

	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЕдиничнаяУпаковка = Выборка.Ссылка;
		// Поиск штрихкодов по единичной упаковке и по единице измерения имеет смысл только тогда,
		// когда у номенклатуры 1 упаковка с коэффициентом 1 и ед. изм. 1.
		Если Не Выборка.Следующий()  Тогда
			Если ЕдиничнаяУпаковка = Структура.Упаковка Тогда
				МассивШтрихкодов = РегистрыСведений.ШтрихкодыНоменклатуры.ШтрихкодыНоменклатуры(Структура.Номенклатура,
				Структура.Характеристика,
				Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка());
				#Вставка
				//++ РС Консалт: Минаков А.С. Задача 20226
				МассивШтрихкодовСерий = РегистрыСведений.ДСТ_ШтрихкодыСерий.ШтрихкодыНоменклатуры(Структура.Номенклатура,
				Структура.Характеристика,
				Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка(),
				?(Структура.Свойство("Серия"), Структура.Серия, Неопределено));
				Для Каждого Штрихкод Из МассивШтрихкодовСерий Цикл 
					МассивШтрихкодов.Добавить(Штрихкод);
				КонецЦикла;
				//++ РС Консалт: Минаков А.С. Задача 20226
				#КонецВставки
			Иначе
				МассивШтрихкодов = РегистрыСведений.ШтрихкодыНоменклатуры.ШтрихкодыНоменклатуры(Структура.Номенклатура,
				Структура.Характеристика,
				ЕдиничнаяУпаковка);
				#Вставка
				//++ РС Консалт: Минаков А.С. Задача 20226
				МассивШтрихкодовСерий = РегистрыСведений.ДСТ_ШтрихкодыСерий.ШтрихкодыНоменклатуры(Структура.Номенклатура,
				Структура.Характеристика,
				ЕдиничнаяУпаковка,
				?(Структура.Свойство("Серия"), Структура.Серия, Неопределено));
				Для Каждого Штрихкод Из МассивШтрихкодовСерий Цикл 
					МассивШтрихкодов.Добавить(Штрихкод);
				КонецЦикла;
				//++ РС Консалт: Минаков А.С. Задача 20226
				#КонецВставки
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Если МассивШтрихкодов.Количество() = 0 Тогда

		МассивШтрихкодов = РегистрыСведений.ШтрихкодыНоменклатуры.ШтрихкодыНоменклатуры(Структура.Номенклатура,
		Структура.Характеристика,
		Структура.Упаковка);

	Иначе

		МассивШтрихкодовУпаковкиСтруктуры = РегистрыСведений.ШтрихкодыНоменклатуры.ШтрихкодыНоменклатуры(Структура.Номенклатура,
		Структура.Характеристика,
		Структура.Упаковка);

		Для Каждого Штрихкод Из МассивШтрихкодовУпаковкиСтруктуры Цикл 
			МассивШтрихкодов.Добавить(Штрихкод);
		КонецЦикла;

	КонецЕсли;
	#Вставка
	//++ РС Консалт: Минаков А.С. Задача 20226
	МассивШтрихкодовСерий = РегистрыСведений.ДСТ_ШтрихкодыСерий.ШтрихкодыНоменклатуры(Структура.Номенклатура,
	Структура.Характеристика,
	Структура.Упаковка,
	?(Структура.Свойство("Серия"), Структура.Серия, Неопределено));
	Для Каждого Штрихкод Из МассивШтрихкодовСерий Цикл 
		МассивШтрихкодов.Добавить(Штрихкод);
	КонецЦикла;
	//++ РС Консалт: Минаков А.С. Задача 20226
	#КонецВставки

	Возврат МассивШтрихкодов;

КонецФункции
