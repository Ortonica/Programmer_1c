
&ИзменениеИКонтроль("ПараметрыРасчетаДатыПлатежа")
Функция РСК_ПараметрыРасчетаДатыПлатежа()

	СтруктураПараметров = Новый Структура;

	СтруктураПараметров.Вставить("ВариантОтсчета",                 ПредопределенноеЗначение("Перечисление.ВариантыОтсчетаДатыПлатежа.ПустаяСсылка"));
	СтруктураПараметров.Вставить("Сдвиг",                          0);
	СтруктураПараметров.Вставить("ДатаПлатежа",                    Дата(1,1,1));
	СтруктураПараметров.Вставить("ДатаЗаказа",                     Дата(1,1,1));
	СтруктураПараметров.Вставить("ДатаСогласования",               Дата(1,1,1));
	СтруктураПараметров.Вставить("ДатаОтгрузки",                   Дата(1,1,1));
	СтруктураПараметров.Вставить("СрокПереходаПраваСобственности", 0);
	СтруктураПараметров.Вставить("Календарь",                      ПредопределенноеЗначение("Справочник.ПроизводственныеКалендари.ПустаяСсылка"));
	СтруктураПараметров.Вставить("ДатаПереходаПраваСобственности", Дата(1,1,1));
	#Вставка
	//++ РС Консалт: Трофимов Евгений 02.03.2023 Задача 23985
	//e1cib/data/Документ.Задание?ref=b37a5a78714d889647e1e0192b7e7134
	СтруктураПараметров.Вставить("ДатаРазмещения",					Дата(1,1,1));
	СтруктураПараметров.Вставить("ДатаПрибытияВПорт",				Дата(1,1,1));
	СтруктураПараметров.Вставить("ДатаОкончанияПроизводства",		Дата(1,1,1));
	//-- КонецЗадачи 23985
	#КонецВставки

	Возврат СтруктураПараметров;

КонецФункции

&ИзменениеИКонтроль("ДатаПлатежаБезКалендаря")
Функция РСК_ДатаПлатежаБезКалендаря(Параметры)

	Если НЕ ЗначениеЗаполнено(Параметры.ДатаСогласования) Тогда
		Параметры.ДатаСогласования = Параметры.ДатаЗаказа;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(Параметры.ДатаОтгрузки) Тогда
		Параметры.ДатаОтгрузки = Параметры.ДатаЗаказа;
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(Параметры.ВариантОтсчета) Тогда
		ДатаПлатежа = Дата(1,1,1);
	ИначеЕсли Параметры.ВариантОтсчета = ПредопределенноеЗначение("Перечисление.ВариантыОтсчетаДатыПлатежа.ОтДатыЗаказа") И ЗначениеЗаполнено(Параметры.ДатаЗаказа) Тогда
		ДатаПлатежа = Параметры.ДатаЗаказа + 86400 * Параметры.Сдвиг;
	ИначеЕсли Параметры.ВариантОтсчета = ПредопределенноеЗначение("Перечисление.ВариантыОтсчетаДатыПлатежа.ОтДатыСогласования") И ЗначениеЗаполнено(Параметры.ДатаСогласования) Тогда
		ДатаПлатежа = Параметры.ДатаСогласования + 86400 * Параметры.Сдвиг;
	ИначеЕсли Параметры.ВариантОтсчета = ПредопределенноеЗначение("Перечисление.ВариантыОтсчетаДатыПлатежа.ДоДатыОтгрузки") И ЗначениеЗаполнено(Параметры.ДатаОтгрузки) Тогда
		ДатаПлатежа = Параметры.ДатаОтгрузки - 86400 * Параметры.Сдвиг;
	ИначеЕсли Параметры.ВариантОтсчета = ПредопределенноеЗначение("Перечисление.ВариантыОтсчетаДатыПлатежа.ОтДатыОтгрузки") И ЗначениеЗаполнено(Параметры.ДатаОтгрузки) Тогда
		ДатаПлатежа = Параметры.ДатаОтгрузки + 86400 * Параметры.Сдвиг;
	ИначеЕсли Параметры.ВариантОтсчета = ПредопределенноеЗначение("Перечисление.ВариантыОтсчетаДатыПлатежа.ОтДатыПереходаПраваСобственности") И ЗначениеЗаполнено(Параметры.ДатаПереходаПраваСобственности) Тогда
		ДатаПлатежа = Параметры.ДатаПереходаПраваСобственности + 86400 * (Параметры.Сдвиг);
	ИначеЕсли Параметры.ВариантОтсчета = ПредопределенноеЗначение("Перечисление.ВариантыОтсчетаДатыПлатежа.ОтДатыПереходаПраваСобственности") И ЗначениеЗаполнено(Параметры.ДатаОтгрузки) Тогда
		ДатаПлатежа = Параметры.ДатаОтгрузки + 86400 * (Параметры.Сдвиг + Параметры.СрокПереходаПраваСобственности);
	#Вставка
	//++ РС Консалт: Трофимов Евгений 03.03.2023 Задача 23985
	//e1cib/data/Документ.Задание?ref=b37a5a78714d889647e1e0192b7e7134
	ИначеЕсли Параметры.ВариантОтсчета = ПредопределенноеЗначение("Перечисление.ВариантыОтсчетаДатыПлатежа.ДоДатыРазмещения") И ЗначениеЗаполнено(Параметры.ДатаРазмещения) Тогда
		ДатаПлатежа = Параметры.ДатаРазмещения - 86400 * Параметры.Сдвиг; 
	ИначеЕсли Параметры.ВариантОтсчета = ПредопределенноеЗначение("Перечисление.ВариантыОтсчетаДатыПлатежа.ДоДатыПрибытияВПорт") И ЗначениеЗаполнено(Параметры.ДатаПрибытияВПорт) Тогда
		ДатаПлатежа = Параметры.ДатаПрибытияВПорт - 86400 * Параметры.Сдвиг; 
	ИначеЕсли Параметры.ВариантОтсчета = ПредопределенноеЗначение("Перечисление.ВариантыОтсчетаДатыПлатежа.ДоДатыОкончанияПроизводства") И ЗначениеЗаполнено(Параметры.ДатаОкончанияПроизводства) Тогда
		ДатаПлатежа = Параметры.ДатаОкончанияПроизводства - 86400 * Параметры.Сдвиг; 
	ИначеЕсли Параметры.ВариантОтсчета = ПредопределенноеЗначение("Перечисление.ВариантыОтсчетаДатыПлатежа.ОтДатыОкончанияПроизводства") И ЗначениеЗаполнено(Параметры.ДатаОкончанияПроизводства) Тогда
		ДатаПлатежа = Параметры.ДатаОкончанияПроизводства + 86400 * Параметры.Сдвиг; 
	//-- КонецЗадачи 23985	
	#КонецВставки
	Иначе
		ДатаПлатежа = Дата(1,1,1);
	КонецЕсли;

	Возврат ДатаПлатежа;

КонецФункции
