
&ИзменениеИКонтроль("ОтобразитьНедогрузПерегруз")
Процедура РСК_ОтобразитьНедогрузПерегруз(Форма, ДанныеВесОбъем)

	Элементы = Форма.Элементы;
	Если ДанныеВесОбъем <> Неопределено Тогда
		#Удаление
		НедогрузВес = Окр(ДанныеВесОбъем.ГрузоподъемностьВЕдПользователя  - ДанныеВесОбъем.Вес);
		НедогрузОбъем = Окр(ДанныеВесОбъем.ВместимостьВЕдПользователя - ДанныеВесОбъем.Объем);
		#КонецУдаления
		#Вставка
		//++РС Консалт: Минаков А.С. Задача 21223
		НедогрузВес = Окр(ДанныеВесОбъем.ГрузоподъемностьВЕдПользователя  - ДанныеВесОбъем.Вес, 1);
		НедогрузОбъем = Окр(ДанныеВесОбъем.ВместимостьВЕдПользователя - ДанныеВесОбъем.Объем, 1);
		//++РС Консалт: Минаков А.С. Задача 21223
		#КонецВставки
	КонецЕсли;

	Шаблон = НСтр("ru = '%Количество% %ЕдиницаИзмерения%';
	|en = '%Количество% %ЕдиницаИзмерения%'");

	Если ДанныеВесОбъем = Неопределено
		Или ДанныеВесОбъем.ГрузоподъемностьВЕдПользователя = 0 Тогда
		Форма.НадписьНедогрузВес = "";
		Элементы.ДекорацияВес.Картинка = Новый Картинка;
	ИначеЕсли НедогрузВес >= 0 Тогда
		Текст = СтрЗаменить(Шаблон, "%Количество%", НедогрузВес);
		Текст = СтрЗаменить(Текст, "%ЕдиницаИзмерения%", Форма.ЕдиницаИзмеренияВеса);
		Форма.НадписьНедогрузВес = Новый ФорматированнаяСтрока(Текст,Новый Шрифт(,,Истина),Форма.ЦветЗеленый);
		ЗаполненностьВес = ДанныеВесОбъем.Вес / ДанныеВесОбъем.ГрузоподъемностьВЕдПользователя * 100;
		Если ЗаполненностьВес > 95 Тогда
			Элементы.ДекорацияВес.Картинка = БиблиотекаКартинок.Состояние100Процентов;
		ИначеЕсли ЗаполненностьВес > 45 Тогда
			Элементы.ДекорацияВес.Картинка = БиблиотекаКартинок.Состояние66Процентов;
		ИначеЕсли ЗаполненностьВес > 5 Тогда
			Элементы.ДекорацияВес.Картинка = БиблиотекаКартинок.Состояние33Процента;
		Иначе
			Элементы.ДекорацияВес.Картинка = БиблиотекаКартинок.Состояние0Процентов;
		КонецЕсли;
	Иначе
		Текст = СтрЗаменить(Шаблон, "%Количество%", -НедогрузВес);
		Текст = СтрЗаменить(Текст, "%ЕдиницаИзмерения%", Форма.ЕдиницаИзмеренияВеса);
		Форма.НадписьНедогрузВес = Новый ФорматированнаяСтрока(Текст,Новый Шрифт(,,Истина),Форма.ЦветКрасный);
		Элементы.ДекорацияВес.Картинка = БиблиотекаКартинок.Остановить;
	КонецЕсли;

	Если ДанныеВесОбъем = Неопределено
		Или ДанныеВесОбъем.ВместимостьВЕдПользователя = 0 Тогда
		Форма.НадписьНедогрузОбъем = "";
		Элементы.ДекорацияОбъем.Картинка = Новый Картинка;
	ИначеЕсли НедогрузОбъем >= 0 Тогда
		Текст = СтрЗаменить(Шаблон, "%Количество%", НедогрузОбъем); 
		Текст = СтрЗаменить(Текст, "%ЕдиницаИзмерения%", Форма.ЕдиницаИзмеренияОбъема);
		Форма.НадписьНедогрузОбъем = Новый ФорматированнаяСтрока(Текст,Новый Шрифт(,,Истина),Форма.ЦветЗеленый);
		ЗаполненностьОбъем = ДанныеВесОбъем.Объем / ДанныеВесОбъем.ВместимостьВЕдПользователя * 100;
		Если ЗаполненностьОбъем > 95 Тогда
			Элементы.ДекорацияОбъем.Картинка = БиблиотекаКартинок.Состояние100Процентов;
		ИначеЕсли ЗаполненностьОбъем > 45 Тогда
			Элементы.ДекорацияОбъем.Картинка = БиблиотекаКартинок.Состояние66Процентов;
		ИначеЕсли ЗаполненностьОбъем > 5 Тогда
			Элементы.ДекорацияОбъем.Картинка = БиблиотекаКартинок.Состояние33Процента;
		Иначе
			Элементы.ДекорацияОбъем.Картинка = БиблиотекаКартинок.Состояние0Процентов;
		КонецЕсли;
	Иначе
		Текст = СтрЗаменить(Шаблон, "%Количество%", -НедогрузОбъем); 
		Текст = СтрЗаменить(Текст, "%ЕдиницаИзмерения%", Форма.ЕдиницаИзмеренияОбъема);
		Форма.НадписьНедогрузОбъем = Новый ФорматированнаяСтрока(Текст,Новый Шрифт(,,Истина),Форма.ЦветКрасный);
		Элементы.ДекорацияОбъем.Картинка = БиблиотекаКартинок.Остановить;
	КонецЕсли;

КонецПроцедуры

&ИзменениеИКонтроль("УстановитьСтраницуДоставки")
Процедура РСК_УстановитьСтраницуДоставки(ЭлементыФормы, СпособДоставки, ИспользоватьЗаданияНаПеревозкуДляУчетаДоставкиПеревозчиками, ЭтоРаспоряжениеПоСоглашению, ЭтоРаспоряжениеПоДоговору)

	Если ЭтоРаспоряжениеПоСоглашению Или ЭтоРаспоряжениеПоДоговору Тогда
		Если ЭлементыФормы.Найти("ГруппаСводнаяИнформацияПоДоставке") <> Неопределено Тогда
			ЭлементыФормы.СтраницыДоставки.ТекущаяСтраница = ЭлементыФормы.ГруппаСводнаяИнформацияПоДоставке;
		КонецЕсли;
		Если ЭлементыФормы.Найти("ГруппаСпособДоставкиПеревозчик") <> Неопределено Тогда
			ЭлементыФормы.ГруппаСпособДоставкиПеревозчик.Видимость = Ложь;
		КонецЕсли;
		Если ЭтоРаспоряжениеПоСоглашению
			И ЭлементыФормы.Найти("ГруппаНадписьНастройкаПараметровДоставки") <> Неопределено Тогда
			ЭлементыФормы.ГруппаНадписьНастройкаПараметровДоставки.ТекущаяСтраница = ЭлементыФормы.ГруппаНадписьНастройкаПараметровДоставкиСоглашение;
		ИначеЕсли Не ЭтоРаспоряжениеПоСоглашению
			И ЭлементыФормы.Найти("ГруппаНадписьНастройкаПараметровДоставки") <> Неопределено Тогда
			ЭлементыФормы.ГруппаНадписьНастройкаПараметровДоставки.ТекущаяСтраница = ЭлементыФормы.ГруппаНадписьНастройкаПараметровДоставкиДоговор;
		КонецЕсли;
	Иначе
		Если ЭлементыФормы.Найти("ГруппаСпособДоставкиПеревозчик") <> Неопределено Тогда
			ЭлементыФормы.ГруппаСпособДоставкиПеревозчик.Видимость = Истина;
		КонецЕсли;
		Если ЭлементыФормы.Найти("СтраницыПеревозчик") <> Неопределено Тогда
			Если СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.СиламиПеревозчика")
				#Вставка
	//++ РС Консалт: Минаков А.С. Задача 22756
				Или СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.РСК_ПеревозчикДоПВЗ")
				Или СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.РСК_ПеревозчикДоКлиента")
				#КонецВставки
				Или СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.СиламиПеревозчикаПоАдресу")
				Или СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.СиламиПеревозчикаДоНашегоСклада")
				Или СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.СиламиПеревозчикаДоПунктаПередачи") Тогда
				ЭлементыФормы.СтраницыПеревозчик.ТекущаяСтраница = ЭлементыФормы.СтраницаПеревозчик;
			Иначе
				ЭлементыФормы.СтраницыПеревозчик.ТекущаяСтраница = ЭлементыФормы.СтраницаПеревозчикПусто;
			КонецЕсли;
		КонецЕсли;

		Если (СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.ДоКлиентаКурьером"))
			И (ЭлементыФормы.Найти("СтраницаДоставкаДоПолучателяКурьером") <> Неопределено) Тогда
			ЭлементыФормы.СтраницыПеревозчик.ТекущаяСтраница = ЭлементыФормы.СтраницаКурьер;
		КонецЕсли;

		Если (СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.ДоКлиента")
			Или СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.КПолучателюОпределяетСлужбаДоставки"))
			И ЭлементыФормы.Найти("СтраницаДоставкаДоПолучателя") <> Неопределено Тогда
			ЭлементыФормы.СтраницыДоставки.ТекущаяСтраница = ЭлементыФормы.СтраницаДоставкаДоПолучателя;

		ИначеЕсли (СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.ДоКлиентаКурьером"))
			И (ЭлементыФормы.Найти("СтраницаДоставкаДоПолучателяКурьером") <> Неопределено) Тогда
			ЭлементыФормы.СтраницыДоставки.ТекущаяСтраница = ЭлементыФормы.СтраницаДоставкаДоПолучателяКурьером;
			
		#Удаление	
		ИначеЕсли СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.СиламиПеревозчика")
		#КонецУдаления
		#Вставка
	//++ РС Консалт: Минаков А.С. Задача 22756
		ИначеЕсли (СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.СиламиПеревозчика")
				Или СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.РСК_ПеревозчикДоПВЗ")
				Или СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.РСК_ПеревозчикДоКлиента"))
		#КонецВставки
			И ЭлементыФормы.Найти("СтраницаДоставкаПеревозчиком") <> Неопределено Тогда
			ЭлементыФормы.СтраницыДоставки.ТекущаяСтраница = ЭлементыФормы.СтраницаДоставкаПеревозчиком;

		ИначеЕсли СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.СиламиПеревозчикаПоАдресу") Тогда
			Если ЭлементыФормы.Найти("СтраницаДоставкаПеревозчикомПоАдресу") <> Неопределено Тогда
				ЭлементыФормы.СтраницыДоставки.ТекущаяСтраница = ЭлементыФормы.СтраницаДоставкаПеревозчикомПоАдресу;
			ИначеЕсли ЭлементыФормы.Найти("СтраницаДоставкаПеревозчиком") <> Неопределено Тогда
				ЭлементыФормы.СтраницыДоставки.ТекущаяСтраница = ЭлементыФормы.СтраницаДоставкаПеревозчиком;
			КонецЕсли;

		ИначеЕсли СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.НашимиСиламиСАдресаОтправителя")
			Или СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.СиламиПеревозчикаДоПунктаПередачи")
			Или СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.ОтОтправителяОпределяетСлужбаДоставки") Тогда

			ЭлементыФормы.СтраницыДоставки.ТекущаяСтраница = ЭлементыФормы.СтраницаДоставкаНашимиСилами;
			Если СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.СиламиПеревозчикаДоПунктаПередачи") Тогда
				ЭлементыФормы.СтраницыАдресПередачи.ТекущаяСтраница = ЭлементыФормы.СтраницаАдресПеревозчика;
			Иначе
				ЭлементыФормы.СтраницыАдресПередачи.ТекущаяСтраница = ЭлементыФормы.СтраницаАдресПоставщика;
			КонецЕсли;

		ИначеЕсли ЭлементыФормы.Найти("СтраницаСамовывоз") <> Неопределено Тогда

			ЭлементыФормы.СтраницыДоставки.ТекущаяСтраница = ЭлементыФормы.СтраницаСамовывоз;

		ИначеЕсли ЭлементыФормы.Найти("СтраницаДоставкаНеНашимиСилами") <> Неопределено Тогда

			ЭлементыФормы.СтраницыДоставки.ТекущаяСтраница = ЭлементыФормы.СтраницаДоставкаНеНашимиСилами;

		КонецЕсли;
	КонецЕсли;

	ЭлементыФормы.СтраницыДоставки.Видимость = Не ((СпособДоставки = ПредопределенноеЗначение("Перечисление.СпособыДоставки.ОпределяетсяВРаспоряжении"))
	И ЭлементыФормы.Найти("СтраницыДоставки") <> Неопределено);

	Если ИспользоватьЗаданияНаПеревозкуДляУчетаДоставкиПеревозчиками = Неопределено
		Или ЭлементыФормы.Найти("СтраницаДоставка") = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если ДоставкаИспользуется(СпособДоставки, ИспользоватьЗаданияНаПеревозкуДляУчетаДоставкиПеревозчиками) Тогда
		ЭлементыФормы.СтраницаДоставка.Картинка = БиблиотекаКартинок.Комментарий;
	Иначе
		ЭлементыФормы.СтраницаДоставка.Картинка = Новый Картинка;
	КонецЕсли;

КонецПроцедуры

&Вместо("СпособыДоставкиДоКлиентаСНашимУчастием")
Функция РСК_СпособыДоставкиДоКлиентаСНашимУчастием(ИспользоватьЗаданияНаПеревозкуДляУчетаДоставкиПеревозчиками)
	
	СпособыДоставки = ПродолжитьВызов(ИспользоватьЗаданияНаПеревозкуДляУчетаДоставкиПеревозчиками);
	
	Если ИспользоватьЗаданияНаПеревозкуДляУчетаДоставкиПеревозчиками Тогда
		СпособыДоставки.Добавить(ПредопределенноеЗначение("Перечисление.СпособыДоставки.РСК_ПеревозчикДоКлиента")); 
		СпособыДоставки.Добавить(ПредопределенноеЗначение("Перечисление.СпособыДоставки.РСК_ПеревозчикДоПВЗ"));
	КонецЕсли;
	
	Возврат СпособыДоставки
	
КонецФункции

&Вместо("ИспользуемыеСпособыДоставки")
Функция РСК_ИспользуемыеСпособыДоставки(Знач ИспользоватьЗаданияНаПеревозкуДляУчетаДоставкиПеревозчиками)
	
	СпособыДоставки = ПродолжитьВызов(ИспользоватьЗаданияНаПеревозкуДляУчетаДоставкиПеревозчиками);
	
	СпособыДоставки.Добавить(ПредопределенноеЗначение("Перечисление.СпособыДоставки.РСК_ПеревозчикДоКлиента"));
	СпособыДоставки.Добавить(ПредопределенноеЗначение("Перечисление.СпособыДоставки.РСК_ПеревозчикДоПВЗ"));
	
	Возврат СпособыДоставки
	
КонецФункции
