
&ИзменениеИКонтроль("ЗаполнитьМенеджера")
Процедура РСК_ЗаполнитьМенеджера(ДокументОбъект, Перезаполнить, ПоляПоиска)

	Если ТипЗнч(ДокументОбъект) = Тип("ДанныеФормыСтруктура") Тогда
		МетаданныеДокумента = ДокументОбъект.Ссылка.Метаданные();
	Иначе
		МетаданныеДокумента = ДокументОбъект.Метаданные();
	КонецЕсли;

	Если МетаданныеДокумента.Реквизиты.Найти("Менеджер") = Неопределено Тогда
		Возврат;
	КонецЕсли;

	СсылкаНеуказанногоПользователя = Пользователи.СсылкаНеуказанногоПользователя();

	Если НЕ Перезаполнить И ЗначениеЗаполнено(ДокументОбъект.Менеджер)
		И ДокументОбъект.Менеджер <> СсылкаНеуказанногоПользователя Тогда
		Возврат;
	КонецЕсли;

	Если ПустаяСтрока(ПоляПоиска) Тогда
		Если МетаданныеДокумента.Реквизиты.Найти("Договор") <> Неопределено
			И ЗначениеЗаполнено(ДокументОбъект.Договор) Тогда
			ИмяРеквизита = "Менеджер";
			Если ТипЗнч(ДокументОбъект.Договор) = Тип("СправочникСсылка.ДоговорыКредитовИДепозитов") 
				//++ НЕ УТ
				ИЛИ ТипЗнч(ДокументОбъект.Договор) = Тип("СправочникСсылка.ДоговорыАренды")
				//-- НЕ УТ
				Тогда
				ИмяРеквизита = "Ответственный";
			КонецЕсли;
			Менеджер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОбъект.Договор, ИмяРеквизита);
			Если ЗначениеЗаполнено(Менеджер) И Менеджер <> СсылкаНеуказанногоПользователя Тогда
				ДокументОбъект.Менеджер = Менеджер;
				Возврат;
			КонецЕсли;
		КонецЕсли;
		Если МетаданныеДокумента.Реквизиты.Найти("Соглашение") <> Неопределено
			И ЗначениеЗаполнено(ДокументОбъект.Соглашение) Тогда
			Менеджер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОбъект.Соглашение, "Менеджер");
			Если ЗначениеЗаполнено(Менеджер) И Менеджер <> СсылкаНеуказанногоПользователя Тогда
				ДокументОбъект.Менеджер = Менеджер;
				Возврат;
			КонецЕсли;
		КонецЕсли;
		Если МетаданныеДокумента.Реквизиты.Найти("Партнер") <> Неопределено
			И ЗначениеЗаполнено(ДокументОбъект.Партнер) Тогда
			Менеджер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОбъект.Партнер, "ОсновнойМенеджер");
			Если ЗначениеЗаполнено(Менеджер) И Менеджер <> СсылкаНеуказанногоПользователя Тогда
				ДокументОбъект.Менеджер = Менеджер;
			КонецЕсли;
		КонецЕсли;
	Иначе
		МассивПолейПоиска = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ПоляПоиска, , Истина, Истина);
		Для Каждого СтрокаПоиска Из МассивПолейПоиска Цикл
			СоставПути = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(
			СтрокаПоиска, ".", Истина, Истина);
			Если ЗначениеЗаполнено(ДокументОбъект[СоставПути[0]]) Тогда
				Менеджер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОбъект[СоставПути[0]], СоставПути[1]);
				Если ЗначениеЗаполнено(Менеджер) И Менеджер <> СсылкаНеуказанногоПользователя Тогда
					ДокументОбъект.Менеджер = Менеджер;
					Возврат;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;

	КонецЕсли;
	#Вставка
	//++ РС Консалт: Трофимов Евгений 11.02.2023
	//Было:
	////**************** Сторожук  Подстановка менеджера из заказа  
	//Попытка
	//	Если ТипЗнч(ДокументОбъект.ссылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда 
	//		ДокументОбъект.Менеджер = ДокументОбъект.ЗаказКлиента.Менеджер; 
	//		ДокументОбъект.Подразделение = ДокументОбъект.ЗаказКлиента.Менеджер.Подразделение; 
	//	ИначеЕсли (ТипЗнч(ДокументОбъект.ссылка) = Тип("Документссылка.ВозвратТоваровОтКлиента"))И(ТипЗнч(ДокументОбъект.ДокументРеализации.ссылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг")) Тогда
	//		ДокументОбъект.Менеджер = ДокументОбъект.ДокументРеализации.ЗаказКлиента.Менеджер; 
	//		ДокументОбъект.Подразделение = ДокументОбъект.ЗаказКлиента.Менеджер.Подразделение; 

	//	// Корректировку реализации реализовал в модуле документа КорректировкаРеализации	
	//	//ИначеЕсли (ТипЗнч(ДокументОбъект.ссылка) = Тип("ДокументСсылка.КорректировкаРеализации"))И(ТипЗнч(ДокументОбъект.ДокументОснование.ссылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг")) Тогда
	//	//	ДокументОбъект.Менеджер = ДокументОбъект.ДокументОснование.ЗаказКлиента.Менеджер;	
	//	КонецЕсли;   
	//Исключение
	//КонецПопытки;
	////********************************
	//
	//Стало:
	//1. Аннотации &Вместо лучше не использовать. Тжело потом адаптировать при смене релиза. Поэтому заменил на &ИзменениеИКонтроль
	//2. Потытки мешают отлаживать остановку по ошибке при расследовании проблем, не относящихся к этому коду.
	//3. Обращение к ссылочным объектам через точку замедляет работу 1С
	Если ТипЗнч(ДокументОбъект.ссылка) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		Если ЗначениеЗаполнено(ДокументОбъект.ЗаказКлиента) Тогда
			СтруктураМенеджера = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументОбъект.ЗаказКлиента,"Менеджер,Менеджер.Подразделение",Истина);
			ДокументОбъект.Менеджер = СтруктураМенеджера.Менеджер; 
			ДокументОбъект.Подразделение = СтруктураМенеджера.МенеджерПодразделение; 
		КонецЕсли;
	ИначеЕсли ТипЗнч(ДокументОбъект.Ссылка) 			= Тип("ДокументСсылка.ВозвратТоваровОтКлиента")
			И ТипЗнч(ДокументОбъект.ДокументРеализации) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
		Если ЗначениеЗаполнено(ДокументОбъект.ДокументРеализации) Тогда
			ДокументОбъект.Менеджер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОбъект.ДокументРеализации,"ЗаказКлиента.Менеджер",Истина);
		КонецЕсли;
		Если ЗначениеЗаполнено(ДокументОбъект.ЗаказКлиента) Тогда
			ДокументОбъект.Подразделение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументОбъект.ЗаказКлиента,"Менеджер.Подразделение", Истина); 
		КонецЕсли;
	КонецЕсли;   
	//-- Конец
	#КонецВставки

КонецПроцедуры
