
&ИзменениеИКонтроль("РеквизитыПлатежаQRКод")
Функция РСК_РеквизитыПлатежаQRКод(СтруктураДанныхШапки)

	РеквизитыПлатежа = Новый Структура;
	РеквизитыПлатежа.Вставить("ТекстПолучателя", СтруктураДанныхШапки.ПредставлениеПоставщикаДляПлатПоручения);
	РеквизитыПлатежа.Вставить("НомерСчетаПолучателя", СтруктураДанныхШапки.СчетПолучателяПредставление);
	РеквизитыПлатежа.Вставить("НаименованиеБанкаПолучателя", СтруктураДанныхШапки.БанкПолучателяПредставление);
	РеквизитыПлатежа.Вставить("БИКБанкаПолучателя", СтруктураДанныхШапки.БИКБанкаПолучателя);
	РеквизитыПлатежа.Вставить("СчетБанкаПолучателя", СтруктураДанныхШапки.СчетБанкаПолучателяПредставление);
	РеквизитыПлатежа.Вставить("СуммаЧислом");
	РеквизитыПлатежа.Вставить("НазначениеПлатежа", СтруктураДанныхШапки.НазначениеПлатежа);
	РеквизитыПлатежа.Вставить("ИННПолучателя", СтруктураДанныхШапки.ИНН);
	РеквизитыПлатежа.Вставить("КПППолучателя", СтруктураДанныхШапки.КПП);
	РеквизитыПлатежа.Вставить("Ссылка");
	#Вставка
	РеквизитыПлатежа.Вставить("ФамилияПлательщика", СтруктураДанныхШапки.ФамилияПлательщика);
	РеквизитыПлатежа.Вставить("ИмяПлательщика", СтруктураДанныхШапки.ИмяПлательщика);
	РеквизитыПлатежа.Вставить("ОтчествоПлательщика", СтруктураДанныхШапки.ОтчествоПлательщика);
	
	РеквизитыПлатежа.Вставить("НомерСчетаНаОплату", СтруктураДанныхШапки.НомерСчетаНаОплату);
	РеквизитыПлатежа.Вставить("ВерсияСтандартаШтрихкода", 1);
	РеквизитыПлатежа.Вставить("КодКодировкиТекста", 2);
    #КонецВставки
	
	Возврат РеквизитыПлатежа

КонецФункции

&ИзменениеИКонтроль("ЗаполнитьРеквизитыШапкиСчетаНаОплату")
Процедура РСК_ЗаполнитьРеквизитыШапкиСчетаНаОплату(ДанныеПечати, Макет, ТабличныйДокумент, ТаблицаЭтапыОплаты, ТаблицаТовары)
	
	#Удаление
	Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьСчетовНаОплату.ПФ_MXL_СчетНаОплату");
    #КонецУдаления
	#Вставка     
	Макет = УправлениеПечатью.МакетПечатнойФормы("Обработка.ПечатьСчетовНаОплату.РСК_СчетНаОплату");
	#КонецВставки
	
	СведенияОПоставщике = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеПечати.Организация, ДанныеПечати.Дата);
	
	ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокСчета");
	ФормированиеПечатныхФорм.ВывестиЛоготипВТабличныйДокумент(Макет, ОбластьМакета, "ЗаголовокСчетаЛоготип", ДанныеПечати.Организация);
	ШтрихкодированиеПечатныхФорм.ВывестиШтрихкодВТабличныйДокумент(ТабличныйДокумент, Макет, ОбластьМакета, ДанныеПечати.Ссылка);
	
	Если ТаблицаЭтапыОплаты.Количество() = 0 Тогда
		ДатаПлатежа = '00010101';
	ИначеЕсли ТаблицаЭтапыОплаты.Количество() = 1 Тогда
		ДатаПлатежа = ТаблицаЭтапыОплаты[0].ДатаПлатежа;
	Иначе
		ДатаПлатежа = ТаблицаЭтапыОплаты[ТаблицаЭтапыОплаты.Количество()-1].ДатаПлатежа;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаПлатежа) Тогда
		СтруктураДанныхЗаголовок = Новый Структура;
		НадписьСрокДействия = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Счет действителен до %1.';
				|en = 'Account is valid till %1.'", ОбщегоНазначения.КодОсновногоЯзыка()), Формат(ДатаПлатежа, "ДЛФ=D")) + " ";
		СтруктураДанныхЗаголовок.Вставить("СрокДействия", НадписьСрокДействия);
		ОбластьМакета.Параметры.Заполнить(СтруктураДанныхЗаголовок);
	КонецЕсли;
	
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	Если ДанныеПечати.ПлатежЗаРубеж Тогда
		
		ОбластьМакета = Макет.ПолучитьОбласть("ОбразецЗаполненияРеквизитыБанка");
		СтруктураДанныхШапки = Новый Структура;
		ПредставлениеПоставщикаДляПлатПоручения = "";
		
		Если ЗначениеЗаполнено(ДанныеПечати.БанковскийСчет) Тогда
			СтруктураДанныхШапки.Вставить("СчетБанкаПолучателяПредставление", ДанныеПечати.НомерБанковскогоСчета);
			СтруктураДанныхШапки.Вставить("БанкПолучателяПредставление", ДанныеПечати.НаименованиеБанкаМеждународное);
			Если ПустаяСтрока(СтруктураДанныхШапки.БанкПолучателяПредставление) Тогда
				СтруктураДанныхШапки.Вставить("БанкПолучателяПредставление", ДанныеПечати.НаименованиеБанка);
			КонецЕсли; 
			СтруктураДанныхШапки.Вставить("АдресБанкаПолучателяПредставление", ДанныеПечати.АдресБанка);
			СтруктураДанныхШапки.Вставить("СВИФТБанка", ДанныеПечати.СВИФТБанка);
			ПредставлениеПоставщикаДляПлатПоручения = ДанныеПечати.БанковскийСчетТекстКорреспондента;
		КонецЕсли;
		
		Если ПустаяСтрока(ПредставлениеПоставщикаДляПлатПоручения) Тогда
			ПредставлениеПоставщикаДляПлатПоручения = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОПоставщике, "ПолноеНаименование,");
		КонецЕсли;
		СтруктураДанныхШапки.Вставить("ПредставлениеПоставщикаДляПлатПоручения", ПредставлениеПоставщикаДляПлатПоручения);
		ОбластьМакета.Параметры.Заполнить(СтруктураДанныхШапки);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		Если Не ПустаяСтрока(ДанныеПечати.НаименованиеБанкаДляРасчетовМеждународное) Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("ОбразецЗаполненияРеквизитыБанкаКорреспондента");
			
			СтруктураДанныхШапки.Очистить();
			СтруктураДанныхШапки.Вставить("БанкКорреспондентПолучателяПредставление",
				ДанныеПечати.НаименованиеБанкаДляРасчетовМеждународное + " " + ДанныеПечати.АдресБанкаДляРасчетов);
			СтруктураДанныхШапки.Вставить("СВИФТБанкаДляРасчетов", ДанныеПечати.СВИФТБанкаДляРасчетов);
			СтруктураДанныхШапки.Вставить("СчетБанкаДляРасчетовПредставление", ДанныеПечати.СчетВБанкеДляРасчетов);
			
			ОбластьМакета.Параметры.Заполнить(СтруктураДанныхШапки);
			ТабличныйДокумент.Вывести(ОбластьМакета);
		КонецЕсли;
		
		ОбластьМакета = Макет.ПолучитьОбласть("ОбразецЗаполненияНазначениеПлатежа");
		СтруктураДанныхШапки.Очистить();
		Если Не ЗначениеЗаполнено(ДанныеПечати.НазначениеПлатежа)
			И ТипЗнч(ДанныеПечати.Ссылка) <> Тип("ДокументСсылка.СчетНаОплатуКлиенту") Тогда
			СтруктураДанныхШапки.Вставить("НазначениеПлатежа", Документы.СчетНаОплатуКлиенту.СформироватьНазначениеПлатежа(
				ДанныеПечати.Номер, ДанныеПечати.Ссылка));
		Иначе
			СтруктураДанныхШапки.Вставить("НазначениеПлатежа", ДанныеПечати.НазначениеПлатежа);
		КонецЕсли;
		ОбластьМакета.Параметры.Заполнить(СтруктураДанныхШапки);
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
	Иначе
	
		#Удаление
		Если ДанныеПечати.КонтрагентЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо
			И ЗначениеЗаполнено(ДанныеПечати.БанковскийСчет) Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("ОбразецЗаполненияППСКодом");
		Иначе
			ОбластьМакета = Макет.ПолучитьОбласть("ОбразецЗаполненияПП");
		КонецЕсли;
		#КонецУдаления
		#Вставка
		БанковскийСчет = ДанныеПечати.БанковскийСчет;
		
		Если Не ЗначениеЗаполнено(БанковскийСчет) Тогда
			БанковскийСчет = ПолучитьОсновнойБанковскийСчет(ДанныеПечати.Организация);
		КонецЕсли;
		
		Если ДанныеПечати.КонтрагентЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо
			И ЗначениеЗаполнено(БанковскийСчет) Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("ОбразецЗаполненияППСКодом");
		Иначе
			ОбластьМакета = Макет.ПолучитьОбласть("ОбразецЗаполненияПП");
		КонецЕсли;
		#КонецВставки
		
		СтруктураДанныхШапки = Новый Структура;
		СтруктураДанныхШапки.Вставить("ИНН", СведенияОПоставщике.ИНН);
		СтруктураДанныхШапки.Вставить("КПП",СведенияОПоставщике.КПП);
		ПредставлениеПоставщикаДляПлатПоручения = "";
		
		СтруктураДанныхШапки.Вставить("ИдентификаторПлатежа", ДанныеПечати.ИдентификаторПлатежа);
		
		#Удаление
		Если ЗначениеЗаполнено(ДанныеПечати.БанковскийСчет) Тогда
			
			Если ЗначениеЗаполнено(ДанныеПечати.БИКБанкаДляРасчетов) Тогда
				Банк        = ?(ЗначениеЗаполнено(ДанныеПечати.НаименованиеРКЦБанкаДляРасчетов),
				ДанныеПечати.НаименованиеРКЦБанкаДляРасчетов + "//" + ДанныеПечати.НаименованиеБанкаДляРасчетов,
				ДанныеПечати.НаименованиеБанкаДляРасчетов);
				БИК         = ДанныеПечати.БИКБанкаДляРасчетов;
				КоррСчет    = ДанныеПечати.КоррСчетБанкаДляРасчетов;
				ГородБанка  = ДанныеПечати.ГородБанкаДляРасчетов;
				НомерСчета  = ДанныеПечати.КоррСчетБанка;
			Иначе
				Банк        = ?(ЗначениеЗаполнено(ДанныеПечати.НаименованиеРКЦБанка),
				ДанныеПечати.НаименованиеРКЦБанка + "//" + ДанныеПечати.НаименованиеБанка,
				ДанныеПечати.НаименованиеБанка);
				БИК         = ДанныеПечати.БИКБанк;
				КоррСчет    = ДанныеПечати.КоррСчетБанка;
				ГородБанка  = ДанныеПечати.ГородБанка;
				НомерСчета  = ДанныеПечати.НомерБанковскогоСчета;
			КонецЕсли;
			#КонецУдаления
			#Вставка     
			Если ЗначениеЗаполнено(БанковскийСчет) Тогда
				
				Если ЗначениеЗаполнено(БанковскийСчет.БИКБанкаДляРасчетов) Тогда
					Банк		= БанковскийСчет.НаименованиеБанкаДляРасчетов;
					БИК         = БанковскийСчет.БИКБанкаДляРасчетов;
					КоррСчет    = БанковскийСчет.КоррСчетБанкаДляРасчетов;
					ГородБанка  = БанковскийСчет.ГородБанкаДляРасчетов;
					НомерСчета  = БанковскийСчет.КоррСчетБанка;
				Иначе
					Банк		= БанковскийСчет.НаименованиеБанка;
					БИК         = БанковскийСчет.БИКБанка;
					КоррСчет    = БанковскийСчет.КоррСчетБанка;
					ГородБанка  = БанковскийСчет.ГородБанка;
					НомерСчета  = БанковскийСчет.НомерСчета;
				КонецЕсли;
				#КонецВставки
				
				СтруктураДанныхШапки.Вставить("БИКБанкаПолучателя", БИК);
				СтруктураДанныхШапки.Вставить("БанкПолучателя", Банк);
				СтруктураДанныхШапки.Вставить("БанкПолучателяПредставление", СокрЛП(Банк) + " " + ГородБанка);
				СтруктураДанныхШапки.Вставить("СчетБанкаПолучателя", КоррСчет);
				СтруктураДанныхШапки.Вставить("СчетБанкаПолучателяПредставление", КоррСчет);
				СтруктураДанныхШапки.Вставить("СчетПолучателяПредставление", НомерСчета);
				СтруктураДанныхШапки.Вставить("СчетПолучателя", НомерСчета);
				ПредставлениеПоставщикаДляПлатПоручения = ДанныеПечати.БанковскийСчетТекстКорреспондента;
				
			КонецЕсли;
	
		Если ПустаяСтрока(ПредставлениеПоставщикаДляПлатПоручения) Тогда
			ПредставлениеПоставщикаДляПлатПоручения = ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОПоставщике, "ПолноеНаименование,");
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДанныеПечати.НазначениеПлатежа)
			И ТипЗнч(ДанныеПечати.Ссылка) <> Тип("ДокументСсылка.СчетНаОплатуКлиенту") Тогда
			
			СтруктураДанныхШапки.Вставить("НазначениеПлатежа", Документы.СчетНаОплатуКлиенту.СформироватьНазначениеПлатежа(
				ДанныеПечати.Номер, ДанныеПечати.Ссылка));
			
		Иначе
			
			СтруктураДанныхШапки.Вставить("НазначениеПлатежа", ДанныеПечати.НазначениеПлатежа);
			
		КонецЕсли;
		
		СтруктураДанныхШапки.Вставить("ПредставлениеПоставщикаДляПлатПоручения", ПредставлениеПоставщикаДляПлатПоручения);
		ОбластьМакета.Параметры.Заполнить(СтруктураДанныхШапки);
		
		#Удаление
		Если ДанныеПечати.КонтрагентЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо
			И ЗначениеЗаполнено(ДанныеПечати.БанковскийСчет) Тогда
		#КонецУдаления		
		#Вставка
		Если ДанныеПечати.КонтрагентЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо
			И ЗначениеЗаполнено(БанковскийСчет) Тогда
			ФИОПлательщика = СтрРазделить(ДанныеПечати.Контрагент.Наименование, " ", Ложь);
			НомерСчетаНаОплату = Прав(ДанныеПечати.Номер,СтрДлина(ДанныеПечати.Номер)-2);
			
			Если ФИОПлательщика.Количество() = 1 Тогда
				
				СтруктураДанныхШапки.Вставить("ФамилияПлательщика", ФИОПлательщика[0]);
				СтруктураДанныхШапки.Вставить("ИмяПлательщика", "");
				СтруктураДанныхШапки.Вставить("ОтчествоПлательщика", "");
				
			ИначеЕсли ФИОПлательщика.Количество() = 2 Тогда
				
				СтруктураДанныхШапки.Вставить("ФамилияПлательщика", ФИОПлательщика[0]);
				СтруктураДанныхШапки.Вставить("ИмяПлательщика", ФИОПлательщика[1]);
				СтруктураДанныхШапки.Вставить("ОтчествоПлательщика", "");
				
			ИначеЕсли ФИОПлательщика.Количество() = 3 Тогда
				
				СтруктураДанныхШапки.Вставить("ФамилияПлательщика", ФИОПлательщика[0]);
				СтруктураДанныхШапки.Вставить("ИмяПлательщика", ФИОПлательщика[1]);
				СтруктураДанныхШапки.Вставить("ОтчествоПлательщика", ФИОПлательщика[2]);
			Иначе
				СтруктураДанныхШапки.Вставить("ФамилияПлательщика", "");
				СтруктураДанныхШапки.Вставить("ИмяПлательщика", "");
				СтруктураДанныхШапки.Вставить("ОтчествоПлательщика", "");	
			КонецЕсли;
				
			СтруктураДанныхШапки.Вставить("НомерСчетаНаОплату", Прав(ДанныеПечати.Номер,СтрДлина(ДанныеПечати.Номер)-2));
			#КонецВставки
			
			РеквизитыПлатежа = РеквизитыПлатежаQRКод(СтруктураДанныхШапки);
			РеквизитыПлатежа.СуммаЧислом = СуммаКОплатеПоСчету(ДанныеПечати, ТаблицаТовары);
			РеквизитыПлатежа.Ссылка = ДанныеПечати.Ссылка;
			
			ВывестиQRКод(РеквизитыПлатежа, ДанныеПечати, ОбластьМакета);
			
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
	КонецЕсли;
	
	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов().ИмяКолонки;
	ВыводитьКоды = ЗначениеЗаполнено(КолонкаКодов);
	
	Смещать = ТипСмещенияТабличногоДокумента.ПоВертикали;
	ОбластьПервойКолонкиТоваров = Макет.Область("ПерваяКолонкаТовара");
	Если НЕ ВыводитьКоды Тогда
		ОбластьПервойКолонкиТоваров.ШиринаКолонки = ОбластьПервойКолонкиТоваров.ШиринаКолонки + Макет.Область("КолонкаКодов").ШиринаКолонки;
		Макет.УдалитьОбласть(Макет.Область("КолонкаКодов"), Смещать);
	КонецЕсли;
	
	ОбластьМакета = Макет.ПолучитьОбласть("Заголовок");
	
	ТекстЗаголовка = ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(ДанныеПечати, НСтр("ru = 'Счет на оплату';
																										|en = 'Commercial invoice'", ОбщегоНазначения.КодОсновногоЯзыка()));
	СтруктураДанныхШапки = Новый Структура;
	СтруктураДанныхШапки.Вставить("ТекстЗаголовка", ТекстЗаголовка);
	ОбластьМакета.Параметры.Заполнить(СтруктураДанныхШапки);
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Поставщик");
	
	СтруктураДанныхПоставщик = Новый Структура;
	СтруктураДанныхПоставщик.Вставить("ПредставлениеПоставщика", 
		ФормированиеПечатныхФорм.ОписаниеОрганизации(ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеПечати.ОрганизацияПоставщик, ДанныеПечати.Дата),
			"ПолноеНаименование,ИНН,КПП,ЮридическийАдрес,Телефоны,"));
	ОбластьМакета.Параметры.Заполнить(СтруктураДанныхПоставщик);
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	ОбластьМакета = Макет.ПолучитьОбласть("Покупатель");
	СтруктураДанныхПокупатель = Новый Структура;
	#Вставка     
	СведенияОбОрганизации = ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеПечати.Контрагент, ДанныеПечати.Дата);
	Если ДанныеПечати.КонтрагентЮрФизЛицо = Перечисления.ЮрФизЛицо.ФизЛицо Тогда
		СведенияОбОрганизации.ПолноеНаименование = ДанныеПечати.Ссылка.Контрагент.Наименование;
	КонецЕсли;
	
	СтруктураДанныхПокупатель.Вставить("ПредставлениеПолучателя", 
	ФормированиеПечатныхФорм.ОписаниеОрганизации(СведенияОбОрганизации,	"ПолноеНаименование,ИНН,КПП,ЮридическийАдрес,Телефоны,"));
	#КонецВставки
	#Удаление
	СтруктураДанныхПокупатель.Вставить("ПредставлениеПолучателя", 
	ФормированиеПечатныхФорм.ОписаниеОрганизации(ФормированиеПечатныхФорм.СведенияОЮрФизЛице(ДанныеПечати.Контрагент, ДанныеПечати.Дата),
	"ПолноеНаименование,ИНН,КПП,ЮридическийАдрес,Телефоны,"));
	#КонецУдаления
	ОбластьМакета.Параметры.Заполнить(СтруктураДанныхПокупатель);
	ТабличныйДокумент.Вывести(ОбластьМакета);
	
	Если ЗначениеЗаполнено(ДанныеПечати.Грузоотправитель) Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("Грузоотправитель");
		СтруктураДанныхГрузоотправитель = Новый Структура;
		СтруктураДанныхГрузоотправитель.Вставить("ПредставлениеГрузоотправителя", ОписаниеОрганизации(ДанныеПечати, "Грузоотправитель"));
		ОбластьМакета.Параметры.Заполнить(СтруктураДанныхГрузоотправитель);
		ТабличныйДокумент.Вывести(ОбластьМакета);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеПечати.Грузополучатель) Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("Грузополучатель");
		СтруктураДанныхГрузополучатель = Новый Структура;
		СтруктураДанныхГрузополучатель.Вставить("ПредставлениеГрузополучателя", ОписаниеОрганизации(ДанныеПечати, "Грузополучатель"));
		ОбластьМакета.Параметры.Заполнить(СтруктураДанныхГрузополучатель);
		ТабличныйДокумент.Вывести(ОбластьМакета);
	КонецЕсли;
	
КонецПроцедуры

&Вместо("ЗаполнитьТабличныйДокументСчетаНаОплату")
Процедура РСК_ЗаполнитьТабличныйДокументСчетаНаОплату(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати, ПараметрыПечати)

	ШаблоныОшибок = Новый Структура;
	ШаблоныОшибок.Вставить("Товары", НСтр("ru = 'В документе %1 отсутствуют товары. Печать счета на оплату не требуется';
	|en = 'Goods are missing in the %1 document. It is not required to print the commercial invoice'"));
	ШаблоныОшибок.Вставить("ЗаменяющиеТовары", НСтр("ru = 'В документе %1 отсутствуют заменяющие товары. Печать счета на оплату не требуется';
	|en = 'Substitute goods are missing in the %1 document. It is not required to print the commercial invoice'"));
	ШаблоныОшибок.Вставить("Этапы", НСтр("ru = 'В документе %1 отсутствуют этапы оплаты. Печать счета на оплату не требуется';
	|en = 'Payment steps are missing in the %1 document. It is not required to print the commercial invoice'"));

	ИспользуетсяУчетНДС = ПолучитьФункциональнуюОпцию("ИспользоватьУчетНДС");
	ИспользоватьРучныеСкидки         = ПолучитьФункциональнуюОпцию("ИспользоватьРучныеСкидкиВПродажах");
	ИспользоватьАвтоматическиеСкидки = ПолучитьФункциональнуюОпцию("ИспользоватьАвтоматическиеСкидкиВПродажах");

	КолонкаКодов = ФормированиеПечатныхФорм.ДополнительнаяКолонкаПечатныхФормДокументов();
	ИмяКолонкиКодов = КолонкаКодов.ИмяКолонки;
	ПредставлениеКолонкиКодов = КолонкаКодов.ПредставлениеКолонки;

	СтруктураИмяДопКолонки = Новый Структура("ИмяКолонкиКодов, ПредставлениеКолонкиКодов", ИмяКолонкиКодов, ПредставлениеКолонкиКодов);

	ДанныеПечати = ДанныеДляПечати.РезультатПоШапке.Выбрать();
	ЭтапыОплаты = ДанныеДляПечати.РезультатПоЭтапамОплаты.Выгрузить();
	Товары = ДанныеДляПечати.РезультатПоТабличнойЧасти.Выгрузить();
	
	//++РС Консалт Петров А.В.
	Если ТипЗнч(ДанныеДляПечати.Документ) <> Тип("ДокументСсылка.РеализацияУслугПрочихАктивов") Тогда
		ТаблицаСопоставлений = ДанныеДляПечати.Документ.Товары;
		
		Если ТаблицаСопоставлений.Количество() > 0 Тогда
			
			Для Каждого Строка из ТаблицаСопоставлений Цикл
				
				СтрокаТовара = Товары.Найти(Строка.Номенклатура, "Номенклатура");
				
				Если СтрокаТовара <> Неопределено Тогда
					Если ЗначениеЗаполнено(Строка.НоменклатураПартнера) Тогда
						
						СтрокаНоменклатура = Строка.НоменклатураПартнера;
						
						СтрокаТовара.Номенклатура = СтрокаНоменклатура;                   
						НаименованиеОригинала = СтрокаТовара.НаименованиеПолное;
						СтрокаТовара.НаименованиеПолное = СтрокаНоменклатура.НаименованиеПолное;
						СтрокаТовара.Характеристика = НаименованиеОригинала;
						
					Иначе 
						
						СтрокаТовара.НаименованиеПолное = Строка.Номенклатура.НаименованиеПолное; 
						
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;	
			
		КонецЕсли;
	КонецЕсли;
	//--РС Консалт Петров А.В.
	
	ЭтапыЗалоговойТары = ЭтапыОплаты.НайтиСтроки(Новый Структура("ЭтоЗалогЗаТару", Истина));
	ТолькоЗалогЗаТару = ЭтапыЗалоговойТары.Количество() = ЭтапыОплаты.Количество() И ЭтапыЗалоговойТары.Количество() > 0;

	Если Товары.Колонки.Найти("Содержание")=Неопределено Тогда
		ЕстьСодержание = Ложь;
	Иначе
		ЕстьСодержание = Истина;
	КонецЕсли;

	ПоказыватьНДС = Константы.ВыводитьДопКолонкиНДС.Получить();
	ПервыйДокумент = Истина;

	Пока ДанныеПечати.Следующий() Цикл

		Отказ = Ложь;

		СтруктураПоиска = Новый Структура("Ссылка", ДанныеПечати.Ссылка);

		ИспользоватьНаборы = Ложь;
		Если Товары.Колонки.Найти("ЭтоНабор") <> Неопределено Тогда
			ИспользоватьНаборы = Истина;
		КонецЕсли;

		ТаблицаТовары = Товары.НайтиСтроки(СтруктураПоиска);
		ТаблицаЭтапыОплаты = ЭтапыОплаты.НайтиСтроки(СтруктураПоиска);

		ПроверкаЗаполненияДокумента(ДанныеПечати, ТаблицаТовары, ТаблицаЭтапыОплаты, ШаблоныОшибок, Отказ, Истина);
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;

		Если ПервыйДокумент Тогда
			ПервыйДокумент = Ложь;
		Иначе
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;

		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;

		ЗаголовокСкидки = ФормированиеПечатныхФорм.НужноВыводитьСкидки(ТаблицаТовары, ИспользоватьРучныеСкидки Или ИспользоватьАвтоматическиеСкидки);
		ЕстьСкидки = ЗаголовокСкидки.ЕстьСкидки;

		НазванияОбластей = НазванияОбластей(ДанныеПечати.УчитыватьНДС И НЕ ТолькоЗалогЗаТару И ПоказыватьНДС, ЕстьСкидки);

		Макет = Новый ТабличныйДокумент;
		ЗаполнитьРеквизитыШапкиСчетаНаОплату(ДанныеПечати, Макет, ТабличныйДокумент, ТаблицаЭтапыОплаты, ТаблицаТовары);

		Если ДанныеПечати.ЧастичнаяОплата ИЛИ ТипЗнч(ДанныеПечати.ДокументОснование) = Тип("ДокументСсылка.ОтчетКомитенту") Тогда

			ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицыЧастичнаяОплата");
			ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);

			ОбластьСтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицыЧастичнаяОплата");
			ОбластьСтрокаТаблицы.Параметры.Заполнить(ДанныеПечати);
			ОбластьСтрокаТаблицы.Параметры.НомерСтроки = 1;
			ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицы);

			ОбластьИтого = Макет.ПолучитьОбласть("ПодвалТаблицыЧастичнаяОплата");
			СтруктураДанныхИтого = Новый Структура;
			СтруктураДанныхИтого.Вставить("Всего", ДанныеПечати.СуммаДокумента);
			ОбластьИтого.Параметры.Заполнить(СтруктураДанныхИтого);
			ОбластьИтого.Параметры.Заполнить(ДанныеПечати);
			ТабличныйДокумент.Присоединить(ОбластьИтого);

			// Вывести ИтогоНДС
			СоответствиеСтавокНДС = Новый Соответствие;
			Если ДанныеПечати.УчитыватьНДС И НЕ ТолькоЗалогЗаТару 
				И НЕ ДанныеПечати.ОперацияОблагаетсяНДСУПокупателя Тогда
				ОбластьИтогоНДС = Макет.ПолучитьОбласть("ПодвалТаблицыНДС");

				Если ТаблицаТовары.Количество() = 0 Тогда
					Если ЗначениеЗаполнено(ДанныеПечати.СтавкаНДС) Тогда
						СуммаНДС = ДанныеПечати.СуммаНДС;
						СоответствиеСтавокНДС.Вставить(ДанныеПечати.СтавкаНДС, СуммаНДС);
					КонецЕсли;
				Иначе
					Для Каждого СтрокаТовара Из ТаблицаТовары Цикл
						Если ИспользоватьНаборы И СтрокаТовара.ЭтоНабор Тогда 
							Продолжить; // Исключаем суммы НДС по набору в целом.
						КонецЕсли;	
						СуммаНДС = СоответствиеСтавокНДС[СтрокаТовара.СтавкаНДС];
						Если СуммаНДС = Неопределено Тогда
							СуммаНДС = СтрокаТовара.СуммаНДС;
						Иначе
							СуммаНДС = СуммаНДС + СтрокаТовара.СуммаНДС;
						КонецЕсли;
						СоответствиеСтавокНДС.Вставить(СтрокаТовара.СтавкаНДС, СуммаНДС);
					КонецЦикла;
				КонецЕсли;
				Если СоответствиеСтавокНДС.Количество() > 0 Тогда
					Для Каждого ТекСтавкаНДС Из СоответствиеСтавокНДС Цикл
						СтруктураДанныхИтогоНДС = Новый Структура;
						СтруктураДанныхИтогоНДС.Вставить("НДС", ФормированиеПечатныхФорм.ТекстНДСПоСтавке(ТекСтавкаНДС.Ключ, ДанныеПечати.ЦенаВключаетНДС));
						Если ЗначениеЗаполнено(ТекСтавкаНДС.Значение) Тогда
							СтруктураДанныхИтогоНДС.Вставить("ВсегоНДС", ФормированиеПечатныхФорм.ФорматСумм(ТекСтавкаНДС.Значение /100 * ДанныеПечати.ПроцентОплаты));
						Иначе
							СтруктураДанныхИтогоНДС.Вставить("ВсегоНДС","-");
						КонецЕсли;
						ОбластьИтогоНДС.Параметры.Заполнить(СтруктураДанныхИтогоНДС);
						ТабличныйДокумент.Вывести(ОбластьИтогоНДС);
					КонецЦикла;
					ОбластьПодвалСНДС = Макет.ПолучитьОбласть("ПодвалТаблицыВсего");
					СтруктураДанныхПодвалСНДС = Новый Структура("ВсегоСНДС", ФормированиеПечатныхФорм.ФорматСумм(ДанныеПечати.СуммаДокумента));
					ОбластьПодвалСНДС.Параметры.Заполнить(СтруктураДанныхПодвалСНДС);
					ТабличныйДокумент.Вывести(ОбластьПодвалСНДС);
				КонецЕсли;
			ИначеЕсли ИспользуетсяУчетНДС Тогда
				СтруктураДанныхПодвалНДС = Новый Структура;
				Если ДанныеПечати.ОперацияОблагаетсяНДСУПокупателя Тогда
					СтруктураДанныхПодвалНДС.Вставить("НДС", НСтр("ru = 'НДС исчисляется налоговым агентом';
					|en = 'VAT is calculated by tax agent'", ОбщегоНазначения.КодОсновногоЯзыка()));
				Иначе
					СтруктураДанныхПодвалНДС.Вставить("НДС", НСтр("ru = 'Без налога (НДС)';
					|en = 'Without tax (VAT)'", ОбщегоНазначения.КодОсновногоЯзыка()));
				КонецЕсли;
				ОбластьПодвалНДС             = Макет.ПолучитьОбласть("ПодвалТаблицыНДС");
				СтруктураДанныхПодвалНДС.Вставить("ВсегоНДС", "-");
				ОбластьПодвалНДС.Параметры.Заполнить(СтруктураДанныхПодвалНДС);
				ТабличныйДокумент.Вывести(ОбластьПодвалНДС);
			КонецЕсли;

			// Вывести Сумму прописью
			ОбластьМакета = Макет.ПолучитьОбласть("СуммаПрописьюЧастичныйСчет");
			СуммаПрописью = НСтр("ru = 'Всего на сумму %СуммаПрописью%';
			|en = 'Total in the amount of %СуммаПрописью%'", ОбщегоНазначения.КодОсновногоЯзыка());
			СуммаПрописью = СтрЗаменить(СуммаПрописью, "%СуммаПрописью%", РаботаСКурсамиВалютУТ.СформироватьСуммуПрописью(ДанныеПечати.СуммаДокумента, ДанныеПечати.Валюта));
			СтруктураДанныхСуммаПрописью = Новый Структура("СуммаПрописью", СуммаПрописью);
			ОбластьМакета.Параметры.Заполнить(СтруктураДанныхСуммаПрописью);
			ТабличныйДокумент.Вывести(ОбластьМакета);

		Иначе

			// Таблица "Товары"
			ОбластьШапкаТаблицы          = Макет.ПолучитьОбласть(НазванияОбластей.ШапкаТаблицы);
			ОбластьСтрокаТаблицыСтандарт = Макет.ПолучитьОбласть(НазванияОбластей.СтрокаТаблицы);
			ОбластьПодвалТаблицы         = Макет.ПолучитьОбласть(НазванияОбластей.ПодвалТаблицы);
			ОбластьПодвалНДС             = Макет.ПолучитьОбласть("ПодвалТаблицыНДС");

			Если ИспользоватьНаборы Тогда
				ОбластьСтрокаТаблицыНабор         = Макет.ПолучитьОбласть(НазванияОбластей.СтрокаТаблицыНабор);
				ОбластьСтрокаТаблицыКомплектующие = Макет.ПолучитьОбласть(НазванияОбластей.СтрокаТаблицыКомплектующие);
			КонецЕсли;

			ПустыеДанные = НаборыСервер.ПустыеДанные();
			ВыводШапки = 0;

			Если ДанныеПечати.УчитыватьНДС И НЕ ТолькоЗалогЗаТару Тогда
				Если ЕстьСкидки Тогда
					ОбластьПодвалСНДС = Макет.ПолучитьОбласть("ПодвалТаблицыВсегоСНДССоСкидкой");
				Иначе
					ОбластьПодвалСНДС = Макет.ПолучитьОбласть("ПодвалТаблицыВсегоСНДС");
				КонецЕсли
			КонецЕсли;

			Если ЕстьСкидки Тогда
				СтруктураЗаголовокСкидки = Новый Структура("Скидка, СуммаБезСкидки", 
				ЗаголовокСкидки.Скидка,
				ЗаголовокСкидки.СуммаСкидки);
				ОбластьШапкаТаблицы.Параметры.Заполнить(СтруктураЗаголовокСкидки);
			КонецЕсли; 
			ОбластьШапкаТаблицы.Параметры.Заполнить(СтруктураИмяДопКолонки);
			ОбластьСуммаПрописью = Макет.ПолучитьОбласть(?(ДанныеПечати.СчетКВозврату, "СуммаПрописьюКВозврату", "СуммаПрописью"));

			МассивПроверкиВывода = Новый Массив;

			Сумма = 0;
			СуммаНДС = 0;
			ВсегоСкидок = 0;
			ВсегоБезСкидок = 0;
			НомерСтроки = 0;
			СоответствиеСтавокНДС = Новый Соответствие;
			Для Каждого СтрокаТовары Из ТаблицаТовары Цикл

				Если НаборыСервер.ИспользоватьОбластьНабор(СтрокаТовары, ИспользоватьНаборы) Тогда
					ОбластьСтрокаТаблицы = ОбластьСтрокаТаблицыНабор;
				ИначеЕсли НаборыСервер.ИспользоватьОбластьКомплектующие(СтрокаТовары, ИспользоватьНаборы) Тогда
					ОбластьСтрокаТаблицы = ОбластьСтрокаТаблицыКомплектующие;
				Иначе
					ОбластьСтрокаТаблицы = ОбластьСтрокаТаблицыСтандарт;
				КонецЕсли;

				Если НаборыСервер.ВыводитьТолькоЗаголовок(СтрокаТовары, ИспользоватьНаборы) Тогда
					НомерСтрокиПечать = "";
				Иначе
					НомерСтроки = НомерСтроки + 1;
					НомерСтрокиПечать = НомерСтроки;
				КонецЕсли;

				Если НомерСтроки = 0 И ВыводШапки <> 2 Тогда
					ВыводШапки = 1;
				КонецЕсли;

				ПрефиксИПостфикс = НаборыСервер.ПолучитьПрефиксИПостфикс(СтрокаТовары, ИспользоватьНаборы);

				ДополнительныеПараметрыПолученияНаименованияДляПечати = НоменклатураКлиентСервер.ДополнительныеПараметрыПредставлениеНоменклатурыДляПечати();
				ДополнительныеПараметрыПолученияНаименованияДляПечати.ВозвратнаяТара = СтрокаТовары.ЭтоВозвратнаяТара;
				ДополнительныеПараметрыПолученияНаименованияДляПечати.КодОсновногоЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
				Если ЕстьСодержание Тогда
					ДополнительныеПараметрыПолученияНаименованияДляПечати.Содержание = СтрокаТовары.Содержание;
				КонецЕсли;

				Товар = ПрефиксИПостфикс.Префикс
				+ НоменклатураКлиентСервер.ПредставлениеНоменклатурыДляПечати(
				СтрокаТовары.НаименованиеПолное,
				СтрокаТовары.Характеристика,
				,
				, // Серия
				ДополнительныеПараметрыПолученияНаименованияДляПечати)
				+ ПрефиксИПостфикс.Постфикс;

				СтруктураДанныхСтроки = Новый Структура;
				СтруктураДанныхСтроки.Вставить("Товар", Товар);
				СтруктураДанныхСтроки.Вставить("НомерСтроки", НомерСтрокиПечать);
				ОбластьСтрокаТаблицы.Параметры.Заполнить(СтрокаТовары);
				Если ЗаголовокСкидки.ЕстьСкидки Тогда
					СтруктураДанныхСтроки.Вставить("СуммаСкидки",
					?(ЗаголовокСкидки.ТолькоНаценка,- СтрокаТовары.СуммаСкидки,СтрокаТовары.СуммаСкидки));
				КонецЕсли;

				Если ЗначениеЗаполнено(СтруктураИмяДопКолонки.ИмяКолонкиКодов) Тогда
					СтруктураДанныхСтроки.Вставить("Артикул", СтрокаТовары[СтруктураИмяДопКолонки.ИмяКолонкиКодов]);
				КонецЕсли;

				Если ДанныеПечати.ОперацияОблагаетсяНДСУПокупателя Тогда
					СтруктураДанныхСтроки.Вставить("СтавкаНДС", НСтр("ru = 'НДС исчисляется налоговым агентом';
					|en = 'VAT is calculated by tax agent'", Метаданные.Языки.Русский.КодЯзыка));
				КонецЕсли;

				ОбластьСтрокаТаблицы.Параметры.Заполнить(СтруктураДанныхСтроки);

				Если НаборыСервер.ВыводитьТолькоЗаголовок(СтрокаТовары, ИспользоватьНаборы) Тогда
					ОбластьСтрокаТаблицы.Параметры.Заполнить(ПустыеДанные);
				КонецЕсли;

				МассивПроверкиВывода.Очистить();
				МассивПроверкиВывода.Добавить(ОбластьСтрокаТаблицы);
				Если НомерСтроки = ТаблицаТовары.Количество() Тогда
					МассивПроверкиВывода.Добавить(ОбластьПодвалТаблицы);
					МассивПроверкиВывода.Добавить(ОбластьПодвалНДС);
					МассивПроверкиВывода.Добавить(ОбластьСуммаПрописью);
				КонецЕсли;

				Если ТабличныйДокумент.ПроверитьВывод(МассивПроверкиВывода) Тогда
					Если (НомерСтроки = 1 И ВыводШапки = 0) ИЛИ (НомерСтроки = 0 И ВыводШапки = 1) Тогда
						ВыводШапки = 2;
						ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
					КонецЕсли;
				Иначе
					ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
				КонецЕсли;

				ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицы);

				Если Не НаборыСервер.ИспользоватьОбластьКомплектующие(СтрокаТовары, ИспользоватьНаборы) Тогда

					Сумма = Сумма + СтрокаТовары.Сумма;
					СуммаНДС = СуммаНДС + СтрокаТовары.СуммаНДС;

					Если ЕстьСкидки Тогда
						ВсегоСкидок = ВсегоСкидок + СтрокаТовары.СуммаСкидки;
						ВсегоБезСкидок = ВсегоБезСкидок + СтрокаТовары.СуммаБезСкидки;
					КонецЕсли;

					Если ДанныеПечати.УчитыватьНДС И НЕ ТолькоЗалогЗаТару Тогда
						СуммаНДСПоСтавке = СоответствиеСтавокНДС[СтрокаТовары.СтавкаНДС];
						Если СуммаНДСПоСтавке = Неопределено Тогда
							СуммаНДСПоСтавке = 0;
						КонецЕсли;
						СоответствиеСтавокНДС.Вставить(СтрокаТовары.СтавкаНДС, СуммаНДСПоСтавке + СтрокаТовары.СуммаНДС);
					КонецЕсли;

				КонецЕсли;

			КонецЦикла;

			СтруктураДанныхВсегоСкидки = Новый Структура;

			// Подвал таблицы "Товары"
			Если ЕстьСкидки Тогда
				СтруктураДанныхВсегоСкидки.Вставить("ВсегоСкидок", ?(ЗаголовокСкидки.ТолькоНаценка,-ВсегоСкидок, ВсегоСкидок));
				СтруктураДанныхВсегоСкидки.Вставить("ВсегоБезСкидок", ВсегоБезСкидок);
				Если ДанныеПечати.УчитыватьНДС И НЕ ТолькоЗалогЗаТару Тогда
					СтруктураДанныхВсегоСкидки.Вставить("ВсегоСуммаНДС", СуммаНДС);
				КонецЕсли;
			КонецЕсли;
			СтруктураДанныхВсегоСкидки.Вставить("Всего", ФормированиеПечатныхФорм.ФорматСумм(Сумма));
			ОбластьПодвалТаблицы.Параметры.Заполнить(СтруктураДанныхВсегоСкидки);
			ТабличныйДокумент.Вывести(ОбластьПодвалТаблицы);

			// Область "ПодвалТаблицыНДС"
			Если ДанныеПечати.УчитыватьНДС И НЕ ТолькоЗалогЗаТару И НЕ ДанныеПечати.ОперацияОблагаетсяНДСУПокупателя Тогда

				Для Каждого ТекСтавкаНДС Из СоответствиеСтавокНДС Цикл
					СтруктураДанныхПодвалНДС = Новый Структура;
					СтруктураДанныхПодвалНДС.Вставить("НДС", ФормированиеПечатныхФорм.ТекстНДСПоСтавке(ТекСтавкаНДС.Ключ, ДанныеПечати.ЦенаВключаетНДС));
					СтруктураДанныхПодвалНДС.Вставить("ВсегоНДС", ФормированиеПечатныхФорм.ФорматСумм(ТекСтавкаНДС.Значение, ,"-"));
					ОбластьПодвалНДС.Параметры.Заполнить(СтруктураДанныхПодвалНДС);
					ТабличныйДокумент.Вывести(ОбластьПодвалНДС);

				КонецЦикла;
				СтруктураДанныхПодвалВсегоСНДС = Новый Структура;
				СтруктураДанныхПодвалНДС.Вставить("ВсегоСНДС", ФормированиеПечатныхФорм.ФорматСумм(Сумма + ?(ДанныеПечати.ЦенаВключаетНДС, 0, СуммаНДС)));
				ОбластьПодвалСНДС.Параметры.Заполнить(СтруктураДанныхПодвалНДС);
				ТабличныйДокумент.Вывести(ОбластьПодвалСНДС);

			ИначеЕсли ИспользуетсяУчетНДС Тогда
				СтруктураДанныхПодвалНДС = Новый Структура;
				Если ДанныеПечати.ОперацияОблагаетсяНДСУПокупателя Тогда
					СтруктураДанныхПодвалНДС.Вставить("НДС", НСтр("ru = 'НДС исчисляется налоговым агентом';
					|en = 'VAT is calculated by tax agent'", ОбщегоНазначения.КодОсновногоЯзыка()));
				Иначе
					СтруктураДанныхПодвалНДС.Вставить("НДС", НСтр("ru = 'Без налога (НДС)';
					|en = 'Without tax (VAT)'", ОбщегоНазначения.КодОсновногоЯзыка()));
				КонецЕсли;
				СтруктураДанныхПодвалНДС.Вставить("ВсегоНДС", "-");
				ОбластьПодвалНДС.Параметры.Заполнить(СтруктураДанныхПодвалНДС);
				ТабличныйДокумент.Вывести(ОбластьПодвалНДС);
			КонецЕсли;

			// Вывести Сумму прописью
			СуммаКПрописи = Сумма + ?(ДанныеПечати.ЦенаВключаетНДС, 0, СуммаНДС);
			ИтоговаяСтрока = НСтр("ru = 'Всего наименований %Количество%, на сумму %Сумма%';
			|en = 'Total number of names %Количество% in the amount of %Сумма%'", ОбщегоНазначения.КодОсновногоЯзыка());
			ИтоговаяСтрока = СтрЗаменить(ИтоговаяСтрока, "%Количество%", НомерСтроки);
			ИтоговаяСтрока = СтрЗаменить(ИтоговаяСтрока, "%Сумма%",      ФормированиеПечатныхФорм.ФорматСумм(СуммаКПрописи, ДанныеПечати.Валюта));

			СтруктураДанныхСуммаПрописью = Новый Структура;
			Если ДанныеПечати.СчетКВозврату Тогда

				СуммаКВозврату = ДанныеПечати.СуммаКВозврату;
				СуммаИтого = СуммаКПрописи-СуммаКВозврату;
				Если СуммаИтого < 0 Тогда
					СуммаИтого = 0;
				КонецЕсли;
				СтруктураДанныхСуммаПрописью.Вставить("СуммаВозврата", ФормированиеПечатныхФорм.ФорматСумм(СуммаКВозврату, ДанныеПечати.Валюта));
				СтруктураДанныхСуммаПрописью.Вставить("СуммаИтогоКОплате", ФормированиеПечатныхФорм.ФорматСумм(СуммаИтого, ДанныеПечати.Валюта, "0"));
				СтруктураДанныхСуммаПрописью.Вставить("СуммаПрописью", РаботаСКурсамиВалютУТ.СформироватьСуммуПрописью(СуммаИтого, ДанныеПечати.Валюта));
			Иначе

				СтруктураДанныхСуммаПрописью.Вставить("СуммаПрописью", РаботаСКурсамиВалютУТ.СформироватьСуммуПрописью(СуммаКПрописи, ДанныеПечати.Валюта));
			КонецЕсли;

			СтруктураДанныхСуммаПрописью.Вставить("ИтоговаяСтрока", ИтоговаяСтрока);
			ОбластьСуммаПрописью.Параметры.Заполнить(СтруктураДанныхСуммаПрописью);
			ТабличныйДокумент.Вывести(ОбластьСуммаПрописью);
		КонецЕсли;
		ЗаполнитьРеквизитыПодвалаСчетаНаОплату(ДанныеПечати, Макет, ТабличныйДокумент, ТаблицаЭтапыОплаты, СоответствиеСтавокНДС, ПараметрыПечати);

		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеПечати.Ссылка);

	КонецЦикла;

КонецПроцедуры

Функция ПолучитьСписокСопоставленийНоменклатуры(СсылкаНаДокумент) 

  Запрос = Новый Запрос;
  Запрос.Текст = "ВЫБРАТЬ
|	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
|	ЗаказКлиента.Партнер КАК Партнер,
|	ЗаказКлиентаТовары.Характеристика КАК Характеристика,
|	ЗаказКлиентаТовары.Номенклатура.НаименованиеПолное КАК НоменклатураНаименованиеПолное,
|	ЗаказКлиентаТовары.Характеристика.НаименованиеПолное КАК ХарактеристикаНаименованиеПолное
|ПОМЕСТИТЬ ВТТовары
|ИЗ
|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ЗаказКлиента
|		ПО (ЗаказКлиентаТовары.Ссылка = ЗаказКлиента.Ссылка)
|ГДЕ
|	ЗаказКлиента.Ссылка = &Ссылка
|;

|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	ВЫБОР
|		КОГДА ЕСТЬNULL(НоменклатураКонтрагентовБЭД.Наименование, 0) = 0
|			ТОГДА ВТТовары.Номенклатура
|		ИНАЧЕ НоменклатураКонтрагентовБЭД.Номенклатура
|	КОНЕЦ КАК ИтоговаяНоменклатура,
|	ВТТовары.Номенклатура КАК НоменклатураПоиска,
|	ВЫБОР
|		КОГДА ЕСТЬNULL(НоменклатураКонтрагентовБЭД.Наименование, 0) = 0
|			ТОГДА ВТТовары.НоменклатураНаименованиеПолное
|		ИНАЧЕ НоменклатураКонтрагентовБЭД.Наименование
|	КОНЕЦ КАК Наименование,
|	ВЫБОР
|		КОГДА ЕСТЬNULL(НоменклатураКонтрагентовБЭД.НаименованиеХарактеристики, 0) = 0
|			ТОГДА ВТТовары.ХарактеристикаНаименованиеПолное
|		ИНАЧЕ НоменклатураКонтрагентовБЭД.НаименованиеХарактеристики
|	КОНЕЦ КАК НаименованиеХарактеристики
|ИЗ
|	ВТТовары КАК ВТТовары
|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НоменклатураКонтрагентовБЭД КАК НоменклатураКонтрагентовБЭД
|		ПО (ВТТовары.Номенклатура = НоменклатураКонтрагентовБЭД.Номенклатура)
|			И (ВТТовары.Партнер = НоменклатураКонтрагентовБЭД.Владелец)
|			И (ВТТовары.Характеристика = НоменклатураКонтрагентовБЭД.Характеристика)";
  
  Запрос.УстановитьПараметр("Ссылка", СсылкаНаДокумент);
  
  Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

&ИзменениеИКонтроль("СформироватьПечатнуюФормуСчетНаОплату")
Функция РСК_СформироватьПечатнуюФормуСчетНаОплату(СтруктураТипов, ОбъектыПечати, ПараметрыПечати)

	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.АвтоМасштаб = Истина;
	ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СЧЕТНАОПЛАТУ";

	НомерТипаДокумента = 0;

	УстановитьПривилегированныйРежим(Истина);

	Для Каждого СтруктураОбъектов Из СтруктураТипов Цикл

		НомерТипаДокумента = НомерТипаДокумента + 1;
		Если НомерТипаДокумента > 1 Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;

		МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(СтруктураОбъектов.Ключ);
		ДанныеДляПечати = МенеджерОбъекта.ПолучитьДанныеДляПечатнойФормыСчетаНаОплату(ПараметрыПечати, СтруктураОбъектов.Значение);
        #Вставка     
		ДанныеДляПечати.Вставить("Документ",СтруктураОбъектов.Значение[0]);
		#КонецВставки
		ЗаполнитьТабличныйДокументСчетаНаОплату(ТабличныйДокумент, ДанныеДляПечати, ОбъектыПечати, ПараметрыПечати);

	КонецЦикла;

	УстановитьПривилегированныйРежим(Ложь);

	Возврат ТабличныйДокумент;

КонецФункции

&ИзменениеИКонтроль("ЗаполнитьРеквизитыПодвалаСчетаНаОплату")
Процедура РСК_ЗаполнитьРеквизитыПодвалаСчетаНаОплату(ДанныеПечати, Макет, ТабличныйДокумент, ТаблицаЭтапыОплаты, СоответствиеСтавокНДС, ПараметрыПечати)

	МассивПроверкиВывода = Новый Массив;

	// Вывести этапы графика оплаты
	Если ТаблицаЭтапыОплаты.Количество() > 1 Тогда

		ИмяКолонкиДатыОплаты = ?(ДанныеПечати.СчетКВозврату,
		НСтр("ru = 'Дата оплаты или возврата';
		|en = 'Date of payment or return'", ОбщегоНазначения.КодОсновногоЯзыка()),
		НСтр("ru = 'Дата оплаты';
		|en = 'Payment date'", ОбщегоНазначения.КодОсновногоЯзыка()));

		ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицыЭтапыОплаты");
		ОбластьПодвалТаблицы = Макет.ПолучитьОбласть("ИтогоЭтапыОплаты");
		СтруктураДанныхШапки = Новый Структура("ИмяКолонкиДатыОплаты",ИмяКолонкиДатыОплаты);
		ОбластьШапкаТаблицы.Параметры.Заполнить(СтруктураДанныхШапки);
		МассивПроверкиВывода.Добавить(ОбластьШапкаТаблицы);
		МассивПроверкиВывода.Добавить(ОбластьПодвалТаблицы);

		ОбластьСтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицыЭтапыОплаты");

		НомерЭтапа = 1;
		Для Каждого ТекЭтап Из ТаблицаЭтапыОплаты Цикл

			ПараметрыСтроки = НовыеПараметрыСтрокиЭтапа();
			ЗаполнитьЗначенияСвойств(ПараметрыСтроки, ТекЭтап);
			ПараметрыСтроки.НомерСтроки = НомерЭтапа;
			Если Не ПараметрыСтроки.ЭтоЗалогЗаТару Тогда
				ПараметрыСтроки.ТекстНДС = ФормированиеПечатныхФорм.СформироватьТекстНДСЭтапаОплаты(
				СоответствиеСтавокНДС,
				ТекЭтап.ПроцентПлатежа);
			Иначе
				ПараметрыСтроки.ПроцентПлатежа = "-";
				ПараметрыСтроки.ТекстНДС = НСтр("ru = 'Залог за тару. Без налога (НДС).';
				|en = 'Packaging deposit. Without tax (VAT).'", ОбщегоНазначения.КодОсновногоЯзыка());
			КонецЕсли;

			ОбластьСтрокаТаблицы.Параметры.Заполнить(ПараметрыСтроки);

			МассивПроверкиВывода.Добавить(ОбластьСтрокаТаблицы);

			Если ТабличныйДокумент.ПроверитьВывод(МассивПроверкиВывода) Тогда
				Если НомерЭтапа = 1 Тогда
					ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
					МассивПроверкиВывода.Удалить(0);
				КонецЕсли;
			Иначе
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ТабличныйДокумент.Вывести(ОбластьШапкаТаблицы);
			КонецЕсли;

			ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицы);
			МассивПроверкиВывода.Удалить(МассивПроверкиВывода.ВГраница());

			НомерЭтапа = НомерЭтапа + 1;

		КонецЦикла;
		ТабличныйДокумент.Вывести(ОбластьПодвалТаблицы);

	КонецЕсли;

	// Вывести дополнительную информацию
	Если ЗначениеЗаполнено(ДанныеПечати.ДополнительнаяИнформация) Тогда

		Область = Макет.ПолучитьОбласть("ДополнительнаяИнформация");
		Область.Параметры.Заполнить(ДанныеПечати);
		ТабличныйДокумент.Вывести(Область);

	КонецЕсли;

	ДолжностьРуководителя = ?(ДанныеПечати.ДолжностьРуководителя = "", 
	Нстр("ru = 'Генеральный директор';
	|en = 'CEO'"), 
	ДанныеПечати.ДолжностьРуководителя);
	
	#Вставка
	//++ Конарев 10.02.2023 Задача № 233396
	ОбластьУсловий = Макет.ПолучитьОбласть("ОбластьУсловия");
	ТабличныйДокумент.Вывести(ОбластьУсловий);
	//
	#КонецВставки
	// Вывести подписи
	Область = Макет.ПолучитьОбласть("ПодвалСчета");
	#Вставка     		
	Область = Макет.ПолучитьОбласть("ПодвалСчетаФизЛицо");	
	#КонецВставки
	СтруктураДанныхПодвал = Новый Структура;
	СтруктураДанныхПодвал.Вставить("ФИОРуководителя", ДанныеПечати.Руководитель);
	СтруктураДанныхПодвал.Вставить("ДолжностьРуководителя", ДолжностьРуководителя);
	СтруктураДанныхПодвал.Вставить("ФИОБухгалтера", ДанныеПечати.ГлавныйБухгалтер);
	СтруктураДанныхПодвал.Вставить("ФИОМенеджер", ФизическиеЛицаУТ.ФамилияИнициалыФизЛица(ДанныеПечати.Менеджер, ДанныеПечати.Дата));
	
	Область.Параметры.Заполнить(СтруктураДанныхПодвал);
	
	#Вставка
	ОтображатьФаксимиле = Ложь;
	Если ПараметрыПечати <> Неопределено И ПараметрыПечати.Свойство("ОтображатьФаксимиле") Тогда
		ОтображатьФаксимиле = ПараметрыПечати.ОтображатьФаксимиле;
	КонецЕсли;
	
	Если ОтображатьФаксимиле Тогда
		ПодписьМенеджера = ПолучитьПечатьИлиПодпись(ДанныеПечати.Ссылка.Менеджер);
		
		Если ЗначениеЗаполнено(ПодписьМенеджера) Тогда
			
			ПутьКФайлу = СтрШаблон("%1%2",ПодписьМенеджера[0].Каталог, ПодписьМенеджера[0].ПутьКФайлу);
			
			ДанныеПодписи = Новый Картинка(ПутьКФайлу,Истина);
			
			Попытка
				Подпись_Рисунок = Область.Рисунки["ПодписьМенеджера0"];
			Исключение
			КонецПопытки;
			Попытка
				Подпись_Рисунок = Область.Рисунки["ПодписьМенеджера1"];
			Исключение
			КонецПопытки;
			Попытка
				Подпись_Высота = Подпись_Рисунок.Высота;
				Подпись_Ширина = Подпись_Рисунок.Ширина;
				Подпись_Рисунок.Картинка = ДанныеПодписи;
			Исключение
			КонецПопытки;
			
		Иначе
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Подпись менеджера не найдена");
			
		КонецЕсли;
	КонецЕсли;
	#КонецВставки
	#Удаление
	ФормированиеПечатныхФорм.ВывестиФаксимилеВТабличныйДокумент(Макет, Область, ДанныеПечати.Организация, ПараметрыПечати);
	#КонецУдаления
	МассивПроверкиВывода.Очистить();
	МассивПроверкиВывода.Добавить(Область);
	Если НЕ ТабличныйДокумент.ПроверитьВывод(МассивПроверкиВывода) Тогда
		ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЕсли;
	ТабличныйДокумент.Вывести(Область);

КонецПроцедуры

Функция ПолучитьПечатьИлиПодпись(Объект)

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Файлы.ТекущаяВерсия.Том.ПолныйПутьWindows КАК Каталог,
	|	Файлы.ПутьКФайлу КАК ПутьКФайлу
	|ИЗ
	|	РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Файлы КАК Файлы
	|		ПО ДополнительныеСведения.Значение = Файлы.Ссылка
	|ГДЕ
	|	ДополнительныеСведения.Объект = &Объект
	|	И ДополнительныеСведения.Свойство = &Свойство";
	
	Запрос.УстановитьПараметр("Объект", Объект);
	
	Если ТипЗнч(Объект) = Тип("СправочникСсылка.Организации") Тогда
				
		Запрос.УстановитьПараметр("Свойство", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя","ПечатьОрганизации"));
		
	Иначе         
		
		Запрос.УстановитьПараметр("Свойство", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя","ПодписьПользователя"));
		
	КонецЕсли; 
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

Функция ПолучитьОсновнойБанковскийСчет(Организация)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	БанковскиеСчетаОрганизаций.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.БанковскиеСчетаОрганизаций.ДополнительныеРеквизиты КАК БанковскиеСчетаОрганизацийДополнительныеРеквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.БанковскиеСчетаОрганизаций КАК БанковскиеСчетаОрганизаций
	|		ПО (БанковскиеСчетаОрганизацийДополнительныеРеквизиты.Ссылка = БанковскиеСчетаОрганизаций.Ссылка)
	|ГДЕ
	|	БанковскиеСчетаОрганизаций.Владелец = &Владелец
	|	И БанковскиеСчетаОрганизацийДополнительныеРеквизиты.Свойство = &Свойство
	|   И БанковскиеСчетаОрганизацийДополнительныеРеквизиты.Значение = &Значение";
	Запрос.УстановитьПараметр("Владелец", Организация);
	Запрос.УстановитьПараметр("Свойство", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ОсновнойСчет"));
	Запрос.УстановитьПараметр("Значение", Истина);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Если ЗначениеЗаполнено(Результат) Тогда
		Возврат Результат[0].Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
    
КонецФункции




