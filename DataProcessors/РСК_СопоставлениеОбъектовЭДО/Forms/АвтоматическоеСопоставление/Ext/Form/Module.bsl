//++ РС Консалт: Трофимов Евгений 20.09.2022 Тикет 20359
//e1cib/data/Документ.Задание?ref=94e137c71231048947b8209affeeaed4
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	СтруктураПараметров = Новый Структура;
	
	РезультатВыполнения = ВыполнитьНаСервере(СтруктураПараметров, УникальныйИдентификатор);
	ИдентификаторЗадания = РезультатВыполнения.ИдентификаторЗадания;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеВыполнения", ЭтаФорма);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполнения, ОповещениеОЗавершении, ПараметрыОжидания);	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 20.09.2022 Тикет 20359
//e1cib/data/Документ.Задание?ref=94e137c71231048947b8209affeeaed4
&НаСервереБезКонтекста
Функция ВыполнитьНаСервере(СтруктураПараметров, УникальныйИдентификатор)

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение = 0; // запускать сразу
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Сопоставление объектов ЭДО'");
	ПараметрыВыполнения.ЗапуститьНеВФоне = Ложь;
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
		"Обработка.РСК_СопоставлениеОбъектовЭДО.МодульОбъекта.АвтоСопоставление",
		СтруктураПараметров, 
		ПараметрыВыполнения
	);
	
	Если РезультатВыполнения.Статус = "Ошибка" Тогда
		ВызватьИсключение РезультатВыполнения.ПодробноеПредставлениеОшибки;
	КонецЕсли;
	
	Возврат РезультатВыполнения;

КонецФункции

//++ РС Консалт: Трофимов Евгений 20.09.2022 Тикет 20359
//e1cib/data/Документ.Задание?ref=94e137c71231048947b8209affeeaed4
&НаКлиенте
Процедура ПослеВыполнения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		ЗагрузитьПодготовленныеДанные(Результат.АдресРезультата);
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.ПодробноеПредставлениеОшибки;
	КонецЕсли;
	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 20.09.2022 Тикет 20359
//e1cib/data/Документ.Задание?ref=94e137c71231048947b8209affeeaed4
&НаСервере
Процедура ЗагрузитьПодготовленныеДанные(АдресРезультата)
	СтруктураРезультата = ПолучитьИзВременногоХранилища(АдресРезультата);
	Если СтруктураРезультата = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураРезультата.Свойство("Протокол", Протокол);
	НеСопоставленнаяНоменклатура.Загрузить(СтруктураРезультата.НеСопоставленнаяНоменклатура);
	Элементы.НеСопоставленнаяНоменклатура.Видимость = НеСопоставленнаяНоменклатура.Количество() > 0;
	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 20.09.2022 Тикет 20359
//e1cib/data/Документ.Задание?ref=94e137c71231048947b8209affeeaed4
&НаКлиенте
Процедура СопоставитьВручную(Команда)
	ОткрытьФорму("Обработка.РСК_СопоставлениеОбъектовЭДО.Форма.Форма");
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 20.09.2022 Тикет 20359
//e1cib/data/Документ.Задание?ref=94e137c71231048947b8209affeeaed4
&НаКлиенте
Процедура НеСопоставленнаяНоменклатураВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
    ДанныеСтроки = Элемент.ДанныеСтроки(ВыбраннаяСтрока);
    ИмяПоля = Сред(Поле.Имя, СтрДлина(Элемент.Имя)+1);
    ПоказатьЗначение(, ДанныеСтроки[ИмяПоля]);
КонецПроцедуры
