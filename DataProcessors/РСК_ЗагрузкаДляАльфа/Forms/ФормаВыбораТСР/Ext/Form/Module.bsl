
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Параметры.Свойство("Заказ", Заказ);
	Параметры.Свойство("Разнарядка", Разнарядка);
	Параметры.Свойство("ИскомоеТСР", ИскомоеТСР);
	Параметры.Свойство("АдресТаблицыВыбранныхТСР", АдресТаблицыВыбранныхТСР);
	ЗаполнитьТаблицуОстатковТСР();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуОстатковТСР()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	тзВыбранные.ТСР_ПоКонтракту КАК ТСР,
		|	тзВыбранные.Количество КАК Количество
		|ПОМЕСТИТЬ тзВыбранные
		|ИЗ
		|	&тзВыбранные КАК тзВыбранные
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаказКлиентаТовары.НоменклатураПартнера КАК ТСР,
		|	МАКСИМУМ(ЗаказКлиентаТовары.Цена) КАК Цена
		|ПОМЕСТИТЬ Цены
		|ИЗ
		|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
		|ГДЕ
		|	ЗаказКлиентаТовары.Ссылка = &Заказ
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказКлиентаТовары.НоменклатураПартнера
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Цены.ТСР КАК ТСР,
		|	СУММА(ЕСТЬNULL(ВложенныйЗапрос.ОстатокЗаказа, 0)) КАК ОстатокЗаказа,
		|	МАКСИМУМ(Цены.Цена) КАК Цена
		|ИЗ
		|	Цены КАК Цены
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			РСК_СостояниеЗаказовКлиентовПоВыдачеОстатки.ТСР КАК ТСР,
		|			РСК_СостояниеЗаказовКлиентовПоВыдачеОстатки.КСозданиюОстаток КАК ОстатокЗаказа
		|		ИЗ
		|			РегистрНакопления.РСК_СостояниеЗаказовКлиентовПоВыдаче.Остатки(&МоментОстатков, Заказ = &Заказ) КАК РСК_СостояниеЗаказовКлиентовПоВыдачеОстатки
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			тзВыбранные.ТСР,
		|			-тзВыбранные.Количество
		|		ИЗ
		|			тзВыбранные КАК тзВыбранные) КАК ВложенныйЗапрос
		|		ПО Цены.ТСР = ВложенныйЗапрос.ТСР
		|
		|СГРУППИРОВАТЬ ПО
		|	Цены.ТСР
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТСР
		|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("Заказ", Заказ);
	Если ЗначениеЗаполнено(Разнарядка) Тогда
		Запрос.УстановитьПараметр("МоментОстатков", Разнарядка.МоментВремени());
	Иначе
		Запрос.УстановитьПараметр("МоментОстатков", Новый Граница(КонецДня(ТекущаяДата()), ВидГраницы.Включая));
	КонецЕсли;
	Запрос.УстановитьПараметр("тзВыбранные", ПолучитьИзВременногоХранилища(АдресТаблицыВыбранныхТСР));
	ОстаткиТСР.Загрузить(Запрос.Выполнить().Выгрузить());
КонецПроцедуры // ЗаполнитьТаблицуОстатковТСР()

&НаКлиенте
Процедура ОстаткиТСРВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ДанныеСтроки = ОстаткиТСР.НайтиПоИдентификатору(ВыбраннаяСтрока);
	Ответ = Новый Структура;
	Ответ.Вставить("ТСР", ДанныеСтроки.ТСР);
	Ответ.Вставить("ОстатокЗаказа", ДанныеСтроки.ОстатокЗаказа);
	Ответ.Вставить("Цена", ДанныеСтроки.Цена);
	Закрыть(Ответ);
КонецПроцедуры


