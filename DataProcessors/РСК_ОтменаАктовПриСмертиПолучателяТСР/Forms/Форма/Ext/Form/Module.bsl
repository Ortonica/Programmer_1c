//++ РС Консалт: Трофимов Евгений 01.11.2022 Задача 21414
//e1cib/data/Документ.Задание?ref=b3ff9ccd1442090a40674c6b43454ea1
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ Параметры.Свойство("СлужебноеИспользование") Тогда
		ОбщегоНазначения.СообщитьПользователю("Обработка вызывается только при смене статуса получателя ТСР на «Умер»");
		Отказ = Истина;
	КонецЕсли;
	Параметры.Свойство("Партнер", Партнер);
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 01.11.2022 Задача 21414
//e1cib/data/Документ.Задание?ref=b3ff9ccd1442090a40674c6b43454ea1
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Партнер", Партнер);
	
	РезультатВыполнения = ВыполнитьНаСервере(СтруктураПараметров, УникальныйИдентификатор);
	ИдентификаторЗадания = РезультатВыполнения.ИдентификаторЗадания;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Истина;
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеВыполнения", ЭтаФорма);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполнения, ОповещениеОЗавершении, ПараметрыОжидания);	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 01.11.2022 Задача 21414
//e1cib/data/Документ.Задание?ref=b3ff9ccd1442090a40674c6b43454ea1
&НаСервереБезКонтекста
Функция ВыполнитьНаСервере(СтруктураПараметров, УникальныйИдентификатор)

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение = 0; // запускать сразу
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Аннулирование актов выдачи ТСР'");
	ПараметрыВыполнения.ЗапуститьНеВФоне = Ложь;
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
		"Обработка.РСК_ОтменаАктовПриСмертиПолучателяТСР.МодульОбъекта.АннулированиеАктовВыдачиТСР",
		СтруктураПараметров, 
		ПараметрыВыполнения
	);
	
	Если РезультатВыполнения.Статус = "Ошибка" Тогда
		ВызватьИсключение РезультатВыполнения.ПодробноеПредставлениеОшибки;
	КонецЕсли;
	
	Возврат РезультатВыполнения;

КонецФункции

//++ РС Консалт: Трофимов Евгений 01.11.2022 Задача 21414
//e1cib/data/Документ.Задание?ref=b3ff9ccd1442090a40674c6b43454ea1
&НаКлиенте
Процедура ПослеВыполнения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		ЗагрузитьПодготовленныеДанные(Результат.АдресРезультата);
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.ПодробноеПредставлениеОшибки;
	КонецЕсли;
	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 01.11.2022 Задача 21414
//e1cib/data/Документ.Задание?ref=b3ff9ccd1442090a40674c6b43454ea1
&НаСервере
Процедура ЗагрузитьПодготовленныеДанные(АдресРезультата)
	СтруктураРезультата = ПолучитьИзВременногоХранилища(АдресРезультата);
	Если СтруктураРезультата = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураРезультата.Свойство("Протокол", Протокол);
	
КонецПроцедуры
