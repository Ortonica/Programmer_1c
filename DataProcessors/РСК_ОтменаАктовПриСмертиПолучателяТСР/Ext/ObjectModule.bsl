// Аннулирование актов выдачи ТСР
//
// Параметры:
//  СтруктураПараметров	 - Структура - Ключи:
//		* Партнер - СправочникСсылка.Партнеры - Получатель ТСР, которому установили статус «Умер»
//  АдресХранилища		 - Строка - Адрес хранилища результатов выполнения процедуры
//
Процедура АннулированиеАктовВыдачиТСР(СтруктураПараметров, АдресХранилища = "") Экспорт

	СтруктураОтвета = Новый Структура;
	СтруктураОтвета.Вставить("Протокол", "");
	
    УстановитьПривилегированныйРежим(Истина);

	МассивСтатусовАктовВыдачи = Новый Массив;
	МассивСтатусовАктовВыдачи.Добавить(Перечисления.РСК_СтатусыАктовВыдачи.ПустаяСсылка());
	МассивСтатусовАктовВыдачи.Добавить(Перечисления.РСК_СтатусыАктовВыдачи.Создан);
	МассивСтатусовАктовВыдачи.Добавить(Перечисления.РСК_СтатусыАктовВыдачи.Подготовлен);
	
	ДополнитьПротокол(СтруктураОтвета, "Получение списка Актов выдачи ТСР...", СтруктураПараметров.Партнер);
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПоручениеЭкспедитору.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ПоручениеЭкспедитору КАК ПоручениеЭкспедитору
		|ГДЕ
		|	ПоручениеЭкспедитору.РСК_АктВыдачиТСР
		|	И ПоручениеЭкспедитору.РСК_СтатусАктаВыдачиТСР В(&МассивСтатусовАктовВыдачи)
		|	И ПоручениеЭкспедитору.Пункт = &Партнер";
	
	Запрос.УстановитьПараметр("МассивСтатусовАктовВыдачи", МассивСтатусовАктовВыдачи);
	Запрос.УстановитьПараметр("Партнер", СтруктураПараметров.Партнер);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ДополнитьПротокол(СтруктураОтвета, "Акты выдачи ТСР с получателем "+СтруктураПараметров.Партнер+", статусы которых требуется изменить, не найдены.");
		ПоместитьВоВременноеХранилище(СтруктураОтвета, АдресХранилища);
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	ДополнитьПротокол(СтруктураОтвета, СтрШаблон(
		"Найдено %1 Актов выдачи ТСР с получателем %2, статусы которых требуется изменить.",
		Выборка.Количество(),
		СтруктураПараметров.Партнер
	), СтруктураПараметров.Партнер);
	
	ё = 0;
	ПрошлыйПроцентВыполнения = 0;
	Пока Выборка.Следующий() Цикл
		ё = ё + 1;
		ПроцентВыполнения = ё * 100 / Выборка.Количество();
		Если ПрошлыйПроцентВыполнения <> Окр(ПроцентВыполнения) Тогда
			ПрошлыйПроцентВыполнения = Окр(ПроцентВыполнения);
			ДлительныеОперации.СообщитьПрогресс(ПрошлыйПроцентВыполнения, "Аннулирование актов выдачи ТСР ("+ё+" из "+Выборка.Количество()+")");
		КонецЕсли;
		
		Акт = Выборка.Ссылка.ПолучитьОбъект();
		Акт.РСК_СтатусАктаВыдачиТСР = Перечисления.РСК_СтатусыАктовВыдачи.Аннулирован;
		Акт.Комментарий = Акт.Комментарий + ?(ПустаяСтрока(Акт.Комментарий),""," ") + "Получатель умер, поручение аннулировано.";
		
		Попытка
			Акт.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
			ДополнитьПротокол(СтруктураОтвета, "Успешно скорректирован "+Выборка.Ссылка, Выборка.Ссылка);
		Исключение
			ДополнитьПротокол(СтруктураОтвета, "Не удалось скорректировать "+Выборка.Ссылка+". "+ОписаниеОшибки(), Выборка.Ссылка);
		КонецПопытки;
		
	КонецЦикла;
	ДополнитьПротокол(СтруктураОтвета, "Аннулирование актов выдачи ТСР завершено.", СтруктураПараметров.Партнер);

	ПоместитьВоВременноеХранилище(СтруктураОтвета, АдресХранилища);
	
КонецПроцедуры

Процедура ДополнитьПротокол(СтруктураОтвета, Текст="", СсылочныйОбъект = Неопределено)

	СтруктураОтвета.Протокол = СтруктураОтвета.Протокол
	+ ?(ПустаяСтрока(СтруктураОтвета.Протокол),"",Символы.ПС)
	+ Формат(ТекущаяДата(),"ДЛФ=T") + " " + Текст;

	Если ЗначениеЗаполнено(СсылочныйОбъект) Тогда
		ЗаписьЖурналаРегистрации(
			"Аннулирование актов выдачи ТСР",
			УровеньЖурналаРегистрации.Предупреждение,
			СсылочныйОбъект.Метаданные(),
			СсылочныйОбъект,
			Текст
		);
	Иначе
		ЗаписьЖурналаРегистрации("Аннулирование актов выдачи ТСР",УровеньЖурналаРегистрации.Предупреждение,,,Текст);
	КонецЕсли;
	
КонецПроцедуры
