&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ГосКонтракт = Параметры.ГосКонтракт;
	ГосударственныйКонтратктПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТаблицы()
	
	ЗаполнитьТаблицуРаспределения();
	ЗаполнитьОбъектыЗакупки();

КонецПроцедуры  

&НаСервере
Процедура ЗаполнитьТаблицуРаспределения()
	
	НоменклатураКРаспределению.Очистить();
	
	Если Не ЗначениеЗаполнено(РеализацияТоваровИУслуг) Тогда 
		Возврат;
	КонецЕсли;
	
	Товары = РеализацияТоваровИУслуг.Товары.Выгрузить();
	
	Для Каждого Строка Из Товары Цикл 	
		
		НоваяСтрока = НоменклатураКРаспределению.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОбъектыЗакупки()
	
	ТаблицаНоменклатура.Очистить();
	
	Для Каждого ОбъектЗакупки Из ОбъектыЗакупки Цикл  
		
		Отбор = Новый Структура;
		Отбор.Вставить("Идентификатор", ОбъектЗакупки.Идентификатор);
		НайденныеСтроки = НоменклатураОбъектовЗакупки.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() = 0 Тогда 
			
			НоваяСтрока = ТаблицаНоменклатура.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ОбъектЗакупки, "Наименование,Количество,Идентификатор,Цена");
			
			Продолжить;
			
		КонецЕсли;
		
		Для Каждого НоменклатураОбъектаЗакупки Из НайденныеСтроки Цикл 
			
			НоваяСтрока = ТаблицаНоменклатура.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ОбъектЗакупки, "Наименование,Количество,Идентификатор,Цена, НомерСтроки");
			ЗаполнитьЗначенияСвойств(НоваяСтрока, НоменклатураОбъектаЗакупки, "Номенклатура,Характеристика,НоменклатураПартнера");
			
		КонецЦикла;
		
	КонецЦикла;
	
	ТаблицаНоменклатура.Сортировать("НомерСтроки");
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьКСтроке(Команда)
	
	НазначитьНоменклатуру();

КонецПроцедуры

&НаКлиенте
Процедура НазначитьНоменклатуру()
	
	Если Элементы.ТаблицаНоменклатура.ВыделенныеСтроки.Количество() > 1 
		Или Элементы.ТаблицаНоменклатура.ВыделенныеСтроки.Количество() = 0 
		Или Элементы.НоменклатураКРаспределению.ВыделенныеСтроки.Количество() > 1 
		Или Элементы.НоменклатураКРаспределению.ВыделенныеСтроки.Количество() = 0 Тогда 
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Необходимо выделить одну строку в каждой таблице для сопоставления");
		Возврат;
	КонецЕсли;  
	
	ТекущиеДанныеНоменклатура = Элементы.ТаблицаНоменклатура.ТекущиеДанные;
	ТекущиеДанныеРаспределение = Элементы.НоменклатураКРаспределению.ТекущиеДанные;
	
	Если ТекущиеДанныеНоменклатура = Неопределено Или ТекущиеДанныеРаспределение = Неопределено Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Необходимо выделить одну строку в каждой таблице для сопоставления");
		Возврат;	
	КонецЕсли;
	
	ТекущиеДанныеНоменклатура.Номенклатура = ТекущиеДанныеРаспределение.Номенклатура;
	ТекущиеДанныеНоменклатура.Характеристика = ТекущиеДанныеРаспределение.Характеристика;
	ТекущиеДанныеНоменклатура.Изменено = Истина;
	
КонецПроцедуры    

&НаКлиенте
Процедура Применить(Команда)  
			
	ЗакрытьФормуСопоставления();
	
КонецПроцедуры  

&НаКлиенте
Процедура ЗакрытьФормуСопоставления(Результат = Неопределено) Экспорт 
	
	Закрыть(ТаблицаНоменклатура);
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураКРаспределениюВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	НазначитьНоменклатуру();
	
КонецПроцедуры

&НаКлиенте
Процедура Сбросить(Команда)
	
	ОбновитьТаблицы();
	
КонецПроцедуры

&НаКлиенте
Процедура РеализацияТоваровИУслугПриИзменении(Элемент)
	
	РеализацияТоваровИУслугПриИзмененииНаСервере();
	
	ОбновитьТаблицы();
	
КонецПроцедуры

&НаСервере
Процедура РеализацияТоваровИУслугПриИзмененииНаСервере()  
	
	Если ЗначениеЗаполнено(РеализацияТоваровИУслуг) Тогда 
		Если ЗначениеЗаполнено(РеализацияТоваровИУслуг.Договор) Тогда 
			ГосКонтракт = РеализацияТоваровИУслуг.Договор.ГосударственныйКонтракт;
			ГосударственныйКонтратктПриИзмененииНаСервере();
		КонецЕсли;
	КонецЕсли;    
	
КонецПроцедуры   

&НаСервере
Процедура ГосударственныйКонтратктПриИзмененииНаСервере()
	
	НоменклатураОбъектовЗакупки.Загрузить(ГосКонтракт.НоменклатураОбъектовЗакупки.Выгрузить());
	ОбъектыЗакупки.Загрузить(ГосКонтракт.ОбъектыЗакупки.Выгрузить());
	ОбновитьТаблицы();
	
КонецПроцедуры

&НаКлиенте
Процедура ГосКонтракт1ПриИзменении(Элемент)
	
	ГосударственныйКонтратктПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьНоменклатуру(Команда)
	
	ТекДанныеГК = ЭтаФорма.Элементы.ТаблицаНоменклатура.ТекущиеДанные;
	ТекДанныеРеализации = ЭтаФорма.Элементы.НоменклатураКРаспределению.ТекущиеДанные;
	
	НовСтрока = ТаблицаНоменклатура.Добавить();
	ЗаполнитьЗначенияСвойств(НовСтрока,ТекДанныеГК); 
	ЗаполнитьЗначенияСвойств(НовСтрока,ТекДанныеРеализации);
	НовСтрока.Изменено = Истина;
	
	ТаблицаНоменклатура.Сортировать("Идентификатор");
	
КонецПроцедуры                      

&НаКлиенте
Процедура РеализацияТоваровИУслугНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;                      
	
	ОбработатьВыбор();	
КонецПроцедуры     

&НаКлиенте
Процедура ОбработатьВыбор()
	
	СписокТипов = СформироватьСписокВыбора();
	
	Оповещение = Новый ОписаниеОповещения("ОбработкаВыбораТипаДанных", ЭтотОбъект);
	
	СписокТипов.ПоказатьВыборЭлемента(Оповещение, "Выбор типа данных");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораТипаДанных(Результат, ДопПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда 
		
		Договор = ПолучитьДоговор(ЭтотОбъект.ГосКонтракт); 
		
		ПараметрыОтбора = Неопределено;
		Если ЗначениеЗаполнено(Договор) Тогда 
			ПараметрыОтбора = Новый Структура("Отбор", Новый Структура("Договор", Договор));	
		КонецЕсли;                 
		
		ИмяДокумента = "ЗаказКлиента";
		Если Результат.Значение = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда 
			ИмяДокумента = "РеализацияТоваровУслуг";
		КонецЕсли;
		
		ОткрытьФорму("Документ." + ИмяДокумента + ".ФормаВыбора",ПараметрыОтбора,ЭтаФорма,,,,Новый ОписаниеОповещения("ПослеВыбораРеализации", ЭтотОбъект));
		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Функция СформироватьСписокВыбора()
	
	СписокДокументов = Новый СписокЗначений;
	СписокДокументов.Добавить(Тип("ДокументСсылка.РеализацияТоваровУслуг"));
	СписокДокументов.Добавить(Тип("ДокументСсылка.ЗаказКлиента"));
	Возврат СписокДокументов;
	
КонецФункции

&НаКлиенте
Процедура ПослеВыбораРеализации(Результат, ДопПараметры)Экспорт 
	
	Если ЗначениеЗаполнено(Результат) Тогда 
		РеализацияТоваровИУслуг = Результат;
		РеализацияТоваровИУслугПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДоговор(ГК)
	
	Договор = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ Первые 1
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.ГосударственныйКонтракт = &ГК";
	Запрос.УстановитьПараметр("ГК",ГК);
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	Если ЗначениеЗаполнено(Результат) Тогда 
		Договор = Результат[0].Ссылка;	
	КонецЕсли; 
	
	Возврат Договор;
	
КонецФункции

&НаКлиенте
Процедура РеализацияТоваровИУслугОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда 
		ЭтотОбъект.РеализацияТоваровИУслуг = ВыбранноеЗначение;		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаменаНоменклатурыПартнера(Команда)                
	
	ТекущиеДанные = Элементы.ТаблицаНоменклатура.ТекущиеДанные;
	
	Отбор = Новый Структура;
	Отбор.Вставить("ВладелецНоменклатуры", ПартнерГосКонтракта()); 
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("Отбор", Отбор); 
	ПараметрыОткрытия.Вставить("Идентификатор", ТекущиеДанные.Идентификатор);
	
	ОткрытьФорму("Справочник.НоменклатураКонтрагентов.ФормаВыбора", ПараметрыОткрытия, ЭтотОбъект,,,,Новый ОписаниеОповещения("ЗаменаНоменклатурыПартнераЗавершение", ЭтотОбъект, ПараметрыОткрытия));
	
КонецПроцедуры

&НаСервере
Функция ПартнерГосКонтракта()
	
	Возврат ГосКонтракт.Контрагент.Партнер;

КонецФункции

&НаКлиенте
Процедура ЗаменаНоменклатурыПартнераЗавершение(Результат, ДопПараметры) Экспорт 
	
	Если ЗначениеЗаполнено(Результат) Тогда 
		ЗаменитьНоменклатуруПартнераВСтрокахПоИдентификатору(ДопПараметры.Идентификатор, Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаменитьНоменклатуруПартнераВСтрокахПоИдентификатору(Знач Идентификатор, Знач НоменклатураПартнера)
	
	СчетчикСтрок = 0;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Идентификатор", Идентификатор);
	
	НайденныеСтроки = ТаблицаНоменклатура.НайтиСтроки(Отбор);		
	Для Каждого Строка Из НайденныеСтроки Цикл 
		Строка.НоменклатураПартнера = НоменклатураПартнера;	
		Строка.Изменено = Истина; 
		СчетчикСтрок = СчетчикСтрок + 1;
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Замена номенклатуры клиента в " + СчетчикСтрок + " строках");
	
КонецПроцедуры




        