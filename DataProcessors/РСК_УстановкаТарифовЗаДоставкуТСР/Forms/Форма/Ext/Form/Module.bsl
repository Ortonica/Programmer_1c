//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Элементы.Этап1_Водители.Видимость = Ложь;
	Элементы.Этап2_Тарифы.Видимость = Ложь;
	Элементы.Этап3_Цены.Видимость = Ложь;
	Элементы.Этап4_Документы.Видимость = Ложь;
	Элементы.Кнопки.Видимость = Ложь;
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПериодПриИзменении(Неопределено);
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаКлиенте
Процедура ОбновитьВидимостьЭлементов()

	Если Период.ДатаНачала = '00010101' ИЛИ Период.ДатаОкончания = '00010101' Тогда
		Возврат;
	Иначе
		Элементы.Кнопки.Видимость = Истина;
		Элементы.Этап1_Водители.Видимость = Истина;
	КонецЕсли;
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.Этап1_Водители Тогда
		Элементы.КнопкаНазад.Заголовок = "Закрыть";
		Элементы.КнопкаДалее.Заголовок = "Далее";
		Элементы.Этап2_Тарифы.Видимость = Ложь;
		Элементы.Этап3_Цены.Видимость = Ложь;
		Элементы.Этап4_Документы.Видимость = Ложь;
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.Этап2_Тарифы Тогда
		Элементы.КнопкаНазад.Заголовок = "Назад";
		Элементы.КнопкаДалее.Заголовок = "Далее";
		Элементы.Этап3_Цены.Видимость = Ложь;
		Элементы.Этап4_Документы.Видимость = Ложь;
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.Этап3_Цены Тогда
		Элементы.КнопкаНазад.Заголовок = "Назад";
		Элементы.КнопкаДалее.Заголовок = "Далее";
		Элементы.Этап4_Документы.Видимость = Ложь;
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.Этап4_Документы Тогда
		Элементы.КнопкаНазад.Заголовок = "Назад";
		Элементы.КнопкаДалее.Заголовок = "Закрыть";
	КонецЕсли;

КонецПроцедуры // ОбновитьВидимостьЭлементов()

//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	ОбновитьВидимостьЭлементов();
	ЗаполнитьВодителейЗаПериод(Неопределено);
	ДатаНачалаДействияЦен = Период.ДатаНачала;
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаКлиенте
Процедура ЗаполнитьВодителейЗаПериод(Команда)
	Если Водители.Количество() = 0 Тогда
		ЗаполнитьВодителейНаСервере();
	Иначе
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ПослеВопросаОПерезаполненииВодителей", ЭтаФорма), 
			"Список водителей будет перезаполнен! Продолжить?",
			РежимДиалогаВопрос.ДаНет
		);
	КонецЕсли;	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаКлиенте
Процедура ПослеВопросаОПерезаполненииВодителей(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьВодителейНаСервере();

КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаСервере
Процедура ЗаполнитьВодителейНаСервере()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ЗаданиеНаПеревозку.Водитель КАК ФизЛицо,
		|	Контрагенты.Партнер КАК Партнер
		|ИЗ
		|	Документ.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|			ПО ФизическиеЛица.ИНН = Контрагенты.ИНН
		|				И (Контрагенты.ИНН <> """")
		|				И (НЕ Контрагенты.ПометкаУдаления)
		|		ПО ЗаданиеНаПеревозку.Водитель = ФизическиеЛица.Ссылка
		|ГДЕ
		|	ЗаданиеНаПеревозку.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ЗаданиеНаПеревозку.Проведен
		|	И ЗаданиеНаПеревозку.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаданийНаПеревозку.Закрыто)
		|";
	
	Запрос.УстановитьПараметр("НачалоПериода", Период.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", Период.ДатаОкончания);
	
	Водители.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры // ЗаполнитьВодителейНаСервере()

//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаКлиенте
Процедура ВодителиФизЛицоПриИзменении(Элемент)
	ТД = Элементы.Водители.ТекущиеДанные;
	Если ТД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ТД.Партнер = ПолучитьПартнёра(ТД.ФизЛицо);
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаСервереБезКонтекста
Функция ПолучитьПартнёра(ФизЛицо)

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		|	Контрагенты.Партнер КАК Партнер
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ФизическиеЛица
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО ФизическиеЛица.ИНН = Контрагенты.ИНН
		|			И (Контрагенты.ИНН <> """")
		|			И (НЕ Контрагенты.ПометкаУдаления)
		|ГДЕ
		|	ФизическиеЛица.Ссылка = &Ссылка
		|";
	Запрос.УстановитьПараметр("Ссылка", ФизЛицо); 
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Партнер;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции // ПолучитьПартнёра()

//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаКлиенте
Процедура ЗаполнитьТарифы(Команда)
	Если Водители.Количество() = 0 Тогда
		ЗаполнитьВодителейНаСервере();
	КонецЕсли;
	Отказ = Ложь;
	ПроверкиПередЗаполнениемТарифов(Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.Этап2_Тарифы.Видимость = Истина;
	Элементы.Страницы.ТекущаяСтраница = Элементы.Этап2_Тарифы;
	ОбновитьВидимостьЭлементов();
	
	Если ДоставленныеТСР.Количество() = 0 Тогда
		ЗаполнитьТарифыНаСервере();
	Иначе
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ПослеВопросаОПерезаполненииТарифов", ЭтаФорма), 
			"Список тарифов будет перезаполнен! Продолжить?",
			РежимДиалогаВопрос.ДаНет
		);
	КонецЕсли;
	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаКлиенте
Процедура ПослеВопросаОПерезаполненииТарифов(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьТарифыНаСервере();

КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаКлиенте
Процедура ПроверкиПередЗаполнениемТарифов(Отказ)

	НомерСтроки = 0;
	Для Каждого Стр Из Водители Цикл
		НомерСтроки = НомерСтроки + 1;
		Если Не ЗначениеЗаполнено(Стр.Партнер) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указан партнёр-водитель",,"Водители["+Формат(НомерСтроки-1,"ЧГ=0")+"].Партнер",,Отказ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ПроверкиПередЗаполнениемТарифов()

//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаСервере
Процедура ЗаполнитьТарифыНаСервере()

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПоручениеЭкспедитору.РСК_Номенклатура КАК Номенклатура,
		|	спрНоменклатура.ВидНоменклатуры КАК ВидНоменклатуры,
		|	ЕСТЬNULL(ЕСТЬNULL(РСК_НоменклатураИТарифыДоставкиТСР.ИмяТарифа, спрНоменклатура.ВидНоменклатуры.Наименование), """") КАК ИмяТарифа
		|ПОМЕСТИТЬ ДоставленныеТСР
		|ИЗ
		|	Документ.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаданиеНаПеревозку.Распоряжения КАК ЗаданиеНаПеревозкуРаспоряжения
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоручениеЭкспедитору КАК ПоручениеЭкспедитору
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
		|				ПО ПоручениеЭкспедитору.РСК_Номенклатура = спрНоменклатура.Ссылка
		|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РСК_НоменклатураИТарифыДоставкиТСР КАК РСК_НоменклатураИТарифыДоставкиТСР
		|				ПО ПоручениеЭкспедитору.РСК_Номенклатура = РСК_НоменклатураИТарифыДоставкиТСР.Номенклатура
		|			ПО ЗаданиеНаПеревозкуРаспоряжения.Распоряжение = ПоручениеЭкспедитору.Ссылка
		|		ПО ЗаданиеНаПеревозку.Ссылка = ЗаданиеНаПеревозкуРаспоряжения.Ссылка
		|ГДЕ
		|	ЗаданиеНаПеревозку.Дата МЕЖДУ &НачалоПериода И &КонецПериода
		|	И ЗаданиеНаПеревозку.Проведен
		|	И ЗаданиеНаПеревозку.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаданийНаПеревозку.Закрыто)
		|	И ЗаданиеНаПеревозку.РСК_ВыдачаТСР
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДоставленныеТСР.Номенклатура КАК Номенклатура,
		|	ДоставленныеТСР.ВидНоменклатуры КАК ВидНоменклатуры,
		|	ДоставленныеТСР.ИмяТарифа КАК ИмяТарифа,
		|	МАКСИМУМ(ЕСТЬNULL(Тарифы.Ссылка, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка))) КАК Тариф
		|ИЗ
		|	ДоставленныеТСР КАК ДоставленныеТСР
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Тарифы
		|		ПО (""Выдача: "" + ДоставленныеТСР.ИмяТарифа = Тарифы.Наименование)
		|			И (НЕ Тарифы.ПометкаУдаления)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДоставленныеТСР.Номенклатура,
		|	ДоставленныеТСР.ВидНоменклатуры,
		|	ДоставленныеТСР.ИмяТарифа
		|";

	Запрос.УстановитьПараметр("НачалоПериода", Период.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", Период.ДатаОкончания);
	ДоставленныеТСР.Загрузить(Запрос.Выполнить().Выгрузить());

КонецПроцедуры // ЗаполнитьТарифыНаСервере()

//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаКлиенте
Процедура КнопкаНазад(Команда)
	Если Элементы.КнопкаНазад.Заголовок = "Закрыть" Тогда
		Закрыть();
		Возврат;
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.Этап2_Тарифы Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.Этап1_Водители;
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.Этап3_Цены Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.Этап2_Тарифы;
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.Этап4_Документы Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.Этап3_Цены;
	КонецЕсли;
	ОбновитьВидимостьЭлементов();	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаКлиенте
Процедура КнопкаДалее(Команда)
	Отказ = Ложь;
	Если Элементы.КнопкаДалее.Заголовок = "Закрыть" Тогда
		Закрыть();
		Возврат;
	КонецЕсли;
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.Этап1_Водители Тогда
		ЗаполнитьТарифы(Неопределено);
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.Этап2_Тарифы Тогда
		ПроверкаЭтапа2(Отказ);
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		СозданиеСвязейНоменклатурыСТарифами();
		СозданиеКарточекТарифов();
		ЗаполнениеТаблицыЦен();
		ОбновитьВидимостьЭлементов();
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.Этап3_Цены Тогда
		СоздатьУстановкиЦен();
		ОбновитьВидимостьЭлементов();
	КонецЕсли;
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаСервере
Процедура СозданиеСвязейНоменклатурыСТарифами()

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ДоставленныеТСР.ВидНоменклатуры КАК Справочник.ВидыНоменклатуры) КАК ВидНоменклатуры,
		|	ВЫРАЗИТЬ(ДоставленныеТСР.ИмяТарифа КАК СТРОКА(150)) КАК ИмяТарифа,
		|	ВЫРАЗИТЬ(ДоставленныеТСР.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура
		|ПОМЕСТИТЬ ДоставленныеТСР
		|ИЗ
		|	&ДоставленныеТСР КАК ДоставленныеТСР
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДоставленныеТСР.Номенклатура КАК Номенклатура,
		|	ДоставленныеТСР.ИмяТарифа КАК ИмяТарифа
		|ИЗ
		|	ДоставленныеТСР КАК ДоставленныеТСР
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РСК_НоменклатураИТарифыДоставкиТСР КАК РСК_НоменклатураИТарифыДоставкиТСР
		|		ПО ДоставленныеТСР.Номенклатура = РСК_НоменклатураИТарифыДоставкиТСР.Номенклатура
		|			И ДоставленныеТСР.ИмяТарифа = РСК_НоменклатураИТарифыДоставкиТСР.ИмяТарифа
		|ГДЕ
		|	ДоставленныеТСР.ВидНоменклатуры.Наименование <> ДоставленныеТСР.ИмяТарифа
		|	И РСК_НоменклатураИТарифыДоставкиТСР.Номенклатура ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДоставленныеТСР.Номенклатура КАК Номенклатура
		|ИЗ
		|	ДоставленныеТСР КАК ДоставленныеТСР
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РСК_НоменклатураИТарифыДоставкиТСР КАК РСК_НоменклатураИТарифыДоставкиТСР
		|		ПО ДоставленныеТСР.Номенклатура = РСК_НоменклатураИТарифыДоставкиТСР.Номенклатура
		|ГДЕ
		|	ДоставленныеТСР.ВидНоменклатуры.Наименование = ДоставленныеТСР.ИмяТарифа
		|";
	
	Запрос.УстановитьПараметр("ДоставленныеТСР", ДоставленныеТСР.Выгрузить());
	Результаты = Запрос.ВыполнитьПакет();
	КСозданию = Результаты[1].Выбрать();
	Пока КСозданию.Следующий() Цикл
		Запись = РегистрыСведений.РСК_НоменклатураИТарифыДоставкиТСР.СоздатьМенеджерЗаписи();
		Запись.Номенклатура = КСозданию.Номенклатура;
		Запись.ИмяТарифа = КСозданию.ИмяТарифа;
		Запись.Записать(Истина);
	КонецЦикла;
	
	КУдалению = Результаты[2].Выбрать();
	Пока КУдалению.Следующий() Цикл
		НЗ = РегистрыСведений.РСК_НоменклатураИТарифыДоставкиТСР.СоздатьНаборЗаписей();
		НЗ.Отбор.Номенклатура.Установить(КУдалению.Номенклатура);
		НЗ.Записать();
	КонецЦикла;
	
КонецПроцедуры // СозданиеСвязейНоменклатурыСТарифами()

//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаКлиенте
Процедура СозданиеКарточекТарифов()
	
	ИменаНесуществующихТарифов = ПолучитьИменаНесуществующихТарифов();
	ё = 0;
	СоответствиеТарифов = Новый Соответствие;
	Для Каждого ИмяТарифа Из ИменаНесуществующихТарифов Цикл
		ё=ё+1;
		Состояние("Создание тарифов",ё*100/ИменаНесуществующихТарифов.Количество(),""+ё+" из "+ИменаНесуществующихТарифов.Количество());
		СоответствиеТарифов.Вставить(ИмяТарифа, СозданиеКарточкиТарифаНаСервере(ИмяТарифа));
	КонецЦикла;
	
	СтрокиБезКарточек = ДоставленныеТСР.НайтиСтроки(Новый Структура("Тариф", ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка")));
	Для Каждого Стр Из СтрокиБезКарточек Цикл
		Стр.Тариф = СоответствиеТарифов[Стр.ИмяТарифа];
	КонецЦикла;

КонецПроцедуры // СозданиеКарточекТарифов()

//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаСервере
Функция ПолучитьИменаНесуществующихТарифов()

	тзТарифы = ДоставленныеТСР.Выгрузить(Новый Структура("Тариф", Справочники.Номенклатура.ПустаяСсылка()));
	Возврат ОбщегоНазначения.ВыгрузитьКолонку(тзТарифы, "ИмяТарифа", Истина);

КонецФункции // ПолучитьИменаНесуществующихТарифов()

//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаСервереБезКонтекста
Функция СозданиеКарточкиТарифаНаСервере(ИмяТарифа)
	
	УстановитьПривилегированныйРежим(Истина);
	Имя = "Выдача: "+ИмяТарифа;
	
	оСпр = Справочники.Номенклатура.СоздатьЭлемент();
	оСпр.Наименование = Имя;
	оСпр.Родитель = Справочники.Номенклатура.НайтиПоНаименованию("УСЛУГИ", Истина);
	оСпр.ВариантОформленияПродажи = Перечисления.ВариантыОформленияПродажи.АктВыполненныхРабот;
	оСпр.ВидНоменклатуры = Справочники.ВидыНоменклатуры.НайтиПоНаименованию("Услуги", Истина);
	оСпр.НаименованиеПолное = Имя;
	оСпр.ИспользованиеХарактеристик = Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.НеИспользовать;
	оСпр.Качество = Перечисления.ГрадацииКачества.Новый;
	оСпр.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
	оСпр.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга;
	оСпр.КоэффициентЕдиницыДляОтчетов = 1;
	оСпр.УстановитьНовыйКод();
	оСпр.Записать();
	
	Возврат оСпр.Ссылка;

КонецФункции // СозданиеКарточкиТарифаНаСервере()

//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаСервере
Процедура ЗаполнениеТаблицыЦен()

	тзЦены = ДоставленныеТСР.Выгрузить(,"ИмяТарифа");
	тзЦены.Свернуть("ИмяТарифа");
	тзЦены.Сортировать("ИмяТарифа");
	ЦеныНаДоставкуТСР.Загрузить(тзЦены);
	Элементы.Этап3_Цены.Видимость = Истина;
	Элементы.Страницы.ТекущаяСтраница = Элементы.Этап3_Цены;
	
КонецПроцедуры // ЗаполнениеТаблицыЦен()

//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаКлиенте
Процедура ДоставленныеТСРИмяТарифаПриИзменении(Элемент)
	ТД = Элементы.ДоставленныеТСР.ТекущиеДанные;
	Если ТД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(ТД.ИмяТарифа) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокиСТакойЖеНоменклатурой = ДоставленныеТСР.НайтиСтроки(Новый Структура("Номенклатура", ТД.Номенклатура));
	Тариф = ПолучитьНоменклатуруПоИмениТарифа(ТД.ИмяТарифа);
	Для Каждого Стр Из СтрокиСТакойЖеНоменклатурой Цикл
		Стр.Тариф = Тариф;
	КонецЦикла;	
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаСервереБезКонтекста
Функция ПолучитьНоменклатуруПоИмениТарифа(ИмяТарифа)

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Номенклатура.Ссылка КАК Тариф
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Наименование = ""Выдача: "" + &ИмяТарифа
		|	И НЕ Номенклатура.ПометкаУдаления";
	Запрос.УстановитьПараметр("ИмяТарифа", ИмяТарифа);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Тариф;
	КонецЕсли;
	
	Возврат Неопределено;

КонецФункции

//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаКлиенте
Процедура ПроверкаЭтапа2(Отказ)
	
	ё=0;
	Для Каждого Стр Из ДоставленныеТСР Цикл
		ё=ё+1;
		Если ПустаяСтрока(Стр.ИмяТарифа) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Укажите имя тарифа",,"ДоставленныеТСР["+Формат(ё-1,"ЧГ=0")+"].ИмяТарифа",,Отказ);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры // ПроверкаЭтапа2()

//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	ОбновитьВидимостьЭлементов();
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаКлиенте
Процедура СоздатьУстановкиЦен()
    Отказ = Ложь;
	АдресХранилища = ПодготовитьДанныеДляСозданияДокументов(Отказ);
	Если ДатаНачалаДействияЦен = '00010101' Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Заполните дату начала действия цен",,"ДатаНачалаДействияЦен",,Отказ);
	КонецЕсли;
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	УстановкиЦен.Очистить();
	ё=0;
	Для Каждого Стр Из Водители Цикл
		ё=ё+1;
		Состояние("Регистрация цен",ё*100/Водители.Количество(),""+ё+" из "+Водители.Количество());
		Результат = СоздатьУстановкуЦенНаСервере(Стр.Партнер, АдресХранилища);
		НС = УстановкиЦен.Добавить();
		ЗаполнитьЗначенияСвойств(НС, Результат);
		НС.Водитель = Стр.Партнер;
	КонецЦикла;
	
	Элементы.Этап4_Документы.Видимость = Истина;
	Элементы.Страницы.ТекущаяСтраница = Элементы.Этап4_Документы;

КонецПроцедуры // СоздатьУстановкиЦен()

// Подготовка данных для создания установок цен
//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
//
// Возвращаемое значение:
//   Строка   - Адрес временного хранилища со структурой
//		* Дата 			- Дата - Дата будущего документа установки цен номенклатуры
//		* ТаблицаЦен 	- ТаблицаЗначений - 
//			* Номенклатура 	- СправочникСсылка.Номенклатура - Тариф
//			* Цена 			- Число - Цена за тариф
//			* Валюта		- СправочникСсылка.Валюты - Валюта
&НаСервере
Функция ПодготовитьДанныеДляСозданияДокументов(Отказ)
	
	Результат = Новый Структура;

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(Цены.ИмяТарифа КАК СТРОКА(150)) КАК ИмяТарифа,
		|	ВЫРАЗИТЬ(Цены.Цена КАК ЧИСЛО(15, 2)) КАК Цена
		|ПОМЕСТИТЬ Цены
		|ИЗ
		|	&Цены КАК Цены
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	спрНоменклатура.Ссылка КАК Номенклатура,
		|	Цены.Цена КАК Цена
		|ИЗ
		|	Цены КАК Цены
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК спрНоменклатура
		|		ПО (спрНоменклатура.Наименование = ""Выдача: "" + Цены.ИмяТарифа)
		|			И (НЕ спрНоменклатура.ПометкаУдаления)
		|ГДЕ
		|	Цены.Цена > 0";
	
	Запрос.УстановитьПараметр("Цены", ЦеныНаДоставкуТСР.Выгрузить());
	Результат.Вставить("ТаблицаЦен", Запрос.Выполнить().Выгрузить());
	Результат.Вставить("Дата", ДатаНачалаДействияЦен);
	Результат.Вставить("Валюта", Константы.ВалютаРегламентированногоУчета.Получить());
	Если Результат.ТаблицаЦен.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьПользователю("В таблице цен не указано ни одной новой цены. Операция остановлена",,,,Отказ);
	КонецЕсли;
	Возврат ПоместитьВоВременноеХранилище(Результат, УникальныйИдентификатор);

КонецФункции // ПодготовитьДанныеДляСозданияДокументов()

// Создание документа установки цен
//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
//
// Параметры:
//  Водитель		 - СправочникСсылка.Партнеры - Водитель
//  АдресХранилища	 - Строка - см. метод ПодготовитьДанныеДляСозданияДокументов()
// 
// Возвращаемое значение:
//   - Структура
//		* Документ - ДокументСсылка.РегистрацияЦенНоменклатурыПоставщика - 
//		* Примечание - Строка
//
&НаСервереБезКонтекста
Функция СоздатьУстановкуЦенНаСервере(Водитель, АдресХранилища)
	УстановитьПривилегированныйРежим(Истина);
	Параметры = ПолучитьИзВременногоХранилища(АдресХранилища);
	
	//Определение/создание вида цен поставщика
	ИмяВидаЦен = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Водитель, "Наименование");
	ВидЦеныПоставщика = Справочники.ВидыЦенПоставщиков.НайтиПоНаименованию(ИмяВидаЦен,Истина,,Водитель);
	Если НЕ ЗначениеЗаполнено(ВидЦеныПоставщика) Тогда
		оСпр = Справочники.ВидыЦенПоставщиков.СоздатьЭлемент();
		оСпр.Наименование = ИмяВидаЦен;
		оСпр.Владелец = Водитель;
		оСпр.Валюта = Параметры.Валюта;
		оСпр.ЦенаВключаетНДС = Истина;
		оСпр.ДоступноДляЗакупки = Истина;
		оСпр.РеквизитДопУпорядочивания = 1;
		оСпр.Записать();
		ВидЦеныПоставщика = оСпр.Ссылка;
	КонецЕсли;
	
	//Подбор/Создание регистрации цен
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	РегистрацияЦенНоменклатурыПоставщика.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.РегистрацияЦенНоменклатурыПоставщика КАК РегистрацияЦенНоменклатурыПоставщика
		|ГДЕ
		|	РегистрацияЦенНоменклатурыПоставщика.Проведен
		|	И НАЧАЛОПЕРИОДА(РегистрацияЦенНоменклатурыПоставщика.Дата, ДЕНЬ) = &Дата
		|	И РегистрацияЦенНоменклатурыПоставщика.Партнер = &Водитель
		|";
	Запрос.УстановитьПараметр("Дата", НачалоДня(Параметры.Дата));
	Запрос.УстановитьПараметр("Водитель", Водитель);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		оДок = Выборка.Ссылка.ПолучитьОбъект();
	Иначе
		оДок = Документы.РегистрацияЦенНоменклатурыПоставщика.СоздатьДокумент();
		оДок.Дата = Параметры.Дата;
		оДок.Партнер = Водитель;
	КонецЕсли;
	тзТарифы = оДок.Товары.Выгрузить(,"Номенклатура,Цена,ВидЦеныПоставщика");	
	Для Каждого Стр Из Параметры.ТаблицаЦен Цикл
		НайденнаяСтрока = тзТарифы.Найти(Стр.Номенклатура, "Номенклатура");
		Если НайденнаяСтрока = Неопределено Тогда
			НайденнаяСтрока = тзТарифы.Добавить();
			НайденнаяСтрока.Номенклатура = Стр.Номенклатура;
		КонецЕсли;
		НайденнаяСтрока.Цена = Стр.Цена;
	КонецЦикла;
	тзТарифы.ЗаполнитьЗначения(ВидЦеныПоставщика, "ВидЦеныПоставщика");
	оДок.Товары.Загрузить(тзТарифы);
	
	Ответ = Новый Структура;
	Ответ.Вставить("Примечание", ПопытатьсяПровестиДокумент(оДок));
	Ответ.Вставить("Документ", оДок.Ссылка);
	
	Возврат Ответ;

КонецФункции // СоздатьУстановкуЦенНаСервере()

//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаСервереБезКонтекста
Функция ПопытатьсяПровестиДокумент(ДокументОбъект)
 
    Попытка
        ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
		Возврат "Документ успешно проведён";
    Исключение
        Если ДокументОбъект.Проведен Тогда
            Попытка
                ДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
            Исключение
                Возврат "Не удалось отменить проведение";
            КонецПопытки;
        Иначе
            ДокументОбъект.ОбменДанными.Загрузка = Истина;
            ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
        КонецЕсли;
        Возврат "Не удалось провести документ. Попробуйте вручную.";
    КонецПопытки;
 
КонецФункции // ПопытатьсяПровестиДокумент()

//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаКлиенте
Процедура ДоставленныеТСРВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Элементы[Поле.Имя].ТолькоПросмотр Тогда
		СтандартнаяОбработка = Ложь;
        ДанныеСтроки = Элемент.ДанныеСтроки(ВыбраннаяСтрока);
        ИмяПоля = Сред(Поле.Имя, СтрДлина(Элемент.Имя)+1);
        ПоказатьЗначение(, ДанныеСтроки[ИмяПоля]);
    КонецЕсли;
КонецПроцедуры

//++ РС Консалт: Трофимов Евгений 03.02.2023 Задача 23088
//e1cib/data/Документ.Задание?ref=abf6db76aafd7de84ef44e63a771c2e5
&НаКлиенте
Процедура УстановкиЦенВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
    Если Элементы[Поле.Имя].ТолькоПросмотр Тогда
		СтандартнаяОбработка = Ложь;
        ДанныеСтроки = Элемент.ДанныеСтроки(ВыбраннаяСтрока);
        ИмяПоля = Сред(Поле.Имя, СтрДлина(Элемент.Имя)+1);
        ПоказатьЗначение(, ДанныеСтроки[ИмяПоля]);
    КонецЕсли;
КонецПроцедуры
