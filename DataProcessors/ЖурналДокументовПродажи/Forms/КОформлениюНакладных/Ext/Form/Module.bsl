  
&НаСервере
&После("ПриСозданииНаСервере")
Процедура РСК_ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//++РС Консалт: Минаков А.С. Задача 21074
	УстановитьОтборПоЗаданиюСервер();
	
	НовыйЭлемент = Элементы.Вставить("СписокРаспоряженияНаОформлениеЗаданиеНаПеревозку", 
	Тип("ПолеФормы"), 
	Элементы.СписокРаспоряженияНаОформление, 
	Элементы.СписокРаспоряженияНаОформлениеТипРаспоряжения);
	
	НовыйЭлемент.ПутьКДанным = "СписокРаспоряженияНаОформление.ЗаданиеНаПеревозку";
	
	НовыйЭлемент = Элементы.Вставить("СписокРаспоряженияНаОформлениеНакладная", 
	Тип("ПолеФормы"), 
	Элементы.СписокРаспоряженияНаОформление, 
	Элементы.СписокРаспоряженияНаОформлениеТипРаспоряжения);
	
	НовыйЭлемент.ПутьКДанным = "СписокРаспоряженияНаОформление.Накладная";
	
	НовыйЭлемент = Элементы.Вставить("СписокРаспоряженияНаОформлениеСтатусОрдера", 
	Тип("ПолеФормы"), 
	Элементы.СписокРаспоряженияНаОформление, 
	Элементы.СписокРаспоряженияНаОформлениеТипРаспоряжения);
	
	НовыйЭлемент.ПутьКДанным = "СписокРаспоряженияНаОформление.СтатусОрдера";
	НовыйЭлемент.Заголовок = "Статус ордера";
	
	НовыйЭлемент = Элементы.Добавить("СписокРаспоряженияНаОформлениеОрдер", 
	Тип("ПолеФормы"), 
	Элементы.СписокРаспоряженияНаОформление);
	
	НовыйЭлемент.ПутьКДанным = "СписокРаспоряженияНаОформление.Ордер";
	НовыйЭлемент.Заголовок = "Ордер";
	
	НовыйЭлемент = Элементы.Вставить("СписокРаспоряженияНаОформлениеСтатусСборки", 
	Тип("ПолеФормы"), 
	Элементы.СписокРаспоряженияНаОформление, 
	Элементы.СписокРаспоряженияНаОформлениеТипРаспоряжения);
	
	НовыйЭлемент.ПутьКДанным = "СписокРаспоряженияНаОформление.СтатусСборки";
	НовыйЭлемент.Заголовок = "Собрка завершена";
	НовыйЭлемент.Вид = ВидПоляФормы.ПолеФлажка;
	
	//++ РС Консалт: Трофимов Евгений 22.02.2023 Задача 23987
	//e1cib/data/Документ.Задание?ref=9e9022aceb4ae0fe4945c336d1841b94
	//Перенёс в процедуру ПоместитьКомплектПечатныхФормВоВременноеХранилищеСервер() 
	//т.к. нужно знать какие миенно документы печатаются.
	//Это позволит применять индивидуальные настройки из регистра сведений НастройкиПечатиОбъектов
	//
	//КоллекцияПечатныхФорм = РегистрыСведений.НастройкиПечатиОбъектов.КомплектПечатныхФорм(
	//	"Документ.РеализацияТоваровУслуг",
	//	Неопределено,
	//	0);
	//
	//Если КоллекцияПечатныхФорм <> Неопределено Тогда
	//
	//	Для Каждого ТекСтрока Из КоллекцияПечатныхФорм Цикл
	//		
	//		НоваяСтрока = КомплектПечатныхФорм.Добавить();
	//		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока)
	//		
	//	КонецЦикла
	//	
	//КонецЕсли	
	//-- КонецЗадачи 23987	
	//++РС Консалт: Минаков А.С. Задача 21074
	
КонецПроцедуры
 
&НаСервере
&ИзменениеИКонтроль("ТекстЗапросаСпискаРаспоряженийНаОформление")
Функция РСК_ТекстЗапросаСпискаРаспоряженийНаОформление()

	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Таблица.ЗаказКлиента КАК Ссылка,
	|	Таблица.Склад КАК Склад,
	|	ВЫБОР
	|		КОГДА НЕ Таблица.Склад.ИспользоватьОрдернуюСхемуПриОтгрузке
	|		ИЛИ Таблица.Склад.ИспользоватьОрдернуюСхемуПриОтгрузке
	|		И Таблица.Склад.ДатаНачалаОрдернойСхемыПриОтгрузке > &ТекущаяДата
	|			ТОГДА 4
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СостояниеОрдераВрем,
	|	ВЫБОР
	|		КОГДА Склады.ЭтоГруппа
	|			И Склады.ВыборГруппы = ЗНАЧЕНИЕ(Перечисление.ВыборГруппыСкладов.РазрешитьВЗаказах)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК РазбиватьРеализациюПоСкладам
	|ПОМЕСТИТЬ ВтОстаткиПоЗаказуКлиента
	|ИЗ
	|	РегистрНакопления.ЗаказыКлиентов.Остатки(, (Склад = &Склад
	|	ИЛИ &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|	И (ЗаказКлиента ССЫЛКА Документ.ЗаказКлиента
	|	ИЛИ ЗаказКлиента ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента)
	|	И
	|	НЕ (ЗаказКлиента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоставкаПодПринципала)
	|	И Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))) КАК Таблица
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|		ПО Склады.Ссылка = Таблица.ЗаказКлиента.Склад
	|
	|ГДЕ
	|	Таблица.КОформлениюОстаток > 0
	|;
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Таблица.Ссылка КАК ЗаказКлиента,
	|	Таблица.Склад КАК Склад
	|ПОМЕСТИТЬ ВтДокументыОформления
	|ИЗ
	|	ВтОстаткиПоЗаказуКлиента КАК Таблица
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаказКлиента,
	|	Склад
	|;
	|//////////////////////////////////////////////////////////////////////////////// 2
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВтДокументыОформления.Склад КАК Склад
	|ПОМЕСТИТЬ ВтДокументыОформленияСклады
	|ИЗ
	|	ВтДокументыОформления КАК ВтДокументыОформления
	|;
	|//////////////////////////////////////////////////////////////////////////////// 3-4
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Таблица.ЗаказКлиента КАК ЗаказКлиента,
	|	Таблица.Склад КАК Склад
	|ПОМЕСТИТЬ ВтЕстьНакладные
	|ИЗ
	|	ВтДокументыОформления КАК Таблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыКлиентов КАК ЗаказыКлиентов
	|		ПО Таблица.ЗаказКлиента = ЗаказыКлиентов.ЗаказКлиента
	|		И Таблица.Склад = ЗаказыКлиентов.Склад
	|ГДЕ
	|	ЗаказыКлиентов.Активность
	|	И ЗаказыКлиентов.КОформлению > 0
	|	И ЗаказыКлиентов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаказКлиента,
	|	Склад
	|;
	|//////////////////////////////////////////////////////////////////////////////// 5
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Таблица.Ссылка КАК Ссылка,
	|	Таблица.Склад КАК Склад,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаЕстьНакладные.ЗаказКлиента ЕСТЬ NULL
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СостояниеНакладнойВрем,
	|	Таблица.СостояниеОрдераВрем КАК СостояниеОрдераВрем,
	|	Таблица.РазбиватьРеализациюПоСкладам КАК РазбиватьРеализациюПоСкладам
	|
	|ПОМЕСТИТЬ ВтСостояниеНакладной
	|ИЗ
	|	ВтОстаткиПоЗаказуКлиента КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтЕстьНакладные КАК ТаблицаЕстьНакладные
	|		ПО ТаблицаЕстьНакладные.ЗаказКлиента = Таблица.Ссылка
	|		И ТаблицаЕстьНакладные.Склад = Таблица.Склад
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|//////////////////////////////////////////////////////////////////////////////// 6
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Таблица.ДокументОтгрузки КАК ДокументОтгрузки,
	|	Таблица.Склад КАК Склад,
	|	Таблица.Номенклатура КАК Номенклатура,
	|	Таблица.Характеристика КАК Характеристика,
	|	Таблица.Серия КАК Серия,
	|	Таблица.Назначение КАК Назначение,
	|	(Таблица.КОтгрузкеОстаток - Таблица.СобраноОстаток) = Таблица.КОформлениюОстаток КАК ОстаткиРавны
	|ПОМЕСТИТЬ ВтОстаткиТоварыКОтгрузке
	|ИЗ
	|	РегистрНакопления.ТоварыКОтгрузке.Остатки(, (Склад = &Склад
	|	ИЛИ &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|	И (ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|	ИЛИ ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|	ИЛИ ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|	ИЛИ ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю)) КАК Таблица
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОтгрузки
	|;
	|//////////////////////////////////////////////////////////////////////////////// 7
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Таблица.ДокументОтгрузки КАК ДокументОтгрузки,
	|	Таблица.Склад КАК Склад
	|ПОМЕСТИТЬ ВтДокументыОтгрузки
	|ИЗ
	|	ВтОстаткиТоварыКОтгрузке КАК Таблица
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОтгрузки,
	|	Склад
	|;
	|//////////////////////////////////////////////////////////////////////////////// 8
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВтДокументыОтгрузки.Склад КАК Склад
	|ПОМЕСТИТЬ ВтДокументыОтгрузкиСклады
	|ИЗ
	|	ВтДокументыОтгрузки КАК ВтДокументыОтгрузки
	|;
	|//////////////////////////////////////////////////////////////////////////////// 9-10
	|ВЫБРАТЬ
	|	Таблица.ДокументОтгрузки КАК ДокументОтгрузки,
	|	Таблица.Склад КАК Склад
	|ПОМЕСТИТЬ ВтЕстьОрдера
	|ИЗ
	|	ВтДокументыОтгрузки КАК Таблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыКОтгрузке КАК ТоварыКОтгрузке
	|		ПО Таблица.ДокументОтгрузки = ТоварыКОтгрузке.ДокументОтгрузки
	|		И Таблица.Склад = ТоварыКОтгрузке.Склад
	|ГДЕ
	|	ТоварыКОтгрузке.Активность
	|	И (ТоварыКОтгрузке.КОтгрузке > 0
	|	И ТоварыКОтгрузке.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	ИЛИ ТоварыКОтгрузке.Собрано > 0
	|	И ТоварыКОтгрузке.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОтгрузки,
	|	Склад
	|;
	|//////////////////////////////////////////////////////////////////////////////// 11
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Таблица.ДокументОтгрузки КАК Ссылка,
	|	Таблица.Склад КАК Склад,
	|	ВЫБОР
	|		КОГДА ТаблицаЕстьОрдера.ДокументОтгрузки ЕСТЬ NULL
	|		И
	|		НЕ СпрСклады.Ссылка ЕСТЬ NULL
	|			ТОГДА 1
	|		КОГДА Таблица.ОстаткиРавны
	|			ТОГДА 0
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК СостояниеОрдераВрем
	|ПОМЕСТИТЬ ВтСостояниеОрдера
	|ИЗ
	|	ВтОстаткиТоварыКОтгрузке КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтЕстьОрдера КАК ТаблицаЕстьОрдера
	|		ПО ТаблицаЕстьОрдера.ДокументОтгрузки = Таблица.ДокументОтгрузки
	|		И ТаблицаЕстьОрдера.Склад = Таблица.Склад
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрСклады
	|		ПО СпрСклады.Ссылка = Таблица.Склад
	|		И СпрСклады.ИспользоватьОрдернуюСхемуПриОтгрузке
	|		И СпрСклады.ДатаНачалаОрдернойСхемыПриОтгрузке <= &ТекущаяДата
	|ГДЕ
	|	ВЫБОР
	|		КОГДА ТаблицаЕстьОрдера.ДокументОтгрузки ЕСТЬ NULL
	|		И
	|		НЕ СпрСклады.Ссылка ЕСТЬ NULL
	|			ТОГДА 1
	|		КОГДА Таблица.ОстаткиРавны
	|			ТОГДА 0
	|		ИНАЧЕ 3
	|	КОНЕЦ > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Таблица.ДокументОтгрузки
	|;
	|//////////////////////////////////////////////////////////////////////////////// 12
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА ВтСостояниеНакладной.Ссылка ЕСТЬ NULL
	|			ТОГДА ВтСостояниеОрдера.Ссылка
	|		ИНАЧЕ ВтСостояниеНакладной.Ссылка
	|	КОНЕЦ КАК ДокументОтгрузки,
	|	ВЫБОР
	|		КОГДА ВтСостояниеНакладной.Склад ЕСТЬ NULL
	|			ТОГДА ВтСостояниеОрдера.Склад
	|		ИНАЧЕ ВтСостояниеНакладной.Склад
	|	КОНЕЦ КАК Склад,
	|	ЕСТЬNULL(ВтСостояниеНакладной.СостояниеНакладнойВрем, 0) КАК СостояниеНакладнойВрем,
	|	ЕСТЬNULL(ВтСостояниеОрдера.СостояниеОрдераВрем, ВтСостояниеНакладной.СостояниеОрдераВрем) КАК СостояниеОрдераВрем,
	|	ЕСТЬNULL(ВтСостояниеНакладной.РазбиватьРеализациюПоСкладам, ЛОЖЬ) КАК РазбиватьРеализациюПоСкладам
	|ПОМЕСТИТЬ ВтНовыеОрдера
	|ИЗ
	|	ВтСостояниеНакладной КАК ВтСостояниеНакладной
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВтСостояниеОрдера КАК ВтСостояниеОрдера
	|		ПО ВтСостояниеНакладной.Ссылка = ВтСостояниеОрдера.Ссылка
	#Вставка
	//++РС Консалт: Минаков А.С. Задача 21074
	|		И ВтСостояниеНакладной.Склад = ВтСостояниеОрдера.Склад
	//++РС Консалт: Минаков А.С. Задача 21074
	#КонецВставки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОтгрузки
	|;
	|//////////////////////////////////////////////////////////////////////////////// 13
	#Вставка
	//++РС Консалт: Минаков А.С. Задача 21074
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	МАКСИМУМ(ЗаданиеНаПеревозку.Ссылка.Дата) КАК Дата,
	|	ЗаданиеНаПеревозку.Распоряжение КАК Распоряжение,
	|	ЗаданиеНаПеревозку.Склад КАК Склад
	|ПОМЕСТИТЬ ВтМаксимальныеЗадания
	|ИЗ
	|	Документ.ЗаданиеНаПеревозку.Распоряжения КАК ЗаданиеНаПеревозку
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтНовыеОрдера КАК ВтНовыеОрдера
	|		ПО (&ЗаданиеНаПеревозку = ЗНАЧЕНИЕ(Документ.ЗаданиеНаПеревозку.ПустаяСсылка)
	|				ИЛИ ЗаданиеНаПеревозку.Ссылка = &ЗаданиеНаПеревозку)
	|			И (ЗаданиеНаПеревозку.Ссылка.Проведен)
	|			И ЗаданиеНаПеревозку.Распоряжение = ВтНовыеОрдера.ДокументОтгрузки
	|			И ЗаданиеНаПеревозку.Склад = ВтНовыеОрдера.Склад
	|			И ЗаданиеНаПеревозку.Ссылка.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыЗаданийНаПеревозку.Формируется), ЗНАЧЕНИЕ(Перечисление.СтатусыЗаданийНаПеревозку.КПогрузке))
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаданиеНаПеревозку.Распоряжение,
	|	ЗаданиеНаПеревозку.Склад
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Распоряжение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаданиеНаПеревозку.Ссылка КАК ЗаданиеНаПеревозку,
	|	ЗаданиеНаПеревозку.Распоряжение КАК Распоряжение,
	|	ЗаданиеНаПеревозку.РСК_Накладная КАК Накладная,
	|	ЗаданиеНаПеревозку.Склад КАК Склад, 
	|	МАКСИМУМ(ЕСТЬNULL(РасходныйОрдерНаТовары.Ссылка, ЗНАЧЕНИЕ(Документ.РасходныйОрдерНаТовары.ПустаяСсылка))) КАК Ордер
	|ПОМЕСТИТЬ ВтЗадания
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		МАКСИМУМ(ЗаданиеНаПеревозку.Ссылка) КАК Ссылка,
	|		ЗаданиеНаПеревозку.Распоряжение КАК Распоряжение,
	|		ЗаданиеНаПеревозку.Склад КАК Склад
	|	ИЗ
	|		Документ.ЗаданиеНаПеревозку.Распоряжения КАК ЗаданиеНаПеревозку
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтМаксимальныеЗадания КАК ВтМаксимальныеЗадания
	|			ПО (&ЗаданиеНаПеревозку = ЗНАЧЕНИЕ(Документ.ЗаданиеНаПеревозку.ПустаяСсылка)
	|					ИЛИ ЗаданиеНаПеревозку.Ссылка = &ЗаданиеНаПеревозку)
	|				И (ЗаданиеНаПеревозку.Ссылка.Проведен)
	|				И ЗаданиеНаПеревозку.Ссылка.Дата = ВтМаксимальныеЗадания.Дата
	|				И ЗаданиеНаПеревозку.Распоряжение = ВтМаксимальныеЗадания.Распоряжение
	|				И ЗаданиеНаПеревозку.Склад = ВтМаксимальныеЗадания.Склад
	|				И ЗаданиеНаПеревозку.Ссылка.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыЗаданийНаПеревозку.Формируется), ЗНАЧЕНИЕ(Перечисление.СтатусыЗаданийНаПеревозку.КПогрузке))
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ЗаданиеНаПеревозку.Распоряжение,
	|		ЗаданиеНаПеревозку.Склад) КАК ВложенныйЗапрос
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаданиеНаПеревозку.Распоряжения КАК ЗаданиеНаПеревозку
	|		ПО ВложенныйЗапрос.Ссылка = ЗаданиеНаПеревозку.Ссылка
	|			И ВложенныйЗапрос.Склад = ЗаданиеНаПеревозку.Склад
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасходныйОрдерНаТовары.ТоварыПоРаспоряжениям КАК РасходныйОрдерНаТовары
	|		ПО (ЗаданиеНаПеревозку.Ссылка = РасходныйОрдерНаТовары.Ссылка.ЗаданиеНаПеревозку)
	|			И (РасходныйОрдерНаТовары.Распоряжение = ЗаданиеНаПеревозку.Распоряжение)
	|			И (РасходныйОрдерНаТовары.Ссылка.Склад = ЗаданиеНаПеревозку.Склад)
	|			И (РасходныйОрдерНаТовары.Ссылка.Проведен)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаданиеНаПеревозку.Ссылка,
	|	ЗаданиеНаПеревозку.Распоряжение,
	|	ЗаданиеНаПеревозку.РСК_Накладная,
	|	ЗаданиеНаПеревозку.Склад
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Распоряжение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МАКСИМУМ(РеализацияТоваровУслуг.Дата) КАК Дата,
	|	ВтНовыеОрдера.ДокументОтгрузки КАК ДокументОтгрузки,
	|	ВтНовыеОрдера.Склад
	|ПОМЕСТИТЬ ВтМаксимальныеНакладные
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтНовыеОрдера КАК ВтНовыеОрдера
	|		ПО (РеализацияТоваровУслуг.Проведен)
	|			И (НЕ РеализацияТоваровУслуг.Ссылка В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						ЗаданиеНаПеревозку.РСК_Накладная
	|					ИЗ
	|						Документ.ЗаданиеНаПеревозку.Распоряжения КАК ЗаданиеНаПеревозку
	|					ГДЕ
	|						ЗаданиеНаПеревозку.Ссылка.Проведен
	|						И ЗаданиеНаПеревозку.Доставлено))
	|			И (ВЫБОР
	|				КОГДА РеализацияТоваровУслуг.РеализацияПоЗаказам
	|					ТОГДА РеализацияТоваровУслуг.ЗаказКлиента
	|				ИНАЧЕ РеализацияТоваровУслуг.Ссылка
	|			КОНЕЦ = ВтНовыеОрдера.ДокументОтгрузки)
	|			И РеализацияТоваровУслуг.Склад = ВтНовыеОрдера.Склад
	|
	|СГРУППИРОВАТЬ ПО
	|	ВтНовыеОрдера.ДокументОтгрузки,
	|	ВтНовыеОрдера.Склад
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОтгрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	МАКСИМУМ(РеализацияТоваровУслуг.Ссылка) КАК Накладная,
	|	ВтМаксимальныеНакладные.ДокументОтгрузки КАК ДокументОтгрузки,
	|	ВтМаксимальныеНакладные.Склад КАК Склад
	|ПОМЕСТИТЬ ВтНакладные
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтМаксимальныеНакладные КАК ВтМаксимальныеНакладные
	|		ПО (РеализацияТоваровУслуг.Проведен)
	|			И РеализацияТоваровУслуг.Дата = ВтМаксимальныеНакладные.Дата
	|			И РеализацияТоваровУслуг.Склад = ВтМаксимальныеНакладные.Склад
	|			И (НЕ РеализацияТоваровУслуг.Ссылка В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						ЗаданиеНаПеревозку.РСК_Накладная
	|					ИЗ
	|						Документ.ЗаданиеНаПеревозку.Распоряжения КАК ЗаданиеНаПеревозку
	|					ГДЕ
	|						ЗаданиеНаПеревозку.Ссылка.Проведен
	|						И ЗаданиеНаПеревозку.Доставлено))
	|			И (ВЫБОР
	|				КОГДА РеализацияТоваровУслуг.РеализацияПоЗаказам
	|					ТОГДА РеализацияТоваровУслуг.ЗаказКлиента
	|				ИНАЧЕ РеализацияТоваровУслуг.Ссылка
	|			КОНЕЦ = ВтМаксимальныеНакладные.ДокументОтгрузки)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВтМаксимальныеНакладные.ДокументОтгрузки,
	|	ВтМаксимальныеНакладные.Склад
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОтгрузки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	МАКСИМУМ(РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка.Дата) КАК Дата,
	|	РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Распоряжение КАК Распоряжение,
	|	ВтНовыеОрдера.Склад КАК Склад
	|ПОМЕСТИТЬ ВтМаксимальныеОрдера
	|ИЗ
	|	Документ.РасходныйОрдерНаТовары.ТоварыПоРаспоряжениям КАК РасходныйОрдерНаТоварыТоварыПоРаспоряжениям
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтНовыеОрдера КАК ВтНовыеОрдера
	|		ПО РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Распоряжение = ВтНовыеОрдера.ДокументОтгрузки
	|			И (РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка.Проведен)
	|			И (РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка.ЗаданиеНаПеревозку = ЗНАЧЕНИЕ(Документ.ЗаданиеНаПеревозку.ПустаяСсылка))
	|			И (РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка.Склад = ВтНовыеОрдера.Склад)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Распоряжение,
	|	ВтНовыеОрдера.Склад
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Распоряжение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РасходныйОрдерНаТовары.Ссылка КАК Ордер,
	|	РасходныйОрдерНаТовары.Статус КАК СтатусОрдера,
	|	ВложенныйЗапрос.Распоряжение КАК Распоряжение,
	|	ВложенныйЗапрос.Склад
	|ПОМЕСТИТЬ ВтОрдера
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		МАКСИМУМ(РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка) КАК Ссылка,
	|		РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Распоряжение КАК Распоряжение,
	|		ВтМаксимальныеОрдера.Склад КАК Склад
	|	ИЗ
	|		Документ.РасходныйОрдерНаТовары.ТоварыПоРаспоряжениям КАК РасходныйОрдерНаТоварыТоварыПоРаспоряжениям
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтМаксимальныеОрдера КАК ВтМаксимальныеОрдера
	|			ПО РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Распоряжение = ВтМаксимальныеОрдера.Распоряжение
	|				И РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка.Дата = ВтМаксимальныеОрдера.Дата
	|				И (РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка.Проведен)
	|				И (РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка.ЗаданиеНаПеревозку = ЗНАЧЕНИЕ(Документ.ЗаданиеНаПеревозку.ПустаяСсылка))
	|				И (РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Ссылка.Склад = ВтМаксимальныеОрдера.Склад)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		РасходныйОрдерНаТоварыТоварыПоРаспоряжениям.Распоряжение,
	|		ВтМаксимальныеОрдера.Склад) КАК ВложенныйЗапрос
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РасходныйОрдерНаТовары КАК РасходныйОрдерНаТовары
	|		ПО ВложенныйЗапрос.Ссылка = РасходныйОрдерНаТовары.Ссылка
	|			И ВложенныйЗапрос.Склад = РасходныйОрдерНаТовары.Склад
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВложенныйЗапрос.Распоряжение
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОтбору.Распоряжение КАК Распоряжение,
	|	СУММА(ТоварыКОтбору.КОтбору + ТоварыКОтбору.Отобрано) КАК ОтобраноОрдер
	|ПОМЕСТИТЬ ВтОтборыОрдера
	|ИЗ
	|	РегистрНакопления.ТоварыКОтбору КАК ТоварыКОтбору
	|ГДЕ
	|	ТоварыКОтбору.Регистратор В
	|			(ВЫБРАТЬ
	|				ВтОрдера.Ордер
	|			ИЗ
	|				ВтОрдера КАК ВтОрдера
	|		
	|			ОБЪЕДИНИТЬ
	|		
	|			ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ВтЗадания.Ордер
	|			ИЗ
	|				ВтЗадания КАК ВтЗадания)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОтбору.Распоряжение
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Распоряжение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОтбору.Распоряжение КАК Распоряжение,
	|	СУММА(ТоварыКОтбору.Отобрано) КАК Отобрано
	|ПОМЕСТИТЬ ВтОтборы
	|ИЗ
	|	РегистрНакопления.ТоварыКОтбору КАК ТоварыКОтбору
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтборРазмещениеТоваров КАК ОтборРазмещениеТоваров
	|		ПО ТоварыКОтбору.Регистратор = ОтборРазмещениеТоваров.Ссылка
	|			И (ТоварыКОтбору.Распоряжение В
	|				(ВЫБРАТЬ
	|					ВтОтборыОрдера.Распоряжение
	|				ИЗ
	|					ВтОтборыОрдера КАК ВтОтборыОрдера))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОтбору.Распоряжение
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Распоряжение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	//++РС Консалт: Минаков А.С. Задача 21074
	#КонецВставки
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТоварыКОтгрузке.ДокументОтгрузки КАК Распоряжение,
	|	ТоварыКОтгрузке.ДокументОтгрузки КАК Ссылка,
	|	ТоварыКОтгрузке.ДокументОтгрузки КАК ЗаказКлиента,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).ПометкаУдаления
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).ПометкаУдаления
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).ПометкаУдаления
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).ПометкаУдаления
	|	КОНЕЦ КАК ПометкаУдаления,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Номер
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Номер
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Номер
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Номер
	|	КОНЕЦ КАК Номер,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Дата
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Дата
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Дата
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Дата
	|	КОНЕЦ КАК ДатаДокумента,
	|	ТИПЗНАЧЕНИЯ(ТоварыКОтгрузке.ДокументОтгрузки) КАК ТипРаспоряжения,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Проведен
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Проведен
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Проведен
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Проведен
	|	КОНЕЦ КАК Проведен,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Партнер
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Партнер
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Партнер
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Партнер
	|	КОНЕЦ КАК Партнер,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Контрагент
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Контрагент
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Контрагент
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Организация
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Организация
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Организация
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Организация
	|	КОНЕЦ КАК Организация,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Соглашение
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Соглашение
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Соглашение
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Соглашение
	|	КОНЕЦ КАК Соглашение,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Договор
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Договор
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Договор
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Договор
	|	КОНЕЦ КАК Договор,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Сделка
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Сделка
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Сделка
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Сделка
	|	КОНЕЦ КАК Сделка,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Валюта
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Валюта
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Валюта
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Валюта
	|	КОНЕЦ КАК Валюта,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).СуммаДокумента
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).СуммаЗамены
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).СуммаДокумента
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).СуммаДокумента
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаДокумента,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).ГрафикОплаты
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).ГрафикОплаты
	|	КОНЕЦ КАК ГрафикОплаты,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Склад
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Склад
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Склад
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Склад
	|	КОНЕЦ КАК СкладШапки,
	|	ТоварыКОтгрузке.Склад КАК Склад,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Статус
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Статус
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Статус
	|	КОНЕЦ КАК Статус,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Менеджер
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Менеджер
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Менеджер
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Менеджер
	|	КОНЕЦ КАК Менеджер,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).ДокументОснование
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).ЗаказКлиента
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).ЗаказКлиента
	|	КОНЕЦ КАК ДокументОснование,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Приоритет
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Приоритет
	|	КОНЕЦ КАК Приоритет,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).ХозяйственнаяОперация
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).ХозяйственнаяОперация
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).ХозяйственнаяОперация
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).ХозяйственнаяОперация
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Касса
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Касса
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Касса
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Кассы.ПустаяСсылка)
	|	КОНЕЦ КАК Касса,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).СпособДоставки
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).СпособДоставки
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).СпособДоставки
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).СпособДоставки
	|	КОНЕЦ КАК СпособДоставки,
	|	ТоварыКОтгрузке.СостояниеНакладнойВрем КАК СостояниеНакладной,
	|	ТоварыКОтгрузке.СостояниеОрдераВрем КАК СостояниеОрдера,
	|	ВЫБОР
	|		КОГДА ВЫБОР
	|			КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|				ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Приоритет
	|			КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|				ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Приоритет
	|		КОНЕЦ В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				Приоритеты.Ссылка КАК Приоритет
	|			ИЗ
	|				Справочник.Приоритеты КАК Приоритеты
	|			УПОРЯДОЧИТЬ ПО
	|				Приоритеты.РеквизитДопУпорядочивания)
	|			ТОГДА 0
	|		КОГДА ВЫБОР
	|			КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|				ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Приоритет
	|			КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|				ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Приоритет
	|		КОНЕЦ В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				Приоритеты.Ссылка КАК Приоритет
	|			ИЗ
	|				Справочник.Приоритеты КАК Приоритеты
	|			УПОРЯДОЧИТЬ ПО
	|				Приоритеты.РеквизитДопУпорядочивания УБЫВ)
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК КартинкаПриоритета,
	|	ТоварыКОтгрузке.РазбиватьРеализациюПоСкладам КАК РазбиватьРеализациюПоСкладам
	#Вставка
	//++РС Консалт: Минаков А.С. Задача 21074
	|	,ВтЗадания.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВтЗадания.Накладная, НЕОПРЕДЕЛЕНО) В (НЕОПРЕДЕЛЕНО, ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка), ЗНАЧЕНИЕ(Документ.ПеремещениеТоваров.ПустаяСсылка))
	|			ТОГДА ВтНакладные.Накладная
	|		ИНАЧЕ ВтЗадания.Накладная
	|	КОНЕЦ КАК Накладная,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(РасходныйОрдерНаТовары.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыРасходныхОрдеров.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СтатусыРасходныхОрдеров.ПустаяСсылка)
	|			ТОГДА ВтОрдера.СтатусОрдера
	|		ИНАЧЕ РасходныйОрдерНаТовары.Статус
	|	КОНЕЦ КАК СтатусОрдера,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВтЗадания.Ордер, ЗНАЧЕНИЕ(Документ.РасходныйОрдерНаТовары.ПустаяСсылка)) = ЗНАЧЕНИЕ(Документ.РасходныйОрдерНаТовары.ПустаяСсылка)
	|			ТОГДА ВтОрдера.Ордер
	|		ИНАЧЕ ВтЗадания.Ордер
	|	КОНЕЦ КАК Ордер,
	|	ЕСТЬNULL(ВтОтборыОрдера.ОтобраноОрдер, 0) = ЕСТЬNULL(ВтОтборы.Отобрано, 0) 
	|	И НЕ ЕСТЬNULL(ВЫБОР
	|		КОГДА ЕСТЬNULL(РасходныйОрдерНаТовары.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыРасходныхОрдеров.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СтатусыРасходныхОрдеров.ПустаяСсылка)
	|			ТОГДА ВтОрдера.СтатусОрдера
	|		ИНАЧЕ РасходныйОрдерНаТовары.Статус
	|	КОНЕЦ, ЗНАЧЕНИЕ(Перечисление.СтатусыРасходныхОрдеров.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.СтатусыРасходныхОрдеров.Подготовлено) КАК СтатусСборки
	//++РС Консалт: Минаков А.С. Задача 21074
	#КонецВставки
	|ИЗ
	|	ВтНовыеОрдера КАК ТоварыКОтгрузке
	#Вставка
	//++РС Консалт: Минаков А.С. Задача 21074
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтЗадания КАК ВтЗадания
	|		ПО ТоварыКОтгрузке.ДокументОтгрузки = ВтЗадания.Распоряжение
	|			И ТоварыКОтгрузке.Склад = ВтЗадания.Склад
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасходныйОрдерНаТовары КАК РасходныйОрдерНаТовары
	|		ПО ВтЗадания.Ордер = РасходныйОрдерНаТовары.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтНакладные КАК ВтНакладные
	|		ПО ТоварыКОтгрузке.ДокументОтгрузки = ВтНакладные.ДокументОтгрузки
	|			И ТоварыКОтгрузке.Склад = ВтНакладные.Склад
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтОрдера КАК ВтОрдера
	|		ПО ТоварыКОтгрузке.ДокументОтгрузки = ВтОрдера.Распоряжение
	|			И ТоварыКОтгрузке.Склад = ВтОрдера.Склад
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтОтборыОрдера КАК ВтОтборыОрдера
	|		ПО (ВЫБОР
	|				КОГДА ЕСТЬNULL(ВтЗадания.Ордер, ЗНАЧЕНИЕ(Документ.РасходныйОрдерНаТовары.ПустаяСсылка)) = ЗНАЧЕНИЕ(Документ.РасходныйОрдерНаТовары.ПустаяСсылка)
	|					ТОГДА ВтОрдера.Ордер
	|				ИНАЧЕ ВтЗадания.Ордер
	|			КОНЕЦ = ВтОтборыОрдера.Распоряжение)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтОтборы КАК ВтОтборы
	|		ПО (ВтОтборыОрдера.Распоряжение = ВтОтборы.Распоряжение)	
	//++РС Консалт: Минаков А.С. Задача 21074
	#КонецВставки
	|ГДЕ
	#Удаление
	|	НЕ ТоварыКОтгрузке.ДокументОтгрузки.Ссылка ЕСТЬ NULL";
	#КонецУдаления
	#Вставка
	//++РС Консалт: Минаков А.С. Задача 21074
	|	НЕ ТоварыКОтгрузке.ДокументОтгрузки.Ссылка ЕСТЬ NULL
	|		И (&ЗаданиеНаПеревозку = ЗНАЧЕНИЕ(Документ.ЗаданиеНаПеревозку.ПустаяСсылка) ИЛИ НЕ ВтЗадания.ЗаданиеНаПеревозку ЕСТЬ NULL)";
	//++РС Консалт: Минаков А.С. Задача 21074
	#КонецВставки

	Возврат ТекстЗапроса;

КонецФункции

&НаКлиенте
Процедура РСК_ЗаданиеНаПеревозкуПриИзмененииВместо(Элемент)
	
	//++РС Консалт: Минаков А.С. Задача 21074
	УстановитьОтборПоЗаданиюСервер()
	//++РС Консалт: Минаков А.С. Задача 21074
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоЗаданиюСервер()
	
	//++РС Консалт: Минаков А.С. Задача 21074
	СписокРаспоряженияНаОформление.Параметры.УстановитьЗначениеПараметра("ЗаданиеНаПеревозку", ЗаданиеНаПеревозку)
	//++РС Консалт: Минаков А.С. Задача 21074
	
КонецПроцедуры

&НаКлиенте
Процедура РСК_ПечатьКомплектаДокументовВместо(Команда)
	
	//++РС Консалт: Минаков А.С. Задача 21074	
	МассивОбъектов = Новый Массив;
	
	Для Каждого ВыделеннаяСтрока Из Элементы.СписокРаспоряженияНаОформление.ВыделенныеСтроки Цикл
		ДанныеСтроки = Элементы.СписокРаспоряженияНаОформление.ДанныеСтроки(ВыделеннаяСтрока);
		Если ЗначениеЗаполнено(ДанныеСтроки.Накладная) Тогда
			МассивОбъектов.Добавить(ДанныеСтроки.Накладная)
		КонецЕсли
	КонецЦикла;
	
	Если Не МассивОбъектов.Количество() Тогда
		Возврат
	КонецЕсли;	
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
		"Документ.РеализацияТоваровУслуг",
		"КомплектДокументов",
		МассивОбъектов,
		ЭтаФорма,
		Новый Структура(
		"ПереопределитьПользовательскиеНастройкиКоличества,АдресКомплектаПечатныхФорм,ФиксированныйКомплект",
		Истина, ПоместитьКомплектПечатныхФормВоВременноеХранилищеСервер(МассивОбъектов), Истина)
	);
	//++РС Консалт: Минаков А.С. Задача 21074
	
КонецПроцедуры

&НаСервере
Функция ПоместитьКомплектПечатныхФормВоВременноеХранилищеСервер(МассивОбъектов)
	//++ РС Консалт: Трофимов Евгений 22.02.2023 Задача 23987
	//e1cib/data/Документ.Задание?ref=9e9022aceb4ae0fe4945c336d1841b94
	КоллекцияПечатныхФорм = РегистрыСведений.НастройкиПечатиОбъектов.КомплектПечатныхФорм(
		"Документ.РеализацияТоваровУслуг",
		МассивОбъектов
	);
	
	Если ТипЗнч(КоллекцияПечатныхФорм) = Тип("ТаблицаЗначений") Тогда
		КомплектПечатныхФорм.Загрузить(КоллекцияПечатныхФорм);
	Иначе
		КомплектПечатныхФорм.Очистить();
	КонецЕсли;	
	//-- КонецЗадачи 23987	
	
	//++РС Консалт: Минаков А.С. Задача 21074
	Возврат ПоместитьВоВременноеХранилище(КомплектПечатныхФорм.Выгрузить())
	//++РС Консалт: Минаков А.С. Задача 21074
	
КонецФункции

&НаКлиенте
&ИзменениеИКонтроль("ОформитьПродажу")
Процедура РСК_ОформитьПродажу()
	
	ЗаполнитьСписокДокументовПоДаннымФормы();
	#Вставка
	//++ РС Консалт: Минаков А.С. Задача 20226
	Если Не СписокРаспоряжений.Количество() Тогда
		Возврат
	КонецЕсли;
	//++ РС Консалт: Минаков А.С. Задача 20226
	#КонецВставки

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", ЭтаФорма);
	ДополнительныеПараметры.Вставить("МассивСсылок", СписокРаспоряжений);
	ДополнительныеПараметры.Вставить("ПоОрдерам", ПоОрдерам);
	ДополнительныеПараметры.Вставить("Склад", Склад);
	ДополнительныеПараметры.Вставить("КлючОбъекта", "Обработка.ЖурналДокументовПродажи.Форма.КОформлениюНакладных/ТекущиеДанные");
	#Вставка
	//++ РС Консалт: Минаков А.С. Задача 20226
	ДополнительныеПараметры.Вставить("ВключитьВЗадание", Истина); 
	//++ РС Консалт: Минаков А.С. Задача 20226
	#КонецВставки

	ПродажиКлиент.СформироватьКомплектДокументовВызов(Неопределено, ДополнительныеПараметры);

	Элементы.СписокРаспоряженияНаОформление.Обновить();

КонецПроцедуры

&НаСервере
&После("ПередЗагрузкойДанныхИзНастроекНаСервере")
Процедура РСК_ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	//++ РС Консалт: Минаков А.С. Задача 20226
	ЗаданиеНаПеревозку = Настройки.Получить("ЗаданиеНаПеревозку");
	
	УстановитьОтборПоЗаданиюСервер()
	//++ РС Консалт: Минаков А.С. Задача 20226
	
КонецПроцедуры

&НаКлиенте
&ИзменениеИКонтроль("ЗаполнитьСписокДокументовПоДаннымФормы")
Процедура РСК_ЗаполнитьСписокДокументовПоДаннымФормы()
	
	#Вставка
	//++ РС Консалт: Минаков А.С. Задача 20226
	ПараметрыОбновленияОрдера = РСК_ВызовСервера.ПараметрыОбновленияОрдера();
	//++ РС Консалт: Минаков А.С. Задача 20226
	#КонецВставки
	ВыделенныеСтроки = Элементы.СписокРаспоряженияНаОформление.ВыделенныеСтроки;
	СписокРаспоряжений.Очистить();
	Для Каждого Строка Из ВыделенныеСтроки Цикл
		НайденнаяСтрока = Элементы.СписокРаспоряженияНаОформление.ДанныеСтроки(Строка);
		#Вставка
		//++ РС Консалт: Минаков А.С. Задача 20226 
		ПараметрыОбновленияОрдера.Ордер = НайденнаяСтрока.Ордер;
		ПараметрыОбновленияОрдера.ЗаданиеНаПеревозку = НайденнаяСтрока.ЗаданиеНаПеревозку;
		ПараметрыОбновленияОрдера.Распоряжение = НайденнаяСтрока.Ссылка;
		ПараметрыОбновленияОрдера.Склад = НайденнаяСтрока.Склад;
		ПараметрыОбновленияОрдера.ЗаполнитьЗадание = Истина;
		ПараметрыОбновленияОрдера.НовыйСтатус = "КОтгрузке";
		ПараметрыОбновленияОрдера.ИсключаемыеСтатусы = "КОтгрузке,Отгружен";
		ПараметрыОбновленияОрдера.ЗаполнитьПоОтбору = Истина;
		
		Отказ = Ложь;
		РСК_ВызовСервера.ВыполнитьПроверкуИзменениеОрдера(ПараметрыОбновленияОрдера, Отказ);
		Если Отказ Тогда
			Продолжить
		КонецЕсли;
		
		Если НайденнаяСтрока.СостояниеОрдера = 1 Тогда
			НайденнаяСтрока.СостояниеОрдера = 3	
		КонецЕсли;
		//++ РС Консалт: Минаков А.С. Задача 20226
		#КонецВставки
		ДобавленнаяСтрока = СписокРаспоряжений.Добавить();
		ЗаполнитьЗначенияСвойств(ДобавленнаяСтрока, НайденнаяСтрока);
	КонецЦикла;

КонецПроцедуры

