//++ РС Консалт: Трофимов Евгений 28.11.2022 Тикет 22072
//e1cib/data/Документ.Задание?ref=97ca727a83736946450ccf89d8cad748
&НаКлиенте
Процедура УслугиБанка(Команда)
	
	РазрешенныеТипы = Новый СписокЗначений;
	РазрешенныеТипы.Добавить(ПредопределенноеЗначение("Перечисление.ТипыПлатежныхДокументов.БанковскийОрдер"),,Истина);
	Для Каждого ВыделеннаяСтрока Из Элементы.СписокПлатежей.ВыделенныеСтроки Цикл
		Если ТипЗнч(ВыделеннаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
			Продолжить;
		КонецЕсли;
		ДанныеСтроки = Элементы.СписокПлатежей.ДанныеСтроки(ВыделеннаяСтрока);
		Если РазрешенныеТипы.НайтиПоЗначению(ДанныеСтроки.ТипПлатежногоДокумента) = Неопределено Тогда
			РазрешенныеТипы.Добавить(ДанныеСтроки.ТипПлатежногоДокумента);
		КонецЕсли;
	КонецЦикла;
	Если РазрешенныеТипы.Количество() > 1 Тогда
		РазрешенныеТипы.ПоказатьОтметкуЭлементов(
			Новый ОписаниеОповещения("ПослеОтметкиРазрешенныхТипов", ЭтаФорма), 
			"Выберите типы обрабатываемых документов"
		);
        Возврат;
	КонецЕсли;
	
	ПослеОтметкиРазрешенныхТипов(РазрешенныеТипы, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтметкиРазрешенныхТипов(РазрешенныеТипы, ДополнительныеПараметры) Экспорт
	
	Если РазрешенныеТипы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МассивВыбранных = Новый Массив;
	Для Каждого Элемент Из РазрешенныеТипы Цикл
		Если Элемент.Пометка Тогда
			МассивВыбранных.Добавить(Элемент.Значение);
		КонецЕсли;
	КонецЦикла;
	
	ИзменитьДокументыПоТипуОперации(Элементы.СписокПлатежей,МассивВыбранных,"УслугиБанка");
	
КонецПроцедуры


//++ РС Консалт: Трофимов Евгений 28.11.2022 Тикет 22072
//e1cib/data/Документ.Задание?ref=97ca727a83736946450ccf89d8cad748
&НаКлиенте
Процедура ИзменитьДокументыПоТипуОперации(СписокЭлемент, РазрешенныеТипы, ТипОперации = Неопределено)

	МассивОбъектов = Новый Массив;
	Для Каждого ВыделеннаяСтрока Из СписокЭлемент.ВыделенныеСтроки Цикл
		Если ТипЗнч(ВыделеннаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
			Продолжить;
		КонецЕсли;
		ТекущаяСтрока = СписокЭлемент.ДанныеСтроки(ВыделеннаяСтрока);
		Если ТекущаяСтрока <> Неопределено Тогда
			
			Если РазрешенныеТипы.Найти(ТекущаяСтрока.ТипПлатежногоДокумента) <> Неопределено Тогда
				МассивОбъектов.Добавить(ТекущаяСтрока.Ссылка);
			КонецЕсли; 
			
		КонецЕсли;
	КонецЦикла;
	
	Если МассивОбъектов.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Команда не может быть выполнена для указанного объекта.'"));
		Возврат;
	КонецЕсли;
	
	ё=0;
	Для Каждого СсылкаНаДокумент Из МассивОбъектов Цикл
		ё=ё+1;
		ОбработкаПрерыванияПользователя();
		Состояние(""+ё+" из "+МассивОбъектов.Количество(), ё*100/МассивОбъектов.Количество(),"Обработка выделенных строк....");
		ИзменитьДокументыПоТипуОперацииНаСервере(СсылкаНаДокумент, ТипОперации);
	КонецЦикла;
	СписокЭлемент.Обновить();
	
КонецПроцедуры 

// Изменить документы по типу операции на сервере
//++ РС Консалт: Трофимов Евгений 28.11.2022 Тикет 22072
//e1cib/data/Документ.Задание?ref=97ca727a83736946450ccf89d8cad748
&НаСервереБезКонтекста
Процедура ИзменитьДокументыПоТипуОперацииНаСервере(СсылкаНаДокумент, ТипОперации)
	
    ДокОбъект = СсылкаНаДокумент.ПолучитьОбъект();
	ДокОбъект.ХозяйственнаяОперация = реа_ОбщийСерверПовтИсп.ПолучитьПредопределенноеЗначение("ЗначенияПоТипуОперации" + ТипОперации,"ХозяйственнаяОперация");
	ДокОбъект.РасшифровкаПлатежа.Очистить();
	НовСтрока = ДокОбъект.РасшифровкаПлатежа.Добавить();
	НовСтрока.СтатьяДвиженияДенежныхСредств = реа_ОбщийСерверПовтИсп.ПолучитьПредопределенноеЗначение("ЗначенияПоТипуОперации" + ТипОперации,"СтатьяДвиженияДенежныхСредств");
	НовСтрока.СтатьяРасходов 	= реа_ОбщийСерверПовтИсп.ПолучитьПредопределенноеЗначение("ЗначенияПоТипуОперации" + ТипОперации,"СтатьяРасходов");
	НовСтрока.АналитикаРасходов = реа_ОбщийСерверПовтИсп.ПолучитьПредопределенноеЗначение("ЗначенияПоТипуОперации" + ТипОперации,"АналитикаРасходов");
	НовСтрока.Подразделение 	= реа_ОбщийСерверПовтИсп.ПолучитьПредопределенноеЗначение("ЗначенияПоТипуОперации" + ТипОперации,"Подразделение");
	НовСтрока.Сумма = ДокОбъект.СуммаДокумента; 
	Попытка
		ДокОбъект.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
		ОбщегоНазначения.СообщитьПользователю("Не удалось провести документ " + СсылкаНаДокумент, СсылкаНаДокумент);
	КонецПопытки; 
		
КонецПроцедуры 
 
 
