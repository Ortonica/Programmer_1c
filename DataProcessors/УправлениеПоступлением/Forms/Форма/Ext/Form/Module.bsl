
&НаСервере
&Перед("ПриСозданииНаСервере")
Процедура РСК_ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)  
	
	//++РС Консалт Назаров М.Ю. 14 октября 2022 г. 9:43:39
	// Задать дннамический список через форму не получается, 
	// необходимо в расширение добавлять измерения и ресурсы регистра "Товары к поступению", 
	// это приводит к вылетам конфигуратора при обновлении
	ПулНоменклатурыПоступление.ТекстЗапроса = "ВЫБРАТЬ
	|	ТоварыКПоступлениюОстаткиНаДату.Номенклатура КАК Номенклатура,
	|	ТоварыКПоступлениюОстаткиНаДату.Характеристика КАК Характеристика,
	|	ТоварыКПоступлениюОстаткиНаДату.Назначение,
	|	ВЫБОР
	|		КОГДА ТоварыКПоступлениюОстаткиНаДату.КОформлениюОрдеровОстаток - ТоварыКПоступлениюОстаткиНаДату.ПринимаетсяОстаток > 0
	|			ТОГДА ТоварыКПоступлениюОстаткиНаДату.КОформлениюОрдеровОстаток - ТоварыКПоступлениюОстаткиНаДату.ПринимаетсяОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Принять,
	|	ВЫБОР
	|		КОГДА ТоварыКПоступлениюОстаткиТекущие.КОформлениюОрдеровОстаток - ТоварыКПоступлениюОстаткиТекущие.ПринимаетсяОстаток < 0
	|			ТОГДА -(ТоварыКПоступлениюОстаткиТекущие.КОформлениюОрдеровОстаток - ТоварыКПоступлениюОстаткиТекущие.ПринимаетсяОстаток)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Перепоставка,
	|	ТоварыКПоступлениюОстаткиНаДату.ПринимаетсяОстаток КАК Принимается
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлению.Остатки(
	|			,
	|			ДокументПоступления = &Распоряжение
	|			) КАК ТоварыКПоступлениюОстаткиНаДату
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыКПоступлению.Остатки(
	|				,
	|				ДокументПоступления = &Распоряжение
	|				) КАК ТоварыКПоступлениюОстаткиТекущие
	|		ПО ТоварыКПоступлениюОстаткиНаДату.ДокументПоступления = ТоварыКПоступлениюОстаткиТекущие.ДокументПоступления
	|			И ТоварыКПоступлениюОстаткиНаДату.Номенклатура = ТоварыКПоступлениюОстаткиТекущие.Номенклатура
	|			И ТоварыКПоступлениюОстаткиНаДату.Характеристика = ТоварыКПоступлениюОстаткиТекущие.Характеристика
	|			И ТоварыКПоступлениюОстаткиНаДату.Склад = ТоварыКПоступлениюОстаткиТекущие.Склад
	|						И ТоварыКПоступлениюОстаткиНаДату.Назначение = ТоварыКПоступлениюОстаткиТекущие.Назначение";  
	
	УстановитьРаспоряжениеВПараметрПулаНоменклатуры(Неопределено);
	//--РС Консалт Назаров М.Ю. 14 октября 2022 г. 9:43:39
	
КонецПроцедуры

&НаСервере
&После("УстановитьОтборыВЗависимыхСписках")
Процедура РСК_УстановитьОтборыВЗависимыхСписках(РаспоряжениеНаПоступление)
	
	//++РС Консалт Назаров М.Ю. 14 октября 2022 г. 9:46:27                  
	УстановитьРаспоряжениеВПараметрПулаНоменклатуры(РаспоряжениеНаПоступление);
	//--РС Консалт Назаров М.Ю. 14 октября 2022 г. 9:46:27
	
КонецПроцедуры

&НаСервере
Процедура УстановитьРаспоряжениеВПараметрПулаНоменклатуры(Знач РаспоряжениеНаПоступление)
	
	// Установка параметра распоряжения в списке номенклатуры (Как в УТ)
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ПулНоменклатурыПоступление, "Распоряжение",  РаспоряжениеНаПоступление,Истина);
	
КонецПроцедуры

